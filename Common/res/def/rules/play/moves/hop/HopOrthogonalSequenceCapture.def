// Defines a sequence of hop move in all the orthogonal directions over an enemy to an empty site. 
//The enemy pieces are removed.
// #1 = Maximum distance before the hop [0].
// #2 = Maximum distance after the hop [0].
// #3 = When to perform the capture (immediately or at the end of the turn) [immediately].
// @example ("HopOrthogonalSequenceCapture")
// @example ("HopOrthogonalSequenceCapture" before:(count Rows) after:(count Rows) at:EndOfTurn)

(define "HopOrthogonalSequenceCapture" 
    (move Hop
        Orthogonal
        (between 
            #1
            #2
            if:("IsEnemyAt" (between)) 
            (apply (remove (between) #3))
       )
        (to if:(is Empty (to)))
        (then 
            (if (can Move 
                    (hop 
                        (from (last To)) 
                        Orthogonal
                        (between 
                            #1
                            #2
                            if:(and 
                                (not (is In (between) (sites ToClear))) 
                                ("IsEnemyAt" (between))
                           )
                       )
                        (to if:(is Empty (to)))
                   )
               ) 
                (moveAgain)
           )
       )
   )
)
