(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:True) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Rongmei Naga Alignment Game"
    (players 2)
    
    (equipment { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            (move
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain" 
            )
        ) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:[3..12]))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (if (> (count Pieces Mover) 3)
                        (forEach Piece)
                        (move 
                            (from (sites Occupied by:Mover))
                            (to (sites Empty))
                        )
                    )
                )
            )
        )
        }
        
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This alignment game was played by the Rongmei Naga people in India in the early twentieth century. ")
        (rules "Three concentric squares, with lines connecting the corners and the midpoints of two opposite sides. Players alternate turns placing pieces on an empty point on the board. When all the pieces are placed, players alternate turns moving a piece to an empty adjacent spot along the lines of the board. When three of a player's pieces form a line, the player removes one of the opponent's pieces. Pieces which are in a line of three cannot be removed. The player who removes all of the opponent's pieces wins.
        ")
        (source "DLP evidence.")
        (id "1095")
        (version "1.3.5")
        (classification "board/space/line/reconstruction")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1911.")
        }
    )
    
)
