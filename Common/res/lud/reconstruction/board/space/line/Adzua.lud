(define "RemoveEnemyPiece" 
    (move
        Remove
        (sites Occupied by:Next container:"Board") 
   )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:True) 
            (moveAgain)
       )
   )
)

//------------------------------------------------------------------------------

(game "Adzua"
    (players 2) 
    (equipment { 
        (board (rectangle 5 6)
            use:Vertex		
       )
        (piece "Marker" Each (move [#])) // Missing knowledge is here !
        (hand Each)
    })
    
    (rules 
        (start (place "Marker" "Hand" count:12))
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                   )  
               )
           ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
       )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (forEach Piece)
               )
           )
       )
        }
        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
   )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Adzua is a game played by men of the Jukun people in Nigeria. ")
        (aliases {"Ado"})
        (rules "5x6 board, rendered as holes in the ground. Twelve pieces per player. Players alternate turns placing a piece in one of the holes. When a player succeeds in making a line of three holes with their pieces, they capture one of the opponent's pieces.")
        (source "DLP evidence.")
        (id "1414")
        (version "1.3.4")
        (classification "board/space/line/reconstruction")
        (credit "Eric Piette")
        (origin  "This game was played in Nigeria, around 1931.")
        }
   )
    
    (graphics {
        (show Edges Hidden)
        (show Symbol "disc" Vertex (sites Board Vertex) fillColour:(colour White) edgeColour:(colour Black) scale:0.5)
    })
    
)
