(game "Rongmei Naga Capturing Game"
    (players 2)
    
    (equipment { 
        (board 
            (merge 
                { 
                (shift 0 2 (square 5 diagonals:Alternating)) 
                (wedge 3) 
                (shift 0 6 (rotate 180 (wedge 3))) 
                } 
            ) 
            use:Vertex
        ) 
        (piece "Marker" Each
            (or 
                ("HopCapture")
                ("StepToEmpty")
            )			
        )		
    }) 
    (rules 
        (start [
            {
            (place "Marker1" (sites Row 0))
            (place "Marker2" (sites Row 8))
            }
            |
            {
            (place "Marker1" (union {(sites Row 0) (sites Row 1)}))
            (place "Marker2" (union {(sites Row 8) (sites Row 7)}))
            }
            |
            {
            (place "Marker1" (union {(sites Row 0) (sites Row 1) (sites Row 2)}))
            (place "Marker2" (union {(sites Row 8) (sites Row 7) (sites Row 6)}))
            }
            |
            {
            (place "Marker1" (union {(sites Row 0) (sites Row 1) (sites Row 2) (sites Row 3)}))
            (place "Marker2" (union {(sites Row 8) (sites Row 7) (sites Row 6) (sites Row 5)}))
            }
        ])
        (play (forEach Piece))
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This capturing game was played by the Rongmei Naga people of Manipur, India in the early twentieth century.")
        (rules "5x5 intersecting lines, with diagonals in each quadrant. Two triangles, the apexes of which intersect with the square at the midpoint of opposite sides. One line bisecting two sides of the triangle.The player's pieces begin on opposite sides of the board. Players alternate turns moving a piece to an empty adjacent spot along the lines of the board. A piece may capture an opponent's piece by hopping over it to an empty adjacent spot immediately on the opposite side of the piece along the lines of the board. The player who captures all of the opponent's pieces wins. Pieces are not promoted when reaching the opposite side of the board from where they begin.
        ")
        (source "DLP evidence.")
        (id "1098")
        (version "1.3.4")
        (classification "board/war/reconstruction")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1911.")
        }
    )
    
    (graphics {
        (show Edges Diagonal (colour Hidden))
    })
)
