(define "HopMan" 
    (move
        Hop 
        (from #1) 
        (directions {FR FL}) 
        (between 
            if:("IsEnemyAt" (between)) 
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)))

//------------------------------------------------------------------------------

(game "Yup'ik Checkers"
    (players 2)  
    (equipment { 
        (board (square 8))
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })  
    
    (rules 
        (start [#])
        
        (play 
            (if "SameTurn"
                (if "IsUnpromoted"
                    ("HopMan" (last To) 
                        (then 
                            (if "JumpToPromotionZone" 
                                (move Promote (last To) (piece "DoubleCounter") Mover)
                                (if (can Move ("HopMan" (last To))) (moveAgain)) 
                            ) 
                        ) 
                    )
                    ("HopDiagonalSequenceCaptureAgain")
                )
                (priority {
                    (or  
                        (forEach Piece "Counter" 
                            ("HopMan" (from) 
                                (then 
                                    (if "JumpToPromotionZone"
                                        (move Promote (last To) (piece "DoubleCounter") Mover) 
                                        (if (can Move ("HopMan" (last To))) (moveAgain)) 
                                    ) 
                                ) 
                            ) 
                        )
                        (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture"))
                    )
                    
                    (or  
                        (forEach Piece "Counter" 
                            (move
                                Step 
                                (directions {FR FL}) 
                                (to if:(is Empty (to)))
                            ) 
                            (then 
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            )
                        )
                        (forEach Piece "DoubleCounter" "StepDiagonalToEmpty")
                    )
                })
            )
        ) 
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Yup'ik Checkers is a game played by the Yup'ik people of Nelson Island, Alaska. ")
        (rules "Played on an 8x8 board with each player having twelve pieces. Pieces move diagonally any number of spaces forward. Capturing must happen if it is possible. Multiple captures are allowed. Once pieces reach the opposite side of the board from their starting position, they are promoted and can move diagonally either forwards or backwards. The goal is to capture all of the opponent's pieces.
        ")
        (source "DLP evidence.")
        (id "1250")
        (version "1.3.4")
        (classification "board/war/leaping/diagonal/reconstruction")
        (credit "Eric Piette")
        (origin  "This game was played in Alaska, from around 1973 to 1974.")
        }
    )
    
    (graphics {
        (board Style Chess)
    })
    
)
