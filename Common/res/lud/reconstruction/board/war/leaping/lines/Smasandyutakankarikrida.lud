(game "Smasandyutakankarikrida"
    (players {(player N) (player S)}) 
    (equipment { 
        (board 
            (merge 
                { 
                (shift 0 2 (square 5 diagonals:Alternating) ) 
                (wedge 3) 
                (shift 0 6 (rotate 180 (wedge 3))) 
                } 
            ) 
            use:Vertex
        )
        (piece "Marker" Each
            (or {
                ("HopCapture") 
                (move
                    Step 
                    (directions {Rightward Leftward Forwards} of:All) 
                    (to if:(is Empty (to)))
                )
            })		
        )
    })  
    
    (rules 
        (start { 
            (place "Marker1" (union {(sites Row 0) (sites Row 1) (sites Row 2) (sites Row 3)}))
            (place "Marker2" (union {(sites Row 5) (sites Row 6) (sites Row 7) (sites Row 8)}))
        })
        (play (forEach Piece))
        (end (if (<= (count Pieces Next) 4) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Smasandyutakankarikrida is a game with leaping captures played in India. It was forbidden to be played in the home, and only was played in lonely places like a cemetery or forest.")
        (rules "5x5 intersecting lines, with diagonals in the four quadrants. Triangles on opposite sides of the board, with the apex intersecting with the midpoint of the side. A line from the apex bisecting the base, and a line bisecting this line and the opposite sides of the triangle. Sixteen pieces per player, which begin on opposite sides off the board. Pieces are captured by hopping over an adjacent opponent's piece to an empty spot immediately on the opposite side of the opponent's piece. The player who reduces their opponent to four pieces wins.")
        (source "DLP evidence.")
        (id "1459")
        (version "1.3.2")
        (classification "board/war/leaping/lines/reconstruction")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1871.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
)
