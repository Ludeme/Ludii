(game "Ijil Buga"
    (players 2) 
    (equipment { 
        (board 
            (merge {
                (rotate 45 (shift 1.5 8.2 (square 2 diagonals:Solid)))
                (shift 0.67 -1.32 (scale 0.66 (wedge 3)))
                (rectangle 9 5 diagonals:Alternating)
                (rotate 45 (shift 4.2 5.5 (square 2 diagonals:Solid)))
                (rotate 45 (shift 4.2 1.5 (square 2 diagonals:Solid)))
                (shift -1.99 1.34 (rotate -90 (scale 0.66 (wedge 3))))
                (shift -1.98 5.35 (rotate -90 (scale 0.66 (wedge 3))))
            })
            use:Vertex
        )
        (piece "Deer" P1 (and ("StepToEmpty") ("HopCapture")))
        (piece "Dog" P2 ("StepToEmpty"))
        (hand P2)
    }) 
    
    (rules 
        (start {
            (place "Deer1" (sites {"E11" "G9" "C9" "C5" "G5" "E3"}))
            (place "Dog2" (forEach (sites [#]) if:(not (is In (site) (expand (union (sites Top) (sites Bottom)) steps:2))))) // Some have to be on the board at the init.
            (place "Dog2" (handSite P2) count:[12..80]) // Some can be introduced later.
        })
        (play (forEach Piece))
        (end {
            (if (no Pieces P2) (result P1 Win))
            (if (no Moves P1) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Ijil Buga is a hunt game played in Mongolia. Said to be either a representation of deer hunting or a reflection of the sacred nature of deer, it is played only in the winter and spring months.")
        (rules "5x9 intersecting lines, with diagonals in each 3x3 square of the board. On one short side, a square with diagonals, with one corner as the midpoint of one of the sides of the main board. on the other short side, a triangle, with a line bisecting the base and another bisecting this line and intersecting with the other two sides of the triangle. The triangle's apex is the midpoint of the opposite side as the square. On the long sides, two identical squares which intersect with the third and seventh horizontal line of the main part of the board, on the right hand side of the square on the short side. Opposite these are two triangles, identical to the one on the short side. One player plays as six deer, which begin on the points where the small squares intersect with the main board. The other player plays with a number of dogs (between 12 and 80), at least some of which begin in the main part of the board, but some may be introduced later. Pieces move to an adjacent empty spot along the lines of the board. The deer may capture an adjacent dog by hopping over it to an empty space immediately on the opposite side of it along the lines of the board. the goal of the deer is to capture all of the dogs, the goal of the dogs is to block the deer from being able to move.
        ")
        (source "DLP evidence.")
        (id "1005")
        (version "1.3.4")
        (classification "board/hunt/reconstruction")
        (credit "Eric Piette")
        (origin "This game was played in Mongolia, from around 1988 to 1991.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
)
