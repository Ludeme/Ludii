(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Mbangbi"
    (players 2)
    (equipment {
        (mancalaBoard 2 5
            (track "Track" <Track> loop:True)
        )
        (regions P1 (sites {1..5}))
        (regions P2 (sites {6..10}))
        (map {(pair 1 0) (pair 2 11)})
        (piece "Seed" Shared)
        }
    )
    (rules

        (start (set Count 8 to:(sites Track) ))

        (play
            (move
                Select
                (from
                    (sites Mover)
                    if:(> (count at:(from)) 0)
                )
                (then
                    (sow
                        if:(or
                            (= (count at:(to)) 2)
                            (= (count at:(to)) 4)
                        )
                        apply:(fromTo
                            (from (to))
                            (to (mapEntry (mover)))
                            count:(count at:(to))
                        )
                    )
                )
            )
        )

        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1))
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Play Rules" <Track> args:{ <direction> }
    {
    (item "Sowing in CCW"   <"1,E,N,W">  "The players sow in counter clockwise.")*
    (item "Sowing in CW"   <"5,W,N,E">  "The players sow in clockwise.")
})

// TODO option with 4 players

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Mbangbi is a two-row mancala-style game from West Central Africa. It is typically played by boys. The game is played in holes dug into the ground.")
        (rules "2x5 board with two stores. Play begins with eight counters in each hole. When sowing, if the final counter makes two or four counters, these are taken. Can be played with 2 or 4 players.")
        (source "Steiber and Steiber 1938: 218.")
        (version "1.3.0")
        (classification "board/sow/two rows/reconstruction")
        (credit "Eric Piette")
        (origin  "This game was played in Cameroon, around 1938.")
        }
    )

    (graphics {
        (board Style Mancala)
    })

)
