(define "FromNextHand" 
    (from (sites Occupied by:Next container:(next))) 
)

(define "FromSelected" 
    (from (last To)) 
)

(define "HopAny" 
    (move
        Hop 
        (between if:(not (is Empty (between))))
        (to if:(is Empty (to)))
    )
)

//------------------------------------------------------------------------------

(game "Tic-Tac-Choose-Chess" 
    (players 2) 
    (equipment { 
        (board (square 3)) 
        (hand Each size:3)
        (piece "Queen" Each (or ("HopAny") (move Slide All)))
        (piece "King" Each (or ("HopAny") ("StepToEmpty")))
        (piece "Rook" Each (or ("HopAny") (move Slide Orthogonal)))
    }) 
    (rules 
        (start { 
            (place "Queen" "Hand")
            (place "King" "Hand")
            (place "Rook" "Hand")
        })
        
        phases:{
        (phase "Placement"
            (play
                (if (or (= (mover) (prev)) (= (count Moves) 0))
                    (move Select ("FromNextHand"))		// Nominate position opponent is allowed to move from
                    (move 
                        ("FromSelected") (to (sites Empty))		// Move nominated piece
                        (then 
                            (if (not ("HandEmpty" P1)) 
                                (moveAgain)		// We get to nominate for opponent now
                            )
                        )
                    )		
                )  	
            )
            (nextPhase ("HandEmpty" P1) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
        )
        }
        
        (end (if (is Line 3 Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (credit "Dennis Soemers")
        }
    )
)
