(game "Lau Kata Kati" 
    (players 2) 
    (equipment { 
        (board 
            (graph
                vertices:{ {0 0} {3 0} {6 0} {1 1} {3 1} {5 1} {2 2} {3 2} {4 2} {3 3} {2 4} {3 4} {4 4} {1 5} {3 5} {5 5} {0 6} {3 6} {6 6}}
                edges:{ {0 1} {0 3} {1 2} {1 4} {2 5} {3 4} {3 6} {4 5} {4 7} {5 8} {6 7} {6 9} {6 9} {7 8} {7 9} {8 9} {9 10} {9 11}
                {9 12} {10 11} {10 13} {11 12} {11 14} {12 15} {13 14} {13 16} {14 15} {14 17} {15 18} {16 17} {17 18}}
            )
            use:Vertex
        )
        (piece "Counter" Each 
            (or 
                ("HopCapture")
                ("StepToEmpty")
            )
        )
    }) 
    
    (rules 
        (start {
            (place "Counter1" (expand (sites Bottom) steps:2) )
            (place "Counter2" (expand (sites Top) steps:2))
        })
        
        (play (forEach Piece))
        
        (end <End>)
    )
)

//------------------------------------------------------------------------------

(option "End Rules" <End> args:{ <rules> }
    {
    (item "No Pieces" <(if (no Pieces Next) (result Next Loss))> "The player loses when he has no piece.")* 
    (item "No Moves" <("NoMoves" Loss)>        "A player loses when he can not move.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Lau Kata Kati is a leaping capture game similar to Alquerque observed during the nineteenth century in India, but which is probably much older.")
        (aliases {"Kowwu Dunki"})
        (rules "Play begins with each player's pieces on the pieces occupying the points of one of the triangles, leaving the shared apex empty. Pieces move to the next adjacent point connected by a line. Captures are made by hopping over an opponent's piece. The player who captures all of the opponent's pieces wins.")
        (source "Datta 1933: 168, Humphries 1906: 123.")
        (version "0.9.3")
        (classification "board/war/leaping")
        (credit "Eric Piette")
        (origin "This game was played in India, from around 1863CE to 1933CE.")
        }
    )
    
    (ai 
        "Lau Kata Kati_ai"
    )
    
)
