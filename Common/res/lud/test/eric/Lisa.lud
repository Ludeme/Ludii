(define "JumpSite" (+ 7 #1))

(define "EscapeAPiece" (move Remove (intersection (sites Occupied by:Mover) (sites Outer))))

(define "PlaceAPiece" 
    (if (not (is Empty (handSite Mover)))
        (move
            (from (handSite Mover))
            (to (intersection (sites Centre) (sites Empty)))
       )
   )		
)

(define "InnerRadial" 
    (sites {#1 (- #1 7) (- #1 14) (- #1 21)})
)

(define "Jump"
    (if (and (= (mover) (who at:#1)) (is Empty ("JumpSite" #1)))
        (if (< 2 (count Sites in:(intersection ("InnerRadial" #1) (sites Occupied by:Mover))))
            (move
                (from #1)
                (to ("JumpSite" #1))
           )
       )
   )		
)

(define "JumpMove"
    (forEach Site (sites {21..27})
        ("Jump" (site))	
   )
)

//------------------------------------------------------------------------------

(game "Lisa" 
    (players 2)
    (equipment {
        (board (concentric {7 7 7 7 0 7 7 7}) use:Vertex)
        (piece "Marker" Each 
            (move Step 
                (to
                    if:(and 
                        (>= (to) (* 7 (/ (from) 7))) // No backward move
                        (is Empty (to))
                   )
               )
           )
       )
        (hand Each)
    })
    (rules 
        (start {
            (place "Marker" "Hand" count:10)
        })
        (play
            (or {
                ("PlaceAPiece")
                ("EscapeAPiece")
                ("JumpMove")
                (forEach Piece)
            })
       )
        (end (if (<= (count Pieces Mover) 5) (result Mover Win)))
   )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info {
        
    })
    
    (graphics {
        (no Sunken)
    })
    
)
