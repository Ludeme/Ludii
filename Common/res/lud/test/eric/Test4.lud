(define "Deploy"
    (move Add (piece (id "Hex" Mover))
        (to
            //(union
                //    (sites Mover)
                (sites Around
                    (sites Occupied by:Mover component:"Hex" top:False)
                )
            //)
            if:(is Empty (to))
        )
        (then
            (add (piece (id "Disc" Mover))
                (to (last To))
                stack:True
            )
        )
    )
)

(define "Step"
    (do
        (if (= 0 (count MovesThisTurn))
            ("ResetMoveCount")
        )
        next:(move
            (from
                if:(=
                    (*
                        (from)
                        (if (< 0 (count MovesThisTurn)) 1 0)
                    )
                    (*
                        (last To)
                        (if (< 0 (count MovesThisTurn)) 1 0)
                    )
                )
            )
            (to
                ("EmptyInRange")
            )
            count:("StackSize" (from))
            stack:True
        )
    )
)

(define "ResetMoveCount"
    (set Var ("StackSize" (from))) //the nameless variable is "MovesLeft"
)

(define "DeductMoves"
    (set Var
        (-
            (var)
            ("StepsTaken")
        )
        #1
    )
)

(define "Capture"
    (do
        (if
            (= 0 (count MovesThisTurn))
            ("ResetMoveCount")
        )
        next:(move
            (from
                if:(=
                    (*
                        (from)
                        (if (< 0 (count MovesThisTurn)) 1 0)
                    )
                    (*
                        (last To)
                        (if (< 0 (count MovesThisTurn)) 1 0)
                    )
                )
            )
            (to
                ("CaptureRange")
                (apply
                    (remove (to) count:("StackSize" (to)))
                )
            )
            count:("StackSize" (from))
            stack:True
        )
    )
)

(define "StackSize"
    (count Stack
        at:#1
        if:(or
            (=
                (id "Disc" Next)
                (what at:(to) level:(level))
            )
            (=
                (id "Disc" Mover)
                (what at:(to) level:(level))
            )
        )
    )
)

(define "EmptyInRange"
    (if
        (is Odd ("StackSize" (from)))
        (sites Distance
            (step
                Forwards
                (to
                    if:(= 0 ("StackSize" (to)))
                )
            )
            from:(from)
            (range 1 ("StackSize" (from)))
        )
        (sites Distance
            (step
                Backwards
                (to
                    if:(= 0 ("StackSize" (to)))
                )
            )
            from:(from)
            (range 1 ("StackSize" (from)))
        )
    )
)

(define "StepsTaken"
    (count Steps
        (step
            (to
                if:(or
                    (= (last To) (to))
                    (= 0 ("StackSize" (to)))
                )
            )
        )
        (last From)
        (last To)
    )

)

(define "Promotes"
    (=
        (+
            (* 1 (% ("StackSize" #1) 2))
            (%
                (mover)
                (-
                    3
                    (% ("StackSize" #1) 2)
                )
            )
        )
        (*
            (who at:#1 level:0)
            (count Stack
                at:#1
                if:(or
                    (=
                        (id "Hex" Next)
                        (what at:(to) level:(level))
                    )
                    (=
                        (id "Hex" Mover)
                        (what at:(to) level:(level))
                    )
                )
            )
        )
    )
)

(define "Promote"
    (if
        ("Promotes" (last To))
        (add (piece (id "Disc" Mover))
            (to (last To))
            stack:True
        )
    )
)

(define "Reachable"
    (sites Distance
        (step
            (to
                if:(= 0 ("StackSize" (to)))
            )
        )
        from:(from)
        (range 1 #1)
    )
)

(define "CaptureRange"
    (if
        (> (var) 1)
        (intersection
            (sites Around
                (intersection
                    ("Reachable" (var)) //(var) er rangeleft
                    (sites Around
                        (sites Occupied by:Enemy component:"Disc")
                    )
                )
            )
            (sites Occupied by:Enemy component:"Disc")
        )
        (intersection
            (sites Around (from))
            (sites Occupied by:Enemy component:"Disc")
        )
    )
)

(game "Seesaw"
    (players {(player N) (player S)})
    (equipment
        {
        (board (rotate 30 (hex <Board>)))
        (piece "Disc" Each)
        (tile "Hex" Each)
        //(regions P1 (sites {0}))
        //(regions P2 (sites {(* <Board> (* 3 (- <Board> 1)))}))
        }
    )
    (rules
        (start {
            (place Stack "Hex1" 0)
            (place Stack "Disc1" 0)
            (place Stack "Hex2" (* <Board> (* 3 (- <Board> 1))))
            (place Stack "Disc2" (* <Board> (* 3 (- <Board> 1))))
        })
        (play
            (if
                (can Move
                    (forEach Piece "Disc"
                        ("Capture")
                        top:True
                    )
                )
                (forEach Piece "Disc"
                    ("Capture")
                    top:True
                    (then
                        ("DeductMoves"
                            (then
                                (if
                                    (< 0 (var)) //the nameless variable is "MovesLeft"
                                    (moveAgain)
                                    ("Promote")
                                )
                            )
                        )
                    )
                )
                (if
                    (< 0 (count MovesThisTurn))
                    (or
                        (forEach Piece "Disc"
                            ("Step")
                            top:True
                        )
                        (move Pass)
                        (then ("Promote"))
                    )
                    (or
                        (forEach Piece "Disc"
                            ("Step")
                            top:True
                            (then ("Promote"))
                        )
                        ("Deploy")
                    )
                )
            )
        )
        (end
            (if
                (no Moves Next)
                (result Next Loss)
            )
        )
    )
)

(option "Board Size" <Board> args:{ <size> }
    {
    (item "4"    <4>  "A base 4 board is currently selected")*
    (item "5"    <5>  "A base 5 board is currently selected")
    (item "6"    <6>  "A base 6 board is currently selected")
    }
)

//(< 0 (count Sites in:("EnemiesInRange")))

(metadata
    (graphics {
        (player Colour P1 (colour Cream))
        (player Colour P2 (colour Grey))
        (piece Colour P1 "Hex"
            fillColour:(colour 255 255 230 200)
            //strokeColour:(colour Black)
        )
        (piece Colour P2 "Hex"
            fillColour:(colour 150 150 150 200)
            //strokeColour:(colour Black)
        )
(region Colour (sites Board) (colour 250 210 148))    //255 230 150 255))
(board Colour InnerEdges (colour 178 151 100))        //130 120 90 255))
(board Colour OuterEdges (colour 178 151 100))        //130 120 90 255))
(stackType Default 0.7)
})
)
