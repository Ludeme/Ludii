(game "Graph Test - Union (UTTT)"
    (players 2)
    (equipment {
        (board
            (union (square 9) (scale 3 (square 3)))
            use:Cell
        )
        (piece "Disc"       P1)
        (piece "DiscLarge"  P1)
        (piece "Cross"      P2)
        (piece "CrossLarge" P2)

        (regions "SubGames"  (sites { 0..80}))
        (regions "SubGame0"  (sites { 0..2   9..11 18..20}))
        (regions "SubGame1"  (sites { 3..5  12..14 21..23}))
        (regions "SubGame2"  (sites { 6..8  15..17 24..26}))
        (regions "SubGame3"  (sites {27..29 36..38 45..47}))
        (regions "SubGame4"  (sites {30..32 39..41 48..50}))
        (regions "SubGame5"  (sites {33..35 42..44 51..53}))
        (regions "SubGame6"  (sites {54..56 63..65 72..74}))
        (regions "SubGame7"  (sites {57..59 66..68 75..77}))
        (regions "SubGame8"  (sites {60..62 69..71 78..80}))
        (regions "SuperGame" (sites {81..89}))
        (map
            "SubToSuper"
            {0..80}
            {
            81 81 81 82 82 82 83 83 83
            81 81 81 82 82 82 83 83 83
            81 81 81 82 82 82 83 83 83
            84 84 84 85 85 85 86 86 86
            84 84 84 85 85 85 86 86 86
            84 84 84 85 85 85 86 86 86
            87 87 87 88 88 88 89 89 89
            87 87 87 88 88 88 89 89 89
            87 87 87 88 88 88 89 89 89
            }
        )
        (map
            "SubToSub"
            {0..80}
            {
            0 1 2 0 1 2 0 1 2
            3 4 5 3 4 5 3 4 5
            6 7 8 6 7 8 6 7 8
            0 1 2 0 1 2 0 1 2
            3 4 5 3 4 5 3 4 5
            6 7 8 6 7 8 6 7 8
            0 1 2 0 1 2 0 1 2
            3 4 5 3 4 5 3 4 5
            6 7 8 6 7 8 6 7 8
            }
        )
        }
    )
    (rules
        (play
            (priority
                {
                // Move at any empty cell in the designated subgame
                // whose supergame is undecided
                (move
                    Add
                    (what (if (= (mover) P1) (id "Disc" P1) (id "Cross" P2)))
                    (to
                        (if (= 0 (mapEntry "SubToSub" (last To)))
                            (sites "SubGame0")
                            (if (= 1 (mapEntry "SubToSub" (last To)))
                                (sites "SubGame1")
                                (if (= 2 (mapEntry "SubToSub" (last To)))
                                    (sites "SubGame2")
                                    (if (= 3 (mapEntry "SubToSub" (last To)))
                                        (sites "SubGame3")
                                        (if (= 4 (mapEntry "SubToSub" (last To)))
                                            (sites "SubGame4")
                                            (if (= 5 (mapEntry "SubToSub" (last To)))
                                                (sites "SubGame5")
                                                (if (= 6 (mapEntry "SubToSub" (last To)))
                                                    (sites "SubGame6")
                                                    (if (= 7 (mapEntry "SubToSub" (last To)))
                                                        (sites "SubGame7")
                                                        (if (= 8 (mapEntry "SubToSub" (last To)))
                                                            (sites "SubGame8")
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                        if:(and
                            (is Empty (to))
                            (is Empty (mapEntry "SubToSuper" (to)))
                        )
                    )
                )

                // Move at any empty cell whose supergame is undecided
                (move Add
                    (what (if (= (mover) P1) (id "Disc1") (id "Cross2")))
                    (to
                        (intersection (sites "SubGames") (sites Empty))
                        if:(and (is Empty (to)) (is Empty (mapEntry "SubToSuper" (to))))
                    )
                )
                }
                (then
                    (if (is Line 3)  // add a piece in the supergame
                        (add
                            (what (if (= (mover) P1) (id "DiscLarge1") (id "CrossLarge2")) )
                            (to (mapEntry "SubToSuper" (last To)))
                        )
                    )
                )
            )
        )
        (end
            (if
                (is Line 3 throughAny:(sites "SuperGame") Mover)
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Ultimate Tic-Tac-Toe is a board game composed of nine Tic-Tac-Toe boards arranged in a 3x3 grid.")
        (aliases {"Super Tic-Tac-Toe" "Strategic Tic-Tac-Toe" "Meta Tic-Tac-Toe" "Tic-Tac-Tic-Tac-Toe-Toe" "(Tic-Tac-Toe)^2"})
        (rules "Each small 3x3 Tic-Tac-Toe board is referred to as a local board, and the larger 3x3 board is referred to as the global board. The game starts with X playing wherever they want in any of the 81 empty spots. This move 'sends' their opponent to its relative location. For example, if X played in the top right square of their local board, then O needs to play next in the local board at the top right of the global board. O can then play in any one of the nine available spots in that local board, each move sending X to a different local board. If a move is played so that it is to win a local board by the rules of normal Tic-Tac-Toe, then the entire local board is marked as a victory for the player in the global board. Once a local board is won by a player or it is filled completely, no more moves may be played in that board. If a player is sent to such a board, then that player may play in any other board. Game play ends when either a player wins the global board or there are no legal moves remaining, in which case the game is a draw.")
        (source "https://en.wikipedia.org/wiki/Ultimate_tic-tac-toe")
        (version "0.9.3")
        (classification "board/space/line")
        (credit "Cameron Browne 16/5/2020")
        }
    )
    (graphics
        {
        (board Style UltimateTicTacToe)
        (piece Scale "Disc"       0.8)
        (piece Scale "Cross"      0.8)
        (piece Scale "DiscLarge"  2.5)
        (piece Scale "CrossLarge" 2.6)
        }
    )
)
