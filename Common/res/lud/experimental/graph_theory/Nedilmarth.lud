(game "Nedilmarth" 
    (players 2) 
    (equipment { 
        (board <Board:type> use:Edge) 
        (piece "Marker" Neutral) 
    }) 
    (rules
        (start
            <Board:start>
        )
        
        (play
            (move Remove
                (sites Occupied by:Neutral on:Edge)
            )
        )
        (end  
            (if (is Tree Neutral) (result Mover Win))
        )  
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> <start> }
    {
    (item "Graph1"    
        <
        (merge (scale 0.707 (concentric {1 8})) (shift -.5 -.5 (square 2))) 
        > 
        <
        {
        (set Neutral Edge (sites Board Edge))      
        }
        >  
        "The game is played on the Graph1."
    )* 
    
    (item "Graph2"    
        <
        (graph
            vertices:{ {4 0} {12 0} {5 3} {8 4} {11 3} {4 6} {6 7} {8 6} {10 7} {12 6} 
            {0 10} {3 9} {7 9} {9 9} {13 9} {16 10} {5 11} {11 11} {8 13} {8 16}}
            edges:{{0 1} {0 2} {0 10} {1 4} {1 15} {2 3} {2 5} {3 4} {3 7} {4 9} {5 6} {5 11}
            {10 11} {11 16} {6 12} {6 7} {7 8} {8 9} {9 14} {14 15} {8 13} {13 12} {12 16} {13 17}
            {14 17} {16 18} {18 17} {18 19} {10 19} {15 19}}
        )
        > 
        <
        {
        (set Neutral Edge (sites Board Edge))      
        }
        >  
        "The game is played on the Graph2."
    ) 
    
    (item "Graph3"    
        <
        (dual (rotate 45 (square 7))) 
        > 
        <
        {
        (set Neutral Edge (sites Board Edge))      
        }
        >  
        "The game is played on the Graph3."
    ) 
    
})

//-----------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Nedilmarth is a graph theory game. It is a game for two players on an undirected cyclic graph.")
        (rules "The board graph is initially coloured with a neutral colour. Players take turns removing colour from a coloured edge. The game is won by the player who removes the last cycle from the coloured graph.")
        (id "701")
        (version "1.3.12")
        (classification "experimental/graph theory")
        (author "Tahmina Begum")
        (credit "Tahmina Begum 25-05-2020")
        (date "25-05-2020")
        }
    )
    
    (graphics {
        (player Colour Neutral (colour Green))
        (player Colour P1 (colour Blue))
        (player Colour P2 (colour Red))
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges ThinDotted (colour 127 127 127))
    })
    
    (ai
        "Nedilmarth_ai"
    )
)
