(game "Ilpion"    
    (players <Version:numPlayers>)  
    (equipment { 
        (board <Board:type> use:Vertex)
        (piece "Disc" Each) 
        (piece "Marker" Shared)          
        }
    )  
    
    (rules
        (start
            <Play:start>
        )
        
        (play            
            <Play:move>
        )
        (end
            (if (no Moves Mover)	
                (byScore)
            )
        )
    )
)
//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> }
    {
    (item "Graph1"             
        <
        (graph
            vertices:{ 
            {1 0.5} {3 0} {5 0} {6 0} {7 0} {8 0} {10 0} {11 0} {12 3} {0 2} {3 2} {5 2} 
            {8 2} {8.5 2} {2 3} {7 3} {1 4} {12 4} {2 5} {5 5} {10 5} {10 8} {2 7} {6 7} 
            {8 7} {0 10} {2 9} {5 9} {10 9} {1 10} {5 14} {10 10} {1 12} {4 12} {7 14} 
            {9 12}}
            edges:{ 
            {0 10} {1 10} {2 11} {3 11} {4 12} {5 12} {6 13} {7 13} {8 13} {10 19} {16 9} {9 10} 
            {11 19} {15 12} {12 20} {13 20} {13 17} {14 18} {18 19} {19 23} {20 23} {20 24} {21 24}
            {23 26} {22 26} {26 33} {26 32} {32 29} {29 25} {23 27} {27 34} {30 34} {27 35} {31 35} {27 28} }
        )
        > 
        
        ""
    )* 
    
    (item "Graph2"             
        <
        (graph
            vertices:{ 
            {1 0} {3 0} {5 0} {6 0} {7 0} {8 0} {10 0} {11 0} {12 0} {0 2} {3 2} {5 2}
            {8 2} {11 2} {1 1} {7 3} {1 4} {12 4} {2 5} {5 5} {8 5} {10 5} {2 7} {6 7}
            {10 7} {1 8} {4 9} {7 9} {10 9} {1 10} {5 10} {10 10} {1 12} {4 12} {7 12} 
            {9 12}}
            edges:{ 
            {0 10} {1 10} {2 11} {3 11} {4 12} {5 12} {6 13} {7 13} {8 13} {10 19} 
            {16 10} {9 10} {11 19} {15 20} {12 20} {13 20} {13 17} {14 10} {18 23} 
            {19 23} {20 23} {20 24} {21 20} {23 26} {22 26} {26 33} {26 32} {26 29}
            {26 25} {23 27} {27 34} {30 27} {27 35} {31 27} {27 28} }
        )
        >  
        
        ""
    ) 
    
})

(option "Play Rules" <Play> args:{ <start> <move> }
    {
    (item "Centroid" 
        <
        {
        (set Shared Edge (sites Board Edge))                                
        }
        >     
        <
        (forEach Site
            (sites Board)
            (if (> (count Sites in:(sites Incident Edge of:Vertex at:(to) Shared)) 0)
                (move Add (to (intersection (sites Empty) (sites {(to)}))) 
                    (then
                        (and
                            (if (is TreeCentre Shared) (addScore Mover 1))
                            (forEach Site
                                (sites Incident Edge of:Vertex at:(last To))
                                (remove Edge (to))						           
                            )
                        )
                    )
                )	            		
            )
        )
        > 
        "The scoring is based on centroid."
    )*
    
    (item "Degree" 
        <
        {
        (set Shared Edge (sites Board Edge))                                
        }
        >     
        <
        (forEach Site
            (sites Board)
            (if (> (count Sites in:(sites Incident Edge of:Vertex at:(to) Shared)) 0)
                (move Add (to (intersection (sites Empty) (sites {(to)}))) 
                    
                    (then
                        (and
                            (if (is TreeCentre Shared)
                            (addScore Mover (count Sites in:(sites Incident Edge of:Vertex at:(last To) Shared))))
                            (forEach Site
                                (sites Incident Edge of:Vertex at:(last To))
                                (remove Edge (to))						           
                            )
                        )
                    )
                )	            		
                
            )
        )
        > 
        "The scoring is based on degree of a centroid."
    ) 
    }
)

(option "Players" <Version> args:{ <numPlayers>}
    {
    (item "2"  
        <2> 
        "The version of the game played with 2 players."
    )*  
    
    (item "3"  
        <3>           
        "The version of the game played with 3 players."
    ) 
    
    (item "4"  
        <4>         
        "The version of the game played with 4 players."
    )     
})

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Ilpion is a graph theory game that uses a centroid decomposition method. It is played on an undirected and unweighted tree. The board graph is initially coloured with a common colour.")
        (rules "Players take turns adding his friendly colour in an uncoloured vertex. The aim of each player is to colour the centre vertex of the coloured tree. It is a scoring based game, and it has two versions. At the centroid version, each player gets one point for correctly coloured a tree centre. In the second version is the degree version, where a player gets a score, which is equal to the degree of his last coloured tree centre. For both versions, all the incident edges of last move are uncoloured. So the tree is divided into subtrees. The game ends when there is no coloured tree available. The player with the highest score wins.")
        (id "694")
        (version "1.3.3")
        (classification "experimental/graph theory")
        (author "Tahmina Begum")
        (credit "Tahmina Begum 01-06-2020")
        (date "01-06-2020")
        }
    )
    
    (graphics {
        (player Colour Shared (colour lightBlue))
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Green))
        (player Colour P3 (colour Yellow)) 
        (player Colour P4 (colour Blue))        
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        
        (show Edges ThinDotted (colour 127 127 127))
    })
    (ai
        "Ilpion_ai"
    )
)

