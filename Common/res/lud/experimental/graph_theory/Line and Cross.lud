(game "Line and Cross" 
    (players <Version:numPlayers>)  
    (equipment 
        { 
        (board <Board:type> use:Edge)
        (piece "Marker" Shared)
        }
    )    
    
    (rules     	 
        (start
            <Board:start>
        )
        
        (play	         
            (move
                Remove (sites Occupied by:Shared)
                (then
                    (and
                        (remove Edge (last To))                	                	 	 	
                        (forEach Site
                            (sites Crossing at:(last To) All)
                            (and	
                                (remove Edge (to))
                                (addScore Mover 1)
                            )	                			
                        )                	 	
                    )
                )
            )	            
        )
        (end
            (if (no Moves Next)	
                (byScore)
            )
        )
    )
)
//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> <start>}
    {
    (item "Graph1"             
        <
        (graph
            vertices:{{2 0} {1 1} {4 1} {7 1} {10 1} {0 2} {2 2} {3 2} {5 2} {9 2}
            {0 3} {6 3} {7 3} {9 3} {10 3} {11 3} {2 4} {4 4} {6 4} {7 4} {8 4}  
            {10 4} {12 4} {7 5} {9 5} {11 5} {13 5} {1 6} {2 6} {4 6} {6 6} {8 6} {10 6} {12 6} {14 6}}
            edges:{ {0 6} {5 7} {1 27} {10 11} {2 17} {16 28} {8 9} {3 12} {17 19} {18 30} {29 30} {23 24} 
            {20 31} {13 15} {4 14} {21 32} {25 26} {22 33} {33 34} {1 2} {6 16} {16 17} {27 29} {11 18} 
            {12 19} {19 23} {19 20} {21 22} {15 25} {13 24}}
        )
        >  
        <
        {
        (set Shared Edge (sites Board Edge))                            
        }
        >                
        ""
    )* 
    
    (item "Graph2"             
        <
        (graph
            vertices:{ 
            {2 0} {1 1} {3 1} {5 1} {1 2} {3 2} {4 2} {7 3} {0 3} {1 3} {3 3} {4 3} 
            {6 3} {8 3} {0 5} {1 6} {3 6} {4 5} {5 8} {6 6} {6 4} {8 6} {1 8} {3 8} {0 6} {1 7} {2 7} {4 7}}
            edges:{ 
            {0 4} {1 5} {2 10} {6 7} {3 18} {9 14} {8 16} {12 15} {11 19} {13 17} {19 14} 
            {20 21} {22 23} {24 25} {26 27} {0 1} {9 10} {14 24} {15 25} {22 26} {15 26} {16 26} {15 24} {4 17} 
            {7 19} {11 20}}
        )
        >  
        <
        {
        (set Shared Edge (sites Board Edge))                             
        }
        >              
        ""
    ) 
    
    (item "Graph3"             
        <
        (graph
            vertices:{ 
            {2 0} {3 0} {1 1} {1 2} {0 2} {0 4} {1 3} {1 5} {2 4} {2 2} {4 2} {3 3} {3 1} {5 1}
            {6 2} {6 1} {8 1} {7 3} {9 3} {9 1} {8 4} {8 2} {10 3} {11 2} {11 1} {13 1} {13 3} {14 2}
            {10 3} {10 5} {8 5} {9 6} {12 3} {11 4} {13 6} {12 4} {14 6} {11 6} {5 7} {11 7} {8 7} {8 6}
            {7 6} {7 8} {4 4} {5 3} {6 4} {5 5} {7 5} {6 6} {1 7} {3 7} {2 6} {4 6} {3 5} {13 4}}
            edges:{ 
            {0 1} {0 2} {2 3} {1 3} {4 5} {5 8} {6 7} {9 10} {11 12} {12 13} {14 15} {15 16} {17 18}
            {18 19} {20 21} {21 22} {23 27} {24 25} {25 26} {28 29} {29 30} {31 32} {33 34} {35 36} {37 55}
            {42 43} {38 39} {40 41} {44 46} {46 49} {45 47} {47 48} {52 53} {50 51} {51 54} {8 11} {11 44}
            {8 54} {44 54} {42 41} {32 26} {17 48}}
        )
        >  
        <
        {
        (set Shared Edge (sites Board Edge))                             
        }
        >            
        ""
    )     
})

(option "Players" <Version> args:{ <numPlayers>}
    {
    (item "2"  
        <2> 
        "The version of the game played with 2 players."
    )*  
    
    (item "3"  
        <3>           
        "The version of the game played with 3 players."
    ) 
    
    (item "4"  
        <4>         
        "The version of the game played with 4 players."
    )     
})
//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Line and Cross is a multi-player mathematical game.")
        (rules "Initially, there is a number of edges in a plane, where some of them cross each other. At each turn, a player selects an edge to remove it and all of its intersecting edges. The number of edges with which a selected edge intersects is added to the player's score. When there are no moves left, the game is over. Finally, the player with the highest score wins.")
        (id "680")
        (version "1.3.3")
        (classification "experimental/graph theory")
        (author "Tahmina Begum")
        (credit "Tahmina Begum 22-06-2020")
        (date "22-06-2020")
        }
    )
    
    (graphics {
        (player Colour Shared (colour 255 200 0))
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges ThinDotted (colour 127 127 127))
        
    })
    (ai
        "Line and Cross_ai"
    )
)

