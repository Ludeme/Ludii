(game "Nein Ari" 
    (players 1)
    (equipment { 
        (board <Board:type> use:Edge)
    }) 
    
    (rules  	 
        (start 
            (set P1 Edge (sites Board Edge))
        )
        
        (play 
            (move Remove
                (sites Occupied by:Mover on:Edge) 
            )
        )
        (end   
            <Version:result>
        )
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> <start> }
    {
    (item "Graph1"    
        <
        (graph
            vertices:{ {0 0} {5 0} {10 0} {0 5} {5 5} {10 5}}
            edges:{ {0 1} {0 2} {0 3} {0 4} {0 5} {1 2} {1 3} {1 4} {1 5} 
            {2 3} {2 4} {2 5} {3 4} {3 5} {4 5}}
        )
        >  
        "The game is played on the Graph1."
    )* 
    
    (item "Graph2"    
        <
        (graph
            vertices:{{0 0} {5 0} {10 0} {0 5} {5 5} {10 5} {0 10} {5 10} {10 10}}
            edges:{ {0 1} {0 2} {0 3} {0 4} {0 5} {0 6} {0 7} {1 2} {1 3} {1 4} {1 5} {1 6} {1 8} {2 3} {2 4} {2 5} 
            {2 7} {2 8} {3 4} {3 6} {3 7} {3 8} {4 5} {4 6} {4 7} {4 8} {5 6} {5 8} {5 7} {6 7} {6 8} {7 8}}
        )
        >  
        "The game is played on the Graph2."
    ) 
    
    (item "Graph3"    
        <
        (graph
            vertices:{{0 0} {7 1} {0 4} {3 1} {6 5} {0 8} {10 5} {6 8} {10 11} {11 2} {10 10} {8 13} {13 14} {15 9}}
            edges:{ 
            {0 1} {0 2} {0 3} {0 4} {0 5} {0 6} {0 7} {0 8} {0 9} {0 10} {0 11} {0 12} {0 13}
            {1 2} {1 3} {1 4} {1 5} {1 6} {1 7} {1 8} {1 9} {1 10} {1 11} {1 12} {1 13}
            {2 3} {2 4} {2 5} {2 6} {2 7} {2 8} {2 9} {2 10} {2 11} {2 12} {2 13}
            {3 4} {3 5} {3 6} {3 7} {3 8} {3 9} {3 10} {3 11} {3 12} {3 13}
            {4 5} {4 6} {4 7} {4 8} {4 9} {4 10} {4 11} {4 12} {4 13}
            {5 6} {5 7} {5 8} {5 9} {5 10} {5 11} {5 12} {5 13}
            {6 7} {6 8} {6 9} {6 10} {6 11} {6 12} {6 13}
            {7 8} {7 9} {7 10} {7 11} {7 12} {7 13}
            {8 9} {8 10} {8 11} {8 12} {8 13}
            {9 10} {9 11} {9 12} {9 13}
            {10 11} {10 12} {10 13}
            {11 12} {11 13} {12 13}
            }
        )
        >  
        "The game is played on the Graph3."
    ) 
})

(option "Version" <Version> args:{ <result>}
    {
    (item "General" 
        <
        (if (is RegularGraph Mover) (result Mover Win))
        > 
        "General version"
    )* 
    
    (item "Parameter-k" 
        <
        (if (is RegularGraph Mover k:3) (result Mover Win))
        >   
        "Parameter-k version"
    )
})

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Nein Ari is a puzzle based on the graph theory concept of k-regular graphs. A k-regular graph is a graph where every vertex has exactly k neighbours (i.e. every vertex has a degree of k). 
        ")
        (rules "The graph is initially coloured. The goal of the general version of the puzzle is to uncolour edges until the coloured graph is a regular graph. The goal of the parameter-k version of the puzzle is to uncolour edges until the coloured graph is a k-regular graph.
        ")
        (id "702")
        (version "1.3.5")
        (classification "experimental/graph theory")
        (author "Tahmina Begum")
        (credit "Tahmina Begum 25-05-2020")
        (date "25-05-2020")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Green))
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges ThinDotted (colour 127 127 127))
    })
    
    (ai
        "Nein Ari_ai"
    )
)

