(game "CrossGeo" 
    (players 2)
    (equipment { 
        (board <Board:type> use:Edge)
        (piece "Disc" Each) 
        (piece "Marker" Shared) 
    }) 
    
    (rules  	 
        (start
            <Version:start>
        )
        
        (play	   
            <Version:play>	   
        )
        (end      
            (if (no Moves Mover) (result Next Win))   
        )
    )
)
//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type>}
    {
    (item "Graph1"    
        <
        (graph
            vertices:{ 
            {2 0} {3 0} {1 1} {1 2} {0 2} {0 4} {1 3} {1 5} {2 4} {2 2} {4 2} {3 3} {3 1} {5 1}
            {6 2} {6 1} {8 1} {7 3} {9 3} {9 1} {8 4} {8 2} {10 3} {11 2} {11 1} {13 1} {13 3} {14 2}
            {10 3} {10 5} {8 5} {9 6} {12 3} {11 4} {13 6} {12 4} {14 6} {11 6} {5 7} {11 7} {8 7} {8 6}
            {7 6} {7 8} {4 4} {5 3} {6 4} {5 5} {7 5} {6 6} {1 7} {3 7} {2 6} {4 6} {3 5} {13 4}}
            edges:{ 
            {0 1} {0 2} {2 3} {1 3} {4 5} {5 8} {6 7} {9 10} {11 12} {12 13} {14 15} {15 16} {17 18}
            {18 19} {20 21} {21 22} {23 27} {24 25} {25 26} {28 29} {29 30} {31 32} {33 34} {35 36} {37 55}
            {42 43} {38 39} {40 41} {44 46} {46 49} {45 47} {47 48} {52 53} {50 51} {51 54}}
        )  
        >   
        ""
    )* 
    
    (item "Graph2"    
        <
        (graph
            vertices:{
            {6 0} {3 1} {5 1} {8 1} {4 4} {7 4} {10 4} {0 6} {5 6} {8 6} {10 6}
            {12 6} {2 7} {4 7} {7 7} {10 8} {14 8} {3 10} {7 10} {1 11} {4 12} 
            {7 12} {10 11} {10 13} {12 12} {3 5} {6 7}}     
            edges:{
            {0 2} {1 4} {2 4} {4 8} {5 8} {8 14} {8 13} {13 17} {12 17} 
            {12 7} {17 19} {17 20} {13 18} {18 21} {18 22} {22 23} {22 24} {22 15} 
            {9 15} {10 15} {15 11} {11 16} {6 11} {3 6} {1 2} {0 3} {4 5} {7 13} 
            {19 20} {13 21} {23 24} {9 10} {15 16} {7 1} {14 9} {5 6} {2 6} {25 3}
            {5 25} {7 26} {20 25} {24 23} {26 7} {26 9} {26 17} {14 23} {14 24} {14 16} }
        )
        > 
        
        ""
    )
    (item "Graph3"    
        <
        (graph
            vertices:{
            {6 0} {3 1} {5 1} {8 1} {4 4} {17 4} {10 4} {0 6} {5 6} {8 6} {10 6}
            {12 6} {2 7} {4 7} {7 17} {10 8} {14 8} {3 1} {7 1} {1 11} {4 12} 
            {7 12} {10 11} {10 13} {12 12} {3 5} {6 7}}     
            edges:{
            {0 2} {1 4} {2 4} {4 8} {5 8} {8 14} {8 13} {13 17} {12 17} 
            {12 7} {17 19} {17 20} {13 18} {18 21} {18 22} {22 23} {22 24} {22 15} 
            {9 15} {10 15} {15 11} {11 16} {6 11} {3 6} {1 2} {0 3} {4 5} {7 13} 
            {19 20} {13 21} {23 24} {9 10} {15 16} {7 1} {14 9} {5 6} {2 6} {25 3}
            {5 25} {7 26} {20 25} {24 23} {26 7} {26 9} {26 17} {14 23} {14 24} {14 16} }
        )
        >
        ""
    )  
})
//------------------------------------------------------------------------------

(option "Version" <Version> args:{ <start> <play> }
    {
    (item "CrossGeo" 
        <
        {
        (set Shared Edge (sites Board Edge))       
        }
        > 
        <
        (move
            Remove (sites Occupied by:Shared)
            (then
                (forEach Site
                    (sites Crossing at:(last To) All)
                    (remove Edge (to))
                )
            )
        )	
        > 
        "The players play on CrossGeo version."
    )* 
    (item "CrossGeoR-B"
        <
        {
        (set P1 Edge (sites {0..9})) 
        (set P2 Edge (difference (sites Board Edge) (sites {0..9})))       
        }
        > 
        <(move
            Remove (sites Occupied by:Mover)
            (then
                (forEach Site
                    (sites Crossing at:(last To) All)
                    (remove Edge (to))
                )
            )
        )
        >  
        "The players play on CrossGeoR-B version."
    ) 
    (item "CrossGeoR-G-B"
        <
        {
        (set P1 Edge (sites {0..5 13..20})) 
        (set P2 Edge (sites {9..12 23..30})) 
        (set Shared Edge (difference (sites Board Edge) (union (sites {0..5 13..20}) (sites {9..12 23..30}))))           
        }
        > 
        <
        (move
            Remove (difference (sites Occupied by:All) (sites Occupied by:Next))
            (then
                (forEach Site
                    (sites Crossing at:(last To) All)
                    (remove Edge (to))
                )
            )
        )	
        >  
        "The players play on CrossGeoR-G-B version."
    ) 
})
//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "The game CrossGeo is a 2-player mathematical game. The game has three different versions: CrossGeo, CrossGeoR-B, and CrossGeoR-G-B. CrossGeo is the basic version, which can be played on graphs with onlu green (shared) edges.")
        (rules "The game CrossGeo can be played on graphs with any number of edges in a geometric plane, where there are some edges crossing with other edges. The edge colours are related to the playing rules of each version. For the basic version, CrossGeo, all edges have the same colour; green. In each turn, a player selects an edge to remove it, and all its intersecting edges. The player who removes the last edge is the winner of the game. The second version is the Red-Blue version; CrossGeoR-B. There are two players, one red and one blue. Players can only select the edges of their own colours. In each turn, a player selects an edge to remove it, and all its intersecting lines (of any colour). The last version is CrossGeoR-G-B; there are sets of red, green, and blue edges, with some edges crossing. Players can select the edges of their own colours or of the green colour. Importantly, the green edges are used as shared edges between both players. At each turn, a player selects an edge to remove it and all its intersecting lines (of any colour). For all the versions of CrossGeo, if the current player has no move, then the opposing player wins. ")
        (id "682")
        (version "1.3.12")
        (classification "experimental/graph theory")
        (author "Tahmina Begum")
        (credit "Tahmina Begum 22-06-2020")
        (date "22-06-2020")
        }
    )
    
    (graphics {
        (player Colour Shared (colour Green))
        (player Colour P1 (colour Red)) 
        (player Colour P2 (colour Blue))
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges ThinDotted (colour 127 127 127))
    })
    (ai
        "CrossGeo_ai"
    )
)

