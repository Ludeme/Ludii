(game "LastEdge" 
    (players 2)
    (equipment { 
        (board <Board:type> use:Vertex)  
        (piece "Disc" Each) 
        (piece "Marker" Shared) 
    }) 
    
    (rules
        (start
            <Version:start>
        ) 
        
        (play   
            <Version:play>
        )
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> <start> }
    {
    (item "Graph1"    
        <
        (graph
            vertices:{ 
            {0 0} {0 1} {0 2} {0 3} {0 4} {1 0} {1 1} {1 2} {1 3} {1 4} 
            {2 0} {2 1} {2 2} {2 3} {2 4} {3 0} {3 1} {3 2} {3 3} {3 4} 
            {4 0} {4 1} {4 2} {4 3} {4 4}}
            edges:{ 
            {0 1} {0 5} {1 6} {1 2} {2 7} {2 3} {3 8} {3 4} {4 9} {5 6} 
            {5 10} {6 11} {6 7} {7 12} {7 8} {8 13} {8 9} {9 14} {10 11} 
            {11 12} {12 13} {13 14} {10 15} {11 16} {12 17} {13 18} {14 19} 
            {15 16} {15 20} {16 21} {20 21} {16 17} {17 22} {21 22} {17 18} 
            {18 23} {22 23} {18 19} {19 24} {23 24}}
        )
        >    
        "The game is played on the Graph1."
    )* 
    
    (item "Graph2"    
        <
        (graph
            vertices:{ 
            {1 0} {3 0} {5 0} {6 0} {7 0} {8 0} {10 0} {11 0} {12 0} {0 2} {3 2} {5 2} 
            {8 2} {11 2} {2 3} {7 3} {1 4} {12 4} {2 5} {5 5} {8 5} {10 5} {2 7} {6 7} 
            {10 7}{1 8} {4 9} {7 9} {10 9} {1 10} {5 10} {10 10} {1 12} {4 12} {7 12} 
            {9 12}}
            edges:{ 
            {0 10} {1 10} {2 11} {3 11} {4 12} {5 12} {6 13} {7 13} {8 13} {10 19} {16 10} 
            {9 10} {11 19} {15 20} {12 20} {13 20} {13 17} {14 10} {18 23} {19 23} {20 23}
            {20 24} {21 20} {23 26} {22 26} {26 33} {26 32} {26 29} {26 25} {23 27} {27 34}
            {30 27} {27 35} {31 27} {27 28} {24 33} {2 3} {30 35} {34 31} {10 18} {28 34} 
            {6 20} {27 24} {21 17} {23 31} {9 25} {20 31} {15 30} {32 22} {23 25} {19 26}
            {18 19} {0 16} {9 1} {1 11} {2 15} {5 11} {4 19} {12 21} {17 8}}
        )
        >      
        "The game is played on the Graph2."
    ) 
})

//------------------------------------------------------------------------------

(option "Version" <Version> args:{ <start> <play> }
    {
    (item "LastEdge" 
        <
        {
        (set Shared Edge (sites Board Edge))       
        }
        > 
        <
        (forEach Site
            (sites Board)
            (if (> (count Sites in:(sites Incident Edge of:Vertex at:(to) Shared)) 0)
                (move Add (to (intersection (sites Empty) (sites {(to)})))
                    (then
                        (forEach Site
                            (sites Incident Edge of:Vertex at:(last To))
                            (remove Edge (to))
                        )
                    )
                )
            )
        )
        > 
        "The players play on LastEdge version."
    )* 
    (item "LastEdgeR-B"
        <
        {
        (set P1 Edge (sites {0..9 34 35 36})) 
        (set P2 Edge (difference (sites Board Edge) (sites {0..9 34 35 36})))       
        }
        > 
        <
        (forEach Site
            (sites Board)
            (if (< 0 (count Sites in:(sites Incident Edge of:Vertex at:(to) Mover)))
                (move Add (to (intersection (sites Empty) (sites {(to)})))
                    (then
                        (forEach Site
                            (sites Incident Edge of:Vertex at:(last To))
                            (remove Edge (to))
                        )
                    )
                )
            )
        )
        >  
        "The players play on LastEdgeR-B version."
    ) 
    (item "LastEdgeR-G-B"
        <
        {
        (set P1 Edge (sites {0..5 13..20})) 
        (set P2 Edge (sites {9..12 23..30})) 
        (set Shared Edge (difference (sites Board Edge) (union (sites {0..5 13..20}) (sites {9..12 23..30}))))           
        }
        > 
        <
        (forEach Site
            (sites Board)
            (if (> (count Sites in:(difference (sites Incident Edge of:Vertex at:(to) All) (sites Incident Edge of:Vertex at:(to) Next))) 0)
                (move Add (to (intersection (sites Empty) (sites {(to)})))
                    (then
                        (forEach Site
                            (sites Incident Edge of:Vertex at:(last To))
                            (remove Edge (to))
                        )
                    )
                )
            )
        )	
        >  
        "The players play on LastEdgeR-G-B version."
    ) 
})

//------------------------------------------------------------------------------

(metadata
    
(info
{
(description "The game LastEdge is a 2-player mathematical game. The game has three different versions: LastEdge, LastEdgeR-B, and LastEdgeR-G-B. The LastEdge is the basic version which can be played on some green (shared) edges.")
(rules "The game LastEdge can be played in an undirected and unweighted graph. The edge colours are related to the playing rules of each version. For the basic version, LastEdge all the edges are in the same colour (green). At each turn, a player selects a vertex, which contains at least one green edge and after each turn, all the edges of the selected vertex are removed. The game is over when there are no green edges in the graph. The second version is the  Red blue version (LastEdgeR-B). There are one red player and one blue player. The players can select only those vertices, that contain at least one friendly edge. After each turn, all the edges (any colour) of the selected vertex are removed. The last version is LastEdgeR-G-B; there is a graph with red, green, and blue edges. The players can select a vertex, which has at least one friendly colour or shared colour edge. Importantly, the green edges are used as shared edges between both players. After each turn, all the edges (any colour) of the selected vertex are removed. For all the versions of LastEdge, if the current player has no move, then the opponent player wins.")
(id "698")
(version "1.3.9")
(classification "experimental/graph theory")
(author "Tahmina Begum")
(credit "Tahmina Begum")
(date "16-06-2020")
}
)
    
    (graphics {
        (player Colour Shared (colour Green))
        (player Colour P1 (colour Red)) 
        (player Colour P2 (colour Blue))
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges ThinDotted (colour 127 127 127))
    })
    
    (ai
        "LastEdge_ai"
    )
)
