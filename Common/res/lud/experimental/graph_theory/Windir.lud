(game "Windir" 
    (players 2)
    (equipment { 
        (board <Board:type> use:Edge)
        (piece "Marker" Neutral) 
    }) 
    
    (rules  	
        
        <Version:play>
        
        (end 
            (if (is RegularGraph Neutral) (result Mover Loss))
        )
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type>}
    {
    (item "Graph1"    
        <
        (complete (subdivide (regular Star 4)))
        > 
        
        "The game is played on the Graph1."
    )* 
    
    (item "Graph2"    
        <
        (complete (subdivide (regular Star 8)))
        > 
        
        "The game is played on the Graph2."
    )** 
    
    (item "Graph3"    
        <
        (complete (subdivide (regular Star 14)))
        > 
        
        "The game is played on the Graph3."
    )  
})

(option "Version" <Version> args:{ <play> }
    {
    (item "Add"
        <
        (play
            (move Add (piece "Marker0")
                (to Edge (sites Empty Edge))	   
            ) 
        ) 
        > 
        "The players play on Addition version."
    )* 
    (item "Deletion"
        <
        (start
            (set Neutral Edge (sites Board Edge)) 
        )
        (play
            (move
                Remove
                (sites Occupied by:Neutral on:Edge)
            )
        )
        >  
        "The players play on Deletion version."
    ) 
})

//------------------------------------------------------------------------------

(metadata
    
(info
{
(description "Windir is a graph theory game based on the graph-theoretic concept of regular graphs. A regular graph is a graph where every vertex has the same number of neighbours; i.e. every vertex has the same degree. ")
(rules "This game has two versions: an addition and a deletion version. In the addition version, the graph is initially uncoloured. Players alternate turns, colouring an uncoloured edge in a common colour. However, in the deletion version, the game starts with all the edges coloured. At each turn, a player chooses to remove the colour of an edge. In both versions, the current player loses by turning the coloured graph into a regular graph.
")
(id "700")
(version "1.3.9")
(classification "experimental/graph theory")
(author "Tahmina Begum")
(credit "Tahmina Begum 26-05-2020")
(date "26-05-2020")
}
)
    
    (graphics {
        (player Colour Neutral (colour 0 127 255))
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges ThinDotted (colour 127 127 127))
    })
    
    (ai
        "Windir_ai"
    )
)
