(define "HopCounter"
    (move
        Hop
        (from #1)
        All
        (between
            if:(and (not (is In (between) (sites ToClear))) (is Enemy (who at:(between))))
            (apply (remove (between) at:EndOfTurn))
        )
        (to if:(is Empty (to)))
        #2
    )
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)) )

//------------------------------------------------------------------------------

(game "Omnidirectional Draughts"
    (players 2)
    (equipment {
        (board (square <Board>))
        (piece "Counter" P1 N)
        (piece "Counter" P2 S)
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })

    (rules

        (start {
            (place "Counter1" (expand (sites Bottom) steps: (- (/ <Board> 2) 2) ) )
            (place "Counter2" (expand (sites Top) steps: (- (/ <Board> 2) 2) ) )
        })

        (play
            (if "SameTurn"
                (if "IsUnpromoted"
                    (max Moves
                        ("HopCounter" (last To)
                            (then
                                (if (can Move ("HopCounter" (last To)))
                                    (moveAgain)
                                    (if "JumpToPromotionZone"
                                        (move Promote (last To) (piece "DoubleCounter") Mover)
                                    )
                                )
                            )
                        )
                    )
                    (max Moves ("HopSequenceCaptureAgain" before:(count Rows) after:(count Rows) at:EndOfTurn) )
                )
                (priority {
                    (max Moves
                        (or
                            (forEach Piece "Counter"
                                ("HopCounter" (from)
                                    (then
                                        (if (can Move ("HopCounter" (last To)))
                                            (moveAgain)
                                            (if "JumpToPromotionZone"
                                                (move Promote (last To) (piece "DoubleCounter") Mover)
                                            )
                                        )
                                    )
                                )
                            )
                            (forEach Piece "DoubleCounter" ("HopSequenceCapture" before:(count Rows) after:(count Rows) at:EndOfTurn))
                        )
                    )
                    (or
                        (forEach Piece "Counter"
                            (move
                                Step
                                (directions Forwards)
                                (to if:(is Empty (to)))
                            )
                            (then
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            )
                        )
                        (forEach Piece "DoubleCounter" (move Slide))
                    )
                })
            )
        )

        (end (if (no Moves Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "6x6"      <6>     "The game is played on a 6x6 board")
    (item "8x8"      <8>     "The game is played on a 8x8 board")**
    (item "10x10"    <10>    "The game is played on a 10x10 board")
})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "A draughts variant with both orthogonal and diagonal movement and capture.")
        (rules "Men can move one step forwards orthogonally or diagonally, or can jump an opponent's adjacent piece to capture it, in any direction. When a man ends its turn on the opposite edge of the board from its starting position, it becomes a king and may move or jump over any number of spaces in any direction. Captures are compulsory and the maximum number of jumps must be made. The first player unable to move, loses.")
        (version "1.3.0")
        (classification "experimental")
        (author "A. Sambruni")
        (credit "A. Sambruni")
        (date "2021")
        }
    )

    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })

    (ai
        "Omnidirectional Draughts_ai"
    )
)
