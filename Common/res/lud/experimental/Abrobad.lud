(define "Shooting"
    (forEach Piece
        (move
            (from)
            (to (sites LineOfSight Farthest at:(from))
                if:(is Empty (to))
            )
        )
    )
)

(define "Hopping"
    (forEach Piece
        (or
            (move
                Hop
                (between if:(not (is Empty (between))))
                (to if:(is Empty (to)))
            )
            (move
                Step
                (to if:(is Empty (to)))
            )
        )
    )
)

(define "Sliding"
    (forEach Piece
        (move
            Slide
        )
    )
)
(define "Stepping"
    (forEach Piece
        (move
            Step
            (to if:(is Empty (to)))
        )
    )
)

(define "Jumping"
    (forEach Piece
        (move Hop
            (between
                (range 0 18)      // upper limit at least 2 times board order.
                if:(not (is Empty (between)))
            )
            (to if:(is Empty (to)))

        )
    )
)

//------------------------------------------------------------

(game "Abrobad"
    (players 2)
    (equipment
        {
        (board (hex Limping <Board:size>))
        (piece "Hex" Each)
        }
    )
    (rules
        (play
            (priority
                (move
                    Add
                    (to
                        (sites Empty)
                        if:(= 0 (count Sites in:(sites Around (to) Own)))
                    )
                )
                (or
                    <Movement:type>
                    (move Propose "Conclude"
                        (then
                            (if
                                (is Proposed "Conclude" )
                                (add
                                    (piece (next))
                                    (to (sites Empty))
                                    (then
                                        (and
                                            (set Score Mover (count Groups if:(= (who at:(to)) (mover))))
                                            (set Score Next (count Groups if:(= (who at:(to)) (next))))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        (end
            (if
                (is Proposed "Conclude")
                (if
                    (le (score Mover) (score Next))
                    (result Mover Win)
                )
                (result Next Win)
            )
        )
    )
)

//---------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "Limping Hex 2-3" <2> "Board & size: Hexhex with edges alternately 2 and 3")*
    (item "Limping Hex 3-4" <3> "Board & size: Hexhex with edges alternately 3 and 4")
    (item "Limping Hex 4-5" <4> "Board & size: Hexhex with edges alternately 4 and 5")
    (item "Limping Hex 5-6" <5> "Board & size: Hexhex with edges alternately 5 and 6")
    (item "Limping Hex 6-7" <6> "Board & size: Hexhex with edges alternately 6 and 7")
    (item "Limping Hex 7-8" <7> "Board & size: Hexhex with edges alternately 7 and 8")
    (item "Limping Hex 8-9" <8> "Board & size: Hexhex with edges alternately 8 and 9")
    (item "Limping Hex 9-10" <9> "Board & size: Hexhex with edges alternately 9 and 10")
    }
)

(option "Movement Type (in any direction)" <Movement> args:{ <type> }
    {
    (item "Step or jump to first empty"  <"Jumping">
        "Movement: Pieces step or jump once, to the first empty position in that direction."
    )**

    (item "Step or hop to empty" <"Hopping">
        "Movement: Pieces step or hop once, to the first empty position in that direction."
    )

    (item "Adjacent step" <"Stepping">
        "Movement: Pieces step to an adjacent empty position."
    )

    (item "Any unobstructed slide"  <"Sliding">
        "Movement: Pieces slide along a grid direction any distance across empty positions."
    )

    (item "Maximal slide"  <"Shooting">
        "Movement: Pieces slide across empty positions as far as they can in any given direction."
    )
    }
)

//---------------------------------------

(metadata
    (info
        {
        (description "Abrobad is arabic, for \"Windswept Clouds\" refering to the art of making swirled patterns by adding pigments on the surface of a liquid and blowing on them, e.g. to make fancy \"marblized\" book end papers. The game is a race to connect one's pieces into the fewest possible groups, and as large gaps are impossible, it results in an abrobad visual effect. It may be played on any size hexagonal \"limping\" board grid.")
        (aliases {"Abr-Bad" "Clouds and Wind"})
        (rules "Goal:
            You try to both reduce the number of your own groups as much as possible, and also to block your opponent's ability to reduce his groups down to fewer than yours. Whoever decides to end the game must fill the empty spaces with opponent's pieces, and only then, wins by having equal or fewer groups.

            Play:
            Starting with the board empty, player's take turns placing a piece of their color on an empty position that does not abut any friendly piece. If it is impossible to place a piece, the player, instead, moves a piece to the first empty position in any direction. But whenever a new opportunity for placement is available, the turn must be used for placement.

            Ending the Game:

            The game ends at the choice of the player about to move, but only at the beginning of a turn in which there is no forced placement.

            Before scoring, the empty spaces are filled with the opponent's pieces. The player who ended the game wins unless the opponent has fewer groups. Because the empty spaces are filled with opponent's pieces, a player who ends the game too soon will lose. On the other hand, since ties go to the player who ends the game, it is important to end the game as soon as you can actually win.

            Variations:
        Options are available to select the board size, and also to use other movement rules, instead of the standard one.")
        (source "<a href=\"https://boardgamegeek.com/thread/2308167/new-game-now-abrobad\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.0")
        (classification "experimental")
        (author "Dale W. Walton")
        (credit "Dale W. Walton")
        (date "18-11-2019")
        }
    )
    (graphics
        {
        (piece Scale "Hex" 1.05)
        (piece Colour P1 fillColour:(colour 230 230 230) strokeColour:(colour 61 61 61))
        (piece Colour P2 fillColour:(colour 61 61 61) strokeColour:(colour 0 0 0))
        (board Colour Phase0 (colour 136 161 252))
        (board Colour OuterEdges (colour 61 61 61))
        (board Colour InnerEdges (colour 99 99 185))
        (board StyleThickness OuterEdges 1.0)
        (board StyleThickness InnerEdges 0.5)
        (board Background image:"square.svg" fillColour:(colour 136 161 252) edgeColour:(colour 136 161 252) scale:1.2)
        }
    )
    (ai
        "Abrobad_ai"
    )
)
