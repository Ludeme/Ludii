(game "Nao Gutiya Baithaneka"
    (players 1) 
    (equipment { 
        (board 
            (splitCrossings (regular Star 5))
            use:Vertex
        )
        (hand Each) 
        (piece "Marker" P1
            (move Hop 
                (between 
                    if:("IsFriendAt" (between))
                    (apply (remove (between)))
                )
                (to if:(is Empty (to)))
            )
        ) 
        }
    ) 
    (rules 
        (start 
            (place "Marker" (handSite P1) count:9)
        )
        
        (play 
            (if (is Even (count Moves))
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                    (then (moveAgain))
                ) 
                (forEach Piece
                    (if (= (from) (last To))
                        (move Hop 
                            (between 
                                if:(or ("IsFriendAt" (between)) (is Empty (between)))
                            )
                            (to if:(is Empty (to)))
                        )
                    )
                )
            )
        )
        (end { 
            (if (and (is Even (count Moves)) ("HandEmpty" Mover)) (result Mover Win))
            (if (no Moves Next) (result Mover Loss))
        })
    )		
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Nao Gutiya Baithaneka is a solitaire game played in the Bihar region of India. The player attempts to place nine pieces on a star-shaped board with a distinct pattern of moves.")
        (rules "The board is a five-pointed star. The player has nine pieces. The player attempts to place all the pieces on the board. The player choses a point, then moves the piece two spaces in a straight line. The piece may move through a spot occupied by another piece, but must land on an empty space. The player wins by successfully placing all nine pieces.
        ")
        (source "Das Gupta 1935: 415-416.")
        (id "919")
        (version "1.3.5")
        (classification "puzzle/planning")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1935.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Nao Gutiya Baithaneka_ai"
    )
)
