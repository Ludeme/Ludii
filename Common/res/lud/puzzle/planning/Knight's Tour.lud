(game "Knight's Tour" 
    (players 1) 
    (equipment { 
        (board (square 8)) 
        (piece "Knight" P1)
        }
    ) 
    (rules 
        phases:{  
        (phase "Placement" 
            (play (move Add (to (sites Empty)))) 
            (nextPhase "Movement")
        )
        (phase "Movement" 
            (play 
                (move
                    Leap
                    (from (last To)) 
                    "KnightWalk" 
                    (to if:(is Empty (to)))
                    (then 
                        (add (to (last From)))
                    )
                ) 
            )
        )
        }
        
        (end {
            (if 
                (>= (count Moves) (count Sites "Board")) 
                (result P1 Win) 
            )
            (if 
                (no Moves P1) 
                (result P1 Loss) 
            ) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Knight's tour is a puzzle by which a Chess knight is moved on a board so that is is placed in every square on the board only once. It has been documented in India, where the movement of the horse piece in Chaturanga has the same movement as the Chess knight.")
        (rules "Played with one knight on a Chess board. The goal is to move the knight onto every square of the board only once using its typical move as in Chess.")
        (source "Murray 1913: 65.")
        (id "122")
        (version "1.3.4")
        (classification "puzzle/planning")
        (credit "Eric Piette")
        (origin "This game was played in India, from around 1500 to 1550.")
        }
    )
    
    (graphics {
        (player Colour P1 (colour DarkGrey))
        (board Style Chess)
    })
    
    (ai 
        "Knight's Tour_ai"
    )
    
)
