(define "Rumba" 0)

(define "AllSitesNotRumba" (difference (sites Board) "Rumba"))

(define "LastHole" (last To afterConsequence:True))

(define "AllCountersInRumba" (= 8 (count at:"Rumba")))

//------------------------------------------------------------------------------

(game "Chuka"
    (players 1)
    (equipment {
        (board
            (concentric {5})
            (track "Track" {0 1 3 4 2} loop:True)
            use:Vertex
        )
        (piece "Seed" Shared)
    })
    (rules
        (start (set Count 2 to:"AllSitesNotRumba"))
        (play
            (move
                Select
                (from
                    (if
                        (!= ("Rumba") (value Pending))
                        (sites {"LastHole"})
                        ("AllSitesNotRumba")
                    )
                    if:(!= 0 (count at:(from)))
                )
                (then
                    (sow
                        apply:(if (> (count at:(to)) 1)
                            (set Pending (to))
                        )
                    )
                )
            )
        )
        (end {
            (if ("AllCountersInRumba") (result Mover Win))
            (if
                (and (= 1 (count at:("LastHole"))) (!= ("LastHole") ("Rumba")))
                (result Mover Loss)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Chuka is a puzzle played in Russia. It contains the sowing mechanism seen in many mancala-style board games. ")
        (rules "Five holes arranged in a circle. One hole is called the Rumba. Two counters in each hole, except the Rumba. The player sows the counters in one direction. When the final counter lands in an occupied hole, the counters in that hole are picked up and sowing continues. When the final counter lands in the Rumba, the player may pick up the counters from any one of the other holes and continue sowing. If the final counter lands in an empty hole that is not the Rumba, they lose. The goal of the player is to accumulate all of the counters into the Rumba. ")
        (source "Delediqc and Popova 1977: 99-100.")
        (version "1.3.0")
        (classification "puzzle/planning")
        (credit "Eric Piette")
        }
    )

    (graphics {
        (board Style Mancala)
    })

    (ai
        "Chuka_ai"
    )
)
