(game "Lam Turki"
    (players 1) 
    (equipment { 
        (board 
            (splitCrossings (regular Star 5))
            use:Vertex
        )
        (hand Each) 
        (piece "Marker" P1
            (move Hop 
                (between 
                    if:("IsFriendAt" (between))
                    (apply (remove (between)))
                )
                (to if:(is Empty (to)))
            )
        ) 
        }
    ) 
    (rules 
        (start 
            {
            (place "Marker" (handSite P1) count:9)
            }
        )
        
        phases:{
        (phase "Placement"
            (play 
                (if (is Even (count Moves))
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty))
                        (then (moveAgain))
                    ) 
                    (forEach Piece
                        (if (= (from) (last To))
                            (move Hop 
                                (between 
                                    if:(or ("IsFriendAt" (between)) (is Empty (between)))
                                )
                                (to if:(is Empty (to)))
                            )
                        )
                    )
                )
            )
            (end 
                (if (no Moves Next) (result Mover Loss))
            )
            (nextPhase Mover (and (is Even (count Moves)) ("HandEmpty" Mover)) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
            (end {
                (if (= (count Pieces) 1) (result Mover Win))
                (if (no Moves Next) (result Mover Loss))
            })
        )
        }
    )		
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Lam Turki is a solitaire game played in India. Played on a board shaped like a five-pointed star, the player attempts to remove all of the pieces from the board,")
        (rules "The board is a five-pointed star. The player has nine pieces. In the first phase, the player attempts to place all the pieces on the board. The player choses a point, then moves the piece two spaces in a straight line. The piece may move through a spot occupied by another piece, but must land on an empty space. Once all of the pieces are placed in this way, the player captures a piece on the board by hopping over with with another one of the pieces to an empty space on the opposite side of the piece to be captured. The goal is to capture all of the pieces except one.")
        (source "Hora 1933: 6-7.")
        (id "916")
        (version "1.3.5")
        (classification "puzzle/planning")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1933.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Lam Turki_ai"
    )
)
