(game "Peg Solitaire"  
    (players 1)  
    (equipment { 
        <Variant:board>
        (piece "Marker" P1 
            (move
                Hop
                Orthogonal 
                (between 
                    if:(is Friend (who at:(between))) 
                    (apply (remove (between)))
                )
                (to if:(is Empty (to)))
            )
        )
        }
    )  
    (rules 
        (start { 
            (place "Marker" (difference (sites Board) (sites <Variant:noPieceSite>) ) )
        })
        
        (play (forEach Piece))
        (end (if <Variant:end> (result P1 Win) ) )  
    )
)

//------------------------------------------------------------------------------

(option "Known Variants" <Variant> args:{ <board> <noPieceSite> <end> }
    {
    (item "English"      
        <(board 
            (merge 
                (shift 0 2 (rectangle 3 7)) 
                (shift 2 0 (rectangle 7 3))
            ) 
            use:Vertex
        )>
        <{"D4"}>
        <(and (is Friend (who at:(centrePoint))) (= 1 (count Pieces All)))>    
    "The game is played on the English board.")   
    (item "European" 
        <(board 
            (keep 
                (square 7) 
                (poly { {-2 3.5} {3.5 -2} {7.5 3.5} {3.5 7.5} })
            ) 
            use:Vertex
        )>
        <{"D6"}>
        <(= 1 (count Pieces All))>   
        "The game is played on the european board."
    )
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/European (Described)" 
        {
        "Known Variants/European"
        }
    )
    
    (ruleset "Ruleset/English (Described)" 
        {
        "Known Variants/English"
        }
    )
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Peg Solitaire is a single-player game attested in Europe since the seventeenth century. There are two board variations, \"European\" which is hexagonal with three spaces per side and \"English\" which has the central hole in each of the six sides of the hexagon removed. Spaces take the form of holes in which pegs are inserted.")
        (useFor "Ruleset/English (Described)" (rules "Play begins with all holes filled except the central hole. The player moves a peg by orthogonally jumping an adjacent peg and removing it from the board. The goal is to remove all but one peg, leaving it in the central hole."))
        (useFor "Ruleset/English (Described)" (source "Murray 1951: 93."))
        (useFor "Ruleset/English (Described)" (id "124"))
        (useFor "Ruleset/European (Described)" (rules "The same rules as for English Peg Solitaire, but four of the holes are removed to make the board into a cruciform shape."))
        (useFor "Ruleset/European (Described)" (source "Murray 1951: 93."))
        (useFor "Ruleset/European (Described)" (id "570"))
        (source "Murray 1951: 93.")
        (id "124")
        (version "1.3.3")
        (classification "puzzle/planning")
        (credit "Eric Piette")
        (origin  "This game was played in Europe, around 1687.")
        }
    )
    
    (graphics {
        (board Style Graph)
        (show Edges Orthogonal Hidden)
        (show Edges Diagonal Hidden)
        (board StyleThickness InnerVertices 3.0)
    })
    
    (ai 
        "Peg Solitaire_ai"
    )
    
)

