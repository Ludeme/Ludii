(game "Spuzzle" 
    (players 1) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball0")
        (piece "Ball1")
        (piece "Ball2")
    }) 
    (rules
        (play
            (or {
                (move Add 
                    (piece "Ball0")
                    (to (sites Empty) 
                        if:(and { (is Flat) (< (count SizeBiggestGroup (sites Around (to)) if:(= (what at:(to)) 1) ) 2) (< (count Sites in:(intersection (sites Around (to)) (sites Occupied by:All component:1)))  2)})
                    )
                    (then (addScore Mover 1))
                )
                (move Add 
                    (piece "Ball1")
                    (to (sites Empty) 
                        if:(and { (is Flat) (< (count SizeBiggestGroup (sites Around (to)) if:(= (what at:(to)) 2) ) 2) (< (count Sites in:(intersection (sites Around (to)) (sites Occupied by:All component:2)))  2)})
                    )
                    (then (addScore Mover 1))
                )
                (move Add 
                    (piece "Ball2")
                    (to (sites Empty) 
                        if:(and { (is Flat) (< (count SizeBiggestGroup (sites Around (to)) if:(= (what at:(to)) 3) ) 2) (< (count Sites in:(intersection (sites Around (to)) (sites Occupied by:All component:3)))  2)})
                    )
                    (then (addScore Mover 1))
                )
            })
        )
        (end {
            (if (no Moves Next) (result Mover Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Spuzzle is a one-player game that can be played with the Series: Shibumi game system. It uses a complete set with a 4×4 board, white, black, and red balls.")
        (rules "The board starts empty. Continue placing a ball of any colour (white, black or red) at any playable point, such that no connected group of three or more same-colored balls is formed. Note that hidden connections count! Your score is the total number of balls placed.")
        (id "4249")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />SHIBUMI Rulebook</a>")
        (version "1.3.14")
        (classification "puzzle/planning")
        (author "Néstor Romeral Andrés")
        (publisher "Computational Creativity Group, nestorgames")
        (credit "Cedric Antoine")
        (date "2011")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour "Ball0" fillColour:(colour White))
        (piece Colour "Ball1" fillColour:(colour Black))
        (piece Colour "Ball2" fillColour:(colour Red))
    })
    
)
