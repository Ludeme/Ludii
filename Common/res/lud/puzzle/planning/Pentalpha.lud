(game "Pentalpha"
    (players 1) 
    (equipment { 
        ("StarBoard" 5)
        (hand Each) 
        (piece "Marker" P1 ("HopFriendCapture")) 
    })
    (rules 
        (start 
            (place "Marker" (handSite P1) count:9)
        )
        
        (play 
            (if (is Even (count Moves))
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                    (then (moveAgain))
                ) 
                (forEach Piece
                    (if (= (from) (last To))
                        (move Hop 
                            (between 
                                if:(or ("IsFriendAt" (between)) (is Empty (between)))
                            )
                            (to if:(is Empty (to)))
                        )
                    )
                )
            )
        )
        (end { 
            (if (and (is Even (count Moves)) ("HandEmpty" Mover)) (result Mover Win))
            (if (no Moves Next) (result Mover Loss))
        })
    )		
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Pentalpha is a solitaire game played on Crete in the early twentieth century. ")
        (rules "The board is a five-pointed star. The player has nine pieces. The player attempts to place all the pieces on the board. The player choses a point, then moves the piece two spaces in a straight line. The piece may move through a spot occupied by another piece, but must land on an empty space. The player wins by successfully placing all nine pieces.
        ")
        (source "Murray 1951: 28.")
        (id "917")
        (version "1.3.10")
        (classification "puzzle/planning")
        (credit "Eric Piette")
        (origin "This game was played in Greece, around 1938.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Pentalpha_ai"
    )
)
