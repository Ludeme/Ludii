(game "Sudoku Mine" 
    (players 1) 
    (equipment { 
        (board (square <Challenge:size>) (values Cell (range 0 1)))
        (hints { <Challenge:hints> })
        (regions {Columns Rows})
        (piece "Bomb" P1)
    }) 
    (rules 
        (play 
            (satisfy {
                (forAll
                    Hint 
                    (is Count 
                        (sites Around (from)) 
                        of:1
                        (hint)
                    )
                )
                (forAll
                    Hint 
                    (= (what at:(from)) 0)
                )
                (is Sum 3)
            })
        )
        (end (if (is Solved) (result P1 Win)))
    )
)

//------------------------------------------------------------------------------

(option "Challenge" <Challenge> args:{ <size> <hints> }
    {
    (item "1" 
        <9>
        <
        (hint 5 1) (hint 7 2) (hint 8 1) 
        (hint 10 3) (hint 19 1) (hint 24 3)
        (hint 29 3) (hint 35 4) (hint 39 3)
        (hint 41 3) (hint 45 4) (hint 51 2)
        (hint 56 3) (hint 61 2) (hint 70 3)
        (hint 72 1) (hint 73 1) (hint 75 1)
        >
        ""
    ) 
})

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Sudoku Mine combines Sudoku puzzle with Minesweeper game.")
        (rules "The goal is to place three mines in every row, column and region. The digits in the grid represent the number of mines in the neighbouring cells.")
        (id "1150")
        (source "<a href=\"http://www.cross-plus-a.com/sudoku.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />cross-plus-a.com</a>")
        (version "1.3.7")
        (classification "puzzle/deduction")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Grey))
        (piece Rename piece:"1" "Bomb")
        (piece Rename piece:"0" "x")
        (show Line {{3 93} {6 96} {39 30} {69 60}} (colour Black) scale:2)
        (show Edges Outer (colour Black))
    })
)
