(define "NoPairValuesEqualInKnightMove"
    (all Sites
        (sites Board)
        if:(if (= (what at:(site)) 0)
            True
            (not 
                (is In 
                    (what at:(site))
                    (results
                        from:(site) 
                        to:(sites (site) "KnightWalk") 
                        (what at:(to))
                    )	
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Anti-Knight Sudoku" 
    (players 1) 
    (equipment { 
        (board (square 9) (values Cell (range 1 9))) 
        (regions {Columns Rows SubGrids})
    }) 
    (rules
        (start <Challenge>)
        (play 
            (satisfy { 
                ("NoPairValuesEqualInKnightMove")
                (all Different)
            })
        )
        (end (if (is Solved) (result P1 Win)))
    )
)

//------------------------------------------------------------------------------

(option "Challenge" <Challenge> args:{ <start> }
    {
    (item "1" 
        <(set { 
            {0 9} {8 7} {12 7} {14 8} {20 7} {24 8} {28 1} {30 8} {32 5}
            {34 7} {40 7} {46 8} {48 3} {50 2} {52 6} {56 6} {60 9}
            {66 6} {68 9} {72 3} {80 4}
        })>
        ""
    ) 
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Anti-Knight Sudoku is a variant of Sudoku involving the concept of Chess knight move.")
        (id "1149")
        (source "<a href=\"http://www.cross-plus-a.com/sudoku.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />cross-plus-a.com</a>")
        (version "1.3.12")
        (classification "puzzle/deduction")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Sudoku)
    })
)
