(define "NoTwoEqualsCellSharingAVertex" 
    (all Sites
        (sites Board Vertex)
        if:(all Different
            (sites Incident Cell of:Vertex at:(site))
        )
    )	
)

//------------------------------------------------------------------------------

(game "Tridoku" 
    (players 1) 
    (equipment { 
        (board (tri 9) (values Cell (range 1 9)))
        (regions {0 1 2 9 10 17 18 25 32})
        (regions {11 19 26 27 39 33 40 34 41})
        (regions {3 4 5 12 13 20 21 28 35})
        (regions {14 22 29 30 36 37 42 43 44})
        (regions {6 7 8 15 16 23 24 31 38})
        (regions {45 46 47 51 52 56 57 61 65})
        (regions {69 70 71 66 67 62 63 58 53})
        (regions {48 49 50 54 55 59 60 64 68})
        (regions {72..80})
        (regions {0..8})
        (regions {0 17 32 45 56 65 72 77 80})
        (regions {80 78 74 68 60 50 38 24 8})
        (regions {4 12 20 27 34 40 46 51 56})
        (regions {4 13 21 29 36 43 49 55 60})
        (regions {60 64 59 63 58 62 57 61 56})
    }) 
    (rules
        (start <Challenge>)
        (play 
            (satisfy {
                ("NoTwoEqualsCellSharingAVertex")
                (all Different)
            })
        )
        (end (if (is Solved) (result P1 Win)))
    )
)

//------------------------------------------------------------------------------

(option "Challenge" <Challenge> args:{ <start> }
    {
    (item "1" 
        <(set {
            {9 6} {17 8} {2 3} {11 9} {12 1} {4 7} {13 6} {14 4} {6 1} {16 7} 
            {18 1} {28 3} {23 5} {24 4} {39 5} {33 4} {34 2} {41 1} {42 6} {36 8}
            {37 3} {44 2} {46 9} {47 5} {53 4} {48 2} {49 4} {56 4} {61 8} {62 1}
            {63 9} {64 5} {60 3} {65 3} {69 5} {70 3} {71 2} {68 6} {75 8} {76 4}
            {77 6} {79 3} {78 7}
        })>
        ""
    ) 
})

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Tridoku is a variant of Sudoku played on a triangle basis.")
        (id "1154")
        (source "<a href=\"http://www.cross-plus-a.com/sudoku.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />cross-plus-a.com</a>")
        (version "1.3.12")
        (classification "puzzle/deduction")
        (author "Japheth Light")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (show Line {
            {0 54} {54 9} {0 9} {3 27} {3 30} {30 6}
            {6 33} {33 27} {30 45} {30 48} {48 45}} 
            (colour Black) scale:2
        )
        
        (region Colour (sites {
                0..3 5..8 24 38 50 68 74
                78 80 77 72 65 45 32 17
            }) (colour 252 252 156)
        )
        
        (region Colour (sites {
                56 4 60
            }) (colour 156 252 204)
        )
        
        (region Colour (sites {
                12 20 27 34 40 46 51 61 57 62 58 
                63 59 64 55 49 43 36 29 21 13
            }) (colour 236 212 196)
        )
    })
)
