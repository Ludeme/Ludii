(game "Takuzu" 
    (players 1)  
    (equipment  { 
        (board (square <Challenge:size>) (values Cell (range 0 1))) 
        (regions {Columns Rows})
    })  
    (rules 
        (start (set { <Challenge:start> }))
        (play 
            (satisfy  {
                (not (is Line 3 Orthogonal))
                (is Unique)
                (is Sum (/ (count Rows) 2))
            })
       )
        (end (if (is Solved) (result P1 Win)))
   )
)

//------------------------------------------------------------------------------

(option "Challenge" <Challenge> args:{ <size> <start> }
    {
    (item "Janko.at #1" 
        <10>
        <{0 1}  {1 1}  {4 1}  {11 1} {18 1} {25 0} {26 0} 
        {29 0} {34 1} {37 0} {51 1} {52 0} {59 1} {65 1} 
        {67 1} {71 1} {86 0} {87 0} {92 0} {96 0}>
        ""
   )   
})

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Takuzu, also known as Binairo, is a logic puzzle involving placement of two symbols, often 1s and 0s, on a rectangular grid.")
        (aliases {"Binairo" "Binero"})
        (rules "Each box should contain a zero or a one. No more than two similar numbers next to or below each other are allowed. Each row and each column should contain an equal number of zeros and ones. Each row is unique and each column is unique.")
        (id "105")
        (source "<a href=\"https://en.wikipedia.org/wiki/Takuzu\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.4")
        (classification "puzzle/deduction")
        (author "Peter De Schepper and Frank Coussement")
        (credit "Eric Piette")
        (date "2009")
        }
   )
)
