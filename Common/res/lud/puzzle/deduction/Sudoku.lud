(game "Sudoku" 
    (players 1) 
    (equipment { 
        (board (square 9) (values Cell (range 1 9))) 
        (regions {Columns Rows SubGrids})
    }) 
    (rules
        (start <Challenge>)
        (play (satisfy (all Different)))
        (end (if (is Solved) (result P1 Win)))
    )
)

//------------------------------------------------------------------------------

(option "Challenge" <Challenge> args:{ <start> }
    {
    (item "1" 
        <(set { {1 9} {6 4} {11 8} {12 5} {16 1} {20 1} {25 6} {26 8} {30 1} {34 3} {40 4} 
        {41 5} {42 7} {46 5} {50 7} {55 7} {58 9} {60 2} {65 3} {66 6} {72 8} })>
        ""
    ) 
    (item "Easy" 
        <(set { {5 1} {8 4} {11 4} {12 6} {13 9} {15 1} {16 7} {17 3} {19 2} {20 9} {26 8} 
            {28 5} {31 4} {33 3} {35 6} {37 9} {39 5} {45 4} {51 2} {52 1} {55 4} {57 3} 
        {59 5} {60 8} {64 1} {68 6} {72 8} {73 7} {74 6} {75 9} })>
        ""
    ) 
})

//------------------------------------------------------------------------------

(metadata 
(info
{
(description "French newspapers in the 19th century had forms of this game, and it has appeared consistently since 1979. Nikoli began publishing Sudoku (\"single number\" in 1986.")
(rules "Played on a 9x9 grid divided into 3x3 groups of 9 or \"nonets.\" Numbers from 1 to 9 are placed in the squares such that each row, column, or nonet has each number appearing only once.")
(source "<a href=\"https://en.wikipedia.org/wiki/Sudoku\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(id "120")
(version "1.3.7")
(classification "puzzle/deduction")
(credit "Eric Piette")
(date "19-11-1892")
}
)
        
        (graphics {
            (board Style Sudoku)
        })
    )
