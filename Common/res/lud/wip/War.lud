(define "ValueOf" (value Piece of:(what at:#1 level:(topLevel at:#1))))
(define "MoveAllCardToBottom"
    (move
        (from #1)
        (to (handSite #2) level:0)
        stack:True
    )
)
(define "MoveCardsOfP1To" ("MoveAllCardToBottom" 0 #1))
(define "MoveCardsOfP2To" ("MoveAllCardToBottom" 1 #1))
(define "PlayACard"
    (move
        (from (handSite #1))
        (to #2)
        #3
        #4
    )
)
(define "P1PlayACard" ("PlayACard" P1 0 #1 #2))
(define "P2PlayACard" ("PlayACard" P2 1 #1 #2))
(define "MaskedCards" (set Masked (union (sites Hand P1) (sites Hand P2)) stack:True) )
(define "MakeTopCardP1Visible" (then (set Visible 0 level:(topLevel at:0))))
(define "MakeTopCardP2Visible" (then (set Visible 1 level:(topLevel at:1))))
(define "TakeCards"
    (and
        ("MoveCardsOfP1To" #1)
        ("MoveCardsOfP2To" #1)
    )
)

(define "PlayACardOfEachDeck"
    (and
        ("P2PlayACard")
        ("P1PlayACard")
    )
)

(define "P1HasNoCard" (and (= (what at:0) 0) ("HandEmpty" P1)))
(define "P2HasNoCard" (and (= (what at:1) 0) ("HandEmpty" P2)))
(define "CardOfP1isHigher" (> ("ValueOf" 0) ("ValueOf" 1)))
(define "CardOfP2isHigher" (< ("ValueOf" 0) ("ValueOf" 1)))

//------------------------------------------------------------------------------

(game "War"
    (players 2)
    (mode Simultaneous)
    (equipment {
        (board
            (union
                (square 1)
                (shift 2 0 (square 1))
            )
        )
        (deck)
        (hand Each)
    })

    (rules
        (start (split Deck))
        (play
            (if (is Odd (count Turns))
                (or
                    ("P1PlayACard" P1 ("MakeTopCardP1Visible"))
                    ("P2PlayACard" P2 ("MakeTopCardP2Visible"))
                )
                (move Pass
                    (then
                        (and
                            (if ("CardOfP1isHigher")
                                ("TakeCards" P1)
                                (if ("CardOfP2isHigher")
                                    ("TakeCards" P2)
                                    ("PlayACardOfEachDeck")  // Else the cards have the same value
                                )
                            )
                            ("MaskedCards")
                        )
                    )
                )
            )
        )

        (end {
            (if
                ("P1HasNoCard")
                (result P2 Win)
            )
            (if
                ("P2HasNoCard")
                (result P1 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "War is a simple card game which is first documented in the early nineteenth century, but its simplicity suggests it may be older. It is first known from France, and it is popular among children where it is played.")
        (aliases {"Battle" "Bataille"})
        (rules "A 52 card pack of cards is divided evenly among two players. Players reveal the top card of their hand, the player whose card has the higher value takes both cards. If they are equal in value, a \"war\" occurs. Players place the next card face down (sometimes three) and then the next face up. The player with the higher card then wins all of the cards. If they are equal, another war occurs, continuing until one player plays a higher card. The player who collects the entire pack wins.")
        (source "https://bicyclecards.com/how-to-play/war/")
        (version "1.0.0")
        (classification "card")
        (credit "Eric Piette")
        (origin  "This game was played in Unknown, around 1827CE.")
        }
    )
    (graphics (no Board))

    (ai
        "War_ai"
    )
)
