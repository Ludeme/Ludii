(define "StepForward" (move Step Forward (to if:(is Empty (to)))))
(define "StepDiagonalForward" 
    (move Step (directions {FR FL} of:All) 
        (to if:(or 
                (is Empty (to)) 
                ("IsEnemyAt" (to))
            ) 
            (apply (remove (to)))
        )
    )		
)

(define "ReachedTarget" (is In (last To) (sites Mover)))

//------------------------------------------------------------------------------

(game "Breakthrough (Placement)" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8) use:Vertex) 
        (piece "Pawn" Each (or ("StepForward") ("StepDiagonalForward")))
        (regions P1 (sites Top))
        (regions P2 (sites Bottom))
        (hand Each)
    }) 
    (rules 
        (start (place "Pawn" "Hand" count:18))
        
        phases:{
        (phase "Placement"
            (play 
                (move
                    (from (handSite Mover))
                    (to (expand (sites Next) steps:2) if:(is Empty (to)))
                )
            )
            (nextPhase Mover (is Empty (handSite Mover)) "Move")
        )
        (phase "Move"
            (play (forEach Piece))
            (end (if "ReachedTarget" (result Mover Win))) 
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (graphics {
        (board Style Chess)
    })
    
)
