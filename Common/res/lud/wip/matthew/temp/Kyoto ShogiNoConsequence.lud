(define "CapturePiece"
    (apply 
        (if ("IsEnemyAt" (to))
            (add 
                (piece (mapEntry "Captured" (what at:(to)))) 
                (to (mapEntry "Where" (what at:(to))))
            )
        )
    )
)

(define "NextCannotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "Osho" Next)))))
)

(define "PlacePiece" 
    (move 
        (from (sites Occupied by:Mover container:"Hand")) 
        (to (sites Empty))
        (then (moveAgain))
    )
)

(define "CapturePiece"
    (apply 
        (add 
            (piece (mapEntry "Captured" (what at:(to)))) 
            (to (mapEntry "Where" (what at:(to))))
        )
    )
)

(define "MovePromote"
    (move Promote (last To) (piece (mapEntry "Promoted" (what at:(last To)))))	
)

(define "Promote"
    (promote (last To) (piece (mapEntry "Promoted" (what at:(last To)))))	
)

(define "SlideMove"
    (move
        Slide
        #1 
        (to if:("IsEnemyAt" (to)) "CapturePiece")
        #2 
    )   	
)

(define "StepMove"
    (move
        Step
        #1
        (to if:(not ("IsFriendAt" (to))) "CapturePiece") 
        #2 
    )   	
)

(define "GoldMove" ("StepMove" (directions {Forward Backward Rightward Leftward FL FR}) (then "Promote")))

//------------------------------------------------------------------------------

(game "Kyoto Shogi"  
    (players {(player N) (player S)})  
    
    (equipment { 
        (board (square 9))
        
        // King
        (piece "Osho" Each ("StepMove"))
        
        //Pawn 
        (piece "Fuhyo" Each 
            ("StepMove" 
                Forward 
                (then "Promote" )
            )
        )
        
        // Silver General
        (piece "Ginsho" Each ("StepMove" (directions {Forward BL BR FL FR}) (then "Promote" )))
        
        // Rook
        (piece "Hisha" Each ("SlideMove" Orthogonal (then "Promote" )))
        
        // Bishop
        (piece "Kakugyo" Each ("SlideMove" Diagonal (then "Promote" )))
        
        // Knight
        (piece "Keima" Each 
            (forEach Direction 
                Forward 
                (to 
                    if:True 
                    (apply 
                        (forEach Direction 
                            (from (to)) 
                            (directions {FR FL}) 
                            (to 
                                if:(not ("IsFriendAt" (to))) 
                                (apply 
                                    (fromTo 
                                        (from) 
                                        (to 
                                            if:(not ("IsFriendAt" (to))) 
                                            (apply 
                                                if:True
                                                (add 
                                                    (piece (mapEntry "Captured" (what at:(to)))) 
                                                    (to (mapEntry "Where" (what at:(to))))
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                ) 
                (then "Promote" ) 
            ) 
        )
        
        // Lance
        (piece "Kyosha" Each
            ("SlideMove" 
                Forward 
                (then "Promote")
            ) 
        )
        
        // Gold General
        (piece "Kinsho" Each "GoldMove")
        
        // Promoted Pawn
        (piece "Tokin" Each "GoldMove")
        
        (map "Where" {(pair 3 85) (pair 4 81) (pair 5 86) (pair 6 82) (pair 15 87) (pair 16 83) (pair 17 88) (pair 18 84) 
        (pair 7 85) (pair 8 81) (pair 9 86) (pair 10 82) (pair 11 87) (pair 12 83) (pair 13 88) (pair 14 84) })
        
        (map "Captured" {(pair 3 4) (pair 4 3) (pair 5 6) (pair 6 5) (pair 15 16) (pair 16 15) (pair 17 18) (pair 18 17)
        (pair 7 4) (pair 8 3) (pair 9 6) (pair 10 5) (pair 11 16) (pair 12 15) (pair 13 18) (pair 14 17) })
        
        (map "Promoted" {(pair 3 7) (pair 4 8) (pair 5 9) (pair 6 10) (pair 11 15) (pair 12 16) (pair 13 17) (pair 14 18)
        (pair 7 3) (pair 8 4) (pair 9 5) (pair 10 6) (pair 15 11) (pair 16 12) (pair 17 13) (pair 18 14) })
        
        (hand Each size:4)
    })
    
    (rules 
        (start { 
            (place "Osho1" coord:"E1") (place "Kinsho1" {"D1" "F1"}) (place "Ginsho1" {"C1" "G1"}) (place "Keima1" {"B1" "H1"}) (place "Kyosha1" {"A1" "I1"}) (place "Kakugyo1" coord:"B2") (place "Hisha1" coord:"H2") (place "Fuhyo1" (sites Row 2))
            (place "Osho2" coord:"E9") (place "Kinsho2" {"D9" "F9"}) (place "Ginsho2" {"C9" "G9"}) (place "Keima2" {"B9" "H9"}) (place "Kyosha2" {"A9" "I9"}) (place "Kakugyo2" coord:"H8") (place "Hisha2" coord:"B8") (place "Fuhyo2" (sites Row 6))
        })
        
        (play
            (if ("SameTurn")
                (or
                    ("MovePromote")
                    (move Pass)
                )
                (do 
                    (or {
                        (move 
                            (from (sites Occupied by:Mover container:"Hand" components:{"Kakugyo" "Ginsho" "Hisha" "Kinsho"})) 
                            (to (sites Empty))
                        ) 
                        ("PlacePiece") 
                        (forEach Piece) 
                    })
                    ifAfterwards:(not ("IsInCheck" "Osho" Mover))
                )
            )
        )
        
        (end
            {
            (if (and 
                    ("IsInCheck" "Osho" Next)
                    ("NextCannotMove")
                ) 
                (result Mover Win)
            ) 
            (if (no Moves Next) (result Mover Win)) 
            }
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (source "https://en.wikipedia.org/wiki/Kyoto_shogi")
        (credit "Matthew Stephenson")
        }
    )
    
    (graphics {
        (player Colour P1 (colour White))
        (player Colour P2 (colour White))
        (piece Families {"Defined" "Study"})
        (board Style Shogi)
        (piece Rotate P2 degrees:180)
        (piece Colour "Tokin" strokeColour:(colour Red))
        (piece Colour "Narigin" strokeColour:(colour Red))
        (piece Colour "Ryuo" strokeColour:(colour Red))
        (piece Colour "Ryuma" strokeColour:(colour Red))
        (piece Colour "Narikei" strokeColour:(colour Red))
        (piece Colour "Narikyo" strokeColour:(colour Red))
        (piece ExtendName P2 piece:"Osho" "1")
    })
    
)
