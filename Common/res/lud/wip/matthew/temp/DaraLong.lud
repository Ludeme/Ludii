
//------------------------------------------------------------------------------

(game "Dara"
    (players 2)
    (equipment
        {
        (board (rectangle 5 6))
        (hand Each)
        (piece "Marker" Each)
        }
   )
    (rules
        (start 
            {
            (place "Marker" "Hand" count:12) 
            }
       )
        
        phases:{
        (phase "Placement"
            <Rules>
            (nextPhase ("HandEmpty" P2) "Movement")
       )
        
        (phase "Movement"
            (play 
                (if "SameTurn"
                    (move
                        Remove
                        (sites Occupied by:Next container:"Board") 
                   )
                    (do 
                        (forEach Site (sites Occupied by:Mover) 
                            (move 
                                Step 
                                (from (site))
                                (to if:(is Empty (to))) 
                                (then 
                                    (if 
                                        (is Line 3) 
                                        (moveAgain)
                                   )
                               )
                           )
                       )
                        ifAfterwards:(not (is Line 4 Orthogonal))
                   )
               )
           )
       )
        }
        
        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
   )	
)

//------------------------------------------------------------------------------

(option "Rules" <Rules> args:{<type>}
    {
    (item "Basic" <(play (do (move (from (handSite Mover)) (to (sites Empty))) ifAfterwards:(not (is Line 4 Orthogonal))))>	"Lines of 4 cannot be made during the drop phase.")**
    (item "Niger" <(play (do (move (from (handSite Mover)) (to (sites Empty))) ifAfterwards:(not (is Line 3 Orthogonal))))>	"Lines of 3 cannot be made during the drop phase.")
    }
)
