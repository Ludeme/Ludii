//------------------------------------------------------------------------------
// Moves

(define "Move"
    (move 
        (from 
            (from)
            if:(> (state at:(from)) 1)	
        ) 
        (to 
            (sites Distance 
                (step
                    Orthogonal
                    (to 
                        if:(#1)
                    )
                ) 
                from:(from) 
                (range 1 #2)
            )
            if:(is Empty (to)) 
            (apply 
                (set State at:(from) #3)
            ) 
        )
    )
)

(define "HumanMove"
    (or
        ("Move" (is In (to) "HumanMoveSpots") (state at:(from)) 1)
        ("Move" (is In (to) "RestrictedHumanMoveSpots") 2 1)				// Mountain move, 1 depth
    )
)

(define "VehicleMove"
    (or
        ("Move" (is In (to) "VehicleMoveSpots") (state at:(from)) #1)
        ("Move" (is In (to) "RestrictedVehicleMoveSpots") 2 #1)				// Forest/Shallow water move, 1 depth
    )
)

(define "AircraftMove"
    ("Move" (is In (to) "AircraftMoveSpots") (state at:(from)) 1)
)

(define "BoatMove"
    ("Move" (is In (to) "BoatMoveSpots") (state at:(from)) 1)
)

//------------------------------------------------------------------------------
// Move spots

// Human

(define "ForbiddenSpotsHuman" (sites "DeepWater"))

(define "RestrictedSpotsHuman" (sites "Mountain"))

(define "HumanMoveSpots"
    (difference (union (sites Empty) (sites Occupied by:Mover container:"Board")) (union ("ForbiddenSpotsHuman") ("RestrictedSpotsHuman")))
)

(define "RestrictedHumanMoveSpots"
    (intersection (sites Empty) ("RestrictedSpotsHuman"))
)

// Vehicle

(define "ForbiddenSpotsVehicle" (union (sites "Mountain") (sites "DeepWater")))

(define "RestrictedSpotsVehicle" (union (sites "Forest") (sites "ShallowWater")))

(define "VehicleMoveSpots"
    (difference (union (sites Empty) (sites Occupied by:Mover container:"Board")) (union ("ForbiddenSpotsVehicle") ("RestrictedSpotsVehicle")))
)

(define "RestrictedVehicleMoveSpots"
    (intersection (sites Empty) ("RestrictedSpotsVehicle"))
)

// Aircraft

(define "AircraftMoveSpots"
    (union (sites Empty) (sites Occupied by:Mover container:"Board"))
)

// Boat

(define "ForbiddenSpotsBoat" 
    (union 
        (sites "Mountain") 
        (difference (sites Board) (union (sites "ShallowWater") (sites "DeepWater"))) 
    )
)

(define "BoatMoveSpots"
    (difference 
        (union (sites Empty) (sites Occupied by:Mover container:"Board")) 
        ("ForbiddenSpotsBoat")
    )
)

//------------------------------------------------------------------------------
// Attacks

(define "BuildingAttackFrom"
    (is In (from) (sites Occupied by:Mover container:"Board" components:{"Demolisher"}))
)

(define "GroundAttackFrom"
    (is In (from) (sites Occupied by:Mover container:"Board" components:{"Bomber" "Battleship" "Artillery" "Launcher"}))
)

(define "AircraftAttackFrom"
    (is In (from) (sites Occupied by:Mover container:"Board" components:{"Antiair" "Fighter"}))
)

(define "BoatAttackFrom"
    (is In (from) (sites Occupied by:Mover container:"Board" components:{"Submarine"}))
)

(define "BuildingAttackCheck"
    (if ("BuildingAttackFrom")
        (is In (to) (union (sites Occupied by:Enemy container:"Board" components:{"Base" "Town" "Factory" "Dock" "Airport"}) (sites Occupied by:Neutral container:"Board")))
        (is In (to) (union (sites Occupied by:Enemy container:"Board") (sites Occupied by:Neutral container:"Board"))) 
    )
)

(define "GroundAttackCheck"
    (if ("GroundAttackFrom")
        (is In (to) (union (sites Occupied by:Enemy container:"Board" components:{"Soldier" "Motorbike" "Tank" "Speeder" "Shooter" "Launcher" "Antiair" "Demolisher" "Builder" "Battleship" "Artillery" "Base" "Town" "Factory" "Dock" "Airport" "Cruiser" "Submarine"}) (sites Occupied by:Neutral container:"Board")))
        (is In (to) (union (sites Occupied by:Enemy container:"Board") (sites Occupied by:Neutral container:"Board"))) 
    )
)

(define "BoatAttackCheck"
    (if ("BoatAttackFrom")
        (is In (to) (sites Occupied by:Enemy container:"Board" components:{"Submarine" "Battleship" "Cruiser"}))
        (is In (to) (union (sites Occupied by:Enemy container:"Board") (sites Occupied by:Neutral container:"Board"))) 
    )
)

(define "AircraftAttackCheck"
    (if ("AircraftAttackFrom")
        (is In (to) (sites Occupied by:Enemy container:"Board" components:{"Fighter" "Helicopter" "Bomber"}))
        (is In (to) (union (sites Occupied by:Enemy container:"Board") (sites Occupied by:Neutral container:"Board"))) 
    )
)

//------------------------------------------------------------------------------
// Counter attacks

(define "CounterAttackCheckFrom"
    (is In (#1) (sites Occupied #2 components:#3))
)

(define "GroundCounterAttackCheck"
    (if ("CounterAttackCheckFrom" #1 #2 #5)
        (is In (#3) (union (sites Occupied #4 components:{"Soldier" "Motorbike" "Tank" "Speeder" "Shooter" "Launcher" "Antiair" "Builder" "Demolisher" "Battleship" "Artillery" "Base" "Town" "Factory" "Dock" "Airport" "Cruiser" "Submarine"}) (sites Occupied by:Neutral container:"Board")))
        (is In (#3) (union (sites Occupied #4) (sites Occupied by:Neutral container:"Board"))) 
    )
)

(define "BoatCounterAttackCheck"
    (if ("CounterAttackCheckFrom" #1 #2 #5)
        (is In (#3) (sites Occupied #4 components:{"Submarine" "Battleship" "Cruiser"}))
        (is In (#3) (union (sites Occupied #4) (sites Occupied by:Neutral container:"Board"))) 
    )
)

(define "AircraftCounterAttackCheck"
    (if ("CounterAttackCheckFrom" #1 #2 #5)
        (is In (#3) (sites Occupied #4 components:{"Fighter" "Helicopter" "Bomber"}))
        (is In (#3) (union (sites Occupied #4) (sites Occupied by:Neutral container:"Board"))) 
    )
)

//------------------------------------------------------------------------------
// Attack Moves

(define "AttackMove"
    (move Select
        (from
            (from)
            if:(> (state at:(from)) 0)
        )
        (to
            (sites Distance Orthogonal from:(from) (range #1 #2))
            if:(and {
                ("BuildingAttackCheck")
                ("GroundAttackCheck")
                ("AircraftAttackCheck")
                ("BoatAttackCheck")
            })
        )
        (then 
            (do
                (and 
                    (set Value at:(last To) (max 0 (- (value Piece at:(last To)) ("AttackerDamage" (last From) (last To) False))))
                    (set State at:(last From) 0)
                )
                next:(if (and (= 8 (state at:(last To))) (!= (mover) (who at:(last To)))) 	// state = 8, means its a building
                    // Attacking a building
                    "AttackBuilding"
                    
                    // Attacking a unit
                    (if (= (value Piece at:(last To)) 0)
                        (remove (last To))
                        // counter-attack
                        (if (and {
                                (= 0 (count Sites in:(intersection (sites {(last To) (last From)}) (sites Occupied by:All components:{"Launcher" "Artillery" "Battleship" "Antiair" "Demolisher"}))))
                                ("GroundCounterAttackCheck" (last To) by:Enemy (last From) by:Mover {"Bomber" "Battleship" "Artillery" "Launcher"})
                                ("AircraftCounterAttackCheck" (last To) by:Enemy (last From) by:Mover {"Antiair" "Fighter"})
                                ("BoatCounterAttackCheck" (last To) by:Enemy (last From) by:Mover {"Submarine"})
                            })
                            (if (<= (- (value Piece at:(last From)) ("AttackerDamage" (last To) (last From) True)) 0)							
                                (remove (last From))
                                (set Value at:(last From) (- (value Piece at:(last From)) ("AttackerDamage" (last To) (last From) True)))			
                            ) 
                            (set Value at:(last From) (value Piece at:(last From)))
                        )
                    ) 
                )
            )
        )
    )
)

//------------------------------------------------------------------------------
// Attacker Damage

// #1 location of the attacking piece
// #2 site that is being attacked (terrain purposes)
// #3 location of the piece being attacked
// #4 If this is a counter Attack (boolean)
(define "AttackerDamage"
    (if (is In (#2) "AircraftSites")
        ("DamageMultiply" (* (value Piece at:(#1)) 4) #1)									// Planes are never affected by terrain									
        (if (and
                (is In (#2) "HumanSites")
                (is In (#1) (sites Occupied by:All component:"Shooter"))					// Shooter does double damage against humans
            )
            (if (is In (#2) (sites "Forest"))
                ("DamageMultiply" (* (value Piece at:(#1)) 6) #1)							// (Shooter does 150% damage against humans in forests)
                (if (is In (#2) (union (sites "ShallowWater") (sites "DeepWater")))
                    ("DamageMultiply" (* (value Piece at:(#1)) 10) #1)						// (Shooter does 250% damage against humans in water)
                    ("DamageMultiply" (* (value Piece at:(#1)) 8) #1)						// (Shooter does 200% damage against humans on land)
                )
            )
            (if (is In (#2) (sites "Forest"))
                ("DamageMultiply" (* (value Piece at:(#1)) 3) #1)							// 75% damage to units in forests
                (if (is In (#2) (union (sites "ShallowWater") (sites "DeepWater")))
                    ("DamageMultiply" (* (value Piece at:(#1)) 5) #1)						// 125% damage to units in water
                    ("DamageMultiply" (* (value Piece at:(#1)) 4) #1)						// 100% damage to units on land
                )
            )
        )
    )
)

(define "DamageMultiply"
    (max (/ (* #1 (mapEntry "DamageMultiplier" (what at:(#2)))) 100) 1)
)

//------------------------------------------------------------------------------
// Specific attacks

(define "AttackBuilding"
    (if (= (value Piece at:(last To)) 0)
        (if (is In (last From) (sites Occupied by:All components:{"Demolisher"}))    
            (remove (last To))
            (and
                (take Control of:All by:Mover at:(last To))
                (set Value at:(last To) 0)
            )
        )
        (set Value at:(last To) (value Piece at:(last To)))
    ) 
)

(define "CloseCombatAttack"
    ("AttackMove" 1 1)
)

(define "RangedAttack"
    ("AttackMove" #1 #2)
)

//------------------------------------------------------------------------------
// Healing

(define "HealMove"
    (move
        Select
        (from
            (from)
            if:(and
                (> (state at:(from)) 0)
                (< (value Piece at:(from)) (mapEntry "MaxHealth" (what at:(from))))
            )
        )
        (to 
            (sites Around (from) Orthogonal
                if:(is In (to) "MoverBuildingSites") 		
            )
        )
        (then 
            (and {
                (set Value at:(last From) 
                    (min 
                        (mapEntry "MaxHealth" (what at:(last From))) 
                        (- (+ (value Piece at:(last From)) (value Piece at:(last To))) 0)
                    )
                )
                (set Value at:(last To) 
                    (max 
                        (- (value Piece at:(last To)) (- (mapEntry "MaxHealth" (what at:(last From))) (value Piece at:(last From))))
                        0
                    )
                )
                (set State at:(last From) 0)
            })
        )
    )
)

//------------------------------------------------------------------------------
// Promotion

(define "PlainSites" (difference (sites Board) (union {(sites "Mountain") (sites "Forest") (sites "ShallowWater") (sites "DeepWater") })))

(define "PromoteBuilder"
    (forEach Site (sites Occupied by:Mover container:"Board" component:"Builder")
        (and {
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) ("PlainSites"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Town" "Factory" "Airport" "Anvil" "Mill" "Silo" "Generator"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "Mountain"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Mine"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "Forest"))
                )
                (move
                    Promote
                    (site)
                    (piece {"LumberCamp"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "DeepWater"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Rig"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "ShallowWater"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Dock" "Lighthouse"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "Corn"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Farm"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "Deer"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Lodge"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "Fishes"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Fishing"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "WildHorses"))
                )
                (move
                    Promote
                    (site)
                    (piece {"Barn"}) 
                    Mover
                )
            )
            (if 
                (and
                    (= (state at:(site)) (mapEntry "Movement" (what at:(site))))
                    (is In (site) (sites "Nuclear"))
                )
                (move
                    Promote
                    (site)
                    (piece {"PowerPlant"}) 
                    Mover
                )
            )
        })
    )
)

//------------------------------------------------------------------------------
// Buying units

(define "BuyMove"
    (forEach Site (sites Occupied by:Neutral container:1 #1)
        (move 
            (from (site)) 
            (to 
                #2
                if:(and 
                    (is Empty (to))
                    (<= (count at:(site)) (score Mover))
                )
            )
            copy:True
            (then 
                (and
                    {
                    (addScore 
                        Mover 
                        (- (count at:(last From)))
                    )	
                    (take Control of:All by:Mover at:(last To))
                    (set State at:(last To) 0)
                    }
                )
            )
        )
    )
)

(define "BuyBuilderMove" 
    ("BuyMove" components:{"Builder" "Demolisher"} 
        (difference 
            (sites Around (forEach (sites Occupied by:Mover component:"Base") if:(> (state at:(site)) 1)) Orthogonal)
            "ForbiddenSpotsVehicle"
        )
    )
)

(define "BuyHumanMove" 
    ("BuyMove" components:{"Soldier" "Motorbike"} 
        (difference 
            (sites Around (forEach (sites Occupied by:Mover component:"Town") if:(> (state at:(site)) 1)) Orthogonal)
            "ForbiddenSpotsHuman"
        )
    )
)

(define "BuyVehicleMove" 
    ("BuyMove" components:{"Speeder" "Shooter" "Tank" "Antiair" "Launcher" "Artillery"} 
        (difference 
            (sites Around (forEach (sites Occupied by:Mover component:"Factory") if:(> (state at:(site)) 1)) Orthogonal)
            "ForbiddenSpotsVehicle"
        )
    )
)

(define "BuyAircraftMove" 
    ("BuyMove" components:{"Helicopter" "Fighter" "Bomber"} 
        (sites Around (forEach (sites Occupied by:Mover component:"Airport") if:(> (state at:(site)) 1)) Orthogonal)
    )
)

(define "BuyBoatMove" 
    ("BuyMove" components:{"Cruiser" "Submarine" "Battleship"} 
        (difference 
            (sites Around (forEach (sites Occupied by:Mover component:"Dock") if:(> (state at:(site)) 1)) Orthogonal)
            "ForbiddenSpotsBoat"
        )
    )
)

//------------------------------------------------------------------------------
// Sites

(define "MoverBuildingSites"
    (sites Occupied by:Mover container:"Board" components:{"Town" "Dock" "Base" "Factory" "Airport"})
)

(define "MoverBuilderSites"
    (sites Occupied by:Mover container:"Board" components:{"Builder"})
)

(define "EnemyBuildingSites"
    (union (sites Occupied by:Enemy container:"Board" components:{"Town" "Dock" "Base" "Factory" "Airport"}) (sites Occupied by:Neutral container:"Board" components:{"Town" "Dock" "Base" "Factory" "Airport"}))
)

(define "AircraftSites"
    (sites Occupied by:All container:"Board" components:{"Fighter" "Bomber" "Helicopter"})
)

(define "RangedUnitSites"
    (sites Occupied by:All container:"Board" components:{"Launcher" "Artillery" "Battleship" "Antiair"})
)

(define "HumanSites"
    (sites Occupied by:All container:"Board" components:{"Soldier" "Motorbike"})
)

(define "BoatSites"
    (sites Occupied by:All container:"Board" components:{"Submarine" "Cruiser" "Battleship"})
)

(define "DirectVehicleSites"
    (sites Occupied by:All container:"Board" components:{"Tank" "Speeder" "Shooter"})
)

(define "RangedVehicleSites"
    (sites Occupied by:All container:"Board" components:{"Launcher" "Antiair" "Artillery"})
)

//------------------------------------------------------------------------------\
// Player removal

(define "RemovePiecesIfPlayerHasLost" 
    (if 
        (and {
            (is Active #1) 
            (> (count Active) 2)
            (= (where "Base" #1) Off)
        }) 
        (forEach Site (sites Occupied by:#1)
            (if (is In (site) (sites Occupied by:#1 components:{"Town" "Base" "Factory" "Dock" "Airport"}))
                (take Control of:All by:Neutral at:(site))
                (remove (site))
            )
        )
    )
)

//------------------------------------------------------------------------------
// Player maps

(define "TwoPlayerMaps"
    // state (movement)
    (map "Movement" { 
        (pair 1 8) (pair 2 8) (pair 3 8) (pair 4 8) (pair 5 8) (pair 6 8) (pair 7 8) (pair 8 8) (pair 9 8) (pair 10 8) 
        (pair 11 4) (pair 12 4) (pair 13 6) (pair 14 6) 
        (pair 15 7) (pair 16 7) (pair 17 5) (pair 18 5) (pair 19 5) (pair 20 5) (pair 21 3) (pair 22 3)
        (pair 23 5) (pair 24 5) (pair 25 4) (pair 26 4) (pair 27 4) (pair 28 4)
        (pair 29 5) (pair 30 5) (pair 31 7) (pair 32 7) (pair 33 6) (pair 34 6)
        (pair 35 6) (pair 36 6) (pair 37 6) (pair 38 6) (pair 39 5) (pair 40 5)
        (pair 41 8) (pair 42 8) (pair 43 8) (pair 44 8) (pair 45 8)
        (pair 46 3) (pair 47 3) 
    })
    
    // value (health)
    (map "MaxHealth" { 
        (pair 1 100) (pair 2 100) (pair 3 100) (pair 4 100) (pair 5 100) (pair 6 100) (pair 7 100) (pair 8 100) (pair 9 100) (pair 10 100) 
        (pair 11 30) (pair 12 30) (pair 13 40) (pair 14 40) 
        (pair 15 60) (pair 16 60) (pair 17 60) (pair 18 60) (pair 19 80) (pair 20 80) (pair 21 70) (pair 22 70)
        (pair 23 50) (pair 24 50) (pair 25 50) (pair 26 50) (pair 27 40) (pair 28 40)
        (pair 29 40) (pair 30 40) (pair 31 100) (pair 32 100) (pair 33 100) (pair 34 100)
        (pair 35 70) (pair 36 70) (pair 37 100) (pair 38 100) (pair 39 120) (pair 40 120)
        (pair 41 100) (pair 42 100) (pair 43 100) (pair 44 100) (pair 45 100)
        (pair 46 50) (pair 47 50)
    })
    
    (map "DamageMultiplier" { 
        (pair 1 0) (pair 2 0) (pair 3 0) (pair 4 0) (pair 5 0) (pair 6 0) (pair 7 0) (pair 8 0) (pair 9 0) (pair 10 0) 
        (pair 11 50) (pair 12 50) (pair 13 50) (pair 14 50) 
        (pair 15 40) (pair 16 40) (pair 17 40) (pair 18 40) (pair 19 50) (pair 20 50) (pair 21 100) (pair 22 100)
        (pair 23 100) (pair 24 100) (pair 25 50) (pair 26 50) (pair 27 100) (pair 28 100)
        (pair 29 70) (pair 30 70) (pair 31 100) (pair 32 100) (pair 33 80) (pair 34 80)
        (pair 35 50) (pair 36 50) (pair 37 80) (pair 38 80) (pair 39 50) (pair 40 50)
        (pair 41 0) (pair 42 0) (pair 43 0) (pair 44 0) (pair 45 0) 
        (pair 46 0) (pair 47 0)
    })
)

(define "FourPlayerMaps"
    // state (movement)
    (map "Movement" { 
        (pair 1 8) (pair 2 8) (pair 3 8) (pair 4 8) (pair 5 8) (pair 6 8) (pair 7 8) (pair 8 8) (pair 9 8) (pair 10 8) 
        (pair 11 8) (pair 12 8) (pair 13 8) (pair 14 8) (pair 15 8) (pair 16 8) (pair 17 8) (pair 18 8) (pair 19 8) (pair 20 8) 
        (pair 21 4) (pair 22 4) (pair 23 4) (pair 24 4) 
        (pair 25 6) (pair 26 6) (pair 27 6) (pair 28 6) 
        (pair 29 7) (pair 30 7) (pair 31 7) (pair 32 7) (pair 33 5) (pair 34 5) (pair 35 5) (pair 36 5)
        (pair 37 5) (pair 38 5) (pair 39 5) (pair 40 5) (pair 41 3) (pair 42 3) (pair 43 3) (pair 44 3)
        (pair 45 5) (pair 46 5) (pair 47 5) (pair 48 5) (pair 49 4) (pair 50 4)
        (pair 51 4) (pair 52 4) (pair 53 4) (pair 54 4) (pair 55 4) (pair 56 4)
        (pair 57 5) (pair 58 5) (pair 59 5) (pair 60 5) (pair 61 7) (pair 62 7)
        (pair 63 7) (pair 64 7) (pair 65 6) (pair 66 6) (pair 67 6) (pair 68 6)
        (pair 69 6) (pair 70 6) (pair 71 6) (pair 72 6) (pair 73 6) (pair 74 6)
        (pair 75 6) (pair 76 6) (pair 77 5) (pair 78 5) (pair 79 5) (pair 80 5)
        (pair 81 8) (pair 82 8) (pair 83 8) (pair 84 8) (pair 85 8)
        (pair 86 3) (pair 87 3) (pair 88 3) (pair 89 3)
    })
    
    // value (health)
    (map "MaxHealth" { 
        (pair 1 100) (pair 2 100) (pair 3 100) (pair 4 100) (pair 5 100) (pair 6 100) (pair 7 100) (pair 8 100) (pair 9 100) (pair 10 100)
        (pair 11 100) (pair 12 100) (pair 13 100) (pair 14 100) (pair 15 100) (pair 16 100) (pair 17 100) (pair 18 100) (pair 19 100) (pair 20 100)
        (pair 21 30) (pair 22 30) (pair 23 30) (pair 24 30)
        (pair 25 40) (pair 26 40) (pair 27 40) (pair 28 40)
        (pair 29 60) (pair 30 60) (pair 31 60) (pair 32 60) (pair 33 60) (pair 34 60) (pair 35 60) (pair 36 60)
        (pair 37 80) (pair 38 80) (pair 39 80) (pair 40 80) (pair 41 70) (pair 42 70) (pair 43 70) (pair 44 70)
        (pair 45 50) (pair 46 50) (pair 47 50) (pair 48 50) (pair 49 50) (pair 50 50)
        (pair 51 50) (pair 52 50) (pair 53 40) (pair 54 40) (pair 55 40) (pair 56 40)
        (pair 57 40) (pair 58 40) (pair 59 40) (pair 60 40) (pair 61 100) (pair 62 100)
        (pair 63 100) (pair 64 100) (pair 65 100) (pair 66 100) (pair 67 100) (pair 68 100)
        (pair 69 70) (pair 70 70) (pair 71 70) (pair 72 70) (pair 73 100) (pair 74 100)
        (pair 75 100) (pair 76 100) (pair 77 120) (pair 78 120) (pair 79 120) (pair 80 120)
        (pair 81 100) (pair 82 100) (pair 83 100) (pair 84 100) (pair 85 100)
        (pair 86 50) (pair 87 50) (pair 88 50) (pair 89 50)
    })
    
    (map "DamageMultiplier" { 
        (pair 1 0) (pair 2 0) (pair 3 0) (pair 4 0) (pair 5 0) (pair 6 0) (pair 7 0) (pair 8 0) (pair 9 0) (pair 10 0)
        (pair 11 0) (pair 12 0) (pair 13 0) (pair 14 0) (pair 15 0) (pair 16 0) (pair 17 0) (pair 18 0) (pair 19 0) (pair 20 0)
        (pair 21 50) (pair 22 50) (pair 23 50) (pair 24 50)
        (pair 25 50) (pair 26 50) (pair 27 50) (pair 28 50)
        (pair 29 40) (pair 30 40) (pair 31 40) (pair 32 40) (pair 33 40) (pair 34 40) (pair 35 40) (pair 36 40)
        (pair 37 50) (pair 38 50) (pair 39 50) (pair 40 50) (pair 41 100) (pair 42 100) (pair 43 100) (pair 44 100)
        (pair 45 100) (pair 46 100) (pair 47 100) (pair 48 100) (pair 49 50) (pair 50 50)
        (pair 51 50) (pair 52 50) (pair 53 100) (pair 54 100) (pair 55 100) (pair 56 100)
        (pair 57 70) (pair 58 70) (pair 59 70) (pair 60 70) (pair 61 100) (pair 62 100)
        (pair 63 100) (pair 64 100) (pair 65 80) (pair 66 80) (pair 67 80) (pair 68 80)
        (pair 69 50) (pair 70 50) (pair 71 50) (pair 72 50) (pair 73 80) (pair 74 80)
        (pair 75 80) (pair 76 80) (pair 77 50) (pair 78 50) (pair 79 50) (pair 80 50)
        (pair 81 0) (pair 82 0) (pair 83 0) (pair 84 0) (pair 85 0)
        (pair 86 0) (pair 87 0) (pair 88 0) (pair 89 0)
    })
)

//------------------------------------------------------------------------------

(game "Mini Empires" 
    (players <Scenario:numPlayers>)
    (equipment {
        (board <Scenario:boardShape>)
        
        <Scenario:boardRegins>
        
        (hand Shared size:<constructionUnits:handSize>)
        (hand Each size:9)
        
        (piece "Base" Each maxState:500)
        (piece "Factory" Each)
        (piece "Town" Each)
        (piece "Airport" Each)
        (piece "Dock" Each)
        
        (piece "Soldier" Each (or {"HumanMove" "CloseCombatAttack" "HealMove"}))
        (piece "Motorbike" Each (or {"HumanMove" "CloseCombatAttack" "HealMove"}))
        
        (piece "Speeder" Each (or {("VehicleMove" 1) "CloseCombatAttack" "HealMove"}))
        (piece "Shooter" Each (or {("VehicleMove" 1) "CloseCombatAttack" "HealMove"}))
        (piece "Tank" Each (or {("VehicleMove" 1) "CloseCombatAttack" "HealMove"}))
        (piece "Demolisher" Each (or {("VehicleMove" 1) "CloseCombatAttack" "HealMove"}))
        
        (piece "Antiair" Each (or {("VehicleMove" 1) ("RangedAttack" 1 2) "HealMove"}))
        (piece "Launcher" Each (or {("VehicleMove" 1) ("RangedAttack" 2 3) "HealMove"}))
        (piece "Artillery" Each (or {("VehicleMove" 0) ("RangedAttack" 3 5) "HealMove"}))
        
        (piece "Helicopter" Each (or {"AircraftMove" "CloseCombatAttack" "HealMove"}))
        (piece "Fighter" Each (or {"AircraftMove" "CloseCombatAttack" "HealMove"}))
        (piece "Bomber" Each (or {"AircraftMove" "CloseCombatAttack" "HealMove"}))
        
        (piece "Cruiser" Each (or {"BoatMove" "CloseCombatAttack" "HealMove"}))
        (piece "Submarine" Each (or {"BoatMove" "CloseCombatAttack" "HealMove"}))
        (piece "Battleship" Each (or {"BoatMove" ("RangedAttack" 2 4) "HealMove"}))
        
        (piece "Base" Neutral) 
        (piece "Factory" Neutral)
        (piece "Town" Neutral)
        (piece "Airport" Neutral)
        (piece "Dock" Neutral)
        
        (piece "Builder" Each (or {("VehicleMove" 1) "HealMove"}))
        
        (piece "Soldier" Neutral)
        (piece "Motorbike" Neutral)
        
        (piece "Speeder" Neutral)
        (piece "Shooter" Neutral)
        (piece "Tank" Neutral)
        (piece "Demolisher" Neutral)
        
        (piece "Antiair" Neutral)
        (piece "Launcher" Neutral)
        (piece "Artillery" Neutral)
        
        (piece "Helicopter" Neutral)
        (piece "Fighter" Neutral)
        (piece "Bomber" Neutral)
        
        (piece "Cruiser" Neutral)
        (piece "Submarine" Neutral)
        (piece "Battleship" Neutral)
        
        (piece "Builder" Neutral)
        
        (piece "Family" Each)
        (piece "Meat" Each)
        (piece "Wood" Each)
        (piece "Stone" Each)
        (piece "Steel" Each)
        (piece "TameHorse" Each)
        (piece "Oil" Each)
        (piece "Electricity" Each)
        (piece "Radiation" Each)
        
        (piece "Anvil" Each)
        (piece "Barn" Each)
        (piece "Fishing" Each)
        (piece "Lodge" Each)
        (piece "LumberCamp" Each)
        (piece "Mill" Each)
        (piece "Mine" Each)
        (piece "PowerPlant" Each)
        (piece "Rig" Each)
        (piece "Farm" Each)
        (piece "Silo" Each)
        (piece "Generator" Each)
        (piece "Lighthouse" Each)
        
        <Scenario:playerMaps>
    }) 
    (rules 
        (meta (passEnd NoEnd))
        (start {  
            <Scenario:buildingSetup>
            <Scenario:pieceSetup>
            <Scenario:teams>
            (set Score Each <Scenario:startingMoney>)
            
            (place "Soldier0" (handSite Shared 0) count:30 state:1 value:30)
            (place "Motorbike0" (handSite Shared 1) count:50 state:1 value:40)
            
            (place "Speeder0" (handSite Shared 2) count:60 state:1 value:60)
            (place "Shooter0" (handSite Shared 3) count:80 state:1 value:60)
            (place "Tank0" (handSite Shared 4) count:100 state:1 value:80)
            
            (place "Antiair0" (handSite Shared 5) count:80 state:1 value:50)
            (place "Launcher0" (handSite Shared 6) count:80 state:1 value:50)
            (place "Artillery0" (handSite Shared 7) count:100 state:1 value:40)
            
            (place "Helicopter0" (handSite Shared 8) count:60 state:1 value:40)
            (place "Fighter0" (handSite Shared 9) count:100 state:1 value:100)
            (place "Bomber0" (handSite Shared 10) count:100 state:1 value:100)
            
            (place "Cruiser0" (handSite Shared 11) count:60 state:1 value:70)
            (place "Submarine0" (handSite Shared 12) count:100 state:1 value:100) 
            (place "Battleship0" (handSite Shared 13) count:100 state:1 value:120) 
            <constructionUnits:shopSetup>
            
            (place "Family1" (handSite 1 0) value:30) (place "Family2" (handSite 2 0) value:30)
            (place "Family3" (handSite 3 0) value:30) (place "Family4" (handSite 4 0) value:30)
            (place "Meat1" (handSite 1 1) value:30) (place "Meat2" (handSite 2 1) value:30)
            (place "Meat3" (handSite 3 1) value:30) (place "Meat4" (handSite 4 1) value:30)
            (place "Wood1" (handSite 1 2) value:30) (place "Wood2" (handSite 2 2) value:30)
            (place "Wood3" (handSite 3 2) value:30) (place "Wood4" (handSite 4 2) value:30)
            (place "Stone1" (handSite 1 3) value:30) (place "Stone2" (handSite 2 3) value:30)
            (place "Stone3" (handSite 3 3) value:30) (place "Stone4" (handSite 4 3) value:30)
            (place "Steel1" (handSite 1 4) value:30) (place "Steel2" (handSite 2 4) value:30)
            (place "Steel3" (handSite 3 4) value:30) (place "Steel4" (handSite 4 4) value:30)
            (place "TameHorse1" (handSite 1 5) value:30) (place "TameHorse2" (handSite 2 5) value:30)
            (place "TameHorse3" (handSite 3 5) value:30) (place "TameHorse4" (handSite 4 5) value:30)
            (place "Oil1" (handSite 1 6) value:30) (place "Oil2" (handSite 2 6) value:30)
            (place "Oil3" (handSite 3 6) value:30) (place "Oil4" (handSite 4 6) value:30)
            (place "Electricity1" (handSite 1 7) value:30) (place "Electricity2" (handSite 2 7) value:30)
            (place "Electricity3" (handSite 3 7) value:30) (place "Electricity4" (handSite 4 7) value:30)
            (place "Radiation1" (handSite 1 8) value:30) (place "Radiation2" (handSite 2 8) value:30)
            (place "Radiation3" (handSite 3 8) value:30) (place "Radiation4" (handSite 4 8) value:30)
        })
        
        (play 
            (or 
                (or { 
                    (forEach Piece)
                    ("BuyHumanMove")
                    ("BuyBuilderMove") 
                    ("BuyBoatMove") 
                    ("BuyAircraftMove") 
                    ("BuyVehicleMove") 
                    ("PromoteBuilder")
                    }
                    (then 
                        (and 
                            (forEach Player
                                ("RemovePiecesIfPlayerHasLost" Player)
                            )
                            (moveAgain)
                        )
                    )
                )
                (move Pass
                    (then
                        (and {
                            (forEach Site (sites Occupied by:Mover container:"Board")
                                (set State at:(site) (mapEntry "Movement" (what at:(site))))
                            )
                            (set Score 
                                Mover
                                (+ {
                                    (* (count Sites in:(sites Occupied by:Mover component:"Base")) 20) 
                                    (* (count Sites in:(sites Occupied by:Mover component:"Town")) 10) 
                                    (score Mover) 
                                })
                            )
                            (forEach Site (sites Occupied by:Mover components:{"Town" "Base" "Factory" "Dock" "Airport"})
                                (set Value at:(site) (min (mapEntry "MaxHealth" (what at:(site))) (+ (value Piece at:(site)) 10)))
                            )
                        })
                    )
                )
            )
        )
        
        (end 
            (forEach NonMover 
                if:(= (where "Base" Player) Off) (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Scenario" <Scenario> args:{<playerMaps> <numPlayers> <boardShape> <boardRegins> <buildingSetup> <pieceSetup> <startingMoney> <teams> <pieceFlip> <terrainScale>}
    {
    
    (item "Battle Royale (4-player, 4-teams, hexagon)"
        <
        "FourPlayerMaps"
        >
        <
        4
        >
        <
        (hex 8)
        >
        <
        (regions "ShallowWater" (sites {"G6" "G5" "G4" "F3" "E2" "E1" "D2" "C2" "B1"})) 
        (regions "DeepWater" (sites {"I10" "H10" "I11" "H11" "I12" "J12" "K13" "J13" "K14" "J14"})) 
        (regions "Forest" (sites {"A8" "B9" "C9" "B8" "A7" "D8" "D9" "C8" "B7" "C7" "G9" "F8" "F7" "H6" "I7" "I6" "J7" "K8" "L8" "M9" "A1" "B2" "A2" "A3" "O15" "N14" "O14" "N15" "C1" "D1" "F1" "G1" "H1" "D7" "F9"})) 
        (regions "Mountain" (sites {"H8" "G7" "G8" "H9" "I9" "I8" "H7" "L15" "K15" "J15" "O10" "O9" "N8" "O8" "N7" "M6" "L5" "N9" "J9" "J8" "I15"})) 
        (regions "Corn" (sites {"C3"}))
        (regions "Deer" (sites {"D3"}))
        (regions "Fishes" (sites {"E3"}))
        (regions "WildHorses" (sites {"E4"}))
        (regions "Nuclear" (sites {"E5"}))
        > 
        <
        (place "Base1" (sites {"C5"}) state:8 value:100) 
        (place "Base2" (sites {"M11"}) state:8 value:100)
        (place "Base3" (sites {"J5"}) state:8 value:100) 
        (place "Base4" (sites {"G12"}) state:8 value:100)
        (place "Airport0" (sites {"F4" "K11"}) state:8 value:100)
        (place "Dock0" (sites {"F2" "I13"}) state:8 value:100)
        (place "Town1" (sites {"A5" "A4" "A6"}) state:8 value:100)
        (place "Town2" (sites {"O12" "O13" "O11"}) state:8 value:100)
        (place "Town3" (sites {"J3" "K4" "I2"}) state:8 value:100)
        (place "Town4" (sites {"G14" "F13" "H15"}) state:8 value:100)
        (place "Town0" (sites {"F6" "G10" "K9" "M7" "D4" "C10" "M15" "K7"}) state:8 value:100)
        (place "Factory0" (sites {"B3" "E11" "M13" "H3"}) state:8 value:100)
        >  
        <
        
        >  
        <
        100
        >     
        <
        
        >  
        
        <
        (piece ExtendName P2 "Flip")
        (piece ExtendName P3 "Flip")
        >
        <
        (show Symbol "forest" (sites "Forest") scale:0.8)
        (show Symbol "waves" (sites "ShallowWater") scale:0.9)
        (show Symbol "waves" (sites "DeepWater") scale:0.9)
        (show Symbol "mountain" (sites "Mountain") scale:0.9)
        (show Symbol "corn" (sites "Corn") scale:0.9)
        (show Symbol "deer" (sites "Deer") scale:0.9)
        (show Symbol "fishes" (sites "Fishes") scale:0.9)
        (show Symbol "wildHorses" (sites "WildHorses") scale:0.9)
        (show Symbol "nuclear" (sites "Nuclear") scale:0.9)
        >
        "Scenario - Battle Royale"
    )**
    
    }
)

//------------------------------------------------------------------------------

(option "Construction Units" <constructionUnits> args:{ <handSize> <shopSetup> }
    {
    (item "Allowed"
        <16>
        <
        (place "Builder0" (handSite Shared 14) count:80 state:1 value:50) 
        (place "Demolisher0" (handSite Shared 15) count:80 state:1 value:80)
        >
        "Construction units can be purchased."
    )**
    (item "Not Allowed"
        <14>
        <
        
        >
        "Construction units cannot be purchased."
    )
})

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Mini Wars is a simplified war game.")
        
        (rules " <br>
            //------------------------------------------------------------------------------
            <b>General Rules:</b>
            
            <u>The following rules apply to all units, unless stated otherwise in their description:</u>
            
            - All units move and measure distance orthogonally.
            - Units may move up to their maximum movement limit and can then attack an adjacent site.
            
            - Attacking a unit will deal damage to it (see damage calculation section).
            - If an attacked unit's health reaches zero, it will be removed from the battlefield.
            - Defending units that survive an attack, will then deal counter damage to their attacker.
            
            <u>The following rules apply to all buildings:</u>
            
            - There are five types of building: Town, Factory, Airport, Dock and Base.
            
            - Attacking a building will deal damage to it (see damage calculation section).
            - If an attacked building's health reaches zero, it will change ownership to the player who attacked it. 
            - Buildings that are attacked, will not deal counter damage.
            
            - Units can heal at an adjacent building, instead of attacking.
            - Healing at a building, will convert health from the building to the unit, transferring as much health as possible.
            
            - Additional units can be purchased using gold at their corresponding production building.
            - To purchase a unit, drag it from your hand to an available site next to a production building.
            - Each player's gold is represented by their score.
            - The cost of purchasing a unit is represented by its count.
            - Purchased units cannot move, attack or heal on the same turn they are purchased.
            
            - A player can only use a building (to produce or heal units) if they own it.
            - Each player receives 10 gold for every Town they own and 20 gold for every Base they own, at the start of their turn.
            - Buildings gain 10 health at the end of the owning player's turn, up to a maximum of 100.
            - Grey buildings are neutral, and are not owned by any player.
            
            - If a player loses ownership of all their Bases, they lose the game.
            
            //------------------------------------------------------------------------------
            <b>Units:</b>
            
            <u>Human</u> (Purchased from Towns)
            
            Soldier
            Move: 3
            Health: 30
            Cost: 30
            Damage Factor: 50%
            
            Motorbike
            Move: 5
            Health: 40
            Cost: 50
            Damage Factor: 50%
            
            <u>Vehicle</u> (Purchased from Factories)
            
            Speeder
            Move: 6
            Health: 60
            Cost: 60
            Damage Factor: 40%
            
            Shooter
            Move: 4
            Health: 60
            Cost: 80
            Damage Factor: 40%
            - Deals double damage against Humans.
            
            Tank
            Move: 4
            Health: 80
            Cost: 100
            Damage Factor: 50%
            
            Anti-Aircraft
            Move: 4
            Health: 50
            Cost: 80
            Damage Factor: 100%
            - Can only attack Aircraft.
            - Can attack units 1-2 spaces away.
            - Does not deal or receive counter damage during an attack.
            
            Missile Launcher
            Move: 3
            Health: 50
            Cost: 80
            Damage Factor: 50%
            - Cannot attack Aircraft.
            - Can only attack units 2-3 spaces away.
            - Does not deal or receive counter damage during an attack.
            
            Artillery
            Move: 3
            Health: 40
            Cost: 100
            Damage Factor: 100%
            - Cannot attack Aircraft.
            - Can only attack units 3-5 spaces away.
            - Does not deal or receive counter damage during an attack.
            - Cannot attack or heal in the same turn that it moved.
            
            <u>Aircraft</u> (Purchased from Airports)
            
            Helicopter
            Move: 4
            Health: 40
            Cost: 60
            Damage Factor: 70%
            
            Fighter Jet
            Move: 6
            Health: 100
            Cost: 100
            Damage Factor: 100%
            - Can only attack Aircraft.
            
            Bomber
            Move: 5
            Health: 100
            Cost: 100
            Damage Factor: 80%
            - Cannot attack Aircraft.
            
            <u>Boat</u> (Purchased from Docks)
            
            Cruiser
            Move: 5
            Health: 70
            Cost: 60
            Damage Factor: 50%
            
            Submarine
            Move: 5
            Health: 100
            Cost: 100
            Damage Factor: 80%
            - Can only attack Boats.
            
            Battleship
            Move: 4
            Health: 120
            Cost: 100
            Damage Factor: 50%
            - Cannot attack Aircraft.
            - Can only attack units 2-4 spaces away.
            - Does not deal or receive counter damage during an attack.
            
            <u>Construction</u> (Purchased from Base)
            
            Builder
            Move: 2
            Health: 50
            Cost: 80
            Damage Factor: 0%
            - Cannot attack or deal counter damage.
            - Can be (permanently) converted into a Town, Factory, Dock or Airport with zero health.
            - Can only be converted to a building if on a plain tile.
            - Cannot be converted to a building in the same turn that it moved or healed.
            - Units cannot be purchased from a building on the same turn it was created.
            
            Demolisher
            Move: 2
            Health: 70
            Cost: 80
            Damage Factor: 100%
            - Can only attack Buildings.
            - Does not deal counter damage when attacked.
            - Buildings that are reduced to zero health by a demolisher attack are destroyed instead of captured.
            
            //------------------------------------------------------------------------------
            <b>Movement Restrictions:</b>
            
            Humans may travel across plains, forests, and shallow water. They may also move 1 space into mountains instead of their regular movement.
            Vehicles and Construction units may travel across plains. They may also move 1 space into forests or shallow water instead of their regular movement.
            Aircraft may travel across all spaces.
            Boats may travel across shallow and deep water.
            Buildings cannot move (obviously).
            Units cannot move over enemy or neutral occupied spaces.
            
            //------------------------------------------------------------------------------
            <b>Damage Calculation:</b>
            
            An attacking unit deals damage equal to its health, multiplied by its damage factor. 
            For example, A submarine has a damage factor of 80%, so a submarine with 20 health will deal 16 damage when it attacks (20 x 0.8).
            If the defending unit is in shallow or deep water, it takes 25% more damage.
            If the defending unit is in a forest, it takes 25% less damage.
            Aircraft are unaffected by terrain when calculating damage against them.
            All damage values are rounded down to the nearest whole number.
            
            //------------------------------------------------------------------------------
        ")
        
        (version "1.1.14")
        (classification "board/war/other")
        (credit "Matthew Stephenson")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Yellow))
        (player Colour P3 (colour Blue))
        (player Colour P4 (colour Green))
        
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (board Colour Symbols (colour Black))
        (piece Background image:"square" fillColour:(colour 255 255 255 1) edgeColour:(colour 255 255 255 1))
        (show Piece Value Top offsetImage:true valueOutline:true)
        
        <Scenario:pieceFlip>
        
        (piece Scale "Factory" 0.55)
        (piece Scale "Base" 0.55)
        (piece Scale "Town" 0.55)
        (piece Scale "Dock" 0.55)
        (piece Scale "Airport" 0.55)
        
        (piece Scale "Antiair" 0.7)
        (piece Scale "Cruiser" 0.7)
        (piece Scale "Motorbike" 0.7)
        (piece Scale "Soldier" 0.6)
        (piece Scale "Builder" 0.7)
        (piece Scale "Demolisher" 0.65)
        (piece Scale 0.8)
        
        <Scenario:terrainScale>
        
        (board Colour Phase0 (colour 208 240 192))
        (region Colour (sites Occupied by:All container:"Board" components:{"Town" "Dock" "Base" "Factory" "Airport"}) (colour Grey))
        (region Colour "Forest" (colour 0 100 0))
        (region Colour "ShallowWater" (colour 173 216 230))
        (region Colour "DeepWater" (colour 28 98 215))
        (region Colour "Mountain" (colour 195 155 119))
    })
    
)
