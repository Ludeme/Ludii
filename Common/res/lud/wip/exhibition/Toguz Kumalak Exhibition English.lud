(define "Columns" 9)

(define "Tuz" (value Player #1))

(define "CaptureFromTuzOf" 
    (if (and (> ("Tuz" #1) 0) (> (count at:("Tuz" #1)) 0))
        (fromTo (from ("Tuz" #1)) (to (mapEntry #1)) count:(count at:("Tuz" #1)))
    )
)

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Toguz Kumalak Exhibition" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 "Columns"
            (track "Track" "1,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))       // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)}) // kalahs (storage pits)
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start (set Count 9 to:(sites Track)))
        
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow 
                        apply:(if 
                            (and 
                                (is In (to) (sites Next)) 
                                (is Even (count at:(to)))
                            ) 
                            (fromTo 
                                (from (to)) 
                                (to (mapEntry (mover))) 
                                count:(count at:(to))
                            )
                            (if (and { 
                                    (is In (to) (sites Next)) 
                                    (= (count at:(to)) 3) 
                                    (< ("Tuz" Mover) 0) 
                                    (if (< ("Tuz" Next) 0)
                                        True
                                        (!= (+ (to) ("Tuz" Next)) 19)
                                    )
                                }) 
                                (and
                                    (set Value Mover (to))
                                    (fromTo 
                                        (from (to)) 
                                        (to (mapEntry (mover))) 
                                        count:(count at:(to))
                                    )
                                )
                            )
                        )
                        origin:(!= (count at:(last From)) 1)
                        (then
                            (and
                                ("CaptureFromTuzOf" P1)
                                ("CaptureFromTuzOf" P2)
                            )
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (rules "​​These are the standard rules for Tokuz Korgool. Counters are dropped one-by-one in a counter clockwise direction. When a counter falls into an opponent's hole with an odd number of counters, these are captured. When a player drops a counter into an opponent's hole with two counters, the player now owns this hole. Every counter dropped into it is captured. The player who captures the most counters wins.")
        }
    )
    
    (graphics {
        (show PlayerHoles)
        (board Style Mancala)
    })
    
    (ai 
        "Toguz Kumalak_ai"
    )
    
)
