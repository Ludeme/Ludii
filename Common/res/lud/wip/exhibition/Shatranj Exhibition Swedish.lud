(game "Shatranj Exhibition" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8)) 
        ("ChessPawn" "Pawn" ~ (then ("ReplayInMovingOn" (sites Mover "Promotion"))))
        ("ChessRook" "Rook")
        ("ChessKing" "King_noCross")
        (piece "Bishop_noCross" Each 
            (move Hop 
                Diagonal 
                (between if:True) 
                (to 
                    if:(or 
                        (is Empty (to)) 
                        ("IsEnemyAt" (to))
                    ) 
                    (apply (remove (to)))
                )
            )
        )
        ("ChessKnight" "Knight")
        (piece "Ferz_noCross" Each ("StepToNotFriend" Diagonal)) 
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Bishop_noCross1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Bishop_noCross2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        
        (play 
            (if "SameTurn"
                (move Promote (last To) (piece "Ferz_noCross") Mover)
                (do (forEach Piece) ifAfterwards:(not ("IsInCheck" "King_noCross" Mover)))
            )
        )
        
        (end {
            ("Checkmate" "King_noCross")
            (if (= (count Pieces Next) 1) (result Mover Win)) 
            ("BlockWin")
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (rules "De här reglerna finns dokumenterade i medeltida arabiska manuskript. >Spelet spelas på samma sätt som Schack. Drottningen kallas för rådgivaren i det här spelet och kan bara flytta en ruta diagonalt. Biskopen kallas för elefant och hoppar två rutor diagonalt. En elefant kan inte fånga en annan elefant.")
        }
    )
    
    (graphics {
        (piece Scale "Pawn" 0.825)
        (show Check "King")
        (board Style Chess)
        (board Placement scale:0.8 offsetY:-0.25)
    })
    
    (ai 
        "Shatranj_ai"
    )
    
)
