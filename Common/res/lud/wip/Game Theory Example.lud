(game "Game Theory Example"
    (players 2)
    (equipment {
        (board 
            (graph
                vertices:{ {0 0} {-1 -1} {1 -1} {-2 -2} {0 -2} {-2.5 -3} {-1.5 -3} {-0.5 -3} {0.5 -3}}
                edges:{ {0 1} {0 2} {1 3} {1 4} {3 5} {3 6} {7 4} {4 8}}
            )
            use:Vertex
        )
        (piece "Marker" Shared 
            (move Step 
                (to 
                    if:(< (row of:(to)) (row of:(from)))
                )
                (then
                    (if (no Moves Next) (moveAgain))
                )
            )
        )
        (map "P1" {(pair 2 3) (pair 5 0) (pair 6 2) (pair 7 6) (pair 8 5)})
        (map "P2" {(pair 2 4) (pair 5 1) (pair 6 0) (pair 7 0) (pair 8 2)})
    })
    (rules
        (start (place "Marker" 0))
        (play 
            (if ("NewTurn")
                (forEach Piece Shared)
                (move Pass
                    (then 
                        (and
                            (set Score P1 (mapEntry "P1" (where "Marker" Shared)))
                            (set Score P2 (mapEntry "P2" (where "Marker" Shared)))
                        )
                    )
                )
            )
        )
        (end (if (no Moves Next) (payoffs {(payoff P1 (toFloat (score P1))) (payoff P2 (toFloat (score P2)))})))
    )
)
