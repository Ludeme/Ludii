(game "Sujjua"
    (players 2)
    (equipment {
        (board
            (concentric Square rings:3)
            use:Vertex
        )
        (piece "Marker" Each
            (move
                Step Orthogonal
                (to if:(is Empty (to)))
            )
        )
        (hand Each)
    })
    (rules
        (start
            (place "Marker" "Hand" count:9)
        )
        (play
            (or
                (if (not ("HandEmpty" Mover))
                    (move
                        (from (handSite Mover))
                        (to (sites Empty))
                    )
                )
                (forEach Piece)
            )
        )
        (end (if (is Line 3 Orthogonal) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Sujjua is a game of alignment played in India. In the Karwi subdivision, it is played in the meeting place of the village. It is also found as graffiti on a ruined temple nearby, leaving the possibility open that the game is much older.")
        (rules "Three concentric squares with lines connecting their midpoints. Nine pieces per player. Players alternate turns placing pieces on the board or moving one piece on the board to an adjacent empty spot. The first player to get three of their pieces in a row wins.")
        (source "Humphries 1906: 124-125.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, from around 1904 to 1905.")
        }
    )

    (graphics {

    })
    (ai
        "Sujjua_ai"
    )
)

