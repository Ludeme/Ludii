(game "Sujjua"
    (players 2)
    (equipment {
        ("NineMensMorrisBoard")
        (piece "Marker" Each ("StepToEmpty" Orthogonal))
        (hand Each)
    })
    (rules 
        (start (place "Marker" "Hand" count:9))
        (play
            (or
                (if ("HandOccupied" Mover)
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty))
                    ) 
                )
                (forEach Piece)
            )
        )
        (end ("Line3Win" Orthogonal))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sujjua is a game of alignment played in India. In the Karwi subdivision, it is played in the meeting place of the village. It is also found as graffiti on a ruined temple nearby, leaving the possibility open that the game is much older.")
        (rules "Three concentric squares with lines connecting their midpoints. Nine pieces per player. Players alternate turns placing pieces on the board or moving one piece on the board to an adjacent empty spot. The first player to get three of their pieces in a row wins.")
        (source "Humphries 1906: 124-125.")
        (id "723")
        (version "1.3.10")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in South Asia, from around 1904 to 1905.")
        }
    )
    
    (ai
        "Sujjua_ai"
    )
)

