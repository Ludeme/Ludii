(game "Dris at-Talata"
    (players 2)
    (equipment {
        (board (square 3))
        (hand Each)
        (piece "Marker" Each)
    })
    (rules
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                )
            )
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play 
                (move 
                    (from (sites Occupied by:Mover))
                    (to (sites Empty))
                )
            )
        )
        }
        
        (end ("Line3Win" Orthogonal))
    )	
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Dris at-Talata is a simple game of alignment played in Egypt during the eighteenth century, but likely to be much older.")
        (aliases {"Dris-ath-Thalatha"})
        (rules "3x3 grid. Three pieces per player. Players alternate turns placing a piece on the board. When all of the pieces are placed, players alternate turns moving a piece to any empty space on the board. The player who places three pieces in an orthogonal  row wins. ")
        (source "Niebuhr 1774: 171-172.")
        (id "1057")
        (version "1.3.12")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Egypt, around 1762.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Dris at-Talata_ai"
    )
)
