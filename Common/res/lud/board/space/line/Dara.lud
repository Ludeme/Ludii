(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach (sites Occupied by:Next) if:(not (is Line 3 Orthogonal through:(site))))
    )
)

(define "IfLine3OrthogonalMoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Dara"
    (players 2)
    (equipment
        {
        (board (rectangle 5 6))
        (hand Each)
        (piece "Marker" Each
            (move 
                Step Orthogonal
                (to if:(is Empty (to))) 
                "IfLine3OrthogonalMoveAgain" 
            )
        )
        }
    )
    (rules
        (start 
            {
            (place "Marker" "Hand" count:12) 
            }
        )
        
        phases:{
        (phase "Placement"
            <Rules>
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (do 
                        (forEach Piece)
                        ifAfterwards:(not (is Line 4 Orthogonal))
                    )
                )
            )
        )
        }
        
        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
    )	
)

//------------------------------------------------------------------------------

(option "Rules" <Rules> args:{<type>}
    {
    (item "Basic" <(play (do (move (from (handSite Mover)) (to (sites Empty))) ifAfterwards:(not (is Line 4 Orthogonal))))>	"Lines of 4 cannot be made during the drop phase.")**
    (item "Niger" <(play (do (move (from (handSite Mover)) (to (sites Empty))) ifAfterwards:(not (is Line 3 Orthogonal))))>	"Lines of 3 cannot be made during the drop phase.")
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Dara is a game that is said to be played in West Africa, particularly among the Dakarkari people of Nigeria and the Zarma of Niger. It is also known as \"dill,\" \"doki\" (horse), or \"derrah.\" It is described on Wikipedia and other websites, but without reference to where the cultural information was obtained.")
        (aliases {"Dill" "Doki" "Derrah."})
        (rules "5x6 board. Players have 12 pieces each. They take turns placing pieces on an empty board.Once all pieces are placed, players can move pieces orthogonally to an adjacent space, attempting to make three in a row. When three in a row are made, an opponent's piece is removed from the board that is not in a three in a row pattern. Placing more than three in a row is not allowed. Three in a row during the initial phase of the game does not count. When a player has only two pieces left, they lose.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Dara_(game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wkipedia</a>")
        (id "89")
        (version "1.3.3")
        (classification "board/space/line")
        (credit "Matthew Stephenson")
        }
    )
    
    (ai
        "Dara_ai"
    )
)
