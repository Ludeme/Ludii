(define "RemoveEnemyPieceNotInLine3"
    (move
        Remove
        (forEach
            (sites Occupied by:Next container:"Board")
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "RemoveEnemyPiece"
    (if (can Move ("RemoveEnemyPieceNotInLine3"))
        ("RemoveEnemyPieceNotInLine3")
        (move
            Remove
            (sites Occupied by:Next container:"Board")
        )
    )
)

(define "IfLine3MoveAgain"
    (then
        (if
            (is Line 3 Orthogonal exact:True)
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Nine Men's Morris"
    (players 2)

    (equipment {
        (board (concentric Square rings:3) use:Vertex)
        (hand Each)
        (piece "Marker" Each
            (move
                Step
                (to if:(is Empty (to)))
                "IfLine3MoveAgain"
            )
        )
    })
    (rules
        (start (place "Marker" "Hand" count:9))

        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move
                        (from (handSite Mover))
                        (to (sites Empty))
                        "IfLine3MoveAgain"
                    )
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )

        (phase "Movement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (if (> (count Pieces Mover) 3)
                        (forEach Piece)
                        (move
                            (from (sites Occupied by:Mover))
                            (to (sites Empty))
                        )
                    )
                )
            )
        )
        }

        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Popular since at least the Roman Empire, Nine Men's Morris is an alignment game that is still played today. It appears in texts from Roman authors such as Ovid, and potentially may date to Hellenistic Greece.")
        (aliases {"Mills" "Mill Game" "Meres" "Merelles" "Morelles" "Marelles" "Ninepenny Marl" "Cowboy Checkers"})
        (rules "Played on a board of three concentric squares, with a line bisecting the perimeters of each square on each side, but not extending inside the perimeter of the central square. Play occurs on the intersections of the lines and the corners of the squares. Each player has nine pieces. Play begins with each player placing pieces on empty points. If they make three in a row along the lines, they can remove one of the opponent's pieces. They cannot remove an opponent's piece that is in a three-in-a-row formation unless there are no other options. Once all pieces are placed, players take turns moving pieces one spot to an adjacent point along the lines. If a player makes three in a row, an opponent's piece is removed as in the first phase of the game. Once a player is reduced to three pieces, that player may move to any open space on the board. The game is won when the opponent is reduced to two pieces.")
        (source "Murray 1951: 43. ")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Unknown, from around 650BCE to 1325.")
        }
    )

    (ai
        "Nine Men's Morris_ai"
    )

)
