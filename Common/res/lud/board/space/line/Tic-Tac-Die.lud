(define "PlayerPiece" 
    (piece 
        (if (is Mover P1) 
            (id "Disc" P1) 
            (id "Cross" P2)
        )
    )
)

//------------------------------------------------------------------------------

(game "Tic-Tac-Die" 
    (players 2) 
    (equipment { 
        (board (square 3)) 
        (piece "Disc" P1) 
        (piece "Cross" P2) 
        (dice d:9 from:0 num:1)
    }) 
    (rules 
        (play 
            ("RollMove" 
                (if (is Empty (count Pips))
                    (move Add ("PlayerPiece") (to (count Pips)))
                    <Play:choice>
                )
            )
        )
        (end {
            ("Line3Win")
            (if (is Full) (result Mover Draw))
        })
    )
)

//------------------------------------------------------------------------------

(option "Play Rules" <Play> args:{ <choice>} {
    (item "No Choice" 
        <(move Pass (then (moveAgain)))> 
        "Players reroll until they roll an empty cell."
    )* 
    (item "Some Choice" 
        <(move Add ("PlayerPiece") (to (sites Empty)))> 
        "Player chooses if the rolled cell is occupied."
    ) 
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Tic-Tac-Die is played as per Tic-Tac-Toe except that players roll a D9 dice each turn to dictate where they move (dice pips show cell index to move to).")
        (rules "Play occurs on a 3x3 grid. One player places an X, the other places an O and players take turns placing their marks in the grid, attempting to get three in a row. The die is showing the cell index to place a piece.")
        (id "1144")
        (version "1.3.9")
        (classification "board/space/line")
        (author "Cameron Browne")
        (credit "Eric Piette")
        (date "11-12-2020")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Tic-Tac-Die_ai"
    )
)
