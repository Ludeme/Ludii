(define "IfLine3MoveAgain"
    (then
        (if
            (is Line 3)
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Tauru"
    (players 2)
    (equipment
        {
        (board (rectangle 5 6))
        (hand Each)
        (piece "Marker" Each
            (move
                Step Orthogonal
                (to if:(is Empty (to)))
                "IfLine3MoveAgain"
            )
        )
        }
    )
    (rules
        (start
            {
            (place "Marker" "Hand" count:12)
            }
        )

        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    (move
                        Remove
                        (sites Occupied by:Next container:"Board")
                    )
                    (move
                        (from (handSite Mover))
                        (to (sites Empty))
                        "IfLine3MoveAgain"
                    )
                )
            )
            (nextPhase ("HandEmpty" P2) "Movement")
        )

        (phase "Movement"
            (play
                (if "SameTurn"
                    (move
                        Remove
                        (sites Occupied by:Next container:"Board")
                    )
                    (forEach Piece)
                )
            )
        )
        }

        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Tauru is an alignment game played by the Bariba people in what is now Benin. It is closely related to the game Dara, but instead of the player to capture the most pieces, the player to make the last possible capture wins. It is played by adult men, but at the time it was recorded was increasingly played by young men who were Wasangari, or in line to rule.")
        (aliases {"Taburu" "Wali"})
        (rules "5x6 board. Each player has twelve pieces. Players alternate placing their pieces until they are all placed on the board. The goal is to make a line of three, which allows the player to capture one of the opponent's pieces. Once the pieces are all on the board, they may be moved one space orthogonally. In the case that a move creates two lines of three, only one capture is made. The player who makes the last possible capture wins.
        ")
        (source "BÃ©art 1955: 456.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Benin, around 1955.")
        }
    )

    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    (ai
        "Tauru_ai"
    )
)
