(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
       )
   )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3) 
            (moveAgain)
       )
   )
)

//------------------------------------------------------------------------------

(game "Tavan Tal"
    (players 2)
    
    (equipment 
        { 
        (board
            (add
                (merge {
                    (shift 2 2
                        (merge {
                            (rectangle 1 3)
                            (shift -1.31 0.95 (rotate 108 (rectangle 1 3)))
                            (shift -0.81 2.49 (rotate 216 (rectangle 1 3)))
                            (shift 1.31 0.95 (rotate -108 (rectangle 1 3)))
                            (shift 0.81 2.49 (rotate -216 (rectangle 1 3)))
                        })
                   )
                    
                    (shift 1 1
                        (scale 2
                            (merge {
                                (rectangle 1 3)
                                (shift -1.31 0.95 (rotate 108 (rectangle 1 3)))
                                (shift -0.81 2.49 (rotate 216 (rectangle 1 3)))
                                (shift 1.31 0.95 (rotate -108 (rectangle 1 3)))
                                (shift 0.81 2.49 (rotate -216 (rectangle 1 3)))
                            })
                       )
                   )
                    
                    (scale 3
                        (merge {
                            (rectangle 1 3)
                            (shift -1.31 0.95 (rotate 108 (rectangle 1 3)))
                            (shift -0.81 2.49 (rotate 216 (rectangle 1 3)))
                            (shift 1.31 0.95 (rotate -108 (rectangle 1 3)))
                            (shift 0.81 2.49 (rotate -216 (rectangle 1 3)))
                        })
                   )
                    
                })
                edges:{
                {26 16} {16 6} {5 15} {15 25} {29 19} {19 9} {7 17} {17 27} {28 18} {18 8} {22 12} {12 2}
                {1 11} {11 21} {20 10} {10 0} {3 13} {13 23} {24 14} {14 4}
                }
           )
            use:Vertex
       ) 
        (hand Each)
        (piece "Marker" Each 
            (move 
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain" 
           )
       ) 
        }
   )  
    (rules 
        (start (place "Marker" "Hand" count:14))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                   )  
               )
           ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
       )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
               )
           )
       )
        }
        
        (end (if (no Pieces Next) (result Mover Win)))
   )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tavan Tal is an alignment game played in Mongolia on a pentagonal board.")
        (rules "Three concentric pentagons, with lines connecting the corners and the midpoints of each side. Fourteen pieces per player. Players alternate turns placing one of their pieces on an empty spot on the board. When all of the pieces are placed, they alternate turns moving one of their pieces to an empty adjacent spot along the lines of the board. During either phase, when a player places three of their pieces in a row, they may remove one of the opponent's pieces from the board. Pieces which are in a three-in-a-row arrangement cannot be removed from the board. The player who captures all of the opponent's pieces wins.
        ")
        (source "Popova 1974: 26-32.")
        (id "1029")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
   )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    (ai
        "Tavan Tal_ai"
   )
)

