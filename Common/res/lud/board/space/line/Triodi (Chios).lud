(define "Surrounded"
    (or
        (if (and (!= (ahead (from) N) (from)) (!= (ahead (from) S) (from)))
            (and
                (= (next) (who at:(ahead (from) N)))
                (= (next) (who at:(ahead (from) S)))
            )
        )
        (if (and (!= (ahead (from) E) (from)) (!= (ahead (from) W) (from)))
            (and
                (= (next) (who at:(ahead (from) E)))
                (= (next) (who at:(ahead (from) W)))
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Triodi (Chios)"
    (players 2)

    (equipment
        {
        (board (square 3) use:Vertex)
        (hand Each)
        (piece "Marker" Each
            (if
                (not ("Surrounded"))
                (move
                    Step
                    (to if:(is Empty (to)))
                )
            )
        )
        }
    )
    (rules
        (start (place "Marker" "Hand" count:3))

        phases:{
        (phase "Placement"
            (play
                (move
                    (from (handSite Mover))
                    (to (sites Empty))
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )

        (phase "Movement" (play (forEach Piece) ))
        }

        (end (if (is Line 3) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Triodi is a name for alignment games in Greece. This game is played by children on the island of Chios.")
        (rules "3x3 intersecting lines. Three pieces per player. Players alternate turns placing a piece on the board. When all of the pieces have been placed, the players alternate turns moving a piece to an empty adjacent spot. A piece which is surrounded on opposite sides by pieces belonging to the opponent cannot be moved. The player who places three of their stones in a row along one of the lines wins.")
        (source "Argenti and Rose 1949: 1018-1019.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Greece, around 1949.")
        }
    )

    (graphics {

        (show Edges Diagonal Hidden)
    })

    (ai
        "Triodi (Chios)_ai"
    )
)
