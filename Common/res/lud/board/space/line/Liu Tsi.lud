(game "Liu Tsi"
    (players 2) 
    (equipment {
        ("AlquerqueBoard" 3 3)
        (hand Each)
        (piece "Marker" Each "StepToEmpty")
    }) 
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        ("PhaseMovePiece" "Movement")
        }
        (end ("Line3Win"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Liu Tsi is a simple game of alignment played in China during the seventeenth century, but which is likely much older.")
        (aliases {"Luk Tsut K'i" "Lo Che"})
        (rules "3x3 intersecting lines, with diagonals. Three pieces per player. Players alternate turns placing a piece on an empty spot on the board. Once all of the pieces are placed, the players alternate turns moving a piece to an empty adjacent spot along the lines. The first player to place three pieces in a line wins.")
        (source "Murray 1951: 42.")
        (id "949")
        (version "1.3.8")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in China, around 1694.")
        }
    )
    
    (ai
        "Liu Tsi_ai"
    )
)
