(game "Engijn Zirge"
    (players 2)  
    (equipment 
        {
        ("AlquerqueBoard" 3 3)
        (hand Each)
        (piece "Marker" Each "StepToEmpty")
        }
    )  
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))) )
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
        )
        }
        (end (if (is Line 3) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Engijn Zirge is a simple game of alignment played in Mongolia.")
        (rules "3x3 intersecting lines, with diagonals. Three pieces per player. Players alternate turns placing pieces on the board. Once all of the pieces have been placed, the players alternate turns moving a piece to an empty adjacent spot on the board. The first player to place their pieces in a row along one of the lines wins.")
        (source "Popova 1974: 26-31.")
        (id "1022")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )
    
    (ai
        "Engijn Zirge_ai"
    )
)
