(define "FromHand" 
    (from (sites Occupied by:Mover container:(mover))) 
)

//------------------------------------------------------------------------------

(game "Teeko" 
    (players 2) 
    (equipment { 
        (board (square 5)) 
        (hand Each size:8)
        (piece "Marker" Each ("StepToEmpty"))
    }) 
    (rules 
        (start { 
            (place "Marker" "Hand" count:8)
        })
        
        phases:{
        (phase "Placement"
            (play (move ("FromHand") (to (sites Empty))))
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        ("PhaseMovePiece" "Movement")
        }
        
        (end 
            (if 
                (or {
                    (is Pattern {F R F R F})
                    (is Line 4)
                })
                (result Mover Win)
            )
        ) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The rules, as summarized above, are very simple, but the strategy is complicated enough to fill a book, Scarne on Teeko, by Scarne (1955).
        ")
        (rules "Players take turn laying their eight circles upon the board, then enter a phase of movement, the sum goal being either a line of four or a 2x2 block.
        ")
        (id "4186")
        (source "<a href=\"https://en.wikipedia.org/wiki/Teeko\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.14")
        (classification "board/space/line")
        (author "John Scarne")
        (credit "Noah Morris")
        (date "1937")
        }
    )
)
