(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:True) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Altan Xaraacaj"
    (players 2) 
    
    (equipment { 
        ("AlquerqueBoard" 5 5) 
        (hand Each)
        (piece "Marker" Each 
            (move
                Step
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain"
            )
        ) 
    })  
    (rules 
        (start (place "Marker" "Hand" count:6))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain"
                    )  
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
                )
            )
        )
        }
        
        (end 
            (if 
                (no Pieces Next) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Altan Xaraacaj (Golden swallows) is an alignment game played in Mongolia. One player plays as golden swallows (the black pieces) and the other plays as spotted swallows (the white pieces).")
        (aliases {"Altan Kharaatsai"})
        (rules "5x5 intersecting lines, with diagonals in each quadrant. Six pieces per player. Players alternate turns placing one of their pieces on an empty spot on the board. When all of the pieces are placed, they alternate turns moving one of their pieces to an empty adjacent spot along the lines of the board. During either phase, when a player places three of their pieces in a row, they may remove one of the opponent's pieces from the board. Pieces which are in a three-in-a-row arrangement cannot be removed from the board. The player who captures all of the opponent's pieces wins. ")
        (source "Popova 1974: 26-31.")
        (id "1023")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1991.")
        }
    )
    
    (ai
        "Altan Xaraacaj_ai"
    )
)

