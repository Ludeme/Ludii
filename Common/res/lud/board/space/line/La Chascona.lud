(define "RemoveEnemyPiece" 
    (if (can Move ("RemoveAnyNextPieceNotInLine3" Orthogonal) )
        ("RemoveAnyNextPieceNotInLine3" Orthogonal) 
        ("RemoveAnyNextPiece")
    )
)

//------------------------------------------------------------------------------

(game "La Chascona"
    (players 2)
    
    (equipment { 
        (board (concentric Square rings:3) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            ("StepToEmpty" ~
                (then ("ReplayIfLine3" Orthogonal exact:True)) 
            )
        ) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:9))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        (then ("ReplayIfLine3" Orthogonal exact:True)) 
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (forEach Piece)
                )
            )
        )
        }
        
        (end (if (or (no Moves Next) (<= (count Pieces Next) 2)) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "La Chascona is an alignment game played on Fuertaventura in the Canary Islands. It is commonly played by women, who have a reputation for being masters of the game. It is also played by shepherds, who played with pieces that were not easily distinguished between one another, but the players were able to keep track of their own pieces.")
        (rules "Three concentric squares, with lines connecting the midpoints of the sides. Nine pieces per player. Players alternate turns placing a piece on the board. When a player places three of their pieces in a line of three along one of the lines on the board, they capture one of the opponent's pieces. When all of the pieces have been placed, players alternate turns moving one of their pieces to an adjacent empty spot on the board. Players cannot capture one of their opponent's pieces which is in a three-in-a-row alignment. The player who either captures seven of their opponent's pieces or blocks the opponent from being able to move wins.")
        (source "Espinel Cejas and González García 1994: 210-212.")
        (id "1944")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Canary Islands, around 1994.")
        }
    )
    
)
