(game "Fart (7x7)"
    (players 2)
    (equipment {
        (board (square 7))
        (piece "Marker" Each 
            (move Slide Orthogonal)
       )
        (hand Each)
    })
    (rules
        (start (place "Marker" "Hand" count:24))
        phases:{
        (phase "Placement"
            (play
                (move 
                    (from (handSite Mover))
                    (to (difference (sites Board) (centrePoint)) if:(is Empty (to)))
                    (then
                        (if (not ("SameTurn")) (moveAgain))
                   )
               )
           )
            (nextPhase Mover (is Empty (handSite Mover)) "Move")
       )
        (phase "Move"
            (play (forEach Piece))	
       )
        }
        (end (if (is Line 7) (result Mover Win)))
   )	
)

//--------------------------------------

(metadata 
    (info
        {
        (description "Fart is an alignment game played by young people in El Oued, Algeria. This version requires the player to place seven of their pieces in a row.")
        (rules "7x7 board. 24 pieces per player. Players alternate turns placing two pieces on the board. When all of the pieces are placed, players alternate turns moving one piece orthogonally any distance. The first player to place seven of their pieces in an orthogonal or diagonal line wins.
        ")
        (source "Bellin 1964: 55-56.")
        (id "1576")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Algeria, around 1964.")
        }
   )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
)
