(define "RemoveEnemyPieceNotInLine3"
    (move
        Remove
        (forEach
            (sites Occupied by:Next container:"Board")
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain"
    (then
        (if
            (is Line 3)
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Xonin Shatar (Complex)"
    (players 2)

    (equipment
        {
        (board
            (add
                (remove
                    (merge {
                        (shift 3 3 (remove (square 3) edges:{{1 4} {3 4} {4 7} {4 5}}))
                        (shift 2 2 (scale 2 (remove (square 3) edges:{{1 4} {3 4} {4 7} {4 5}})))
                        (scale 4 (remove (square 3) edges:{{1 4} {3 4} {4 7} {4 5}}))
                    })
                    vertices:{4}
                )
                edges:{
                {17 19} {17 20} {20 22} {22 19}
                {17 9} {19 11} {22 14} {12 20}
                {21 13} {23 15} {18 10} {16 8}
                {11 9} {9 12} {12 14} {14 11}
                {9 1} {11 3} {6 14} {4 12}
                {0 8} {5 13} {7 15} {10 2}
                }
            )
            use:Vertex
        )
        (hand Each)
        (piece "Marker" Each
            (move
                Step
                (to if:(is Empty (to)))
                "IfLine3MoveAgain"
            )
        )
        }
    )
    (rules
        (start (place "Marker" "Hand" count:12))

        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move
                        (from (handSite Mover))
                        (to (sites Empty))
                        "IfLine3MoveAgain"
                    )
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )

        (phase "Movement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
                )
            )
        )
        }

        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Xonin Shatar is an alignment game played in Mongolia. This version is played on a more complex board, allowing more movement options.")
        (rules "Three concentric squares, with lines connecting the corners and the midpoints of the sides. Two diamonds drawn, one connecting the midpoints of the outer square and intersecting the corners of the second square; the second connecting the midpoints of the second square and intersecting the corners of the central square. Twelve pieces per plays. Players alternate turns placing a piece on an empty spot on the board. When all of the pieces are placed, players alternate turns moving a piece to an empty adjacent spot along the lines. During either phase, when a player places three of their pieces in a row, they remove one of the opponent's pieces. Pieces which are in a three-in-a-row pattern cannot be removed. The player who removes all of the opponent's pieces wins.")
        (source "Popova 1974: 26-32.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )

    (graphics {
        (show Edges Diagonal Hidden)

    })
    (ai
        "Xonin Shatar (Complex)_ai"
    )
)

