(game "Marelle Quadruple"
    (players 2) 
    (equipment {
        ("AlquerqueBoard" 5 5)
        (hand Each)
        (piece "Marker" Each "StepToEmpty")
    }) 
    (rules 
        (start (place "Marker" "Hand" count:5))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        ("PhaseMovePiece" "Movement")
        }
        (end (if (is Line 5) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Marelle Quadruple is an alignment game played in nineteenth century France.")
        (rules "5x5 intersecting lines, with diagonals in the quadrants. Five pieces per player. Players alternate turns placing a piece on the board. Once all of the pieces are placed on the board, players alternate turns moving a piece to an empty adjacent spot on the board. The player who places all five of their pieces in a line wins.")
        (source "de Moulidars 1840: 100.")
        (id "1510")
        (version "1.3.7")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in France, around 1840.")
        }
    )
    
    (ai
        "Marelle Quadruple_ai"
    )
)
