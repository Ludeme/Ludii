(game "Yavalanchor" 
    (players 2) 
    (equipment { 
        (board (rotate 90 (hex 5))) 
        (piece "Marker" Each)
        (piece "Marker" Shared)
        (hand Shared)
    }) 
    
    (rules 
        (meta (swap))
        (start (place "Marker" (handSite Shared)))
        (play 
            (or
                (move Add 
                    (to 
                        (sites Around 
                            (sites Occupied by:Shared component:"Marker") 
                            if:(is Empty (to))
                        )
                    )
                ) 
                (move 
                    (from (handSite Shared)) 
                    (to (sites Empty)) 
                    copy:True
                )
            )
        )
        (end 
            (forEach Player
                if:(and 
                    (or 
                        (= (what at:(last To)) (id "Marker" Player)) 
                        (= (what at:(last To)) (id "Marker")) 
                    )
                    (is Line 5 whats:{(id "Marker" Player) (id "Marker")})
                )
                (result Player Win)
            )
        )
    )
)

(metadata
    
    (info
        {
        (description "Yavanalanchor is a Yavalath derivative invented by Néstor Romeral Andrés in 2012.")
        (rules "Two players play, with white and black pieces, with neutral red pieces as well. Players take turns placing either a red counters on an empty space or a counter of their color on a space adjacent to a red one. Play continues until one player creates five in a row with only their color and red. The player that does so first wins.")
        (source "<a href=\"https://boardgamegeek.com/boardgame/126653/yavalanchor\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (id "453")
        (version "1.3.11")
        (classification "board/space/line")
        (author "Néstor Romeral Andrés")
        (publisher "<a href=\"https://www.nestorgames.com\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a> (used with permission)")
        (credit "Eric Piette")
        (date "2012")
        }
    )
    
    (graphics {
        (player Colour All (colour Red))
    })
    
    (ai 
        "Yavalanchor_ai"
    )
    
)
