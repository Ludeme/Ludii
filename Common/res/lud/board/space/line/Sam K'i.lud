(define "PlaceOnTop" 
    (move
        (from (handSite Mover))
        (to
            (forEach 
                (sites Occupied by:Next container:"Board")
                if:(= (size Stack at:(site)) 1)
            )
        )
    )
)

(define "RemoveDeadPieces" 
    (forEach Site
        (forEach 
            (sites Occupied by:#1 container:"Board")
            if:(= (size Stack at:(site)) 2)
        )
        (and {(remove (site)) (remove (site)) (add (piece (id "Marker" #1)) (to (site)))})
    )
)

//------------------------------------------------------------------------------

(game "Sam K'i"
    (players 2)
    
    (equipment { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            ("StepToEmpty" ~ (then ("ReplayIfLine3" if:("IsSingletonStack" (to)))))
        ) 
    }) 
    (rules 
        (start {
            (place Stack "Marker1" (handSite P1) count:12)
            (place Stack "Marker2" (handSite P2) count:12)
        })
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "PlaceOnTop"
                    (if ("HandOccupied" Mover)
                        (move 
                            (from (handSite Mover)) 
                            (to (sites Empty)) 
                            (then ("ReplayIfLine3" if:("IsSingletonStack" (to)))) 
                        ) 
                    )
                    (then 
                        (if (and (not (is Next Mover)) (and ("HandEmpty" P1) ("HandEmpty" P2)))
                            (and ("RemoveDeadPieces" P1) ("RemoveDeadPieces" P2))
                        )
                    )
                )
            ) 
            (nextPhase (and (not (is Next Mover)) (and ("HandEmpty" P1) ("HandEmpty" P2))) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveAnyEnemyPiece"
                    (if (> (count Pieces Mover) 3)
                        (forEach Piece)
                        (move 
                            (from (sites Occupied by:Mover))
                            (to (sites Empty))
                        )
                    )
                )
            )
            (end ("BlockWin"))
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sam K'i is an alignment game played in China since at least the nineteenth century. It differs from some other games of this type during the placement phase, when a player can place another piece on top of a piece they capture from the opponent. ")
        (rules "Three concentric squares, with lines connecting the corners and the midpoints of the sides. Twelve pieces per player. Players alternate turns placing a piece on the board. When a player places three pieces in a row along the lines of the board, the player places another of their pieces on top of one of the opponent's pieces; the opponent's piece is considered \"dead.\" Once all of the pieces have been placed on the board, all \"dead\" pieces are removed from the board. Players then alternate moving pieces to an empty adjacent spot along the lines. When three are placed in a row, one of the opponent's pieces is taken. A player wins by capturing all of the opponent's pieces or by blocking them from being able to move.")
        (source "Culin 1895: 102.")
        (id "953")
        (version "1.3.8")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in China, around 1895.")
        }
    )
    
    (graphics {
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)
