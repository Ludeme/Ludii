(define "RemoveEnemyPiece" 
    (move
        Remove
        (sites Occupied by:Next container:"Board") 
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:True) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Nao Guti"
    (players 2)
    
    (equipment 
        { 
        (board (concentric Square rings:3) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            (move
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain" 
            )
        ) 
        }
    )  
    (rules 
        (start (place "Marker" "Hand" count:9))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                    )  
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (forEach Piece)
                )
            )
        )
        }
        
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Nao Guti is a game of alignment played in South Asia, with nine pieces on a board that is widely used throughout the world.")
        (rules "Three concentric squares, with lines connecting the midpoints. Nine pieces per player. Players alternate turns placing a piece on the board on an empty spot. When all of the pieces have been placed, the players alternate turns moving a piece to an empty adjacent spot along the lines of the board. During either phase, if a player places three of their pieces in a row along the lines on the board, the player removes one of the opponent's pieces. The player who removes all of the opponent's peices wins.")
        (source "Das Gupta 1926: 144-145.")
        (id "941")
        (version "1.3.3")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, around 1926.")
        }
    )
    
    (graphics {
        
    })
    
    (ai
        "Nao Guti_ai"
    )
)
