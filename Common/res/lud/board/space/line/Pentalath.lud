(define "HasFreedom" (> (count Liberties Orthogonal) 0))

(define "CaptureSurroundedPiece"
    (enclose
        (from (last To))
        (between
            if:(is Enemy (who at:(between)))
            (apply (remove (between) ) )
        )
    )
)

//------------------------------------------------------------------------------

(game "Pentalath"
    (players 2)
    (equipment {
        (board
            <Board:type>
        )
        (piece "Marker" Each)
        }
    )

    (rules
        (play
            (do
                (move
                    Add
                    (to (sites Empty))
                    (then "CaptureSurroundedPiece")
                )
                ifAfterwards:"HasFreedom"
            )
        )
        (end (if (is Line 5) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{<type>}
    {
    (item "HalfHexHexBoard"
        <
        (intersect
            {
            (hex 7)
            (shift -5.20 0 (hex Rectangle 7 14))
            }
        )
        >
        "The game is played on the half of a HexHex board of size 7."
    )*
    (item "HexHexBoard"  <(hex 5)> "The game is played on a HexHex board of size 5.")
    }
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Pentalath was invented by Cameron Browne in 2009. It is a connection game with the goal of creating a group of five in a row, but with complications.")
        (rules "The board starts empty. White plays first. Players take turns placing a piece of their colour on an empty cell. Passing is not allowed. After each move, the opponent's pieces with no freedom are captured and removed from the board. A piece has freedom if the group it belongs to touches an empty cell. It is not allowed to place a piece in a space without freedom, unless that move captures the other player's pieces to create freedom. The game ends when one of the players wins a game by making a line of five (or more) stones of their colour.")
        (source "<a href=\"https://boardgamegeek.com/boardgame/51401/pentalathm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.0")
        (classification "board/space/line")
        (author "Cameron Browne")
        (publisher "<a href=\"https://www.nestorgames.com\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a> (used with permission)")
        (credit "Eric Piette")
        (date "2009")
        }
    )

    (ai
        "Pentalath_ai"
    )

)
