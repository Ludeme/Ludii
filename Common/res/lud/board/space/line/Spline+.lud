(game "Spline+" 
    (players 2) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each) 
    }) 
    (rules
        (meta {(noStackOn Fallen) (pin SupportMultiple) (gravity)})
        (play
            (or
                (move Add 
                    (to (sites Empty) 
                        if:(is Flat)
                    )
                )
                (move
                    (from (sites Occupied by:Mover))
                    (to (sites Empty) 
                        if:(is Flat) //and not on fallen ball
                    )
                )
            )
        )
        (end {
            (if 
                (and (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 1) (= (layer of:(to)) 0))) 4) (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 2) (= (layer of:(to)) 0))) 4))
                (result Mover Win)
            )
            (if 
                (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 1) (= (layer of:(to)) 0))) 4) 
                (result P1 Win)
            )
            (if 
                (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 2) (= (layer of:(to)) 0))) 4) 
                (result P2 Win)
            )
            
            //----------------
            
            (if 
                (and (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 1) (= (layer of:(to)) 1))) 3) (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 2) (= (layer of:(to)) 1))) 3))
                (result Mover Win)
            )
            (if 
                (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 1) (= (layer of:(to)) 1))) 3) 
                (result P1 Win)
            )
            (if 
                (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 2) (= (layer of:(to)) 1))) 3) 
                (result P2 Win)
            )
            
            //----------------
            
            (if 
                (and (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 1) (= (layer of:(to)) 2))) 2) (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 2) (= (layer of:(to)) 2))) 2))
                (result Mover Win)
            )
            (if 
                (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 1) (= (layer of:(to)) 2))) 2) 
                (result P1 Win)
            )
            (if 
                (= (count SizeBiggestLine SameLayer if:(and (= (who at:(to)) 2) (= (layer of:(to)) 2))) 2) 
                (result P2 Win)
            )
            
            //----------------
            
            (if
                (= (count Pieces in:(sites Occupied by:All container:"Board")) 29)
                (result All Draw)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Spline+ was evolved from Spline by adding ball movement. It's a ball-stacking game that can be played with a Series: Shibumi set.")
        (rules "The board starts empty. Starting with white, players take turns either: placing a ball of their colour on an empty space or platform (2×2 arrangement of marbles) or moving a movable ball of their colour already on the board to another valid location. If supported balls drop as a consequence of the ball removal, you can't place your ball on top of the dropped balls. Movable balls are balls that are not pinned. A pinned ball is a ball that supports 2 or more balls on the level above.")
        (id "4231")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a>")
        (version "1.3.14")
        (classification "board/space/line")
        (author "Nestor Romeral Andrés")
        (credit "Cédric Antoine")
        (date "2011")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
    })
)
