(game "Tsoro Yemutatu (Triangle)"  
    (players 2)  
    (equipment { 
        (board
            (scale 1 2 (wedge 3))
            use:Vertex
        )
        (hand Each) 
        (piece "Marker" Each (move (from) (to (sites Empty))))
    })  
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))) )
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
        )
        }
        (end (if (is Line 3) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tsoro Yemutatu is an alignment game played by boys, young men, and sometimes adults in Zimbabwe. This version is played on a triangular board, drawn on the ground and played with stones.")
        (rules "Triangle, with a line from the apex bisecting the base, and a line bisecting this one and the opposite sides. Three pieces per player. Players alternate turns placing a piece on one of the empty points on the board. When all of the pieces have been placed, players alternate turns moving any one of their pieces to the empty point on the board. The first player to make a line of three wins. ")
        (source "Matthews 1964: 68.")
        (id "1563")
        (version "1.3.3")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Zimbabwe, around 1964.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
)
