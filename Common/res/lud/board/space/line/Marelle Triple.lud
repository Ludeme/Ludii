
(define "RemoveEnemyPiece"
    (move
        Remove
        (sites Occupied by:Next container:"Board")
    )
)

(define "IfLine3MoveAgain"
    (then
        (if
            (or {
                (is Line 3 N exact:True) (is Line 3 S exact:True)
                (is Line 3 E exact:True) (is Line 3 W exact:True)
            })
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Marelle Triple"
    (players 2)

    (equipment
        {
        (board (concentric Square rings:3 joinCorners:True) use:Vertex)
        (hand Each)
        (piece "Marker" Each
            (move
                Step
                (to if:(is Empty (to)))
                "IfLine3MoveAgain"
            )
        )
        }
    )
    (rules
        (start (place "Marker" "Hand" count:9))

        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move
                        (from (handSite Mover))
                        (to (sites Empty))
                        "IfLine3MoveAgain"
                    )
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )

        (phase "Movement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (if (> (count Pieces Mover) 4)
                        (forEach Piece)
                        (move
                            (from (sites Occupied by:Mover))
                            (to (sites Empty))
                        )
                    )
                )
            )
        )
        }

        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Marelle Triple is a game of alignment played in France. Described in the nineteenth century, it is played on a board common worldwide, with slight variations in the rules.")
        (rules "Three concentric squares, the corners and midpoints of the squares connected with lines. Nine pieces per player. Players alternate turns placing a piece on an empty spot on the board. When all of the pieces are placed, the players alternate turns moving a piece to an empty adjacent spot along the lines. During either phase, when a player places three of their pieces in a row along the orthogonal lines (not along the diagonal lines), the player removes any one of the opponent's pieces. When a player is reduced to four pieces, they may move a piece to any open spot on the board. The player who reduces the opponent to two pieces wins.
        ")
        (source "de Moulidars 1840: 99-100.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in France, around 1840.")
        }
    )

    (graphics {

    })

    (ai
        "Marelle Triple_ai"
    )
)
