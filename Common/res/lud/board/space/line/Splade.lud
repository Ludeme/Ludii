(game "Splade" 
    (players 3) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each) 
    }) 
    (rules 
        (play 
            (move Add 
                (to (sites Empty) 
                    if:(is Flat)
                )
            )
        )
        (end {
            (if 
                (is Line (- (count Rows) (layer of:(last To))) SameLayer whats:{(id "Ball" Mover) (id "Ball" Next)} throughHowMuch:2)
                (result Next Win)
            )
            (if 
                (is Line (- (count Rows) (layer of:(last To))) SameLayer whats:{(id "Ball" Mover) (id "Ball" Prev)} throughHowMuch:2)
                (result Mover Win)
            )
            (forEach NonMover
                if:(= (count Active) 2) 
                (result Player Loss)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Splade is an evolution of Spline for 3 players that can be played with a Series: Shibumi set.")
        (rules "The board starts empty. Starting with white (then black and red), players take turns placing a marble of their colour on an empty space or platform (2×2 arrangement of marbles). Whenever a full line with exactly 2 colors is formed, the game ends. The winner is the player with a color in that line whose position in the player order is immediately after the other player with a color in that line.")
        (id "4233")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a>")
        (version "1.3.14")
        (classification "board/space/line")
        (author "Nestor Romeral Andrés")
        (credit "Cédric Antoine")
        (date "2011")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour P1 fillColour:(colour White))
        (piece Colour P2 fillColour:(colour Black))
        (piece Colour P3 fillColour:(colour Red))
    })
)
