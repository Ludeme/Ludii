(game "Ring"
    (players 2) 
    (equipment { 
        (board (concentric {1 8}) use:Vertex)
        (hand Each)
        (piece "Marker" Each "StepToEmpty") 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (difference (sites Empty) (sites Centre)))
                ) 
            )
            (nextPhase Mover "PlacementAdjacent")
        )
        
        (phase "PlacementAdjacent"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (difference (sites Empty) (sites Centre))
                        if:(is In (to) (sites Around (sites Occupied by:Mover)))
                    )
                ) 
            )
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement" (play (forEach Piece)))
        }
        
        (end (if (is Line 3 through:(centrePoint) what:(mover)) (result Mover Win))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Ring is a game played by the Warlpiri people of Australia in the mid-twentieth century. Old men described it has always having been played in the town. It was originally reported as a likely imported game, but the ubiquity of alignment games worldwide and the fact that alignment games on circular boards are rare elsewhere show that the game is probably an indigenous development.")
        (rules "A circle with eight radii. Three pieces per player. Each player has three pieces, which must start on the outer ring adjacent to each other. Players alternate turns moving a piece to an empty point along the lines of the board. The player who makes a line of three through the center of the circle wins.")
        (source "Meggitt 1958: 194-194.")
        (id "1974")
        (version "1.3.5")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Australia, from around 1953 to 1954.")
        }
    )
    
)
