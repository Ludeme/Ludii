(define "NumTiles" 24)

//------------------------------------------------------------------------------

(game "Bravalath"
    (players 2)
    (equipment {
        (boardless <Tiling:type>)
        (tile <Tiling:tile> Each <Tiling:numSides>)
        }
    )

    (rules
        (start {
            (place <Tiling:piece1> (centrePoint))
            (place <Tiling:piece2> (ahead (centrePoint) E))
        })

        (play
            (move
                Add
                (to
                    (sites Playable)
                    if:(<= 1
                        (count Sites in:(sites Around (to) NotEmpty))
                    )
                )
            )
        )

        (end {
            (if (is Line 4) (result Mover Win))
            (if (is Line 3) (result Mover Loss))
        })
    )
)

//------------------------------------------------------------------------------

(option "Tiling" <Tiling> args:{ <type> <tile> <numSides> <piece1> <piece2> }
    {
    (item "Hexagonal"      <Hexagonal>  <"Hex">   <numSides:6>   <"Hex1">      <"Hex2">     "The game uses hexagonal tiles.")**
    (item "Square"         <Square>     <"Square"> <numSides:4>  <"Square1">   <"Square2">    "The game uses square tiles")
    (item "Triangle"       <Triangular>     <"Triangle"> <numSides:3>  <"Triangle1">   <"Triangle2">    "The game uses triangle tiles")
})

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Invented after 1 second's thought.")
        (rules "Start with a tile of each colour placed next to each other. Players take turns adding a tile of their colour touching at least one existing tile. Win by making a line of four (or more) of your colour, but lose by making a line of three of your colour beforehand.")
        (version "1.3.0")
        (classification "board/space/line")
        (author "Cameron Browne")
        (credit "Cameron Browne (8/12/2020)")
        (date "2020")
        }
    )

    (graphics {
        (player Colour P1 (colour 250 240 127))
    })
    (ai
        "Bravalath_ai"
    )
)
