(game "Score Four" 
    (players 2) 
    (equipment { 
        (board (skew .5 (scale 1 .5 (square 4))))
        (piece "discFlat" Each)
    }) 
    (rules
        (play 
            (move Add 
                (to 
                    (forEach 
                        (sites Board) 
                        if:(< (size Stack at:(site)) 4)
                    )
                ) 
                stack:True
            )
        )
        (end (if (is Line 4 byLevel:True) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Score Four is a 3D version of Connect Four on a 4x4x4 board.")
        (rules "Place a piece of your colour each turn. Win with 4 in a row of your colour.")
        (id "656")
        (source "<a href=\"https://boardgamegeek.com/boardgame/3656/score-four\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.10")
        (classification "board/space/line")
        (author "H. Heller and A. P. Nienstaedt")
        (credit "Cameron Browne 26/6/2020")
        (date "1967")
        }
    )
    (ai
        "Score Four_ai"
    )
)
