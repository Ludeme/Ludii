(define "RemoveEnemyPiece" 
    (move Remove
        (sites Occupied by:Next container:"Board") 
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:True) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Fettas"
    (players 2)
    
    (equipment { 
        (board (concentric Square rings:3) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            (move
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain" 
            )
        ) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:11))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    ("RemoveEnemyPiece")
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    ("RemoveEnemyPiece")
                    (forEach Piece)
                )
            )
        )
        }
        
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Fettas is an alignment game played in Morocco. It was described by Moroccan immigrants in France, who described games played during their childhood.")
        (rules "Three concentric squares, with lines connecting the midpoints of the sides. Eleven pieces per player. Players alternate turns placing a piece on the board. When a player lines up three of their pieces in a row along one of the lines of the board, the player removes one of the opponent's pieces. When all of the pieces are placed, players move their pieces one spot along the lines of the board. The player who captures all of the opponent's pieces wins.")
        (source "Bouhajbane 1989: 28-29.")
        (id "1935")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Morocco, from around 1960 to 1982.")
        }
    )
    
)
