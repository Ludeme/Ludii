(game "Triodi"
    (players 2)
    
    (equipment { 
        ("NineMensMorrisBoard")
        (hand Each)
        (piece "Marker" Each 
            ("StepToEmpty" ~ (then ("ReplayIfLine3" Orthogonal exact:True)))
        ) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:9))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveAnyEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        (then ("ReplayIfLine3" Orthogonal exact:True)) 
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveAnyEnemyPiece"
                    (forEach Piece)
                )
            )
        )
        }
        
        (end ("HavingLessPiecesLoss" Next 2))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Triodi is an alignment game played in Greece. Boards for this game have been found in Greece going back to antiquity, and it was still played there in the early twentieth century.")
        (aliases {"Triodion"})
        (rules "Three concentric squares, the midpoints of the squares connected with lines. Nine pieces per player. Players alternate turns placing a piece on an empty spot on the board. When all of the pieces are placed, the players alternate turns moving a piece to an empty adjacent spot along the lines. During either phase, when a player places three of their pieces in a row along the lines, the player removes any one of the opponent's pieces. The player who reduces the opponent to two pieces wins.")
        (source "Abbott 1903: 295-297.")
        (id "955")
        (version "1.3.7")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Greece, from around 1900 to 1901.")
        }
    )
    
    (ai
        "Triodi_ai"
    )
)
