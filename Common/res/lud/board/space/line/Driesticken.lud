(game "Driesticken"
    (players 2) 
    (equipment {
        (board (square 3) use:Vertex)
        (hand Each)
        (piece "Marker" Each (move (from) (to (sites Empty))))
    }) 
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        ("PhaseMovePiece" "Movement")
        }
        (end ("Line3Win" Orthogonal))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Driesticken is a simple game of alignment played in the Netherlands in the seventeenth century. ")
        (rules "3x3 intersecting lines. Three pieces per player. Players alternate turns placing pieces on an empty spot on the board. When all pieces are placed, players alternate turns moving a piece to any empty spot on the board. The first player to place their pieces in an orthogonal row along the lines wins.
        ")
        (source "Murray 1951: 39.")
        (id "952")
        (version "1.3.5")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Netherlands, around 1694.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Driesticken_ai"
    )
)
