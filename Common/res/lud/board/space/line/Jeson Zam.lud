(define "RemoveEnemyPieceNotInLine3"
    (move
        Remove
        (forEach
            (sites Occupied by:Next container:"Board")
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain"
    (then
        (if
            (is Line 3)
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Jeson Zam"
    (players 2)

    (equipment
        {
        (board
            (add
                (remove
                    (add
                        (merge {
                            (rotate 30 (tri 2))
                            (shift 0.2 0 (rotate -30 (tri 2)))
                        })
                        vertices:{{0.6 0.3}} // 6
                    )
                    edges:{{0 2} {0 1} {1 2} {3 5} {5 4} {4 3}}
                )
                vertices:{
                {0.6 0.625} // 7
                {0.915 0.3} // 8
                {0.285 0.3} // 9
                {0.6 -0.01} // 10
                {0.285 0.1725} // 11
                {0.285 0.4375} // 12
                {0.915 0.4375} // 13
                {0.915 0.1725} // 14
                {0.75 0.075} // 15
                {0.45 0.075} // 16
                {0.75 0.535} // 17
                {0.45 0.535} // 18
                }
                edges:{
                {1 13} {13 17} {17 7} {7 2}
                {2 12} {12 9} {9 11} {11 0}
                {5 13} {13 8} {8 14} {14 4}
                {1 14} {14 15} {15 10} {10 0}
                {5 7} {7 18} {18 12} {12 3}
                {3 11} {11 16} {16 10} {10 4}
                {0 16} {16 6} {6 17} {17 5}
                {4 15} {15 6} {6 18} {18 2}
                {3 9} {9 6} {6 8} {8 1}
                }
            )
            use:Vertex
        )
        (hand Each)
        (piece "Marker" Each
            (move
                Step
                (to if:(is Empty (to)))
                "IfLine3MoveAgain"
            )
        )
        }
    )
    (rules
        (start (place "Marker" "Hand" count:9))

        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move
                        (from (handSite Mover))
                        (to (difference (sites Empty) (sites Centre)))
                        "IfLine3MoveAgain"
                    )
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )

        (phase "Movement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
                )
            )
        )
        }

        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Jesön Zam (The nine ways) is an alignment game played in Mongolia. The game is described as boys with white hair dancing with boys with black hair.")
        (aliases {"Jesön Zam"})
        (rules "Two triangles, which form a six-pointed star, with lines connecting the corner of one triangle with the corner of the other triangle which is opposite it. Nine pieces per player. Players alternate turns placing a piece on an empty point on the board. Players cannot place a piece in the central point during this phase. When all of the pieces are placed, players alternate turns moving a piece to an empty adjacent point on the board. During either phase, when a player places three of their pieces in a row, they remove one of the opponent's pieces. When three pieces are in a three-in-a-row pattern, they cannot be removed. The player who removes all of the opponent's pieces from the board wins.")
        (source "Popova 1974: 26-32.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )

    (graphics {
        (show Edges Diagonal Hidden)

    })
    (ai
        "Jeson Zam_ai"
    )
)

