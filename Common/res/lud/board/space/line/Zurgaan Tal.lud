(game "Zurgaan Tal"
    (players 2)
    
    (equipment { 
        (board
            (rotate 30 (concentric Hexagon rings:3 joinCorners:True))
            use:Vertex
        ) 
        (hand Each)
        (piece "Marker" Each 
            ("StepToEmpty" ~ (then ("ReplayIfLine3")))
        ) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:17))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    ("RemoveAnyEnemyPieceNotInLine3" Orthogonal)
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        (then ("ReplayIfLine3")) 
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    ("RemoveAnyEnemyPieceNotInLine3" Orthogonal)
                    (forEach Piece)
                )
            )
        )
        }
        
        (end ("CaptureAll" Next))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Zurgaan Tal is a game of alignment from Mongolia played on a hexagonal board.")
        (rules "Three concentric hexagons, with lines connecting the corners and midpoints of the sides. Seventeen pieces per player. Players alternate turns placing one of their pieces on an empty spot on the board. When all of the pieces are placed, they alternate turns moving one of their pieces to an empty adjacent spot along the lines of the board. During either phase, when a player places three of their pieces in a row, they may remove one of the opponent's pieces from the board. Pieces which are in a three-in-a-row arrangement cannot be removed from the board. The player who captures all of the opponent's pieces wins.")
        (source "Popova 1974: 26-32.")
        (id "1030")
        (version "1.3.5")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    (ai
        "Zurgaan Tal_ai"
    )
)

