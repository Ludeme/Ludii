(game "Djara-Badakh"
    (players 2)  
    
    (equipment { 
        (board (square 3) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            (move
                Step 
                (to if:(is Empty (to))) 
           )
       ) 
    })
    
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty)) 
               )  
           ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
       )
        
        (phase "Movement" 
            (play 
                (forEach Piece)
           )
       )
        }
        
        (end 
            (if 
                (is Line 3)
                (result Mover Win)
           )
       )
   )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Djara-Badakh is a simple game of alignment played by children in Somalia.")
        (rules "3x3 intersecting lines. Three pieces per player. Players alternate turns placing a piece on an empty spot on the board. When all of the pieces are placed, players alternate turns moving a piece to an empty adjacent spot. The first player to place their three pieces along one of the lines wins.")
        (source "Marin 1931: 505.")
        (id "792")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Horn of Africa, around 1931.")
        }
   )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    (ai
        "Djara-Badakh_ai"
   )
)

