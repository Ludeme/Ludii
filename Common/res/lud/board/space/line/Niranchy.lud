(game "Niranchy"
    (players 2)
    
    (equipment { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each ("StepToEmpty")) 
    }) 
    (rules 
        (start (place "Marker" "Hand" count:12))
        
        phases:{
        (phase "Placement"
            (play
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty)) 
                ) 
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        ("PhaseMovePiece" "Movement")
        }
        
        (end {
            (if (no Moves <Players:player1>) (result <Players:player2> Win))
            (if (is Line 3 <Players:player1>) (result <Players:player1> Win))
        })
    )
)

//------------------------------------------------------------------------------

(option "Player Order" <Players> args:{ <player1> <player2> }
    {
    (item "Player 1 wins by blocking" <P2> <P1> "Player 1 wins by blocking.")* 
    (item "Player 2 wins by blocking" <P1> <P2> "Player 2 wins by blocking.") 
    }
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Niranchy is a game of alignment played in Sri Lanka during the nineteenth century. It was a common game played by people of all ages, often played in breaks between \"intervals of business.\"")
        (aliases {"Nerenchi"})
        (rules "Three concentric squares, with lines connecting the midpoints and diagonals of the squares. Twelve pieces per player. Players alternate turns placing one of their pieces on the board. When all of the pieces are placed, the pieces may be moved to an empty adjacent spot on the board. One player wins  by placing three of their pieces in a row. The other player wins by blocking their opponent from being able to move.")
        (source "Ludovisi 1873: 34, Diagram C.")
        (id "740")
        (version "1.3.13")
        (classification "board/space/line")
        (credit "Matthew Stephenson")
        (origin "This game was played in Sri Lanka, around 1873.")
        }
    )
    
    (ai
        "Niranchy_ai"
    )
)
