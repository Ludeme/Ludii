(game "Symbol S.2" 
    (players 2) 
    
    (equipment { 
        (board 
            (graph
                vertices:{ {1 0} {2 0} {0 1} {1 1} {2 1} {3 1} {0 2} {1 2} {2 2} {3 2} {1 3} {2 3}}
                edges:{ {0 2} {0 3} {3 2} {3 4} {1 4} {4 5} {1 5} {3 7} {4 8} {6 7} {7 8} {8 9} {6 10} {11 9} {10 7} {11 8}}
            )
            use:Vertex
        )
        (hand Each) 
        (piece "Marker" Each 
            ("StepToEmpty" ~ (then ("ReplayIfLine3")))
        ) 
    }) 
    (rules 
        
        (start (place "Marker" "Hand" count:4))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveAnyEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        (then ("ReplayIfLine3"))
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveAnyEnemyPiece"
                    (forEach Piece)
                )
            )
        )
        }
        (end ("HavingLessPiecesLoss" Next 2))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Game created by Ludii team as a test.")
(rules "Same rules as Six Men's Morris.")
(source "Bell and Rouech√© 2007: 108 (S.2).")
(id "595")
(version "1.3.9")
(classification "board/space/line")
(credit "Eric Piette")
}
)
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai 
        "Symbol S.2_ai"
    )
    
)
