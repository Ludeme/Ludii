(define "RemoveEnemyPiece" 
    (move
        Remove
        (sites Occupied by:Next container:"Board") 
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Symbol S.2"  
    (players 2)  
    
    (equipment { 
        (board 
            (graph
                vertices:{ {1 0} {2 0} {0 1} {1 1} {2 1} {3 1} {0 2} {1 2} {2 2} {3 2} {1 3} {2 3}}
                edges:{ {0 2} {0 3} {3 2} {3 4} {1 4} {4 5} {1 5} {3 7} {4 8} {6 7} {7 8} {8 9} {6 10} {11 9} {10 7} {11 8}}
            )
            use:Vertex
        )
        (hand Each) 
        (piece "Marker" Each 
            (move
                Step
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain"
            )
        ) 
    })  
    (rules 
        
        (start (place "Marker" "Hand" count:4))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain"
                    )  
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (forEach Piece)
                )
            )
        )
        }
        (end 
            (if 
                (<= (count Pieces Next) 2) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Game created by Ludii team as a test.")
        (rules "Same rules as Six Men's Morris.")
        (source "Bell and RouechÃ© 2007: 108 (S.2).")
        (id "595")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    
    (ai 
        "Symbol S.2_ai"
    )
    
)
