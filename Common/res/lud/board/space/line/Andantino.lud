(define "NumTiles" 24)

//------------------------------------------------------------------------------

(game "Andantino"  
    (players 2)  
    (equipment { 
        (boardless <Tiling:type>) 
        (tile <Tiling:tile> Each <Tiling:numSides>) 
    })  
    
    (rules 
        (start {
            (place <Tiling:piece1> (centrePoint)) 
            (place <Tiling:piece2> (ahead (centrePoint) E))
        })
        
        (play 
            (move
                Add
                (to 
                    (sites Playable) 
                    if:(<= 2 
                        (count Sites in:(sites Around (to) NotEmpty)) 
                    )
                )
            )
        )
        
        (end {
            (if (or (is Loop surround:Next) (is Line 5)) (result Mover Win))
            (if (= (count Moves) (* (count Players) "NumTiles")) (result Mover Draw))
        })
    )
)

//------------------------------------------------------------------------------

(option "Tiling" <Tiling> args:{ <type> <tile> <numSides> <piece1> <piece2> }
    {
    (item "Hexagonal"      <Hexagonal>  <"Hex">   <numSides:6>   <"Hex1">      <"Hex2">     "The game uses hexagonal tiles.")**   
    (item "Square"         <Square>     <"Square"> <numSides:4>  <"Square1">   <"Square2">    "The game uses square tiles")     
    (item "Triangle"       <Triangular>  <"Triangle"> <numSides:3>  <"Triangle1">   <"Triangle2">    "The game uses triangle tiles")    
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Invented in 1995 by David L. Smith, Andantino is a tile-placing game using hexagonal tiles published by Nestor Games.")
        (rules "The game starts with a tile of each color touching one another. Players take turns placing tiles which must touch at least two other tiles. A player wins by either completely surrounding one or more of the opponent's tiles, or by creating a straight line of five tiles.")
        (source "<a href=\"https://boardgamegeek.com/boardgame/11893/andantino\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (id "86")
        (version "1.3.3")
        (classification "board/space/line")
        (author "David L. Smith")
        (publisher "nestorgames (used with permission)")
        (credit "Eric Piette")
        (date "1995")
        }
    )
    
    (graphics {
        (player Colour P1 (colour LightGrey))
    })
    
    (ai
        "Andantino_ai"
    )
)
