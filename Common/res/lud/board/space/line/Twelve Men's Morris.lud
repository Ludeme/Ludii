(define "RemoveEnemyPieceNotInLine3"
    (move
        Remove
        (forEach
            (sites Occupied by:Next container:"Board")
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "RemoveEnemyPiece"
    (if (can Move ("RemoveEnemyPieceNotInLine3"))
        ("RemoveEnemyPieceNotInLine3")
        (move
            Remove
            (sites Occupied by:Next container:"Board")
        )
    )
)

(define "IfLine3MoveAgain"
    (then
        (if
            (is Line 3)
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Twelve Men's Morris"
    (players 2)

    (equipment {
        (board <Board> use:Vertex)
        (hand Each)
        (piece "Marker" Each
            (move
                Step
                (to if:(is Empty (to)))
                "IfLine3MoveAgain"
            )
        )
    })
    (rules
        (start (place "Marker" "Hand" count:12))

        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move
                        (from (handSite Mover))
                        (to (sites Empty))
                        "IfLine3MoveAgain"
                    )
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )

        (phase "Movement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (if (> (count Pieces Mover) 3)
                        (forEach Piece)
                        (move
                            (from (sites Occupied by:Mover))
                            (to (sites Empty))
                        )
                    )
                )
            )
        )
        }

        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{<type>}
    {
    (item "Without Diagonal" <(concentric Square rings:4)>                 "The board has no diagonal.")
    (item "With Diagonal"    <(concentric Square rings:3 joinCorners:True)>   "The board has some diagonals.")**
    }
)

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/With Diagonal (Suggested)"
        {
        "Board/With Diagonal"
        }
    )

    (ruleset "Ruleset/Board Game Geek (Suggested)"
        {
        "Board/Without Diagonal"
        }
    )

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Twelve Men's Morris is a game of alignment known since at least the seventh century. It is played on a Nine Men's Morris board with corners of the concentric squares connected with diagonals.")
        (aliases {"Qvarn" "Dubbel-Qvarn"})
        (useFor "Ruleset/With Diagonal (Suggested)" (rules "Each player uses twelve pieces. Play begins with each player placing pieces on empty points. If they make three in a row along the lines, they can remove one of the opponent's pieces. They cannot remove an opponent's piece that is in a three in a row formation unless there are no other options. Once all pieces are place, players take turns moving pieces one spot to an adjacent point along the lines. If a player makes three in a row, an opponent's piece is removed as in the first phase of the game. Once a player is reduced to three pieces, that player may move to any open space on the board. The game is won when the opponent is reduced to two pieces."))
        (useFor "Ruleset/With Diagonal (Suggested)" (source "<a href=\"http://www.cyningstan.com/game/111/twelve-mens-morris\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />cyningstan</a>"))
        (useFor "Ruleset/Board Game Geek (Suggested)" (rules "The board has four concentric squares, with lines connecting the midpoints of their sides. "))
        (useFor "Ruleset/Board Game Geek (Suggested)" (source "<a href=\"https://boardgamegeek.com/boardgame/29080/twelve-mens-morris.\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>"))
        (source "Murray 1951:43-44.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Unknown, from around 600 to 799.")
        }
    )

    (ai
        "Twelve Men's Morris_ai"
    )
)

