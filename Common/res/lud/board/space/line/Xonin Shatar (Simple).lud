(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Xonin Shatar (Simple)"
    (players 2)
    
    (equipment 
        { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            (move 
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain" 
            )
        ) 
        }
    )  
    (rules 
        (start (place "Marker" "Hand" count:12))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                    )  
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
                )
            )
        )
        }
        
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Xonin Shatar (Sheep Chess) is a game of alignment played in Mongolia. ")
        (rules "Three concentric squares, with lines connecting the corners and the midpoints of the sides. Twelve pieces per player. Players alternate turns placing a piece on an empty spot on the board. When all of the pieces have been placed, players alternate turns moving a piece to an empty adjacent spot along the lines. During either phase, when a player places three of their pieces in a row, they remove one of the opponent's pieces. Pieces which are in a three-in-a-row pattern cannot be removed from the board. The player who removes all of the opponent's pieces wins.")
        (source "Popova 1974: 26-32.")
        (id "1024")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )
    
    (graphics {
        
    })
    
    (ai
        "Xonin Shatar (Simple)_ai"
    )
)

