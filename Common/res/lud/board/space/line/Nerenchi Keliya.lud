(define "RemoveEnemyPiece"
    (move
        Remove
        (sites Occupied by:Enemy container:"Board")
        (then (moveAgain))
    )
)

(define "IfLine3MoveAgainPlacement"
    (then
        (if
            (and (is Line 3) (not ("HandEmpty" Mover)))
            (moveAgain)
        )
    )
)

(define "IfLine3MoveAgain"
    (then
        (if
            (is Line 3)
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Nerenchi Keliya"
    (players 2)

    (equipment
        {
        (board (concentric Square rings:3) use:Vertex)
        (hand Each)
        (piece "Marker" Each
            (move
                Step
                (to if:(is Empty (to)))
                "IfLine3MoveAgain"
            )
        )
        }
    )
    (rules
        (start (place "Marker" "Hand" count:9))

        phases:
        {
        (phase "Placement"
            (play
                (move
                    (from (handSite Mover))
                    (to (sites Empty))
                    "IfLine3MoveAgainPlacement"
                )
            )
            (nextPhase (and ("HandEmpty" P1) ("HandEmpty" P2)) "Movement")
        )

        (phase "Movement"
            (play
                (forEach Piece)
            )
            (nextPhase Mover (is Line 3) "Capture")
        )

        (phase "Capture"
            (play
                "RemoveEnemyPiece"
            )
            (nextPhase Mover "Movement")
        )
        }

        (end {
            (if (<= (count Pieces P1) 2) (result P2 Win))
            (if (<= (count Pieces P2) 2) (result P1 Win))
        } )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Nerenchi Keliya is an alignment game played in Sri Lanka. Graffiti on earlier monuments may suggest the game has a certain degree of antiquity on the island.")
        (rules "Three concentric squares with lines connecting the midpoints of the squares. Nine pieces per player. Players alternate turns placing one of their pieces on an empty space. If a player places three of their pieces along one of the straight lines, they take another turn. Once all of the pieces are on the board, players take turns moving a piece to an empty space on the board. When a player creates a line of three in this phase, the player removes one of the opponent's pieces, and receives another turn. The player who reduces the opponent to two pieces wins.")
        (source "Parker 1909: 577-580.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Matthew Stephenson and Eric Piette")
        (origin  "This game was played in Sri Lanka, around 1909.")
        }
    )

    (graphics {

    })
    (ai
        "Nerenchi Keliya_ai"
    )
)
