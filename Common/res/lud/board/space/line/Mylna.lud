(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach (sites Occupied by:Next container:"Board") if:(not (is Line 3 Orthogonal through:(site))))
    )
)

(define "IfLine3OrthogonalMoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Mylna"
    (players 2)
    (equipment {
        (board
            (concentric Square rings:3)
            use:Vertex
        ) 
        (piece "Marker" Each
            (move 
                Step Orthogonal
                (to if:(is Empty (to))) 
                "IfLine3OrthogonalMoveAgain" 
            )
        )
        (hand Each)
    })
    (rules 
        (start 
            (place "Marker" "Hand" count:9)
        )
        phases:{
        (phase "Placement"
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty))
                        "IfLine3OrthogonalMoveAgain"
                    ) 
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement"
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
                )
            )
        )
        }
        (end {
            (if (<= (count Pieces Next) 2) (result Mover Win))
            ("BlockWin")
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mylna is an alignment game played in Iceland. It is closely related to other games played on this board, such as Nine Men´s Morris.")
        (rules "Played on a board of three concentric squares, with a line bisecting the perimeters of each square on each side, but not extending inside the perimeter of the central square. Play occurs on the intersections of the lines and the corners of the squares. Each player has nine pieces. Play begins with each player placing pieces on empty points. If they make three in a row along the lines, they can remove one of the opponent's pieces. They cannot remove an opponent's piece that is in a three-in-a-row formation. Once all pieces are placed, players take turns moving pieces one spot to an adjacent point along the lines. If a player makes three in a row, an opponent's piece is removed as in the first phase of the game. The game is won when the opponent is reduced to two pieces. A player may also win by blocking their opponent from being able to move.
        ")
        (source "Arnarson and Daviðsson 1888-1892: 298-300.")
        (id "817")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Iceland, from around 1862 to 1889.")
        }
    )
    
    (graphics {
        
    })
    
    (ai
        "Mylna_ai"
    )
)

