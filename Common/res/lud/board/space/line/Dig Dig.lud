(define "RemoveEnemyPiece" 
    (move Remove
        (forEach
            (sites Occupied by:Next container:"Board") 
            if:("IsSingletonStack" (site))
        )
    )
)

(define "RemoveDeadPieces" 
    (forEach Site
        (forEach 
            (sites Occupied by:#1 container:"Board")
            if:(= (size Stack at:(site)) 2)
        )
        (and 
            (remove (site)) 
            (remove (site)) 
        )
    )
)

//------------------------------------------------------------------------------

(game "Dig Dig"
    (players 2)
    
    (equipment { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            ("StepToEmpty" ~ 
                (then ("ReplayIfLine3" if:("IsSingletonStack" (to))))
            )
        ) 
    }) 
    
    (rules 
        (start {
            (place Stack "Marker1" (handSite P1))
            (place Stack "Marker2" (handSite P2))
        })
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to 
                            (union 
                                (forEach
                                    (sites Occupied by:Next container:"Board") 
                                    if:(= 1 (size Stack at:(site)))
                                )
                                (sites Empty)
                            )
                        )
                        copy:True
                        (then ("ReplayIfLine3" if:("IsSingletonStack" (to)))) 
                    )
                    (then 
                        (if 
                            (and (not (is Next Mover)) (is Full))	
                            (and {
                                ("RemoveDeadPieces" P1) 
                                ("RemoveDeadPieces" P2)
                                (set Pending)
                            })
                        )		
                    )
                )
            ) 
            (nextPhase (is Pending) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (forEach Piece)
                )
            )
            (end (if (all Sites (sites Occupied by:Next) if:(not (is In (site) (sites Board)))) (result Mover Win)))
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Dig Dig is an alignment game played by Teochew Chinese people in Malaysia during the twentieth century. One player generally plays with twigs, and the other as pieces of grass.")
(rules "Three concentric squares, with lines connecting the corners and midpoints of the squares. Two players. Players alternate turns placing one of their pieces on one of the spots on the board. A player may place a piece on top of a piece belonging to their opponent during this phase of the game, and they are both canceled out. When three uncanceled pieces form a row along the lines on the board, the player may capture one of the opponent's pieces. Once all of the spots have been occupied, the canceled pieces are removed. Players alternate turns moving one of their pieces to an empty adjacent spot on the board.The player who captures all of the opponent's pieces wins.")
(source "Newell 1959: 29.")
(id "1418")
(version "1.3.7")
(classification "board/space/line")
(credit "Eric Piette")
(origin "This game was played in Malaysia, around 1959.")
}
)
    
    (graphics {
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)
