(game "Three Men's Morris"
    (players 2) 
    (equipment {
        ("ThreeMenMorrisBoard")
        (hand Each)
        (piece "Marker" Each <Piece>)
    }) 
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
        )
        }
        (end (if (is Line 3) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Version" <Piece> args:{<move>}
    {
    (item "Nine Holes"  <(move (from) (to (sites Empty)))> "The pieces can move to any empty sites.")
    (item "Small Merels" <"StepToEmpty">        "The pieces can move to any empty adjacent sites.")**
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Three Men's Morris is a simple alignment game that is still played today, that has been played in Europe for centuries. It may also appear in Roman sources, and similar boards have been found as graffiti at Roman sites.")
        (aliases {"Smell Merels" "Castro" "Pedreria" "Marro" "Smerelli" "Mulino" "Semplice Mulinello" "Filo" "Filetto" "Tavoletta" "Riga de Tre" "MÃ©relles" "Marelles" "Carree Chinois" "Cashlan Gherra" "Qirq" "Dris" "Hujura" "Three Men" "Three Pin" "Three Penny" "Three Peg" "Copped Crown" "Corsi Crown."})
        (rules "3x3 intersecting lines with diagonals. Play occurs on the intersections of the lines. Each player has three pieces. Play begins with each player placing pieces on empty points. If they make three in a row along the lines, they win. Once all pieces are place, players take turns moving pieces one spot to an adjacent point along the lines trying to make three in a row.")
        (source "Murray 1951: 40-41.")
        (id "98")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        }
    )
    
    (ai 
        "Three Men's Morris_ai"
    )
    
)
