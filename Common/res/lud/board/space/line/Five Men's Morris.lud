(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "RemoveEnemyPieceInMillOnlyIfNoOtherChoice" 
    (if (can Move ("RemoveEnemyPieceNotInLine3"))
        ("RemoveEnemyPieceNotInLine3") 
        (move
            Remove
            (sites Occupied by:Next container:"Board") 
        )
    )
)

(define "RemoveEnemyPiece" 
    (move
        Remove
        (sites Occupied by:Next container:"Board") 
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:True) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Five Men's Morris"
    (players 2)  
    
    (equipment { 
        (board (concentric Square rings:2) use:Vertex)
        (hand Each)
        (piece "Marker" Each 
            (move
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain"
            )
        ) 
    })
    
    (rules 
        (start (place "Marker" "Hand" count:5))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    <Remove:rule>
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain"
                    )  
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    <Remove:rule>
                    (forEach Piece)
                )
            )
        )
        }
        
        (end 
            (if 
                (<= (count Pieces Next) 2)
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Play Rules" <Remove> args:{<rule>}
    {
    (item "Any Capture"  <"RemoveEnemyPiece"> "Any Enemy piece can be captured.")**
    (item "Restriction on Capture"  <"RemoveEnemyPieceInMillOnlyIfNoOtherChoice"> 
    "We cannot remove an opponent's piece that is in a three in a row formation unless there are no other options.")
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Murray (Suggested)" 
        {
        "Play Rules/Any Capture"
        }
    )
    
    (ruleset "Ruleset/Cyningstan (Suggested)" 
        {
        "Play Rules/Restriction on Capture"
        }
    )
    
    (ruleset "Ruleset/Historical Information (Incomplete)" {
        
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Five Men's Morris is attested in the seventeenth century, but it is possible that it was played earlier than this. It is played on a Six Men's Morris board, but each player has five pieces. Otherwise, the rules are the same as for Six Men's Morris.")
        (aliases {"Fivepenny Morris" "Five Pin Morris" "Fipenny Morrell"})
        (useFor "Ruleset/Cyningstan (Suggested)" (rules "Same rules as Murray, except players cannot remove an opponent's piece that is in a three in a row formation unless there are no other options."))
        (useFor "Ruleset/Cyningstan (Suggested)" (source "http://www.cyningstan.com/game/108/five-mens-morris"))
        (useFor "Ruleset/Cyningstan (Suggested)" (id "90"))
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "None. Probably similar to other Morris or Merels games. "))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP evidence. "))
        (useFor "Ruleset/Historical Information (Incomplete)" (id "564"))
        (useFor "Ruleset/Murray (Suggested)" (rules "The board has two concentric squares, with a line connecting the center points of each side of the two squares, but not extending inside the perimeter of the central square. Play occurs on the intersections of the lines and the corners of the squares. Each player has five pieces. Play begins with each player placing pieces on empty points. If they make three in a row along the lines, they can remove one of the opponent's pieces. Once all pieces are place, players take turns moving pieces one spot to an adjacent point along the lines. If a player makes three in a row, an opponent's piece is removed as in the first phase of the game. The game is won when the opponent is reduced to two pieces."))
        (useFor "Ruleset/Murray (Suggested)" (source "Murray 1951: 42-43."))
        (useFor "Ruleset/Murray (Suggested)" (id "565"))
        (source "Murray 1951: 43")
        (id "90")
        (version "1.3.3")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Europe, around 1694.")
        }
    )
    
    (graphics {
        
    })
    (ai
        "Five Men's Morris_ai"
    )
)

