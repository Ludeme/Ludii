(game "Connect6" 
    (players 2) 
    (equipment { 
        (board (square 19) use:Vertex) 
        (piece "Marker" Each) 
    }) 
    (rules 
        
        phases:{
        (phase "Opening" 
            (play (move Add (to (sites Empty))))
            (nextPhase "Playing")
        )
        (phase "Playing" 
            (play 
                (move Add
                    (to (sites Empty)) 
                    (then 
                        (if 
                            (is Even (count Moves)) 
                            (moveAgain)
                        )
                    )
                )
            )
            (nextPhase "Playing")
        )
        }
        
        (end (if (is Line 6 All) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Connect6 is a game invented by I-Chen Wu in Taiwan in 2003 and played on a Go board. The goal of the game is to make a line of six pieces.")
(rules "Played on a Go-like board (6x6 or larger, but a 19x19 go board is ideal) with two colors of stones (usually white and black). Black goes first by placing one stone. Play continues with each player playing two stones per turn. The first player to make a line of six in a row wins.")
(source "<a href=\"https://en.wikipedia.org/wiki/Connect6\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(id "88")
(version "1.3.9")
(classification "board/space/line")
(author "Professor I-Chen Wu")
(credit "Eric Piette")
(origin "This game is from Taiwan, on 2003.")
}
)
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Style Go)
    })
    
    (ai 
        "Connect6_ai"
    )
    
)
