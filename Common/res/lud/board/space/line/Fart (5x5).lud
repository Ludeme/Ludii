(game "Fart (5x5)"
    (players 2)
    (equipment {
        (board (square 5))
        (piece "Marker" Each (move Slide Orthogonal))
        (hand Each)
    })
    (rules
        (start (place "Marker" "Hand" count:12))
        phases:{
        (phase "Placement"
            (play
                (move 
                    (from (handSite Mover))
                    (to (difference (sites Board) (centrePoint)) if:(is Empty (to)))
                    (then (if ("NewTurn") (moveAgain)))
                )
            )
            (nextPhase Mover (is Empty (handSite Mover)) "Move")
        )
        (phase "Move" (play (forEach Piece)))
        }
        (end (if (is Line 5) (result Mover Win)))
    )	
)

//--------------------------------------

(metadata 
    (info
        {
        (description "Fart is an alignment game played by young people of El Oued, Algeria. In this version, players must place their pieces in a row of five to win.")
        (rules "5x5 board. Twelve pieces per player. Players alternate turns placing two pieces on the board. When all of the pieces are placed, players alternate turns moving one piece orthogonally any distance. The first player to place five of their pieces in an orthogonal or diagonal line wins.
        ")
        (source "Billen 1964: 55-56.")
        (id "1575")
        (version "1.3.9")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in Algeria, around 1964.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    (ai
        "Fart (5x5)_ai"
    )
)
