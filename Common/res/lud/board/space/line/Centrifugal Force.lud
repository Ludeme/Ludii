(define "DroppableFromDirection"
    (and (
            (= 0
                (count Pieces All in:(sites LineOfSight at:(site) #1))
            )
            
            (= 1
                (count Pieces in:(sites Around (site) #2))
            )    
    ))
)

(define "FromEdgeToEdge"
    (and (
            (
                (is In (site) (sites #1))
            )
            
            (
                (= 0
                    (count Pieces All in:(sites LineOfSight at:(site) #2))
                ) 
            )
    ))
)

//------------------------------------------------------------------------------

(game "Centrifugal Force"
    (players 2)
    
    (equipment
        {
        (board (square <Board:size>))
        
        (piece "Cross" P1) 
        (piece "Disc" P2)
        
        (piece "Square" Neutral)
        }
    )
    
    (rules
        (start
            {
            (place "Square0" coord:"A1")
            (place "Square0" coord:<Board:bottomRightSquare>)
            (place "Square0" coord:<Board:topRightSquare>) 
            (place "Square0" coord:<Board:topLeftSquare>)   
            }     
        )
        
        (play
            (move Add 
                (to   
                    (forEach (sites Empty)
                        if:
                        (or (
                                (or
                                    {
                                    ("FromEdgeToEdge" Top S)
                                    ("FromEdgeToEdge" Bottom N)
                                    ("FromEdgeToEdge" Left E)
                                    ("FromEdgeToEdge" Right W)
                                    }
                                )
                                
                                (or
                                    {
                                    ("DroppableFromDirection" N S)
                                    ("DroppableFromDirection" S N)
                                    ("DroppableFromDirection" W E)  
                                    ("DroppableFromDirection" E W)
                                    }
                                )
                        ))
                    )
                )
            )
        )
        
        (end
            {
            (if (is Line 5) (result Mover Win))
            
            (if (no Moves Next) (result All Draw))
            }
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{<size> <bottomRightSquare> <topRightSquare>  <topLeftSquare>}
    {
    (item "13x13" <13> <"M1"> <"M13"> <"A13"> "The game is played on a 13x13 board.")   
    (item "7x7" <7> <"G1"> <"G7"> <"A7"> "The game is played on a 7x7 board.") 
    (item "9x9" <9> <"I1"> <"I9"> <"A9"> "The game is played on a 9x9 board.") 
    (item "11x11" <11> <"K1"> <"K11"> <"A11"> "The game is played on a 11x11 board.")
    (item "15x15" <15> <"O1"> <"O15"> <"A15"> "The game is played on a 15x15 board.")
    (item "17x17" <17> <"Q1"> <"Q17"> <"A17"> "The game is played on a 17x17 board.") 
    (item "19x19" <19> <"S1"> <"S19"> <"A19"> "The game is played on a 19x19 board.")   
    }
)

(option "Look and feel" <Graphics> args:{<boardColour> <squareColour> <crossColour> <circleColour>}
    {
    (item "Retro" <(colour 249 245 235)> <(colour 51 51 51)> <(colour 48 62 205)> <(colour 234 84 85)> 
    "Look and feel: Retro")
    (item "Black&White" <(colour 255 255 255)> <(colour 57 57 57)> <(colour 0 0 0)> <(colour 0 0 0)> 
    "Look and feel: Black&White")
    (item "Garden" <(colour 182 226 161)> <(colour 63 94 55)> <(colour 252 226 42)> <(colour 232 226 214)> 
    "Look and feel: Garden")
    (item "Bathroom" <(colour 125 185 182)> <(colour 245 233 207)> <(colour 77 69 93)> <(colour 233 100 121)> 
    "Look and feel: Bathroom")
    (item "PurpleTiger" <(colour 242 205 92)> <(colour 180 80 0)> <(colour 64 14 50)> <(colour 166 31 105)> 
    "Look and feel: PurpleTiger")
    }
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Centrifugal Force is a paper-and-pencil game for two players inspired by Connect4, but using four different directions from which you can drop the tokens.
        It was invented by Diego Soranzo and Gabriele Soranzo while they were talking about Centripetal Force, a similar game invented about forty years before by Gabriele Soranzo.")
        (rules "At the beginning of the game, the board is empty except for the four edge cells, which are occupied by a square which belongs to neither player.
            
            The first player plays with crosses, the second with circles.
            Each player, on his turn, must place his marker on the grid onto an empty cell respecting the following rule:
            the marker is dropped from one of the four sides of the board choosing a row or a column, and, proceeding in a straight line so that it goes inside of the board, it stops in front of the first obstacle (the border of the board, a marker of any of the two players or one of the four squares).
            Pretty much the same as what happens in Connect4, except here there are four dropping directions.
            
            To win the game you need to be the first to form an horizontal, vertical, or diagonal line of at least five of your markers.
        If there are no available moves, the game ends in a draw.")
        (id "3977")
        (version "1.3.12")
        (classification "board/space/line")
        (author "Diego Soranzo, Gabriele Soranzo")
        (credit "Diego Soranzo")
        (date "17-03-2023")
        }
    )
    
    (graphics 
        {
        (board Colour Phase0 <Graphics:boardColour>)
        (board Colour InnerEdges <Graphics:squareColour>)  
        (board Colour OuterEdges <Graphics:squareColour>)            
        (board StyleThickness InnerEdges 1.35)
        (board StyleThickness OuterEdges 1.90)
        
        (piece Colour Neutral "Square" fillColour:<Graphics:squareColour> strokeColour:<Graphics:squareColour>)    
        (piece Scale "Square" 0.9)
        
        (player Name P1 "Cross")
        (player Colour P1 <Graphics:crossColour>)
        (piece Scale "Cross" 0.81)
        
        (player Name P2 "Circle")
        (player Colour P2 <Graphics:circleColour>)
        (piece Scale "Disc" 0.88)
        }
    )
    
    (ai
        (bestAgent "MC-GRAVE")
        trainedHeuristics:(heuristics {
            (lineCompletionHeuristic)
        })
    )
)

