(define "IfLine3MoveAgain"
    (then
        (if
            (is Line 3 Orthogonal exact:True)
            (moveAgain)
        )
    )
)

(define "NoLine3"
    (not (is Line 3 Orthogonal))
)

(define "RemoveEnemyPiece"
    (move
        Remove
        (sites Occupied by:Next container:"Board")
    )
)

//------------------------------------------------------------------------------

(game "Dra"
    (players 2)

    (equipment
        {
        (board (rectangle 5 6))
        (hand Each)
        (piece "Marker" Each
            (move
                Step
                Orthogonal
                (to
                    if:(is Empty (to))
                )
                "IfLine3MoveAgain"
            )
        )
        }
    )
    (rules
        (start (place "Marker" "Hand" count:12))

        phases:{
        (phase "Placement"
            (play
                (do
                    (move
                        (from (handSite Mover))
                        (to (sites Empty))
                    )
                    ifAfterwards:"NoLine3"
                )
            )
            (nextPhase ("HandEmpty" P2) "Movement")
        )

        (phase "Movement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (forEach Piece)
                )
            )

            (end (if (= (count Pieces Next) 2) (result Mover Win)))
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Dra is a game popular in West Africa, particularly in the areas of the Sahel and across several groups including the Touareg, Hausa, Bambara, Bozo, Sourai, and Fula. Typically, one player plays with twelve sticks, the other with twelve stones.")
        (aliases {"Dili" "Wali" "Kyoti"})
        (rules "5x6 board. Each player has twelve pieces. Players alternate placing their pieces until they are all placed on the board. Players cannot make a row of three pieces in the placement phase. Once the pieces are all on the board, they may be moved one space orthogonally. The goal is to make a line of three, which allows the player to capture one of the opponent's pieces. In the case that a move creates two lines of three, only one capture is made. The player who captures the most pieces wins. Matches are typically played to a score of ten, with a player scoring one point by winning a game, and two points for winning without any of their pieces being captured.")
        (source "Monod 1939: 12-13.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in West Africa, around 1939.")
        }
    )

    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })

    (ai
        "Dra_ai"
    )

)
