(game "Splice" 
    (players 2) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each)
        (piece "Ball" Neutral)
        (hand Each)
        (hand Shared)
    }) 
    
    (rules 
        (start {
            (place "Ball" "Hand" count:16)
            (place "Ball0" 32 count:16)
        })
        (play 
            (or
                (move 
                    (from (handSite Mover))
                    (to 
                        (sites Empty) 
                        if:
                        (and
                            (is Flat)
                            (is Within (id "Ball" Neutral) in:(sites Layer (layer of:(to))))
                        )
                    )
                )
                (move
                    (from (handSite Shared))
                    (to
                        (sites Empty)
                        if: (is Flat)
                    )
                )
            )
        )
        (end {
            (if
                (is Line (- (count Rows) (layer of:(last To))) SameLayer whats:{(id "Ball" Mover) (id "Ball" Neutral)} throughHowMuch:2)
                (result Mover Win)
            )
            (if
                (is Line (- (count Rows) (layer of:(last To))) SameLayer whats:{(id "Ball" Next) (id "Ball" Neutral)} throughHowMuch:2)
                (result Next Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Splice is an evolution of Spline that can be played with a Series: Shibumi set.")
        (rules "The board starts empty. Players take turns placing either: a ball of their colour on any hole or any platform that contains at least one red ball, or a red ball on any hole or any platform. A player wins by making a flat line spanning side to side, or corner to corner, on any level, composed entirely of friendly and neutral balls (at least one of each). If the placement of a red ball achieves this for both players, then the mover wins.")
        (id "4232")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a>")
        (version "1.3.14")
        (classification "board/space/line")
        (author "Nestor Romeral Andrés")
        (credit "Cédric Antoine")
        (date "2011")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour Neutral fillColour:(colour Red))
        (piece Colour P1 fillColour:(colour White))
        (piece Colour P2 fillColour:(colour Black))
    })
    
)
