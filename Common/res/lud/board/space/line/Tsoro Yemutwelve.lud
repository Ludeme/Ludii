(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "RemoveEnemyPiece" 
    (if (can Move ("RemoveEnemyPieceNotInLine3"))
        ("RemoveEnemyPieceNotInLine3") 
        (move
            Remove
            (sites Occupied by:Next container:"Board") 
        )
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal exact:True) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Tsoro Yemutwelve"
    (players 2)
    
    (equipment 
        { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            (move
                Step 
                (to if:(is Empty (to))) 
                "IfLine3MoveAgain" 
            )
        ) 
        }
    )  
    (rules 
        (start (place "Marker" "Hand" count:12))
        
        phases:{
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain" 
                    )  
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPiece"
                    (if (> (count Pieces Mover) 3)
                        (forEach Piece)
                        (move 
                            (from (sites Occupied by:Mover))
                            (to (sites Empty))
                        )
                    )
                )
            )
        )
        }
        
        (end (if (<= (count Pieces Next) 2) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tsoro Yemutwelve is a board game played in Zimbabwe, usually by boys and young men, though sometimes by adults as well. The board is typically scratched on the ground and played with stones.")
        (rules "Three concentric squares, with lines connecting the diagonals and the midpoints of the sides. Twelve pieces per player. Players alternate turns placing one of their pieces on one of the empty intersections of the board. If a player places three of their stones in a straight line, the player may remove one of the opponent's pieces. When all of the pieces have been placed, the players alternate turns moving one of their pieces to an empty adjacent point along the lines of the board. When a player is reduced to three pieces, they may move one of their stone to any empty space on the board. The player who reduces their opponent to two pieces wins.")
        (source "Matthews 1964: 67-68.")
        (id "1288")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Zimbabwe, around 1964.")
        }
    )
    
)
