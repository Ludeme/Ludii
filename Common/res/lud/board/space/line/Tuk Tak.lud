(game "Tuk Tak"
    (players 2) 
    (equipment {
        (board (square 3))
        (hand Each)
        (piece "Marker" Each "StepToEmpty")
    }) 
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement" (play (forEach Piece)))
        }
        (end ("Line3Win"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tuk Tak is a simple alignment game played in the Bengal region of India. It differs little from similar games known worldwide, and is generally played by children. ")
        (rules "3x3 board, without the outer edges of the square. Three pieces per player. Players alternate turns placing the pieces on an empty space on the board. When all of the pieces have been placed, players move pieces to an adjacent empty space on the board. the first player to place all three of their pieces in an orthogonal or diagonal line wins. ")
        (source "Datta 1934: 17-18")
        (id "908")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1934.")
        }
    )
    
    (graphics {
        (board Style Graph) 
        (show Edges Diagonal Hidden)
        (board Colour OuterEdges (colour Hidden))
        (board Colour OuterVertices (colour Hidden))
    })
    
    (ai
        "Tuk Tak_ai"
    )
)
