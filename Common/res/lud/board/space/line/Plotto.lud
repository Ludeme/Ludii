(define "CheckEndLine"
    (and {
        (is Occupied (ahead (last To) steps:1 #1))
        (is Occupied (ahead (last To) steps:2 #1))
        (= (what at:(last To)) (what at:(ahead (last To) steps:3 #1)))
    })	
)

(define "IsLine4WithEndSameColour"
    (and
        (is Line 4 whats:{(id "Hex" P1) (id "Hex" P2) (id "SecondHex" P1) (id "SecondHex" P2)})
        (or {
            ("CheckEndLine" W)
            ("CheckEndLine" E)
            ("CheckEndLine" NNW)
            ("CheckEndLine" SSW)
            ("CheckEndLine" SSE)
            ("CheckEndLine" NNE)
        })
    )
)

//------------------------------------------------------------------------------

(game "Plotto" 
    (players 2) 
    (equipment { 
        (boardless Hexagonal) 
        (tile "Hex" Each numSides:6) 
        (tile "SecondHex" Each numSides:6) 
        (hand Each size:2)
    }) 
    
    (rules 
        (start {
            (place "Hex1" (centrePoint)) 
            (place "Hex2" (ahead (centrePoint) E))
            (place "Hex1" (handSite P1 0))
            (place "Hex2" (handSite P2 0))
            (place "SecondHex1" (handSite P1 1))
            (place "SecondHex2" (handSite P2 1))
        })
        
        (play 
            (move 
                (from (sites Hand Mover))
                (to 
                    (sites Playable) 
                    if:(and
                        (<= 2 (count Sites in:(sites Around (to) NotEmpty)))
                        (not (is In (what at:(from)) (results from:(last From) to:(sites Around (to)) (what at:(to)))))
                    )
                )
                copy:True
            )
        )
        
        (end (if ("IsLine4WithEndSameColour") (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Plotto is a twist on 4-in-a-row played on a Hex board(less) with four colours of stones.")
        (rules "MOVE - Players move in turn to place one stone of either color on an empty cell.
            - However, the dropped stone must be adjacent, at least, to two other stones.
            - A stone cannot be adjacent to another stone of the same color.
            
        GOAL - Wins the player that makes a 4 in-a-row, with stones of his color at both ends (one player has Black and Blue, the other has White and Red).")
        (id "1823")
        (source "<a href=\"http://www.di.fc.ul.pt/~jpn/gv/plotto.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.di.fc.ul.pt</a>")
        (version "1.3.6")
        (classification "board/space/line")
        (author "David Smith")
        (credit "Eric Piette")
        (date "1995")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour LightGrey))
        (piece Colour P1 "SecondHex1" fillColour:(colour Blue))
        (piece Colour P2 "SecondHex2" fillColour:(colour Red))
        (piece Rename P1 piece:"SecondHex" "Hex")
        (piece Rename P2 piece:"SecondHex" "Hex")
        (piece Colour P1 "Hex1" fillColour:(colour Black))
        (piece Colour P2 "Hex2" fillColour:(colour LightGrey))
    })
    
)
