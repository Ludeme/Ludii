(game "Spava" 
    (players 2) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each)
        (piece "Ball" Neutral)
        (hand Each)
        (hand Shared)
    }) 
    (rules 		
        (start {
            (place "Ball" "Hand" count:16)
            (place "Ball0" 32 count:16)
        })
        (play 
            (or
                (move 
                    (from (handSite Mover))
                    (to 
                        (sites Empty) 
                        if:(is Flat)
                    )
                )
                (move
                    (from (handSite Shared))
                    (to
                        (sites Empty)
                        if: (is Flat)
                    )
                )
            )
        )
        (end {
            (if 
                (and (is Line (- (count Rows) (layer of:(last To))) SameLayer what:(id "Ball" Mover)) (<= 2 (- (count Rows) (layer of:(last To))))) 
                (result Mover Win)
            )
            (if 
                (and (is Line (- (- (count Rows) (layer of:(last To))) 1) SameLayer what:(id "Ball" Mover)) (<= 2 (- (- (count Rows) (layer of:(last To))) 1)))
                (result Next Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Spava is a game for the Shibumi set, made as an entry for Shibumi Challenge.")
        (rules "Two players, White and Black, take turns adding either a ball of their colour or a neutral red ball. A player wins by completing a line of length L of their colour on any LxL level. A player loses by completing a line of length L-1 on any LxL level. Lines can be orthogonal or diagonal but must contain at least two balls.")
        (id "1655")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a>")
        (version "1.3.14")
        (classification "board/space/line")
        (author "Cameron Browne")
        (credit "CÃ©dric Antoine")
        (date "2012")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour Neutral fillColour:(colour Red))
        (piece Colour P1 fillColour:(colour White))
        (piece Colour P2 fillColour:(colour Black))
    })
)
