(define "NotAllPieceInHome" (not ("NoSites" (difference (sites Occupied by:Mover) (sites Mover)))) )

//------------------------------------------------------------------------------

(game "Epelle"
    (players 2)
    (equipment
        {
        ("AlquerqueBoard" 3)
        (piece "Marker" Each "StepToEmpty")
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
        }
    )
    (rules
        (start {
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
        })
        (play (forEach Piece) )

        (end
            (if
                (and
                    "NotAllPieceInHome"
                    (is Line 3)
                )
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Epelle is a simple game of alignment played by the Urhobo people of Nigeria.")
        (rules "3x3 intersecting lines with diagonals. Play occurs on the intersections of the lines. Players each have three pieces, initially placed on the sides closest to the player. Players move the pieces to an adjacent unoccupied intersection along the lines. The goal is to make three in a row which cannot be in the starting position.")
        (source "Murray 1951: 42.")
        (version "1.3.0")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin  "This game was played in Nigeria, around 1951.")
        }
    )

    (ai
        "Epelle_ai"
    )
)
