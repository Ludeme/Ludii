(define "RemoveEnemyPieceNotInLine3" 
    (move
        Remove
        (forEach 
            (sites Occupied by:Next container:"Board") 
            if:(not (is Line 3 Orthogonal through:(site)))
        )
    )
)

(define "IfLine3MoveAgain" 
    (then 
        (if 
            (is Line 3 Orthogonal) 
            (moveAgain)
        )
    )
)

//------------------------------------------------------------------------------

(game "Mlabalaba"
    (players 2)
    
    (equipment 
        { 
        (board (concentric Square rings:3 joinCorners:True) use:Vertex) 
        (hand Each)
        (piece "Marker" Each 
            (if (= 3 (count Pieces Mover))
                (move 
                    (from (from))
                    (to (sites Empty))
                )
                (move 
                    Step 
                    (to if:(is Empty (to))) 
                )
                "IfLine3MoveAgain"
            )
        ) 
        }
    ) 
    (rules 
        (start (place "Marker" "Hand" count:12))
        
        phases:
        {
        (phase "Placement"
            (play
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        "IfLine3MoveAgain"
                    ) 
                )
            ) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        (phase "Movement" 
            (play 
                (if "SameTurn"
                    "RemoveEnemyPieceNotInLine3"
                    (forEach Piece)
                )
            )
        )
        }
        
        (end
            (forEach NonMover
                if:(= 2 (count Pieces Player))
                (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Mlabalaba (more frequently known as Morabaraba) is an alignment game played in South Africa.")
        (aliases {"Morabaraba" "Umlabalaba" "Meu"})
        (rules "Three concentric squares, with lines connecting the corners and midpoints of the squares. Twelve pieces per player. In the first phase, players take turns placing pieces on an empty spot on the board. If they place three pieces so they are in a line, forming a \"meul.\" they remove one of the opponent's pieces that is not in a \"meul.\" Once all the pieces are placed, the second phase begins, in which players take turns moving one piece to an adjacent empty spot. When a \"meul\" is formed, the player removes one of the opponent's pieces that is not in a \"meul.\" When one player is reduced to three pieces, they may move their pieces to any empty spot on the board. The player who reduces their opponent to two pieces wins.
        ")
        (source "Coertze 2002.")
        (id "1285")
        (version "1.3.4")
        (classification "board/space/line")
        (credit "Eric Piette")
        (origin "This game was played in South Africa, from around 1907 to 2002.")
        }
    )
    
    (graphics {
        
    })
    (ai
        "Mlabalaba_ai"
    )
)

