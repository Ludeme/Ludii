(game "Sponnect" 
    (players 2) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each)
        (piece "Ball" Neutral)
    }) 
    
    (rules 
        (start {
            (place "Ball0" {8 10 14 18 20})
        })
        (play
            (or
                (move Add 
                    (to 
                        (sites Empty) 
                        if:(is Flat)
                    )
                )
                (if
                    (not (was Pass))
                    (move Pass)
                )
            )
        )
        (end {
            (if
                (and {
                    (= (who at:(last To)) 2) 
                    (> (size Array (array (intersection (sites Top) (sites Group Vertex at:(last To))))) 0) 
                    (> (size Array (array (intersection (sites Bottom) (sites Group Vertex at:(last To))))) 0)
                })
                (result Mover Win)
            )
            (if
                (and {
                    (= (who at:(last To)) 1) 
                    (> (size Array (array (intersection (sites Right) (sites Group Vertex at:(last To))))) 0) 
                    (> (size Array (array (intersection (sites Left) (sites Group Vertex at:(last To))))) 0)
                })
                (result Mover Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sponnect is a simple connection game for 2 players that can be played with a Series: Shibumi set.")
        (rules "The board starts with 5 red marbles forming a pyramid in the middle of the board. Starting with white, each player places a marble on an empty space or platform (2x2 arrangement of marbles). A player may also pass if the other player didn't pass on his turn. White wins by forming a chain of touching marbles connecting the east and the west side. Black wins by connecting the south and the north side.")
        (id "4235")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a>")
        (version "1.3.14")
        (classification "board/space/connection")
        (author "Martin Windischer")
        (credit "CÃ©dric Antoine")
        (date "2011")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour Neutral fillColour:(colour Red))
        (piece Colour P1 fillColour:(colour White))
        (piece Colour P2 fillColour:(colour Black))
    })
    
)
