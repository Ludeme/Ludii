(game "Hex" 
    (players 2) 
    (equipment { 
        (board (hex Diamond <Board>)) 
        (piece "Marker" Each)
        (regions P1 {(sites Side NE) (sites Side SW) })
        (regions P2 {(sites Side NW) (sites Side SE) })
    }) 
    (rules 
        <Meta:rule>
        (play (move Add (to (sites Empty))))
        (end (if (is Connected Mover) (result Mover <Result>))) 
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> } {
    (item "3x3" <3> "The game is played on a 3x3 board.") 
    (item "4x4" <4> "The game is played on a 4x4 board.") 
    (item "5x5" <5> "The game is played on a 5x5 board.") 
    (item "6x6" <6> "The game is played on a 6x6 board.") 
    (item "7x7" <7> "The game is played on a 7x7 board.") 
    (item "8x8" <8> "The game is played on a 8x8 board.") 
    (item "9x9" <9> "The game is played on a 9x9 board.") 
    (item "10x10" <10> "The game is played on a 10x10 board.")* 
    (item "11x11" <11> "The game is played on a 11x11 board.")** 
    (item "12x12" <12> "The game is played on a 12x12 board.") 
    (item "13x13" <13> "The game is played on a 13x13 board.") 
    (item "14x14" <14> "The game is played on a 14x14 board.")* 
    (item "15x15" <15> "The game is played on a 15x15 board.") 
    (item "16x16" <16> "The game is played on a 16x16 board.") 
    (item "17x17" <17> "The game is played on a 17x17 board.")* 
    (item "18x18" <18> "The game is played on a 18x18 board.") 
    (item "19x19" <19> "The game is played on a 19x19 board.") 
})

(option "Swap Rules" <Meta> args:{ <rule>} {
    (item "On" <(meta (swap))> "The game uses the swap rule.")* 
    (item "Off" <> "The game does not use the swap rule.") 
})

(option "End Rules" <Result> args:{ <type>} {
    (item "Standard" <Win> "The first player to connect their two sides wins.")* 
    (item "Misere" <Loss> "The first player to connect their two sides loses.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Hex was invented by Piet Hein in 1942 then described again by John Nash in 1944. It is played on a rhombus tiled by hexagons, most commonly 11x11 in size.")
        (rules "Players take turns placing a piece of their colour at an empty cell, and win by connecting their board sides with a chain of their pieces.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Hex_(board_game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "82")
        (version "1.3.6")
        (classification "board/space/connection")
        (credit "Eric Piette")
        (date "1942")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
        (board Colour Phase0 (colour VeryLightGrey))
        (board Style ConnectiveGoal)
    })
    
    (ai 
        "Hex_ai"
    )
    
)
