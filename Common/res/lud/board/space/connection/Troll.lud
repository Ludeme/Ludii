(define "ConnectedEdges"
    (!= 0 (count Sites in:(forEach (sites Occupied by:#1) if:(is Connected at:(site) Orthogonal #1))))
)

//------------------------------------------------------------------------------

(game "Troll"
    (players 2)
    (equipment {
        (board (square 8))
        (piece "Ball" Each)
        (regions P1 { (sites Top) (sites Bottom) } )
        (regions P2 { (sites Left) (sites Right) } )
    })
    (rules
        (play 
            (move Add
                (to
                    (if (is Mover P1)
                        (difference 
                            (sites Empty)
                            (union (sites Left) (sites Right))
                        )
                        (difference 
                            (sites Empty)
                            (union (sites Top) (sites Bottom))
                        )
                    )
                )
                (then
                    (custodial 
                        (from (last To)) 
                        (between 
                            if:(is Enemy (who at:(between))) 
                            (apply
                                (and
                                    (remove (between))
                                    (add (piece (id "Ball" Mover)) (to (between)))
                                )
                            )
                        )
                        (to if:(is Friend (who at:(to)))) 
                    )
                )
            )
        )
        
        (end {
            (if ("ConnectedEdges" P1) (result P1 Win))
            (if ("ConnectedEdges" P2) (result P2 Win))
        }) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 254 194 17))
        (board Colour OuterEdges (colour Black))
        (board Colour InnerEdges (colour Black))
    })
    
)
