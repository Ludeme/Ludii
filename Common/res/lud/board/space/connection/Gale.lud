(game "Gale" 
    (players 2) 
    (equipment { 
        (board (square <Board>)) 
        (piece "Square" Each)
        (regions P1 {(sites Top) (sites Bottom) })
        (regions P2 {(sites Left) (sites Right) })
    }) 
    (rules 
        (start {
            (place "Square1" (forEach (sites Board) if:(and (is Even (column of:(site))) (is Odd (row of:(site))))))
            (place "Square2" (forEach (sites Board) if:(and (is Even (row of:(site))) (is Odd (column of:(site))))))
        })
        (play (move Add (to (sites Empty))))
        
        (end (if (is Connected Mover) (result Mover <Result>)))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "5x5" <5> "The game is played on a 5x5 board") 
    (item "7x7" <7> "The game is played on a 7x7 board") 
    (item "9x9" <9> "The game is played on a 9x9 board") 
    (item "11x11" <11> "The game is played on a 11x11 board") 
    (item "13x13" <13> "The game is played on a 13x13 board")** 
    (item "15x15" <15> "The game is played on a 15x15 board") 
    (item "17x17" <17> "The game is played on a 17x17 board") 
    (item "19x19" <19> "The game is played on a 19x19 board") 
    
})

(option "End Rules" <Result> args:{ <type>}
    {
    (item "Standard" <Win> "The first player to connect their two sides wins.")* 
    (item "Misere" <Loss> "The first player to connect their two sides loses.") 
})

//------------------------------------------------------------------------------
(metadata 
    
    (info
        {
        (description "Gale is a connection game on two overlapping rectangular grids to form a square game board.")
        (rules "A player wins by connecting opposite sides of the square board with a chain of their pieces.")
        (id "964")
        (source "<a href=\"https://en.wikipedia.org/wiki/Shannon_switching_game\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.11")
        (classification "board/space/connection")
        (author "David Gale")
        (credit "Woody Pidcock")
        (date "1958")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Yellow))
        (player Colour P2 (colour DarkBlue))
        (board Colour Phase0 (colour LightGrey))
        (board Colour InnerEdges (colour LightGrey))
        (board Style ConnectiveGoal)
        (board StyleThickness OuterEdges 4.0)
        (show Symbol "square-alt1" (sites Perimeter) fillColour:(colour LightGrey) edgeColour:(colour LightGrey) scale:1.1)
    })
    
    (ai
        "Gale_ai"
    )
)

