(game "Spaji" 
    (players 2) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each maxValue:16)
    }) 
    
    (rules 
        (play  		
            (if
                (not ((is Prev Mover)))
                (or
                    (move Add (piece 1)
                        (to 
                            (sites Empty) 
                            if:(is Flat)
                        )
                        (then (moveAgain))
                    )
                    (move Add (piece 2)
                        (to 
                            (sites Empty) 
                            if:(is Flat)
                        )
                        (then (moveAgain))
                    )
                )
                (if (= (who at:(last To)) 1)
                    (move Add (piece 2)
                        (to 
                            (intersection (sites Empty) (sites Around (last To))) 
                            if:(is Flat)
                        )
                    )
                    (move Add (piece 1)
                        (to 
                            (intersection (sites Empty) (sites Around (last To))) 
                            if:(is Flat)
                        )
                    )
                )	    			
            )
        )
        
        (end {
            (if 
                (and (= (count Pieces in:(sites Occupied by:All container:"Board")) 30) (= (count SizeBiggestGroup if:(= (who at:(to)) Mover) isVisible:True) (count SizeBiggestGroup if:(= (who at:(to)) Next) isVisible:True)))
                (result P2 Win)
            )
            (if 
                (and (= (count Pieces in:(sites Occupied by:All container:"Board")) 30) (> (count SizeBiggestGroup if:(= (who at:(to)) Mover) isVisible:True) (count SizeBiggestGroup if:(= (who at:(to)) Next) isVisible:True)))
                (result Mover Win)
            )
            (if 
                (and (= (count Pieces in:(sites Occupied by:All container:"Board")) 30) (< (count SizeBiggestGroup if:(= (who at:(to)) Mover) isVisible:True) (count SizeBiggestGroup if:(= (who at:(to)) Next) isVisible:True)))
                (result Next Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Spaji is a simple game for 2 players that can be played with a Series: Shibumi set. It uses a 4×4 set of stacking spheres in 2 colors.")
        (rules "The board starts empty. Players take turns adding a white ball and a black ball each turn, in any order, at any playable points, provided that both balls touch. The game ends when the pyramid is complete. The owner of the largest visibly connected group wins. If tied, the second player (Black) wins.")
        (id "4246")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />SHIBUMI Rulebook</a>")
        (version "1.3.14")
        (classification "board/space/connection")
        (author "Néstoe Romeral Andrés")
        (publisher "Computational Creativity Group, nestorgames")
        (credit "Cédric Antoine")
        (date "2011")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour P1 fillColour:(colour White))
        (piece Colour P2 fillColour:(colour Black))
    })
    
)
