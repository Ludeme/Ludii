// #1,#2 - sides (S, N, NE, NW, SE, SW)
// #3 optional 3rd region
(define "IsSidesConnected"
    (is Connected {(sites Side #1) (sites Side #2) #3})
)

(define "WhiteVC" 
    (or {
        ("IsSidesConnected" S N)
        ("IsSidesConnected" SW NE)
        ("IsSidesConnected" SE NW)
    })
)

(define "BlackVC" 
    (or
        ("IsSidesConnected" S NW (sites Side NE))
        ("IsSidesConnected" N SW (sites Side SE))
    )
)

(define "IsPlayingPhase" (= 1 (var)))

(game "Unlur" 
    (players 2) 
    (equipment { 
        (board (hex <Board:size>)) 
        (piece "Disc" Each) 
    }) 
    (rules 
        phases:{
        (phase "Opening"
            (play
                (or
                    (move Add
                        (piece (id "Disc" P2))
                        (to <Edge:target_zone>)
                    )
                    (move Pass
                        (then
                            (if (is Mover P1)
                                (swap Players P1 P2
                                    (then
                                        (note "Player 1 is now Black. Player 2 is now White." to:All)
                                    )
                                )
                                (note "Player 1 remains White. Player 2 remains Black." to:All)
                                (then
                                    (do
                                        (set NextPlayer (player 1))
                                        next:(set Var 1) // (var) represents the phase here
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase ("IsPlayingPhase") "Playing")
        )
        (phase "Playing"
            (play (move Add (to (sites Empty))))
        )
        }
        (end {
            (if (and {("IsPlayingPhase") (= (id P1) (mover)) ("BlackVC") (not ("WhiteVC"))}) (result P1 Loss)) 
            (if (and {("IsPlayingPhase") (= (id P2) (mover)) ("WhiteVC") (not ("BlackVC"))}) (result P2 Loss)) 
            (if (and {("IsPlayingPhase") (= (id P1) (mover)) ("WhiteVC")}) (result P1 Win)) 
            (if (and {("IsPlayingPhase") (= (id P2) (mover)) ("BlackVC")}) (result P2 Win)) 
        })
    )
)

(option "Board Size" <Board> args:{ <size> } {
    (item "6" <6> "The game is played on a board with 6 hexes per side.")**
    (item "7" <7> "The game is played on a board with 7 hexes per side.")
    (item "8" <8> "The game is played on a board with 8 hexes per side.")
    (item "9" <9> "The game is played on a board with 9 hexes per side.")
    (item "10" <10> "The game is played on a board with 10 hexes per side.")
    (item "11" <11> "The game is played on a board with 11 hexes per side.")
})

(option "Edge Rule" <Edge> args:{ <target_zone> } {
    (item "No Restriction" <(sites Empty)> "You may place stones on the edge in the opening phase.")
    (item "Edge Restriction" <(intersection (sites Empty) (sites Inner))> "You cannot place stones on the edge in the opening phase.")**
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Unlur is a game between two people taking turns placing tiles on a hexagonal board. Each player has a different objective to achieve victory. It is classified as an abstract, connecting and unequal forces board game.This game was the winner of the second Annual Game Design Competition in 2002, whose theme that year was the design of games of unequal forces (organized by Abstract Games magazine, About Board Games, and the Strategy Gaming Society). The name came from a symmetric ancestor game called 'Lur'. When the game was supported on Ludoteka, the Edge rule was added because placing on the edge is usually bad for Black. More information about the origins of the game can be found in Spanish on the designer's blog:<a href=\"http://mesadejuegos.blogspot.com/2005/12/unlur.html\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />mesadejuegos.blogspot.com</a> ")
        (rules "This game is played on a hexagonal board made up of hexagonal squares, usually 6 squares per side, although other sizes are possible. Initially the board is empty and black pieces are placed in turns until one player passes. The player who passed becomes Black. The other player becomes White and takes the next turn. Play continues, with each player in turn placing pieces of their colour on unoccupied squares on the board. White wins if they manage to connect two opposite sides of the board. Black wins if they manage to connect three non-adjacent sides of the board. To avoid ties, if a player meets the opponent's goal with their pieces, they lose the game.")
        (id "1226")
        (source "<a href=\"https://fr.wikipedia.org/wiki/Unlur\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.11")
        (classification "board/space/connection")
        (author "Jorge GÃ³mez Arrausi")
        (credit "Nicholas Bamber")
        (date "2002")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Unlur_ai"
    )
)
