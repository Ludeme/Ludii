(game "Y" 
    (players 2) 
    (equipment { 
        (board (hex Triangle <Board>)) 
        (piece "Disc")
    }) 
    (rules 
        (play (move Add (to (sites Empty))))
        (end 
            (if (is Connected 3 Sides) 
                (result Mover <Result>) 
            ) 
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "3" <3> "Played on a size 3 board.") 
    (item "4" <4> "Played on a size 4 board.") 
    (item "5" <5> "Played on a size 5 board.") 
    (item "6" <6> "Played on a size 6 board.") 
    (item "7" <7> "Played on a size 7 board.") 
    (item "8" <8> "Played on a size 8 board.") 
    (item "9" <9> "Played on a size 9 board.") 
    (item "10" <10> "Played on a size 10 board.") 
    (item "11" <11> "Played on a size 11 board.")* 
    (item "12" <12> "Played on a size 12 board.") 
    (item "13" <13> "Played on a size 13 board.") 
    (item "14" <14> "Played on a size 14 board.") 
    (item "15" <15> "Played on a size 15 board.") 
    (item "16" <16> "Played on a size 16 board.") 
    (item "17" <17> "Played on a size 17 board.") 
    (item "18" <18> "Played on a size 18 board.") 
    (item "19" <19> "Played on a size 19 board.") 
    }
)

(option "End Rules" <Result> args:{ <type> }
    {
    (item "Standard" <Win> "The first player to connect all three sides wins.")* 
    (item "Misere" <Loss> "The first player to connect all three sides loses.") 
    }
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Place pieces to connect all three board sides.")
        (aliases {"The Game of Y"})
        (rules "Players take turns placing a piece of their colour on an empty cell. A player wins by connecting all three board sides with a connected group of their pieces.")
        (id "704")
        (source "M. Gardner, `The Game of Hex', The 2nd Scientific American Book of Mathematical Puzzles and Diversions, Simon and Schuster, 1959; C. Schensted and C. Titus, Mudcrack Y & Poly-Y, NEO Press, 1975.")
        (version "1.3.13")
        (classification "board/space/connection")
        (author "J. W. Milnor")
        (credit "Cameron Browne 4/7/2020")
        (origin "This game is from USA., on 1952.")
        }
    )
    (ai
        "Y_ai"
    )
)
