(game "Y (Hex)"  
    (players 2)  
    (equipment { 
        (board (tri Hexagon 7) use:Vertex) 
        (piece "Marker" Each)
        (regions "SSW" {(sites Side S) (sites Side SW) })
        (regions "NNW" {(sites Side NW) (sites Side N) })
        (regions "NESE" {(sites Side NE) (sites Side SE) })
        }
    )  
    (rules 
        (play (move Add (to (sites Empty))))
        (end 
            (if 
                (is Connected {
                    (sites "SSW") 
                    (sites "NNW") 
                    (sites "NESE")
                })  
                (result Mover Win) 
            ) 
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Y (Hex) is a version of Hex created by Eric Piette for Ludii. It is played with hexagonal spaces arranged in a triangular board, 8 spaces on each side is typical.")
        (rules "Players take turns placing their pieces on a space on the board, with the goal of connecting all three sides of the triangle with their pieces. Corners count as belonging to both sides they touch.")
        (id "83")
        (version "1.3.4")
        (classification "board/space/connection")
        (author "Eric Piette")
        (publisher "Ludii")
        (credit "Eric Piette")
        (date "01-09-2019")
        }
    )
    
    (graphics {
        (region Colour "SSW" Vertex (colour Green))
        (region Colour "NNW" Vertex (colour Blue))
        (region Colour "NESE" Vertex (colour Red))
        
    })
    
    (ai
        "Y (Hex)_ai"
    )
)
