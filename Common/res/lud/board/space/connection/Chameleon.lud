(game "Chameleon" 
    (players 2) 
    (equipment { 
        (board (hex Diamond <Board>)) 
        (piece "Marker" Each)
        (hand Shared size:2)
        (regions P1 {(sites Side NE) (sites Side SW) })
        (regions P2 {(sites Side NW) (sites Side SE) })
    }) 
    (rules 
        (meta (swap))
        (start {
            (place "Marker1" (handSite Shared)) 
            (place "Marker2" (handSite Shared 1)) 
        })
        (play 
            (move 
                (from (sites Hand Shared)) 
                (to (sites Empty))
                copy:True
            )
        ) 
        (end {
            (if (is Connected Mover) (result Mover <Result:mover>))
            (if (is Connected Next) (result Next <Result:next>))
        })
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "3x3"  <3>  "The game is played on a 3x3 board")  
    (item "4x4"  <4>  "The game is played on a 4x4 board")  
    (item "5x5"  <5>  "The game is played on a 5x5 board")  
    (item "6x6"  <6>  "The game is played on a 6x6 board")  
    (item "7x7"  <7>  "The game is played on a 7x7 board")  
    (item "8x8"  <8>  "The game is played on a 8x8 board")  
    (item "9x9"  <9>  "The game is played on a 9x9 board")  
    (item "10x10" <10> "The game is played on a 10x10 board")*  
    (item "11x11" <11> "The game is played on a 11x11 board")**  
    (item "12x12" <12> "The game is played on a 12x12 board")  
    (item "13x13" <13> "The game is played on a 13x13 board")  
    (item "14x14" <14> "The game is played on a 14x14 board")*  
    (item "15x15" <15> "The game is played on a 15x15 board")  
    (item "16x16" <16> "The game is played on a 16x16 board")  
    (item "17x17" <17> "The game is played on a 17x17 board")*  
    (item "18x18" <18> "The game is played on a 18x18 board")  
    (item "19x19" <19> "The game is played on a 19x19 board")  
})

(option "End Rules" <Result> args:{ <mover> <next>}
    {
    (item "Standard"  <Win> <Win>  "The first player to connect his two sides wins.")*  
    (item "Misere"  <Loss> <Loss>  "The first player to connect his two sides loses.")  
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Invented by Randy Cox and Bill Taylor, Chameleon is a Hex variant")
        (rules "Played on a Hex board. Players may play a piece of any color piece. The goal is to form a line connecting the player's two sides with either color.")
        (id "466")
        (source "C. Browne (2005), Connection Games: Variations on a Theme, AK Peters, Massachusetts (ISBN 1568812248).")
        (version "1.3.4")
        (classification "board/space/connection")
        (author "Randy Cox (I), Bill Taylor")
        (credit "Eric Piette")
        (date "2003")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Blue))
        (player Colour P2 (colour Red))
        (board Colour Phase0 (colour VeryLightGrey))
        (board Colour InnerEdges (colour LightGrey))
        (board Style ConnectiveGoal)
    })
    
    (ai 
        "Chameleon_ai"
    )
    
)
