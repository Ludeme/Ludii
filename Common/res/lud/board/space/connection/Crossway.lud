
(define "IsNotCut" (not (is Pattern {F R F R F} whats:{(mover) (next)})))

//------------------------------------------------------------------------------

(game "Crossway"  
    (players 2) 
    (equipment { 
        (board <Board> use:Vertex) 
        (piece "Marker" Each)
        (regions P1 { (sites Side N) (sites Side S) } )
        (regions P2 { (sites Side W) (sites Side E) } )
    })
    
    (rules 
        (meta (swap))
        (play 
            (do
                (move 
                    Add
                    (to (sites Empty))
                )
                ifAfterwards:"IsNotCut"
            )
        )
        
        (end {
            (if (is Connected All Mover) (result Mover Win))
        }) 
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "9x9"      <(square 9)>     "The game is played on a 9x9 board")   
    (item "13x13"    <(square 13)>    "The game is played on a 13x13 board")   
    (item "15x15"    <(square 15)>    "The game is played on a 15x15 board")   
    (item "19x19"    <(square 19)>    "The game is played on a 19x19 board")*   
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The most elegant drawless square connection game. Stones possess orthodiagonal connectivity, and crosscuts are disallowed.")
        (rules "Crossway is played with a Go set. The board starts out empty. Each player takes possession of all of the stones of one color. Players take turns adding their stones to the board, one stone per turn. A player must never create a crosscut formation. Black makes the first placement of the game. If you have no more available placements, you forfeit your turn and your opponent can continue making placements until completing his goal connection. For White to win, White must form a contiguous sequence of white stones connecting the West edge to the East edge of the board. Each stone in the sequence must be connected to neighboring stones in the sequence by horizontal, vertical, or diagonal adjacencies. Likewise for Black and the North and South edges. A corner is considered to be part of both adjoining edges. Crossway makes use of the pie rule. This means Black makes the first placement, and White has the option of switching colors with Black, and claiming Black's first move as his own. If White chooses to exercise the pie rule, Black then becomes White and now makes the second placement of the game. The pie rule can only be used once and on the second move of the game.")
        (id "1049")
        (source "Mark Steere Games")
        (version "1.3.3")
        (classification "board/space/connection")
        (author "Mark Steere")
        (credit "Alek Erickson")
        (date "2007")
        }
    )
    
    (graphics {
        (board Style Go)
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
    })   
    (ai
        "Crossway_ai"
    )
)
