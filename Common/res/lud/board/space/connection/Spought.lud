(game "Spought" 
    (players 2) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each)
    }) 
    
    (rules 
        (meta {(noStackOn Fallen) (pin SupportMultiple) (gravity)})
        (start {
            (place "Ball1" {0 2 8 12 17 20 27 29})
            (place "Ball2" {1 3 7 10 18 22 26 28})
        })
        (play
            (or
                (move
                    (from (intersection (sites Layer 0) (sites Occupied by:Mover)))
                    (to (sites Empty) 
                        if:(is Flat)
                    )
                )
                (move
                    (from (intersection (sites Layer 1) (sites Occupied by:Mover)))
                    (to (intersection (sites Empty) (sites Layer 1))
                        if:(is Flat)
                    )
                )
            )
        )
        (end 
            (if
                (= (size Array (array (sites Group Vertex at:(last To)))) 8) //isVisible:True
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Spought is a ball stacking and connection game that can be played with a Series: Shibumi set. It uses a board of 4x4 holes, 8 white balls and 8 black balls.")
(rules "Two players, White and Black, alternate turns moving a ball of their colour either: a) moving from a hole to another empty hole; b) moving up from a hole to a 2x2 platform; c) passing if there are no available moves. Notice that other balls may drop as a result. Win is achieved by connecting all your pieces into a single group.")
(id "4299")
(source "<a href=\\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\\" target=\\"_blank\\" class=\\"style1\\" style=\\"color: #0000EE\\">SHIBUMI Rulebook</a>")
(version "1.3.14")
(classification "board/space/connection")
(author "Cameron Browne, Nestor Romeral Andres")
(publisher "Computational Creativity Group, nestorgames")
(credit "CÃ©dric Antoine")
(date "2011")
}
)

(graphics {
    (piece Scale "Ball" 1.0)
    (board Style Shibumi)
    (piece Colour Neutral fillColour:(colour Red))
    (piece Colour P1 fillColour:(colour White))
    (piece Colour P2 fillColour:(colour Black))
})

)
