(game "Manalath"  
    (players 2) 
    (equipment { 
        (board (rotate 90 (hex 5))) 
        (piece "Marker" Each)
        (hand Shared size:2)
        }
    )  
    (rules 
        (start {
            (place "Marker1" (handSite Shared)) 
            (place "Marker2" (handSite Shared 1))
        })
        (play 
            (do
                (move 
                    (from (sites Hand Shared)) 
                    (to (sites Empty)) 
                    copy:True
                )
                ifAfterwards:(< (size Group at:(last To)) 6)
            )
        )
        (end {
            (if (= (size Group at:(last To)) 5)
                (if (= (who at:(last To)) P1) (result P1 Win))
                (result P2 Win)
            ) 
            (if (= (size Group at:(last To)) 4)
                (if (= (who at:(last To)) P1) (result P1 Loss))
                (result P2 Loss)
            ) 
            ("NoMoves" Loss)
        })  
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Manalath was invented in 2012 by Dieter Stein and Néstor Romeral Andrés. It is a Yavalath-like game in which the players must create a group of five without first making a group of four.")
        (rules "Players take turns placing pieces. They attempt to create groups of five pieces which are adjacent to each other. If a player creates a group of four, they lose. The first player to make a group of five without making a group of four wins.")
        (source "<a href=\"https://boardgamegeek.com/boardgame/127993/manalath\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.2.0")
        (classification "board/space/group")
        (author "Dieter Stein Néstor Romeral Andrés")
        (publisher "<a href=\"https://www.nestorgames.com\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a> (used with permission)")
        (credit "Eric Piette")
        (date "2012")
        }
    )
    
    (ai 
        "Manalath_ai"
    )
    
)
