(define "UpdateScore"
    (do
        (set Var "CutOff" 0)
        next:(do
            (while 
                (and
                    (= 0
                        (-
                            (count Groups if:(= (id P1) (who at:(to))) min:(var "CutOff"))
                            (count Groups if:(= (id P2) (who at:(to))) min:(var "CutOff"))
                        )
                    )
                    (!= 0
                        (+
                            (count Groups if:(= (id P1) (who at:(to))) min:(var "CutOff"))
                            (count Groups if:(= (id P2) (who at:(to))) min:(var "CutOff"))
                        )
                    )
                )
                (set Var "CutOff"
                    (+ 1 (var "CutOff"))
                )
            )
            next:(and
                (set Score P1 (count Groups if:(= (id P1) (who at:(to))) min:(var "CutOff")))
                (set Score P2 (count Groups if:(= (id P2) (who at:(to))) min:(var "CutOff")))
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Yoin"
    (players 2)
    (equipment {
        (board (hex <Size>))
        (piece "Disc" Each)
    })
    (rules
        (meta (swap))
        (play
            (move Add
                (to (sites Empty))
                (then <Score>)
            )
        )
        (end 
            (if (is Full) 
                {(if
                    (< 
                        (score P1)
                        (score P2)
                    )
                    (result P1 Win)
                )
                (if
                    (> 
                        (score P1)
                        (score P2)
                    )
                    (result P2 Win)
                )}
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Size> args:{ <size> }
    {
    (item "3"	<3>		"A size 3 board is currently selected")
    (item "4"	<4>		"A size 4 board is currently selected")
    (item "5"	<5>		"A size 5 board is currently selected")
    (item "6"	<6>		"A size 6 board is currently selected")
    (item "7"	<7>		"A size 7 board is currently selected")*
    (item "8"	<8>		"A size 8 board is currently selected")
    (item "9"	<9>		"A size 9 board is currently selected")
    (item "10"	<10>	"A size 10 board is currently selected")
    (item "11"	<11>	"A size 11 board is currently selected")
    (item "12"	<12>	"A size 12 board is currently selected")
    }
)

(option "Running Score" <Score> args:{ <score> }
    {
    (item "On" <("UpdateScore")> "The score is currently updated after each move.")
    (item "Off" <(if (is Full) ("UpdateScore"))> "The score is currently updated only when the board is full. (This makes the AI stronger.)")*
    }
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Yoin is a drawless game for two players: Black and White. It is played on the spaces (cells) of an initially empty hexagonal grid of hexagons (board).Yoin was invented by Luis Bolaños Mures in 2021.")
        (rules "Definitions
            A group is a stone along with all other stones one could reach from it through a series of steps between adjacent stones of its color.
            
            The size of a group is the number of stones in it.
            
            Play
            Black plays first, then turns alternate. On your turn, place a stone of your color on an empty cell. When the board is full, the winner is determined as follows:
            
            (1) Let n equal 0.
            (2) The player with fewer groups bigger than n stones wins.
            (3) If no winner is found, increase n by 1 and go back to (2).
            
        The pie rule is used in order to make the game fair. This means White will have the option, on their first turn only, to swap sides with their opponent instead of making a regular move.")
        (id "1978")
        (source "<a href=\"https://boardgamegeek.com/boardgame/358594/yoin\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.8")
        (classification "board/space/group")
        (author "Luis Bolaños Mures")
        (credit "Michael Amundsen")
        (date "2021")
        }
    )
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (region Colour (sites Board) (colour 250 210 148))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
    })
    (ai
        "Yoin_ai"
    )
)
