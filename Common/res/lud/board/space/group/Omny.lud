(define "StarCells"
    <Stars>
)

//------------------------------------------------------------------------------

(game "Omny"
    (players 2)
    (equipment
        {
        (board (hex <Shape> <Size>))
        (piece "Disc" Each)
        }
    )
    (rules
        (play
            (if
                (= 1 (var))
                (or
                    (move Swap Players P1 P2)
                    (move Add (to (sites Empty)) (then (set Var 0)))
                )
                (move Add
                    (to (sites Empty))
                    <Balance>
                )
            )   
        )
        (end 
            (if 
                (>=
                    (count Sites 
                        in:("StarCells")
                    )
                    (max
                        (results
                            from:(difference
                                (sites Board)
                                (sites Group at:(last To))
                            )
                            to:2 
                            (* (to)
                                (count Sites
                                    in:(intersection
                                        ("StarCells")
                                        (sites Group at:(from)
                                            if:(not
                                                (is In
                                                    (to)
                                                    (sites Group at:(last To))
                                                )
                                            )
                                        )
                                    )
                                )
                            )   
                        )
                    )
                )
                (result Mover <Result>)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Shape" <Shape> args:{ <shape> }
    {
    (item "Hex"         <>  "A hex board is currently selected")*
    (item "Triangle"    <Triangle>  "A triangluar board is currently selected")
    (item "Diamond"     <Diamond>  "A diamond shaped board is currently selected")
    (item "Star"        <Star>  "A star shaped board is currently selected")
    (item "Limping Hex" <Limping>  "A limping hex board is currently selected")
    }
)

(option "Board Size" <Size> args:{ <size> }
    {
    (item "3"    <3>  "A size 3 board is currently selected")
    (item "4"    <4>  "A size 4 board is currently selected")
    (item "5"    <5>  "A size 5 board is currently selected")
    (item "6"    <6>  "A size 6 board is currently selected")
    (item "7"    <7>  "A size 7 board is currently selected")*
    (item "8"    <8>  "A size 8 board is currently selected")
    (item "9"    <9>  "A size 9 board is currently selected")
    (item "10"    <10>  "A size 10 board is currently selected")
    (item "11"    <11>  "A size 11 board is currently selected")
    (item "12"    <12>  "A size 12 board is currently selected")
    }
)

(option "Star Cells" <Stars> args:{ <rule> } {
    (item "All" 
        <(sites Board)> 
        "The currently selected star cells are all the cells on the board."
    )*
    (item "Perimeter"  
        <(sites Outer)>  
        "The currently selected star cells are the perimeter cells cells"
    )
    (item "Corners"  
        <(sites Corners)>  
        "The currently selected star cells are the corner cells"
    )
    (item "Perimeter + center"  
        <(union (sites Outer) (sites {(centrePoint)}))>  
        "The currently selected star cells are the perimeter cells and the center cell"
    )
    (item "Corners + center"  
        <(union 
            (sites Corners) 
            (sites {(centrePoint)})
        )>  
        "The currently selected star cells are the corner cells and the center cell"
    )
    (item "Perimeter + central triangle"  
        <(union 
            (sites Outer) 
            (sites {
                (ahead (centrePoint) N) 
                (ahead (centrePoint) WSW) 
                (ahead (centrePoint) ESE)
            })
        )>  
        "The currently selected star cells are the perimeter cells and a central triangle of cells"
    )
    (item "Corners + central triangle"  
        <(union 
            (sites Corners) 
            (sites {
                (ahead (centrePoint) N) 
                (ahead (centrePoint) WSW) 
                (ahead (centrePoint) ESE)
            })
        )>  
        "The currently selected star cells are the corner cells and a central triangle of cells"
    )
})

(option "Balance Rule" <Balance> args:{ <rule> }
    {
    (item "Pie"   
        <(then 
            (if
                (= 1 (count Moves))
                (set Var 1)
            )
        )>  
        "The pie rule is currently in force"
    )*
    (item "12*"  
        <(then
            (if
                (= 1 (% (+ 1 (count Moves)) 2))
                (moveAgain)
            )
        )>  
    "The 12* placement protocol is currently in force")
    (item "None"  <>  "No balance rule is currently in force")
    }
)

(option "End Rule" <Result> args:{ <rule> }
    {
    (item "Standard" <Win>   "The standard win condition is currently selected.")*
    (item "Misère"   <Loss>  "The misère win condition is currently selected")
    }
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Omny is a system of connection games that originated as a generalization of Mark Steere's Gyre. It includes Y as one of its many variations. Luis Bolaños Mures invented Omny in 2017.")
        (rules "Omny is played on the cells of any hexagonal grid where some or all of the cells are designated star cells. Players take turns placing a stone of one's own color on an empty cell. A group is a set of connected stones of the same color. A cut of a group X is any set of connected cells (empty or occupied by either player) including no stones in X. You win by making a group such that none of its cuts include more than half the cells of the board. For serious play it is advised to play with a balance rule in force.
            
        In Misère Omny you lose if you make a group whose cuts each contain no more than half of the star cells.")
        (id "1529")
        (version "1.3.4")
        (classification "board/space/group")
        (author "Luis Bolaños Mures (used with permission)")
        (credit "Michael Amundsen")
        (date "2017")
        }
    )
    (graphics {
        (show Symbol "StarOutline"
            ("StarCells")
            edgeColour:(colour 0 0 0 48)
            scale:0.5
        )
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
    })
    
    (ai
        "Omny_ai"
    )
)
