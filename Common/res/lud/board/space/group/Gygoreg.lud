(define "Place"
    (move Add
        (to 
            (sites Around (sites Occupied by:Mover) Empty Orthogonal)
        )
    )
)

(define "Remove"
    (move Select
        (from 
            (sites Occupied by:Enemy)
        )
        (then (forEach Site (sites Group at:(last From) Orthogonal) (remove (site))))
    )
)

//-------------------------------------------------------------------------

(game "Gygoreg"
    (players 2)
    (equipment
        {
        (board (square Diamond <Size>))
        (piece "Disc" Each)
        }
    )
    (rules
        (start {
            (place "Disc1" (forEach (sites Phase 0) if:(is Odd (site))))
            (place "Disc2" (forEach (sites Phase 0) if:(is Even (site))))
            
        })
        (play
            (or
                ("Place")
                ("Remove")
            )
        )
        (end {
            (if 
                (= 1 (count Groups Orthogonal if:(= 1 (who at:(to)))))
                (result P1 Win)
            ) 
            (if 
                (= 1 (count Groups Orthogonal if:(= 2 (who at:(to)))))
                (result P2 Win)
            ) 
        })
    )
)

//-------------------------------------------------------------------------

(option "Board Size" <Size> args:{ <size> }
    {
    (item "4"    <4>  "A size 4 board is currently selected")
    (item "6"    <6>  "A size 6 board is currently selected")
    (item "8"    <8>  "A size 8 board is currently selected")
    (item "10"    <10>  "A size 10 board is currently selected")*
    }
)

//-------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Gygoreg is a deceptively simple unification game invented by Mike Zapawa in 2020.")
        (rules "Each turn you must perform exactly one of the following two actions: 
            
            1. Add a friendly piece orthogonally adjacent to one of your pieces. 
            2. Remove an enemy group.
            
        As soon as one player only has 1 group, the game is over and that player is the winner.")
        (id "1654")
        (version "1.3.4")
        (classification "board/space/group")
        (author "Mike Zapawa")
        (credit "Michael Amundsen")
        (date "2020")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
        (region Colour (sites Board) (colour 250 210 148))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
    })
)
