(game "Feed the Ducks"  
    (players 2) 
    (equipment { 
        (board (hex 5)) 
        (piece "Marker" Each)
        (piece "Disc" Neutral)
    })  
    (rules 
        (start {
            (place "Marker1" {"A5" "C7" "D8" "F9" "H9" "I9" "I7" "I5" "F2" "C1" "A1" "A2"}) 
            (place "Marker2" {"A4" "A3" "B1" "D1" "E1" "G3" "H4" "I6" "I8" "G9" "E9" "B6" })
        })
        phases:{
        (phase "Throw Bread" 
            (play 
                (move 
                    Add 
                    (piece "Disc0")
                    (to (sites Empty)) 
                    (then (attract (from (last To))))
                )
            ) 
            (nextPhase "Move Bread")
        )
        (phase "Move Bread" 
            (play 
                (move 
                    (from (sites Occupied by:Neutral)) 
                    (to (difference (sites Empty) (sites Direction from:(last To))))
                    (then (attract (from (last To))))
                )
            )
        )
        }
        (end {
            (if (= 1 (count Groups if:(= (who at:(to)) P1))) (result P1 Win)) 
            (if (= 1 (count Groups if:(= (who at:(to)) P2))) (result P2 Win))
        })  
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Feed the Ducks was invented in 2011 by Néstor Romeral Andrés.
        It is a game played on a hexagonal board where the goal is to group all of a player's pieces together.")
        (rules "The game is played in turns. White starts by placing the breadcrumb on any empty space on the board. Now, any and every duck in the six lines radiating out from the breadcrumb move straight toward the breadcrumb as far as they can. There will never be more than one duck in a space, and the ducks stop just before they reach the breadcrumb (a fish gets it first). Then the turn passes to the next player. From now on, on your turn, you must take the breadcrumb and place it on an empty space of the board that is not on any of the six lines radiating from the breadcrumb's previous location and then move the ducks toward the breadcrumb as usual. The game ends when all the ducks of one colour have gathered into a single connected group. In case of both groups (White and Black) are created simultaneously, the player who placed the game-ending breadcrumb wins.")
        (source "<a href=\"https://boardgamegeek.com/boardgame/112532/feed-ducks\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (id "442")
        (version "1.3.4")
        (classification "board/space/group")
        (author "Néstor Romeral Andrés")
        (publisher "<a href=\"https://www.nestorgames.com\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a> (used with permission)")
        (credit "Eric Piette")
        (date "2011")
        }
    )
    
    (graphics {
        (player Colour Neutral (colour Yellow))
    })
    
    (ai 
        "Feed the Ducks_ai"
    )
    
)
