(define "DistInColumn" (count Sites in:(difference (sites Column (column of:(from))) (sites Empty))))

(define "DistInRow"    (count Sites in:(difference (sites Row (row of:(from))) (sites Empty))))

(define "DistInNESW"   (count Sites in:(difference (sites Direction from:(from) (directions {NE SW}) included:True) (sites Empty))))

(define "DistInNWSE"   (count Sites in:(difference (sites Direction from:(from) (directions {NW SE}) included:True) (sites Empty))))

(define "MoveTo" 
    (move
        Hop
        #1 
        (between 
            (exact (- #2 1))
            if:(not (is Enemy (who at:(between))))
        )
        (to 
            if:(not (is Friend (who at:(to))))
            (apply (if (is Enemy (who at:(to))) (remove (to))))
        )
    )
)

//------------------------------------------------------------------------------

(game "Lines of Action"  
    (players 2)  
    
    (equipment { 
        (board (square 8)) 
        (piece "Disc" Each 
            (or {
                ("MoveTo" (directions {N S}) "DistInColumn")
                ("MoveTo" (directions {W E}) "DistInRow")
                ("MoveTo" (directions {NE SW}) "DistInNESW")
                ("MoveTo" (directions {NW SE}) "DistInNWSE")
            })
        )
        }
    )  
    
    (rules 
        (start
            { 
            (place "Disc1" (difference (union (sites Top) (sites Bottom)) (sites Corners)))
            (place "Disc2" (difference (union (sites Right) (sites Left)) (sites Corners)))
            }
        )
        
        (play (forEach Piece))
        
        (end { 
            (if (and (= 1 (count Groups if:(= (who at:(to)) P1))) (= 1 (count Groups if:(= (who at:(to)) P2)))) (result Mover <End:result>) ) 
            (if (= 1 (count Groups if:(= (who at:(to)) P1))) (result P1 Win) ) 
            (if (= 1 (count Groups if:(= (who at:(to)) P2))) (result P2 Win) ) 
        })  
    )
)

//------------------------------------------------------------------------------

(option "End rules" <End> args:{ <result> }
    {
    (item "Simultaneous Group = Draw"    <Draw>    "If the two players make a group in the same turn, the game ends in a draw.")*  
    (item "Simultaneous Group = Mover Win"    <Win>    "If the two players make a group in the same turn, the mover wins.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Lines of Action was invented by Claude Soucie in 1969. It is played on an 8x8 board with twelve pieces per player.")
        (rules "A player wins by connecting all of one's pieces into a contiguous body so that they are connected orthogonally or diagonally. Players alternate moves. Pieces move orthogonally or diagonally. A piece moves exactly as many spaces as there are pieces (belonging to both the player and their opponent) on the line in which it is moving. A piece may jump over the player's own pieces, but not over one belonging to the opponent. A piece can capture piece belonging to the opponent by landing on it.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Lines_of_Action\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "394")
        (version "1.3.4")
        (classification "board/space/group")
        (author "Claude Soucie")
        (credit "Eric Piette")
        (date "1988")
        }
    )
    
    (graphics {
        (board Style Chess)
        (player Colour P1 (colour VeryDarkGrey))
        (player Colour P2 (colour White))
    })
    
    (ai 
        "Lines of Action_ai"
    )
    
)
