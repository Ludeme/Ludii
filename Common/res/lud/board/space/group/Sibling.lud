(define "SitesPlayable"
    (difference
        (intersection
            (sites Direction from:#1)
            (sites Empty)
        )
        (sites Around #1)
    )
)
(define "SecondLargestGroup"
    (max (difference (sizes Group #1) (max (sizes Group #1))))
)
(define "LargestGroupCascading"
    (max 
        (difference (sizes Group #1) (sizes Group #2)) 
    )
)
(define "Score"
    (if (!= ("SecondLargestGroup" #1) ("SecondLargestGroup" #2))
        ("SecondLargestGroup" #1)
        ("LargestGroupCascading" #1 #2)
    )
)

//------------------------------------------------------------------------------

(game "Sibling" 
    (players 2) 
    (equipment 
        { 
        (board (hex <Board>))
        (piece "Disc" Each)
        }
    ) 
    (rules
        phases:{
        (phase "First" 
            (play
                (move Add
                    (to (sites Board))
                )
            )
            (nextPhase "Play")
        )
        (phase "Play"
            (play
                (if (= (count MovesThisTurn) 0)
                    (move Add
                        (to (sites Empty)
                            if:(!=
                                ("SitesPlayable" (to))
                                (sites {})
                            )
                        )
                        (then (moveAgain))
                    )
                    (move Add
                        (to
                            ("SitesPlayable" (last To))
                        )
                    )
                )
            )
        )   
        }
        (end
            (if
                (no Moves Mover) 
                (byScore
                    {
                    (score P1 ("Score" P1 P2))
                    (score P2 ("Score" P2 P1))
                    }
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "3" <3> "Played on a size 3 board.")
    (item "4" <4> "Played on a size 4 board.")
    (item "5" <5> "Played on a size 5 board.")*
    (item "6" <6> "Played on a size 6 board.")
    (item "7" <7> "Played on a size 7 board.")
    (item "8" <8> "Played on a size 8 board.")
    }
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Sibling is a simple placement game where the goal is to maximize the group size.")
        (rules "Starting with the White player. The first player places one of his/her pieces on the board.
            From the next Black's turn, the turn player places exactly two of his/her pieces on the board. The two pieces must be on the same straight line of the grid and must not be adjacent to each other.
            
            The game ends when no more pieces can legally be placed.
            The second largest group of each color is compared and the larger player wins.
        In case of a tie, the first largest group is compared, and if there is still a tie, the comparison continues with the third, fourth, and so on. No draws occur.")
        (id "4215")
        (version "1.3.14")
        (classification "board/space/group")
        (author "Kanare Kato
        ")
        (credit "Takuro Kawasaki")
        (date "13-12-2023")
        }
    )
    (graphics
        {
        (show Score P1 ("Score" P1 P2))
        (show Score P2 ("Score" P2 P1))
        }
    )
)
