(define "ComputeScore"
    (and {
        (if
            (or {
                (and (> (count SizeBiggestGroup if:(= (who at:(to)) 1)) (score P1)) (> (count SizeBiggestGroup if:(= (who at:(to)) 1)) (score P2)))
                (and (> (count SizeBiggestGroup if:(= (who at:(to)) 2)) (score P1)) (> (count SizeBiggestGroup if:(= (who at:(to)) 2)) (score P2)))
            })
            (and {
                (forget Value "BiggerGroup" All)
                (remember Value "BiggerGroup" 1)
            })
        )
        (set Score P1 (count SizeBiggestGroup if:(= (who at:(to)) 1)))
        (set Score P2 (count SizeBiggestGroup if:(= (who at:(to)) 2)))	
    })	
)

//------------------------------------------------------------------------------

(game "Spice" 
    (players 2) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each maxValue:16)
        (piece "Ball" Neutral)
    }) 
    
    (rules 
        (meta {(pin SupportMultiple) (gravity)})
        (start {
            (place "Ball0" {8 10 14 18 20})
        })
        (play  		
            (if
                (and (not ((is Prev Mover))) (not ((is Prev Next))))
                (move Add 
                    (to 
                        (sites Empty) 
                        if:(is Flat)
                    )
                    (then 
                        "ComputeScore"
                    )
                )
                (or {
                    (if 
                        (and (= (arrayValue (values Remembered "BiggerGroup") index:0) 1) (not "SameTurn"))
                        (or {
                            (move Remove
                                (sites Occupied by:P1)
                                (then
                                    (and {
                                        (forget Value "BiggerGroup" All)
                                        (remember Value "BiggerGroup" 0)
                                        "ComputeScore"
                                        (set Pending)
                                        (moveAgain)
                                    })
                                )
                            )
                            (move Remove
                                (sites Occupied by:P2)
                                (then
                                    (and {
                                        (forget Value "BiggerGroup" All)
                                        (remember Value "BiggerGroup" 0)
                                        "ComputeScore"
                                        (set Pending)
                                        (moveAgain)
                                    })
                                )
                            )
                            (move Add 
                                (to 
                                    (sites Empty) 
                                    if:(is Flat)
                                )
                                (then
                                    (and {
                                        (forget Value "BiggerGroup" All)
                                        (remember Value "BiggerGroup" 0)
                                        "ComputeScore"
                                        (moveAgain)
                                    })	
                                )
                            )
                        })
                    )
                    (if
                        (and (= (arrayValue (values Remembered "BiggerGroup") index:0) 0) (not "SameTurn"))
                        (move Add 
                            (to 
                                (sites Empty) 
                                if:(is Flat)
                            )
                            (then 
                                (and {
                                    "ComputeScore"
                                    (moveAgain)	
                                })          			    	
                            )
                        )
                    )
                    (if
                        (and "SameTurn" (is Pending))
                        (move Add 
                            (to 
                                (sites Empty) 
                                if:(is Flat)
                            )
                            (then 
                                (and
                                    "ComputeScore"
                                    (moveAgain)
                                )
                            )
                        )
                    )
                    (if
                        (and "SameTurn" (not (is Pending)))
                        (or {
                            (move Add 
                                (to 
                                    (sites Empty) 
                                    if:(is Flat)
                                )
                                (then "ComputeScore")
                            )
                            (move Pass)
                        })
                    )
                })
            )
        )
        
        (end {
            (if 
                (and (= (count Pieces in:(sites Occupied by:All container:"Board")) 29) (> (count SizeBiggestGroup if:(= (who at:(to)) 1)) (count SizeBiggestGroup if:(= (who at:(to)) 2))))
                (result P1 Win)
            )
            (if 
                (and (= (count Pieces in:(sites Occupied by:All container:"Board")) 29) (< (count SizeBiggestGroup if:(= (who at:(to)) 1)) (count SizeBiggestGroup if:(= (who at:(to)) 2))))
                (result P2 Win)
            )
            (if 
                (and (= (count Pieces in:(sites Occupied by:All container:"Board")) 29) (= (count SizeBiggestGroup if:(= (who at:(to)) 1)) (count SizeBiggestGroup if:(= (who at:(to)) 2))))
                (result All Draw)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Spice is a short game for two players. Spice is a very simple adaptation of Ketchup for play with the Series: Shibumi game system.")
        (rules "In Spice, the goal is to have the largest group of stones when the game is complete. A group is defined as stones that are \\"touching\\", either orthogonally or one on top of the other. The game is over when all the spaces on the gameboard have been filled. Players play with either White or black stones, and the game begins with a small red 5-stone 2x2 pyramid in the center of the gameboard. The red stones cannot be removed and are neutral, they do not count toward either player's groups of stones. On the first turn of the game, White must place 1 white stone in any empty space. On subsequent turns, players put 1 or 2 stones of their color in any empty space on the board. If the largest group of stones on the board after your turn is larger than the largest group of stones (of either color) before your turn, then your opponent may remove one of your \\"free\\" stones on their next turn (and only on their next turn). The game ends immediately when the board is full, but the top stone is removed before scoring.")
        (id "4236")
        (source "<a href=\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a>")
        (version "1.3.14")
        (classification "board/space/group")
        (author "Avri Klemer")
        (credit "CÃ©dric Antoine")
        (date "2011")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour Neutral fillColour:(colour Red))
        (piece Colour P1 fillColour:(colour White))
        (piece Colour P2 fillColour:(colour Black))
    })
    
)
