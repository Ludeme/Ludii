
(define "CombinedGroupCounts"
    (
        + {
        (* 10000 (count Groups Orthogonal #1))
        (* 100 (- (count Groups Orthogonal #1) (count Groups Orthogonal #1 min:3)))
        (-
            (- (count Groups Orthogonal #1) (count Groups Orthogonal #1 min:5))
            (- (count Groups Orthogonal #1) (count Groups Orthogonal #1 min:3))
        )
        }
    )
)

//*************************************************************

(game "Hermit"
    (players 2)
    (equipment {
        (board <Board:type> use:Vertex )
        (piece "Marker" Each )
        (piece "Marker" Neutral)
    })
    (rules
        (start {
            <Board:seed>
            (set Score P1 0)
            (set Score P2 0)
        })

        (play
            (do
                (move
                    Add (to (sites Empty))
                )
                ifAfterwards:
                (or {
                    (= 1 (size Group at:(last To) Orthogonal) )
                    (= 3 (size Group at:(last To) Orthogonal) )
                    (= 5 (size Group at:(last To) Orthogonal) )
                    }
                )
                (then
                    (addScore Mover (- ("CombinedGroupCounts" if:(= (who at:(to)) (mover)))  (score Mover)))
                )
            )
        )
        (end
            (if (all Passed)
                {
                (if (> ("CombinedGroupCounts" if:(= (who at:(to)) P1))
                        ("CombinedGroupCounts" if:(= (who at:(to)) P2))
                ) (result P1 Win) )
                (if (< ("CombinedGroupCounts" if:(= (who at:(to)) P1))
                        ("CombinedGroupCounts" if:(= (who at:(to)) P2))
                ) (result P2 Win) )
                }
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Type" <Board> args:{ <type> <seed> }
    {
    (item "Tri Hex 3"       <(tri Hexagon 3)>
    <>  "Base 3 hexagonal board of triangles")
    (item "Tri Hex 5*"      <(tri Hexagon 5)>
    <>  "Base 5 hexagonal board of triangles")*
    (item "Square 5"       <(square 5)>
    <>  "Base 5 square board")
    (item "Square 7**"       <(square 7)>
    <> "Base 7 square board")
    (item "Square 9"       <(square 9)>
        <>
    "Base 9 square board")
    (item "Hex 2"             <(hex 2)>
        <(place Random {"Marker0"} count:1) >
    "Base 2 hexagonal board of hexagons")
    (item "Hex 3"             <(hex 3)>
        <(place Random {"Marker0"} count:1) >
    "Base 3 hexagonal board of hexagons")
    (item "2x3 triangle"    <(tri Limping 3)>
        <>
    "Base 2x3 truncated triangular board")
    (item "3x4 triangle"    <(tri Limping 4)>
        <>
    "Base 3x4 truncated triangular board")
    (item "TriSquares"     <(tiling T33434 2)>
        <(place Random {"Marker0"} count:1) >
    "Stub square board")
    (item "Kensington 1"  <(rotate 90 (tiling T3464 1))>
        <(place Random {"Marker0"} count:1) >
    "Kensington mini board")
    (item "Kensington 2"  <(rotate 90 (tiling T3464 2))>
        <(place Random {"Marker0"} count:3) >
    "Kensington board")
})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Hermit is a game in which players place stones on empty vertices to create connected groups of sizes 1, 3 and 5.
            It is easy to learn but challenging to master. Although the game is static (pieces do not move),  there is still a dynamic effect because legal placements turn on and off!
        Hermit can be played on the nodes of any graph, even on a four-dimensional tesseract! Twelve 2-dimensional graphs are included. Some graph require a neutral token to prevent symmetric play by the second player.")
        (rules "Hermit is a two player abstract game in which players drop stones to create edgewise connected, owned groups of odd sizes 1, 3 and 5 exclusively.
            The game ends when neither player can legally place a stone.
            The player with the most groups on the board at game's end wins.
            Ties are broken by the most groups of size 1 (hermits), then by the most groups of size 3, and finally, the last player to place a stone wins.

            Scores are formatted 'aa1133', where aa = count of all groups, 11 = count of size 1 groups, and 33 = count of size 3 groups.

        It is helpful to turn on 'Show Legal Moves' (Alt-M).")
        (source "<a href=\"https://sites.google.com/site/theowlsnest02/home/hermit\\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />The Owl's Nest - Hermit</a>

        ")
        (version "1.3.0")
        (classification "board/space/group")
        (author "Phil Leduc")
        (credit "Phil Leduc")
        (date "2020")
        }
    )

    (graphics {
        (board Colour Phase0 (colour "#f0ffff"))
        (board Colour InnerEdges (colour GunMetal))
        (board Colour OuterEdges (colour Black))

        (player Colour Neutral (colour LightGrey))
        (player Colour P1 (colour "#9ade00"))
        (player Colour P2 (colour "#0084c8"))

        (show Edges Diagonal Hidden)

        (show Score Always)

    })

)
