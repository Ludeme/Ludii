(define "PossibleSquareFrom" (and (!= (site) (ahead (site) #1)) (!= (site) (ahead (site) #2))))
(define "SquareFrom" (sites { (site) (ahead (site) #1) (ahead (site) #2) (ahead (ahead (site) #1) #2)}))

(define "PossibleSquareFromBottomLeft" ("PossibleSquareFrom" N E))
(define "SquareFromBottomLeft" ("SquareFrom" N E))

(define "PossibleSquareFromTopLeft" ("PossibleSquareFrom" S E))
(define "SquareFromTopLeft" ("SquareFrom" S E))

(define "PossibleSquareFromTopRight" ("PossibleSquareFrom" S W))
(define "SquareFromTopRight" ("SquareFrom" S W))

(define "PossibleSquareFromBottomRight" ("PossibleSquareFrom" N W))
(define "SquareFromBottomRight" ("SquareFrom" N W))

(define "EnoughOwnedInSquare" 
    (and 
        (= 0 (count Sites in:(intersection (sites Empty) (#2))))
        (<= 3 (count Sites in:(intersection (sites Occupied by:#1) (#2))))
    )
)

(define "AddOnTopSquare" (add (piece (id "Ball" #1)) (to (ahead (site) #2) if:(is Flat))))

(define "TopExistAndEmptySite"
    (and
        (not (is Empty (site)))
        (is Empty (ahead (site) #1))
    )
)

(define "AddOnTopSquareIfLegal" 
    (and {
        (if (and ("TopExistAndEmptySite" UNE) ("PossibleSquareFromBottomLeft"))
            (if ("EnoughOwnedInSquare" #1 "SquareFromBottomLeft")
                ("AddOnTopSquare" #1 UNE)
            )
        )
        (if (and ("TopExistAndEmptySite" USE) ("PossibleSquareFromTopLeft"))
            (if ("EnoughOwnedInSquare" #1 "SquareFromTopLeft")
                ("AddOnTopSquare" #1 USE)
            )
        )
        (if (and ("TopExistAndEmptySite" USW) ("PossibleSquareFromTopRight"))
            (if ("EnoughOwnedInSquare" #1 "SquareFromTopRight")
                ("AddOnTopSquare" #1 USW)
            )
        )
        (if (and ("TopExistAndEmptySite" UNE) ("PossibleSquareFromBottomRight"))
            (if ("EnoughOwnedInSquare" #1 "SquareFromBottomRight")
                ("AddOnTopSquare" #1 UNE)
            )
        )
    })		
)

(define "APieceCanGoOnTop" 
    (!= 0 
        (count Sites in:(forEach (sites Board) 
                if:(can Move 
                    (or 
                        ("AddOnTopSquareIfLegal" P1)
                        ("AddOnTopSquareIfLegal" P2)
                    )
                )
            )
        )
    )		
)

(define "PutOnTop" 
    (while 
        ("APieceCanGoOnTop")
        (forEach Site 
            (sites Board)
            (and 
                ("AddOnTopSquareIfLegal" P1)
                ("AddOnTopSquareIfLegal" P2)
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Upper Hand"
    (players 2)
    (equipment {
        (board (square 5 pyramidal:True) use:Vertex) 
        (piece "Ball" Each) 
        (piece "Ball" Neutral)
    })
    (rules 
        (start 
            (place "Ball0" (centrePoint))
        )
        (play 
            (move 
                Add 
                (to (sites Empty) 
                    if:(is Flat)
                )
                (then ("PutOnTop"))
            )
        )
        (end 
            (if 
                (<= 27 (count Sites in:(sites Occupied by:Mover))) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "A 3 dimensional game of skill and strategy for two players.")
        (aliases {"Pyramids" "Use Your Marbles" "Au sommet de la pyramide"})
        (rules "Each player in turn places one of his marbles in an open space on the board, on top of a square (i.e. 4 marbles next to each other). After the placement of each marble, check to see if a square or marbles have been formed. If at least 3 of the 4 marbles that form a square are of the same color, a piece of that color go on top of that square. If as a result other squares are formed, the same rule is applied. The winner is the first player to have at least 27 marbles of his colour on the board.
            
        ")
        (id "1105")
        (source "<a href=\"https://boardgamegeek.com/boardgame/4545/upper-hand\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.4")
        (classification "board/space/pattern")
        (author "Margalith Akavya")
        (publisher "BSM Jeux, Hilco Corporation, Orda Industries Ltd.")
        (credit "Eric Piette")
        (date "1992")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (player Colour Neutral (colour Yellow))
        (player Colour P1 (colour Blue))
        (player Colour P2 (colour Red))
    })
    (ai
        "Upper Hand_ai"
    )
)
