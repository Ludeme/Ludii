(define "AllVerticesOfCell"
    (sites Incident Vertex of:Cell at:(site))
)

(define "SquareFilled"
    (= 
        4 
        (count Sites in:(forEach ("AllVerticesOfCell") if:(is Mover (who at:(site)))))
   )
)

(define "IsATriangle"
    (= 3 (count Sites in:(sites Incident Vertex of:Cell at:(site))))
)

(define "TwoFriendPieces"
    (= 2 (count Sites in:(forEach (sites Incident Vertex of:Cell at:(site)) if:(is Mover (who at:(site))))))	
)

(define "OneEnemyPiece"
    (= 1 (count Sites in:(forEach (sites Incident Vertex of:Cell at:(site)) if:(is Next (who at:(site))))))
)

(define "ReplaceEnemyPieceByNeutral"
    (and
        (remove (site))
        (add (piece (id "Disc" Neutral)) (to (site)))
   )	
)

(define "TrianglesWithCaptures"
    (forEach (sites Incident Cell of:Vertex at:(last To))
        if:(and {
            ("IsATriangle")
            ("TwoFriendPieces")
            ("OneEnemyPiece")
        })
   )
)

//------------------------------------------------------------------------------

(game "Diamond"  
    (players 2)  
    (equipment { 
        (board 
            (rotate 90 (tiling T33434 3))
            use:Vertex
       ) 
        (piece "Disc" Each 
            (move Step (to if:(is Empty (to)))
                (then
                    (if (= 1 (count Sites in:("TrianglesWithCaptures")))
                        (forEach Site (sites Incident Cell of:Vertex at:(last To))
                            (if (and {
                                    ("IsATriangle")
                                    ("TwoFriendPieces")
                                    ("OneEnemyPiece")
                                })
                                (and
                                    (forEach Site
                                        (forEach (sites Incident Vertex of:Cell at:(site)) if:(is Next (who at:(site))))
                                        ("ReplaceEnemyPieceByNeutral")
                                   )
                                    (set Counter)
                               )
                           )
                       )
                   )
               )
           )
       ) 
        (piece "Disc" Neutral)
        (hand Each)
        (regions P1 {(sites Side W) (sites Side E) })
        (regions P2 {(sites Side N) (sites Side S) })
    })  
    (rules
        (meta (swap))
        
        (start (place "Disc" "Hand" count:12))
        
        phases:{
        (phase "Placement"
            (play 
                (move
                    (from (handSite Mover))
                    (to (sites Empty))
                    (then
                        (and
                            (forEach Site (sites Incident Cell of:Vertex at:(last To))
                                (if ("SquareFilled") (trigger "Win" Mover))
                           )
                            (set Counter)
                       )
                   )
               )
           )
            (nextPhase Mover (is Empty (handSite Mover)) "Movement")
       )
        (phase "Movement"
            (play 
                (or
                    (forEach Piece)
                    (move Remove (sites Occupied by:Neutral))
               )
           )
       )
        }
        (end { 
            (if (is Triggered "Win" Mover)
                (result Mover Win)
           )
            (if (or (no Moves Mover) (= (counter) 49)) (result Mover Draw)) 
        }) 
   )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Diamond is a two-player abstract strategy board game invented by Larry Back. The invention was inspired by the game Kensington, which uses a similar board pattern and game objective. Rules for Diamond were conceived in 1985 and finalized in 1994. Diamond introduces a new board geometry and neutral pieces, with the aim of enhancing the game dynamic and lowering the potential for draws.
            
        Diamond was featured in the February 2013 issue of Games magazine.")
        (rules "The game begins with an empty Diamond board. Black moves first, then turns alternate. (To offset any advantage Black has in moving first, the pie rule is used: White can choose to switch sides after Black's first move, playing from then on as Black. White has this option only after Black's first move.) 
            
            The game is executed in two phases:
            
        1) Placement phase. Players take turns placing one of their pieces on any open point on the board. No placements result in a capture in this phase. A player can win the game in this phase if they are able to occupy all four corners of a board square; otherwise, play proceeds to the Movement phase once all 24 pieces have been placed.
        
    2) Movement phase. For their turn, a player may either:
    - move one of their pieces along a straight line to an adjacent empty point; or,
    - remove a neutral piece from the boardâ€”but only if no white or black piece is adjacent to it.
    
    Capturing moves are possible in the Movement phase. If the points of a triangle contain exactly one white and one black piece, either player can capture the opponent piece by occupying the remaining open point (\"cornering\" the enemy piece on the triangle). 
    
    If a move simultaneously corners two opponent pieces on two different triangles, then neither enemy piece is captured. A piece can move safely to a triangle point even if the other two points of the triangle are occupied by enemy pieces.
    
    A player wins by being the first to occupy all four corners (points) of a board square with their pieces.
    
    The game is drawn if any of the following occurs:
    - The player whose turn it is to move cannot move a piece.
- In the last 50 moves (25 moves per player) no capture has been made, nor neutral piece removed.")
(id "1771")
(source "<a href=\"https://en.wikipedia.org/wiki/Diamond_(game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(version "1.3.4")
(classification "board/space/pattern")
(author "Larry Back")
(credit "Eric Piette")
(date "1994")
}
)

(graphics {
    (player Colour Neutral (colour Red))
    (player Colour P1 (colour Black))
    (player Colour P2 (colour White))
    (board Background image:"square.svg" fillColour:(colour 218 164 50) edgeColour:(colour Black) scale:1.3)
    (board Colour InnerEdges (colour Black))
    (board Colour OuterEdges (colour Black)) 
    (board Colour InnerVertices (colour Hidden))
    (board Colour OuterVertices (colour Hidden)) 
    (show Edges Diagonal (colour Hidden))
})
)
