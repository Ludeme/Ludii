(define "ReverseBoundedPieces" 
    (custodial
        (from (site))
        (between 
            if:(is Enemy (state at:(between))) 
            (apply 
                (allCombinations 
                    (add (piece "Ball0" state:(mover)) (to (site))) 
                    (set State at:(between) (mover))
                )
            )
        )
        (to if:(is Friend (state at:(to))))
    )
)

//------------------------------------------------------------------------------

(game "Rolit"
    (players <Players:num>)
    (equipment {
        (board (square 8))
        (piece "Ball" Neutral)
    })
    (rules 
        
        (start {
            (place "Ball0" coord:"D5" state:1)
            (place "Ball0" coord:"E5" state:2)
            (place "Ball0" coord:"E4" state:3)
            (place "Ball0" coord:"D4" state:4)
        })
        
        (play
            (priority 
                (forEach Site 
                    (sites Empty)
                    (append 
                        "ReverseBoundedPieces" 
                    )
                )
                (move Add (piece "Ball0" state:(mover))
                    (to (sites Around (sites Occupied by:Neutral) Empty))
                )
                (then
                    (forEach Player 
                        (set Score Player 
                            (count Sites in:(sites State (player)))
                        )	
                    )
                )
            )
        )
        
        (end
            (if (all Passed)
                (byScore)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Players" <Players> args:{ <num>} {
    (item "2" <2> "The game is playing with two players.") 
    (item "3" <3> "The game is playing with three players.") 
    (item "4" <4> "The game is playing with four players.")* 
})

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Rolit is an easy to learn strategy game that’s fun for the whole family. The aim of the game is to capture your opponent’s Rolit balls and turn balls to make them your color. The player with the most Rolit balls of their color wins!")
        (rules "This is basically 4-player Othello, albeit more \"friendly\", since you can place a piece of your color even if you have none on the board. It features unique ball-shaped objects that have four colors. You \"roll\" them to the proper color when converting them, as opposed to flipping in 2-player Othello. The game adds a higher chaos factor in the 3 and 4 player game since you don't have any control over what your opponents do.")
        (id "1547")
        (source "<a href=\"https://boardgamegeek.com/boardgame/8243/rolit\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.7")
        (classification "board/space/territory")
        (author "Adi Golad")
        (publisher "<a href=\"https://www.goliathgames.us/\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Goliath Games</a>")
        (credit "Eric Piette")
        (date "1997")
        }
    )
    
    (graphics {
        (piece Colour state:1 fillColour:(colour Red))
        (piece Colour state:2 fillColour:(colour Yellow))
        (piece Colour state:3 fillColour:(colour Green))
        (piece Colour state:4 fillColour:(colour Blue))
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Yellow))
        (player Colour P3 (colour Green))
        (player Colour P4 (colour Blue))
        (board Style Graph)
        (show Edges Hidden)
        (board Colour OuterVertices (colour Hidden))
        (board Colour InnerVertices (colour Hidden))
        (show Symbol "Disc" (sites Board) scale:0.9)
        (board Background image:"square.svg" fillColour:(colour Black) edgeColour:(colour Black) scale:1.15)
    })
    
)
