(define "IsLethal"
    (not
        (all Sites
            (union
                (sites Around (last To) Empty)
                (sites Around (last To) Enemy)
            )
            if:(is In
                (site)
                (sites Group
                    at:(min
                        (array
                            (union
                                (sites Around (last To) Empty)
                                (sites Around (last To) Enemy)
                            )
                        )
                    )
                    if:(not (is Mover (who at:(to))))
                )
            )
        )
    )
)

(define "FrozenGroups"
    (forEach
        (sites Occupied by:(player #1))
        if:(= #1
            (state at:(site))
        )
    )
)

//------------------------------------------------------------------------------

(game "Verge"
    (players 2)
    (equipment
        {
        (board (hex <Size>))
        (piece "Disc" Each)
        }
    )
    (rules
        <Pie>
        (play
            (move Add
                (to 
                    (difference
                        (sites Empty)
                        (sites Around ("FrozenGroups" (mover)) Orthogonal)
                    )
                )
                (then
                    (if
                        ("IsLethal")
                        (and
                            (forEach Site
                                (sites Group at:(last To) if:(is Mover (who at:(to))))
                                (set State at:(site) (mover))
                            )
                            (remove
                                (sites Group
                                    from:(sites Around 
                                        (sites Group at:(last To) if:(is Mover (who at:(to))))
                                        Enemy
                                        if:(!= (next) (state at:(to)))
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        (end 
            (if 
                (no Moves Next)
                (result Next Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Size> args:{ <size> }
    {
    (item "4"    <4>  "A size 4 board is currently selected")
    (item "5"    <5>  "A size 5 board is currently selected")*
    (item "6"    <6>  "A size 6 board is currently selected")
    (item "7"    <7>  "A size 7 board is currently selected")
    (item "8"    <8>  "A size 8 board is currently selected")
    }
)

(option "Pie" <Pie> args:{ <pie> }
    {
    (item "On" <(meta (swap))> "The pie rule is currently in force.")*
    (item "Off" <> "The pie rule is currently not in force.")
    }
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Verge is a territory game of placement and group capture.
        ")
        (rules "Verge is a two-player game played on an initially empty hexhex board. 
            The players take turns placing a stone of their color on an empty hex. 
            The first player UNABLE to place a stone has WON.
            • A partitioning group is a group separating two hexes outside the group, in the sense that all paths between the two hexes go through the group.
            • You're not allowed to place adjacent to a friendly partitioning group.
            • When a group becomes partitioning, remove all nonpartitioning groups adjacent to it.
            
        The pie rule is used to balance the game. It can be turned off in the options.")
        (id "4198")
        (version "1.3.12")
        (classification "board/space/territory")
        (author "Michael Amundsen")
        (credit "Michael Amundsen")
        (date "07-2023")
        }
    )
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (region Colour (sites Board) (colour 250 210 148))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
    })
)
