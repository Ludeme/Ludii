(game "Can The Sardines"
    (players 2)
    (equipment
        {
        (board (square 9))
        (regions "Can" (sites {"C4" "C5" "C6" "D4" "D5" "D6" "E4" "E5" "E6" "F4" "F5" "F6" "G4" "G5" "G6"}))
        (regions "Border" (difference (sites "Can") (sites {"D5" "E5" "F5"})))
        (piece "Fish" Each 
            (if (not (is In (from) (sites "Can")))
                (move Slide)
            )
        )
        }
    )
    (rules
        (start 
            {
            (place "Fish1" {"A1" "B1" "C1" "D1" "F1" "G1" "H1" "I1"})
            (place "Fish2" {"A9" "B9" "C9" "D9" "F9" "G9" "H9" "I9"})
            }
        )
        (play
            (if (not (all Sites (sites "Border") if:(is In (site) (sites Occupied by:All))))
                (forEach Piece)
            )
        )
        (end 
            {
            (forEach Player if:(all Sites (sites Occupied by:Player) if:(is In (site) (sites "Can"))) (result Player Win))
            (if (no Moves Mover)
                (byScore
                    {
                    (score P1 (count Sites in:(forEach (sites "Can") if:(= (who at:(site)) P1))))
                    (score P2 (count Sites in:(forEach (sites "Can") if:(= (who at:(site)) P2))))
                    }
                )
            )
            }
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Fred Horn designed this game at the 9th Board Games Studies Colloquium, in Brazil, as a kind of challenge to professor Jos√© Carlos Quadrado to show that it was possible to quickly design a good abstract game. And here it is!
            
        ")
        (rules "The central 3x5 area is called the 'can'.
            
            TURN - A player must move one of his stones sliding it orthogonally or diagonally over a line of empty cells. Stones inside the can cannot move.
            
        GOAL - Wins the player that moves all his stones into the can. If it is no longer possible to move the remaining stones into the can, wins the player with more stones there.")
        (id "4223")
        (source "<a href=\"http://www.di.fc.ul.pt/~jpn/gv/can_sardines.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />The world of Abstract Games</a>")
        (version "1.3.14")
        (classification "board/space/territory")
        (author "Fred Horn")
        (credit "Sebastien Vercammen")
        (date "2006")
        }
    )
    (graphics
        {
        (region Colour "Can" (colour Green))
        }
    )
)
