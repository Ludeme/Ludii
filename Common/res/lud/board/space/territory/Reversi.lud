(define "ReverseBoundedPieces" 
    (custodial
        (from (site))
        (between 
            if:(is Enemy (state at:(between))) 
            (apply 
                (allCombinations 
                    (add (piece "Disc0" state:(mover)) (to (site))) 
                    (flip (between)) 
                )
            )
        )
        (to if:(is Friend (state at:(to))))
    )
)

//------------------------------------------------------------------------------

(game "Reversi"  
    (players 2)  
    (equipment { 
        (board (square <Board:size>))
        (piece "Disc" Neutral (flips 1 2))
        }
    )  
    (rules 
        
        <Rules:start> 
        
        phases:{ 
        <Rules:play>
        (phase "Playing" 
            (play (forEach Site 
                    (sites Empty)
                    (append 
                        "ReverseBoundedPieces" 
                        (then
                            (and
                                (set Score 
                                    P1 
                                    (count Sites in:(sites State 1))
                                )
                                (set Score 
                                    P2 
                                    (count Sites in:(sites State 2))
                                )
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end
            (if (all Passed)
                (byScore)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Start Rules" <Rules> args:{ <start> <play> }
    {
    (item "Fixed Start"      
        <   (start { 
            (place "Disc0" (difference (sites Centre) (sites Phase 1)) state:1)
            (place "Disc0" (difference (sites Centre) (sites Phase 0)) state:2)
        } )
        >
        <>
        "The pieces are placed on the board."
    )**   
    
    (item "Not Fixed Start" 
        <>
        <
        (phase "Placement"
            (play
                (move
                    Add
                    (piece
                        "Disc0" 
                        state:(mover)
                    )
                    (to 
                        (intersection 
                            (sites Empty) 
                            (sites Centre)
                        )
                    )
                )
            ) 
            (nextPhase Mover (>= (count Moves) 3) "Playing")
        )
        >
        "The pieces are placed on the board."
    )
    
})

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "4x4" <4> "The board is a square board of size 4x4.") 
    (item "6x6" <6> "The board is a square board of size 6x6.") 
    (item "8x8" <8> "The board is a square board of size 8x8.")**  
    (item "10x10" <10> "The board is a square board of size 10x10.")  
    (item "12x12" <12> "The board is a square board of size 12x12.")  
    (item "14x14" <14> "The board is a square board of size 14x14.")  
    (item "16x16" <16> "The board is a square board of size 16x16.")  
    (item "18x18" <18> "The board is a square board of size 18x18.")  
    (item "20x20" <20> "The board is a square board of size 20x20.")  
    (item "22x22" <22> "The board is a square board of size 22x22.")  
    (item "24x24" <24> "The board is a square board of size 24x24.")  
})

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Reversi is a game purportedly invented in the early nineteenth century by either Lewis Waterman or John W. Mollett, but may actually be an older game. It is currently sold and marketed as Othello worldwide.")
        (aliases {"Othello"})
        (rules "Reversi is played on an 8x8 board. Pieces are double-sided, with each side distinct in some way from the other. Each side designates ownership of that pieces to a certain player when face-up. Play begins with the players taking turns placing pieces into the central four squares until they are full. Then players may place their pieces next to an opponent's piece, a long as a straight line can be drawn between the new piece and an existing piece belonging to that player that goes through the opponent's piece. The opponent's pieces between the new piece and the old piece are then flipped and now belong to the player who just played. If a player cannot make a legal move, they pass. Play continues until the board is full or neither player cannot make a legal move. The player with the most pieces on the board wins.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Reversi\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "104")
        (version "1.3.3")
        (classification "board/space/territory")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (piece Colour state:1 fillColour:(colour Black))
        (piece Colour state:2 fillColour:(colour White))
        (board Colour OuterEdges (colour Black))
        (board Colour InnerEdges (colour Black))
        (board StyleThickness InnerEdges 0.5)
        (board Colour Symbols (colour Black))
        (board Colour Phase0 (colour 52 158 52))
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (useFor "Board Size/4x4" (show Symbol "dot" Vertex {16 18 6 8} scale:0.2))
        (useFor "Board Size/6x6" (show Symbol "dot" Vertex {36 40 12 8} scale:0.2))
        (useFor "Board Size/8x8" (show Symbol "dot" Vertex {20 24 56 60} scale:0.2))
        (useFor "Board Size/10x10" (show Symbol "dot" Vertex {90 96 30 24} scale:0.2))
        (useFor "Board Size/12x12" (show Symbol "dot" Vertex {132 140 36 28} scale:0.2))
        (useFor "Board Size/14x14" (show Symbol "dot" Vertex {182 192 42 32} scale:0.2))
        (useFor "Board Size/16x16" (show Symbol "dot" Vertex {240 252 48 36} scale:0.2))
        (useFor "Board Size/18x18" (show Symbol "dot" Vertex {306 320 54 40} scale:0.2))
        (useFor "Board Size/20x20" (show Symbol "dot" Vertex {380 396 60 44} scale:0.2))
        (useFor "Board Size/22x22" (show Symbol "dot" Vertex {462 48 480 66} scale:0.2))
        (useFor "Board Size/24x24" (show Symbol "dot" Vertex {552 572 72 52} scale:0.2))
    })
    
    (ai 
        "Reversi_ai"
    )
    
)
