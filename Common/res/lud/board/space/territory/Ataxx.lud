(game "Ataxx" 
    (players 2) 
    (equipment { 
        (board (square 7)) 
        (piece "Marker" Each 
            (or 
                (move 
                    (from) 
                    (to (sites Around (from)) if:(is Empty (to))) 
                    copy:True
                )
                (move 
                    (from) 
                    (to 
                        (difference 
                            (expand origin:(from) steps:2) 
                            (expand origin:(from))
                        ) 
                        if:(is Empty (to))
                    )
                )
                (then
                    (forEach Site 
                        (intersection 
                            (sites Around (last To)) 
                            (sites Occupied by:Next)
                        )
                        (and 
                            (remove (site)) 
                            (add (piece (id "Marker" Mover)) (to (site))) 
                        )
                    )
                )
            )
        )
        }
    ) 
    (rules 
        (start {
            (set Score Each 2)
            (place "Marker1" {"A1" "G7"})
            (place "Marker2" {"A7" "G1"})
        })
        (play (forEach Piece
                (then 
                    (and 
                        (set Score P1 (count Sites in:(sites Occupied by:P1)))	 
                        (set Score P2 (count Sites in:(sites Occupied by:P2)))
                    )
                )	
            )
        )
        
        (end
            (if (or 
                    ("NoSites" (sites Occupied by:Next)) 
                    (is Full)
                )
                (byScore)
            )
        )
    )
)

(metadata
    
    (info
        {
        (description "Ataxx is an arcade game invented in 1990 by the Leland Corporation and was originally called Infection.")
        (aliases {"Infection"})
        (rules "Each player begins with two pieces on the four corners of the board, with each player's pieces in opposite corners. Players move one piece one or two spaces in any direction orthogonally or diagonally. If the piece is moved on space, another piece is added on the origin space. All of the opponent's pieces adjacent to the space to which the player moved now belong to the player who moved. Play ends when all spaces have been filled or one of the players has no remaining pieces. The player with the most pieces wins.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Ataxx\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "419")
        (version "1.3.11")
        (classification "board/space/territory")
        (author "Dave Crummack, Craig Galley")
        (publisher "Capcom Co., Ltd., Leland Corporation, Virgin")
        (credit "Eric Piette")
        (date "1990")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
    })
    
    (ai 
        "Ataxx_ai"
    )
    
)
