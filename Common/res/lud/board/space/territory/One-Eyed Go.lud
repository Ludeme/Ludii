(define "CaptureSurroundedPiece"
    (enclose 
        (from (last To)) 
        Orthogonal 
        (between 
            if:("IsEnemyAt" (between)) 
            (apply    
                (and 
                    (addScore Mover 1) 
                    (remove (between)) 
                )
            ) 
        )
    )
)

//------------------------------------------------------------------------------

(game "One-Eyed Go" 
    (players 2) 
    (equipment { 
        (board <Board> use:Vertex) 
        (piece "Marker" Each)
    })
    
    (rules 
        //  (meta
            //   (no Repeat)
        //  )
        (play 
            (or
                (do
                    (move Add
                        (to (sites Empty))
                        
                    )
                    ifAfterwards:("HasFreedom" Orthogonal)
                    (then "CaptureSurroundedPiece")
                )
                (move Pass)
            )
        )
        
        (end
            (if (all Passed)
                (byScore {
                    (score P1 (+ (score P1) (size Territory P1))) 
                    (score P2 (+ (score P2) (size Territory P2)))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "9x9"  <(square 9)>  "The game is played on a 9x9 board") 
    (item "13x13" <(square 13)> "The game is played on a 13x13 board") 
    (item "15x15" <(square 15)> "The game is played on a 15x15 board") 
    (item "19x19" <(square 19)> "The game is played on a 19x19 board")** 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "One-Eyed Go is a variant of Go.")
        (rules "One-Eyed Go follows the same rules as go (e.g. Japanese Rules) with the exception that it is never allowed to place a stone such that your own group has no liberties, even when they would capture other stones. This renders the ko rule superfluous.")
        (id "1031")
        (source "<a href=\"https://senseis.xmp.net/?OneEyedGo\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />senseis.xmp.net</a>")
        (version "1.3.10")
        (classification "board/space/territory")
        (credit "Eric Piette")
        (date "2013")
        }
    )
    
    (graphics {
        (board Style Go)
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
    })
    
    (ai
        "One-Eyed Go_ai"
    )
)
