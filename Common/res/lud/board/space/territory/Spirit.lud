(game "Spirit" 
    (players <Num:players>) 
    (equipment { 
        (board (square 4 pyramidal:True) use:Vertex) 
        (piece "Ball" Each) 
    }) 
    (rules
        (play
            (move Add 
                (to (sites Empty) 
                    if:(and
                        (is Flat) 
                        (= 0 
                            (size Array 
                                (array
                                    (intersection { 
                                        (difference (sites Occupied by:All) (sites Support)) 
                                        (sites Occupied by:Mover) 
                                        (union {
                                            (sites Direction from:(to) N) 
                                            (sites Direction from:(to) S) 
                                            (sites Direction from:(to) E) 
                                            (sites Direction from:(to) W)
                                        })
                                    })
                                )
                            )
                        )
                    )
                )
                (then (addScore Mover 1))
            )
        )
        (end
            (if
                (or
                    (all Passed)
                    (= (size Array (array (sites Occupied by:All))) 30)
                )
                (byScore)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Players" <Num> args:{ <players> }
    {
    (item "3" <3> "The game is played with three players.") 
    (item "2" <2> "The game is played with two players.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Spirit (of Shibumi) is a 2-3-player ball-stacking game that can be played with a Series: Shibumi set. It uses a 4x4 holes board and balls in 3 colours.")
        (rules "Each player has an allocated colour, and they take turns during the game (white -> black -> red). On your turn, place one of your balls on an empty hole or platform so that no other FREE ball (no balls on top) of the SAME colour is on the same row or column of the same level. If you can't, then you must pass. If all players pass in a single round the game ends. The player with most balls on the board wins.")
        (id "4252")
        (source "<a href=\\"http://www.nestorgames.com/rulebooks/SHIBUMI_EN.pdf\\" target=\\"_blank\\" class=\\"style1\\" style=\\"color: #0000EE\\">SHIBUMI Rulebook</a>")
        (version "1.3.14")
        (classification "board/space/territory")
        (author "Néstor Romeral Andrés")
        (publisher "Computational Creativity Group, nestorgames")
        (credit "Cedric Antoine")
        (date "2012")
        }
    )
    
    (graphics {
        (piece Scale "Ball" 1.0)
        (board Style Shibumi)
        (piece Colour P1 fillColour:(colour White))
        (piece Colour P2 fillColour:(colour Black))
        (piece Colour P3 fillColour:(colour Red))
    })
    
)
