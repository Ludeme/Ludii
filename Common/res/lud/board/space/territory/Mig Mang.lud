(define "CaptureSurroundedPiece"
    (enclose 
        (from (last To)) 
        Orthogonal 
        (between 
            if:(or ("IsEnemyAt" (between)) (is Empty (between)))
            (apply
                (and
                    (if ("IsEnemyAt" (between))
                        (remove (between)) 
                    )
                    (set State at:(between) (mover))
                )
            ) 
        )
    )
)

(define "RemovePreviousTerritoryTaken"
    (forEach Site (sites Empty)
        (if (!= 0 (state at:(site)))
            (set State at:(site) 0)
        )
    )
)

//------------------------------------------------------------------------------

(game "Mig Mang" 
    (players 2) 
    (equipment { 
        (board (square 17) use:Vertex) 
        (piece "Ball" Each)
    })
    
    (rules 
        (start {
            (place "Ball1" (sites {"C7" "K15" "C15" "G3" "O11" "O3"}) state:1)
            (place "Ball2" (sites {"C3" "K3" "C11" "G15" "O15" "O7"}) state:1)
        })
        (play 
            (move Add
                (to (sites Empty) if:(= (state at:(to)) 0))
                (then 
                    (and
                        ("RemovePreviousTerritoryTaken")
                        ("CaptureSurroundedPiece")
                    )
                )
            )
        )
        (end
            (if (and (no Moves P1) (no Moves P2))
                (byScore {
                    (score P1 (+ (count Pieces P1) (size Territory P1))) 
                    (score P2 (+ (count Pieces P2) (size Territory P2)))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mig Mang is a territory game played in Tibet on a 17x17 board.")
        (rules "17x17 intersecting lines. 151 pieces per player. Each player begins with six larger pieces on the board, on the third line from the perimeter on all four sides, one piece on every fourth intersection, alternating between black and white. These pieces are never moved from the  board. There is a star marking at each of these points. Players alternate turns placing one of their pieces on an empty intersection of the board. When a player encloses an area with their pieces, it becomes their territory, and any pieces of the opponent inside it are captured. The opponent cannot place one of their pieces inside a territory won on the previous turn. White plays first. When players can no longer play, the number of  counters and each territory are counted to determine the winner.")
        (source "Fairbairn 1990: 10-15.")
        (id "1181")
        (version "1.3.6")
        (classification "board/space/territory")
        (credit "Eric Piette")
        (origin "This game was played in Tibet, from around 1879 to 1959.")
        }
    )
    
    (graphics {
        (board Style Board)
        (board Colour InnerEdges (colour 160 140 100))
        (board Colour OuterEdges (colour Hidden))
        (board Colour Phase0 (colour 255 230 150))
        (show Edges Diagonal Hidden)
        (piece Scale "Ball" state:1 1.15)
    })
    
)
