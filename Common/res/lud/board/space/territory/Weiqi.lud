(game "Weiqi"
    (players 2)
    (equipment {
        (board (square 19) use:Vertex)
        (piece "Marker" Each)
    })
    (rules 
        (meta
            (no Repeat)
        )
        (play     
            (or
                (move Add
                    (to (sites Empty))
                    (then ("EncloseCapture" Orthogonal))
                )
                (move Pass)
            )
        )
        (end
            (if (all Passed)
                (byScore {
                    (score P1 (+ (count Pieces P1) (size Territory P1))) 
                    (score P2 (+ (count Pieces P2) (size Territory P2)))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Weiqi is a territory game that is one of the oldest surviving games today. It is the ancestor of Go, and its rules are largely the same. It originated in China, and inspired similar games throughout East Asia, most notably Go in Japan and Baduk in Korea.")
        (aliases {"Yi"})
        (rules "Played on a 19x19 board. The board begins empty. One player plays as black, the other as white. The black player begins by placing a piece on one of the intersections on the board. Players alternate turns placing a piece on the board. A player may pass at any time. A piece or a group of pieces are captured when they are completely surrounded on all sides on adjacent intersections by the opposing player. Stones may be placed in a position where they are surrounded. Stones cannot be placed to recreate a previous position. The game ends when both players pass consecutively. Players then fill in the spaces in their captured territory with their own pieces. Players total the number of intersections their pieces occupy. The player with the highest total wins.
        ")
        (source "<a href=\"https://en.wikipedia.org/wiki/Rules_of_Go#Chinese_rules\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "772")
        (version "1.3.5")
        (classification "board/space/territory")
        (credit "Eric Piette")
        (origin  "This game was played in China, from around 500 to 599.")
        }
    )
    
    (graphics {
        (board Style Go)
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
    })
    (ai
        "Weiqi_ai"
    )
)
