// PeerPress Follow-up consequenses as separate moves.

(define "PeersA" (sites #1 {{F} {R R F} {L L F}} rotations:False))
(define "PeersB" (sites #1 {{R R R F} {R F} {L F}} rotations:False))

(define "Pressed"
    (and
        {
        (= 3 (count Pieces in:#1))
        (no Pieces Next in:#1)
        (is In (from) #1)
        }
)) 

(define "ToConvert"
    (and
        {
        (> 1 (state at:(to)))
        (or (is Next (who at:(to))) (= (id "Disc0") (what at:(to))))
        (or ("Pressed" ("PeersA" (to))) ("Pressed" ("PeersB" (to))))
        }
))

(define "SitesToConvert"
    (sites Around #1 if:("ToConvert" #1))
)

(define "ConvertPressedPieces" 
    (if
        (= (id "Ball" Next) (what at:#1))
        (add (piece "Disc0") (to #1 (apply (and (remove #1 ) (set State at:#1 1)))))
        (add (piece (id "Ball" Mover)) (to #1 (apply (and (remove #1 ) (set State at:#1 1)))))
))

(define "ScorableSites"
    (difference (sites Board) (sites ConvexCorners))
)

(define "SetScore"
    (set Score #1 (count Pieces #1 in:("ScorableSites")))
)

(define "SelectConversionSequence"
    (move Select
        (from
            (sites State 1)
            if:(< 0 (size Array (array ("SitesToConvert" (from)))))
)))

//--------------------------
(game "PeerPress"
    (players 2)
    (equipment
        {
        (board (renumber (rotate 90 (trim <Board:type>))) use:Cell)
        (piece "Disc" Neutral N maxState:2)
        (piece "Ball" P1 N maxState:2)
        (piece "Ball" P2 N maxState:2)
        }
    )
    (rules
        (play
            (priority
                
                ("SelectConversionSequence")
                
                (move Add
                    (piece (id "Ball" Mover))
                    (to
                        (sites Empty) 
                ))
                
                (then
                    (forEach Site
                        ("SitesToConvert" (last To))
                        ("ConvertPressedPieces" (site))
                        noMoveYet:(and
                            ("SetScore" Mover)
                            ("SetScore" Next)
                        )
                        (then 
                            (if
                                (can Move ("SelectConversionSequence"))
                                (set State at:(last To) 2 (then (moveAgain)))
                                (and
                                    {
                                    ("SetScore" Mover)
                                    ("SetScore" Next)
                                    (forEach Site (difference (sites Board) (sites State 0)) (set State at:(site) 0))
                                    }
        )))))))
        (end 
            (if 
                (or
                    (no Moves Next)
                    (all Passed)
                )
                (if 
                    (> (score Mover) (score Next))
                    (result Mover Win)
                )
                (result Mover Loss)
)))) 

//--------------------------------------

(option "Board" <Board> args:{ <type> }
    {
    (item "Dia 6"
        <(remove (hex Hexagon 4) cells:{0 3 15 21 33 36})> 
        "Diagonal Hexhex board with diameter 9"
    )
    (item "Dia 7"
        <(remove (hex Hexagon 5) cells:{0 1 3 4 5 10 18 25 26 34 35 42 50 55 56 57 59 60})> 
        "Diagonal Hexhex board with diameter 9"
    )
    (item "Dia 9"
        <(remove (hex Hexagon 6) cells:{0 1 4 5 6 12 30 39 40 50 51 60 78 84 85 86 89 90})> 
        "Diagonal Hexhex board with diameter 9"
    )
    (item "Dia 10" 
        <(remove (hex Hexagon 7) cells:{0 1 2 4 5 6 7 8 13 14 15 23 34 44 45 56 57 58 68 69 70 81 82 92 103 111 112 113 118 119 120 121 122 124 125 126})>  
        "Diagonal Hexhex board with diameter 10"
    )**
    (item "Dia 12" 
        <(remove (hex Hexagon 8) cells:{0 1 2 5 6 7 8 9 15 16 17 26 50 62 63 76 77 78 90 91 92 105 106 118 142 151 152 153 159 160 161 162 163 166 167 168})> 
        "Diagonal Hexhex board with diameter 12"
    )
    (item "Dia 13" 
        <(remove (hex Hexagon 9) cells:{0 1 2 3 5 6 7 8 9 10 11 16 17 18 19 20 28 29 30 41 55 68 69 83 84 85 98 99 100 101 115 116 117 118 131 132 133 147 148 161 175 186 187 188 196 197 198 199 200 205 206 207 208 209 210 211 213 214 215 216})> 
        "Diagonal Hexhex board with diameter 13"
    )
    }
) 

//------------------------------------------------------------------------------

(metadata
    (info
        {
        }
    )
    
    (graphics
        {
        (board Colour Phase0 (colour 200 250 210 120))
        (board Colour OuterEdges (colour Black))
        (board Colour InnerEdges (colour Black))
        (board StyleThickness OuterEdges 1.5)
        (board StyleThickness InnerEdges 0.4)
        (board StyleThickness InnerVertices 0.4)
        (board StyleThickness OuterVertices 0.4)
        (board Colour Symbols (colour Black))
        (piece Colour Neutral state:0 fillColour:(colour 180 225 190) strokeColour:(colour 50 50 50 150))
        (piece Colour Neutral state:1 fillColour:(colour 125 162 138 60) strokeColour:(colour 50 50 50 50))   
        (piece Colour Neutral state:2 fillColour:(colour 180 225 190) strokeColour:(colour 50 50 50 150))
        (piece Colour P1 state:0 fillColour:(colour Black))
        (piece Colour P1 state:1 fillColour:(colour 0 0 0 60))
        (piece Colour P1 state:2 fillColour:(colour Black))
        (piece Colour P2 state:0 fillColour:(colour LightOrange))   
        (piece Colour P2 state:1 fillColour:(colour 255 120 20 40))
        (piece Colour P2 state:2 fillColour:(colour LightOrange))   
        (player Colour P1 (colour Black))
        (player Colour P2 (colour LightOrange))
        (region Colour (sites ConvexCorners) (colour LightGrey))
        }
    )
    (ai (heuristics (score weight:3)))
)
