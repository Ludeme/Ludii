(define "Placeable"
    (not
        (or {
            (and 
                ("OpponentHas" E W)
                ("OpponentHas" NNW SSE)
            )
            (and
                ("OpponentHas" E W)
                ("OpponentHas" NNE SSW)
            )
            (and
                ("OpponentHas" NNW SSE)
                ("OpponentHas" NNE SSW)
            )
        })
    )
)

(define "OpponentHas"
    (>
        (min
            (count Sites
                in:(if
                    (< 0 
                        (count Sites
                            in:(intersection
                                (sites Occupied by:Mover)
                                (sites Direction
                                    from:(to)
                                    #1
                                    stop:(= (mover) (who at:(to)))
                                    stopIncluded:True
                                ) 
                            )
                        )
                    )
                    (sites Direction
                        from:(to)
                        #1 
                        stop:(= (mover) (who at:(to)))
                    )
                    (sites Board)
                )
            )
            (count Sites
                in:(if
                    (< 0 
                        (count Sites
                            in:(intersection
                                (sites Occupied by:Mover)
                                (sites Direction
                                    from:(to)
                                    #2
                                    stop:(= (mover) (who at:(to)))
                                    stopIncluded:True
                                ) 
                            )
                        )
                    )
                    (sites Direction
                        from:(to)
                        #2
                        stop:(= (mover) (who at:(to)))
                    )
                    (sites Board)
                )
            )
        )
        (min
            (count Sites
                in:(if
                    (< 0 
                        (count Sites
                            in:(intersection
                                (sites Occupied by:Next)
                                (sites Direction
                                    from:(to)
                                    #1
                                    stop:(= (next) (who at:(to)))
                                    stopIncluded:True
                                ) 
                            )
                        )
                    )
                    (sites Direction
                        from:(to)
                        #1 
                        stop:(= (next) (who at:(to)))
                    )
                    (sites Board)
                )
            )
            (count Sites
                in:(if
                    (< 0 
                        (count Sites
                            in:(intersection
                                (sites Occupied by:Next)
                                (sites Direction
                                    from:(to)
                                    #2
                                    stop:(= (next) (who at:(to)))
                                    stopIncluded:True
                                ) 
                            )
                        )
                    )
                    (sites Direction
                        from:(to)
                        #2
                        stop:(= (next) (who at:(to)))
                    )
                    (sites Board)
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Mity"
    (players 2)
    (equipment
        {
        (board (hex <Board>))
        (piece "Disc" Each)
        }
    )
    (rules
        (meta (swap))
        (play
            (move Add
                (to
                    (sites Empty)
                    if:("Placeable")
                )
            )
        )
        (end 
            (if 
                (no Moves Next) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "4"    <4>  "A size 4 board is currently selected")
    (item "5"    <5>  "A size 5 board is currently selected")* 
    (item "6"    <6>  "A size 6 board is currently selected")
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mity is a simple territory game of pure placement invented by Michael Amundsen in 2021.")
        (rules "Players place one token of their color each turn and the first player unable to do so has lost. You may place on any empty cell unless your opponent has a token closer to that cell than you have along at least 2 of the 3 lines (axes) the cell is on. 
            
        The game uses the pie rule: One player chooses the first move and the other player chooses whose move it is.")
        (id "1520")
        (version "1.3.4")
        (classification "board/space/territory")
        (author "Michael Amundsen")
        (credit "Michael Amundsen")
        (date "2021")
        }
    )
    (ai
        "Mity_ai"
    )
)
