(define "Select"
    (move Select
        (from (sites Occupied by:Mover top:True))
        (to
            (sites Direction from:(from) 
                Orthogonal
                stop:(= (next) (who at:(to)))
                stopIncluded:True
                distance:(size Stack at:(from))
            )
            if:(>=
                (count Steps (from) (to))
                (size Stack at:(to))
            )    
        )
        #1
    )
)

(define "Move"
    (and
        (fromTo 
            (from (last From))
            (to (last To))
            count:(count Steps (last From) (last To))
            stack:True
        )
        (add 
            (to (last To)) 
            stack:True
        )
    )
)

//------------------------------------------------------------------------------

(game "Lava"
    (players 2)
    (equipment {
        (board (rotate 30 (hex <Board>)))
        (piece "Disc" Each)
    })
    (rules
        (start {
            (place "Disc1" (min (array (sites Board))))
            (place "Disc2" (max (array (sites Board))))
        })
        (play
            ("Select" (then ("Move")))
        )
        (end 
            (if (is Full) 
                (byScore {
                    (score P1 (count Sites in:(sites Occupied by:P1 top:True)))
                    (score P2 (count Sites in:(sites Occupied by:P2 top:True)))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <board> }
    {
    (item "4"    <4>  "A size 4 board is currently selected")*
    (item "5"    <5>  "A size 5 board is currently selected")
    }
)

(option "Show Stack Size As Number" <Stack> args:{ <stack> }
    {
    (item "No"    <Default 0.6>  "The number of pieces in a stack is currently not shown on the top piece.")*
    (item "Yes"    <DefaultAndCount 0.6>  "The number of pieces in a stack is shown on the top piece.")
    }
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Lava is a territorial stacking game invented by Alek Erickson and Michael Amundsen in August 2021.")
        (rules "Lava is a territorial stacking game for two players, Red and Black. Play using a Hexhex board size 4 or 5, and a sufficient supply of Red/Black stacking checkers. To setup, place one Red and one Black checker in opposite corners of the hex hex board.
            
            Definitions:
            Stack: One or more checkers occupying the same cell.
            Control: You control a stack if your color is on top of it.
            
            Players take turns, with Black moving first.
            
            Each turn, take N checkers (where N is any number from 1 to stack size) from the top of a stack you control and move them N spaces in a straight line to a destination containing N or less checkers. Then, add a new checker of your color to the destination. Stacks may not jump over enemies, but may land on enemy or friendly stacks, thereby burying them.
            
            If you at any point cannot make a move, you must pass. This does not end the game, only your turn.
            
        When the board is full, the player controlling more stacks wins the game.")
        (id "1651")
        (version "1.3.4")
        (classification "board/space/territory")
        (author "Alek Erickson and Michael Amundsen")
        (credit "Michael Amundsen")
        (date "2021")
        }
    )
    
    (graphics {
        (player Colour P1 (colour DarkGrey))
        (player Colour P2 (colour Red))
        (region Colour (sites Board) (colour 250 210 148))
        (board Colour InnerEdges (colour 178 151 100))
        (board Colour OuterEdges (colour 178 151 100))
        (stackType <Stack>)
    })
)
