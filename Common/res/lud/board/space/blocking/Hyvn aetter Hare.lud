(game "Hyvn aetter Hare"
    (players 2)  
    (equipment 
        {
        (board 
            (splitCrossings
                (merge
                    (shift .5 .5 (scale 0.707 (concentric {1 8})))
                    (square 2)
                )
            )
            use:Vertex
        )
        (piece "Hare" P1 "StepToEmpty") 
        (piece "Dog" P2 "StepToEmpty") 
        (hand Each)
        }
    )  
    (rules 
        (start 
            { 
            (place "Hare1" (handSite P1) count:1)
            (place "Dog2" (handSite P2) count:3)
            }
        )
        <Play>
    )
)

//------------------------------------------------------------------------------

(option "Play Rules" <Play> args:{ <rules> }
    {
    (item "White moves only when all placed" 
        <
        phases:{
        (phase "Placement"
            (play 
                (if (not ("HandEmpty" Mover)) 
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty))
                    ) 
                )
            )
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
            (end ("NoMoves" Loss) )
        )
        }
        >
        "The First player moves only when all the pieces are placed."
    )*   
    (item "White moves only when his piece is placed"    
        <
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                ) 
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        (phase "Movement"
            (play (forEach Piece))
        )
        }
        (end ("NoMoves" Loss) ) 
        > 
        "The First player can move after placing his piece."
    ) 
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Hyvn ætter Hare is a blackade game played on Funen Island in Denmark. One player plays as the hare, and the other as three hounds, which attempt to trap the hare. ")
        (aliases {"Hyvn ætter Hare"})
        (rules "Square board, divided into eight equal triangles, and a circle around the square, touching it at the corners, One player plays as the hare, the other player as three hounds. Players take turns placing one of their pieces on the board. When a player has placed all their pieces, they move to an adjacent empty spot. When the hounds block the hare from being able to move, the hounds win. ")
        (source "Kristianssen 1937: 30.")
        (version "1.2.8")
        (classification "board/space/blocking")
        (credit "Eric Piette")
        (origin  "This game was played in Scandinavia, around 1860.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        
    })
    
    (ai 
        "Hyvn aetter Hare_ai"
    )
    
)
