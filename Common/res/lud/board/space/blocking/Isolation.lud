(game "Isolation"
    (players 2)
    (equipment {
        (board (rectangle <Board:size>))
        (piece "Pawn" Each (move Step (to if:(is Empty (to))) (then (moveAgain))))
        (piece "Square" Neutral)
    })
    (rules
        (start {
            <Board:placement>
        })
        (play
            (if
                (is Even (count Moves))
                (forEach Piece)
                (move Add (piece "Square0") (to (sites Empty)))
            )
        )
        (end (if (no Moves Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <placement> } {
    (item "6x8" <6 8> <(place "Pawn1" {"A4"}) (place "Pawn2" {"H3"})> "The game is played on a 6x8 board.")
    (item "7x7" <7 7> <(place "Pawn1" {"A4"}) (place "Pawn2" {"G4"})> "The game is played on a 7x7 board.")*
})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Blocking game played on a 6x8 or 7x7 board.")
        (rules "Each player has two pawns, that can move to any neighboring cell. After moving, the player places a block on the board to try to block the opponent's pawn. First player who cannot move loses.")
        (source "<a href=\"https://boardgamegeek.com/boardgame/1875/isolation\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.0")
        (classification "board/space/blocking")
        (author "Bernd Kienitz")
        (credit "Fabio Barbero")
        (date "1972")
        }
    )
    (ai
        "Isolation_ai"
    )
)
