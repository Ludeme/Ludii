(define "IsEmpty" (is Empty (to)))

(define "Centre" (= (to) (centrePoint)))

//------------------------------------------------------------------------------

(game "Mu Torere"
    (players 2)  
    (equipment 
        { 
        <Board:type>
        
        (piece "Marker" Each <Play>) 
        }
    )  
    (rules 
        (start { 
            <Board:start>
        })
        (play (forEach Piece))
        (end ("NoMoves" Loss))
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> <start>}
    {
    (item "Wheel Shape"         
        <(board (concentric {1 8}) use:Vertex)>
        <(place "Marker1" {1..3 8})
        (place "Marker2" {4..7})>
        "The game is played on a wheel board."
    )*   
    (item "Square Shape" 
        <("AlquerqueBoard" 3 3)>
        <(place "Marker1" {0 1 2 5})
        (place "Marker2" {3 6 7 8})>                                  
    "The game is played on a 3x3 alquerque board.")   
})

(option "Play Rules" <Play> args:{ <rules>}
    {
    (item "Complete"         
        <(move
            Step 
            (to if:(or 
                    (and 
                        (not "Centre") 
                        ("IsEmpty")
                    ) 
                    (and 
                        (is Related Adjacent (from) (sites Occupied by:Next))
                        (and ("IsEmpty") ("Centre"))
                    )
                ) 
            )
        )>
        "The game is played with all the rules"
    )*
    
    (item "Simplified"            
        <"StepToEmpty">                                 
        "The game is played with no constraint on the centre site."
    )   
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Simple (Suggested)" 
        {
        "Board/Wheel Shape" "Play Rules/Simplified"
        }
    )
    
    (ruleset "Ruleset/Complete (Observed)" 
        {
        "Board/Wheel Shape" "Play Rules/Complete"
        }
    )
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mu Torere is a game played by the Maori of New Zealand. It is unknown how old the game is, Best claims that it was not played by Maori people in all parts of New Zealand. The board takes the form of a circle divided into eighths, with eight points on the circumference (kewai) and the central point (putahi) being playing spaces.")
        (useFor "Ruleset/Complete (Observed)" (rules "Play begins with the pieces on the kewai, each player on half of the circle. Players move by moving one piece to an empty adjacent space. Players can only move to the putahi when the piece being moved is next to an opponent's piece. The player that blocks the other from moving is the winner."))
        (useFor "Ruleset/Complete (Observed)" (source "Best 1925: 60-61."))
        (useFor "Ruleset/Complete (Observed)" (id "81"))
        (useFor "Ruleset/Simple (Suggested)" (rules "There is no restriction on moving a piece to the center hole, except that the first move must allow the second player to move. "))
        (useFor "Ruleset/Simple (Suggested)" (source "Murray 1951: 93."))
        (useFor "Ruleset/Simple (Suggested)" (id "559"))
        (source "Murray 1951: 93")
        (id "81")
        (version "1.3.3")
        (classification "board/space/blocking")
        (credit "Eric Piette")
        (origin  "This game was played in New Zealand, around 1912.")
        }
    )
    
    (graphics {
        (no Curves)
    })
    
    (ai 
        "Mu Torere_ai"
    )
    
)

