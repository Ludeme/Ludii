(game "Gala (Buginese)"
    (players 2)
    (equipment {
        (board (square 7))
        (piece "Marker" Each
            (move Step
                Orthogonal
                (to if:(is Empty (to)))
                (then
                    (and
                        (custodial
                            (from (last To))
                            Orthogonal
                            (between
                                (max 1)
                                if:(is Enemy (who at:(between)))
                                (apply (remove (between)))
                            )
                            (to if:(is Friend (who at:(to))))
                        )
                        (surround
                            (from (last To))
                            Orthogonal
                            (between
                                if:(and
                                    (is Enemy (who at:(between)))
                                    (is In (between) (sites Corners))
                                )
                                (apply (remove (between)))
                            )
                            (to
                                if:(is Friend (who at:(to)))
                            )
                        )
                    )
                )
            )
        )
        (hand Each)
        (regions "HalfBoard" P1 (expand (sites Bottom) steps:3))
        (regions "HalfBoard" P2 (expand (sites Top) steps:3))
    })
    (rules
        (start {
            (place "Marker1" (handSite P1) count:13)
            (place "Marker2" (handSite P2) count:10)
        })
        phases:{
        (phase "CentrePlacing" P1
            (play
                (move
                    (from (handSite P1))
                    (to (centrePoint))
                )
            )
            (nextPhase "Placement")
        )
        (phase "Placement" P2
            (play
                (if (not ("HandEmpty" Mover))
                    (move
                        (from (handSite Mover))
                        (to (intersection (sites Empty) (sites Mover)))
                    )
                    (move Pass)
                )
            )
            (nextPhase (and ("HandEmpty" P1) ("HandEmpty" P2)) "Playing")
        )
        (phase "Playing"
            (play (forEach Piece))
            (end {
                (if (no Moves P2) (result P1 Win))
                (if (< (count Pieces P1) 2) (result P2 Win))
            })
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Gala is a game used to refer to at least two games played in the Makassar Peninsula of Sulawesi, Indonesia. This is the Buginese version, which is played on a larger board and the ability to capture in corners.")
        (rules "7x7 board, with an X in each corner space, the middle space along each side, and the central space. Black plays with thirteen pieces, white plays with ten. Black places the first piece on the central square, then players alternate turns placing a piece on their own half of the board. When all of the pieces are placed, players alternate turns moving pieces one space orthogonally. A piece is captured when it is surrounded on two opposite sides by opponent's pieces, or if it is in the corner and there are two opponent's pieces blocking it from moving. Capturing cannot happen in the placement phase. Black wins if it can block white from being able to move. White wins if it is impossible for black to block them.")
        (source "Matthes 1874: 71-72.")
        (version "1.3.0")
        (classification "board/space/blocking")
        (credit "Eric Piette")
        (origin  "This game was played in Indonesia, around 1874.")
        }
    )

    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (show Symbol "thinCross" {0 3 6 21 27 3 45 42 48 24} scale:0.9)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })

    (ai
        "Gala (Buginese)_ai"
    )
)
