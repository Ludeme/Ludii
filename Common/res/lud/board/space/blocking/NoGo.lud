(define "NoCapture" (not (can Move ("EncloseCapture" Orthogonal))))

//------------------------------------------------------------------------------

(game "NoGo"  
    (players 2) 
    (equipment { 
        (board <Board> use:Vertex) 
        (piece "Marker" Each)
    })  
    
    (rules 
        (play 
            (do
                (move Add (to (sites Empty)))
                ifAfterwards:(and 
                    ("HasFreedom" Orthogonal)
                    "NoCapture"
               )
           )
       )
        
        (end ("NoMoves" Loss))  
   )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "6x7"     <(rectangle 6 7)> "The game is played on 6x7 board.")
    (item "9x9"     <(square 9)>   "The game is played on 9x9 board.")*
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "NoGo is a combinatorial game for two players: Black and White. It's played on the intersections (points) of an initially empty square or rectangular board. Board sizes bigger than 9x9 are not recommended. 6x7 boards have been used in some human matches.
            
        NoGo takes the Go concepts of group and liberty and uses them in the most minimalistic possible way. The resulting game, with no captures, no cycles and no scoring, is no rival for Go, but remains a fertile ground for tactical challenges in the Dots and Boxes style.")
        (rules "Black plays first, then turns alternate. On his turn, a player must place a stone of his color on an empty point of the board. At the end of a turn, all groups on the board (both friendly and enemy) must have at least one liberty. Otherwise the player's move is illegal. Whoever can't make a move on his turn loses.")
        (id "580")
        (version "1.3.4")
        (classification "board/space/blocking")
        (author "John Moore (II)")
        (credit "Eric Piette")
        (date "04-2005")
        }
   )
    
    (graphics {
        (board Style Go)
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
    })
    
    (ai 
        "NoGo_ai"
   )
    
)
