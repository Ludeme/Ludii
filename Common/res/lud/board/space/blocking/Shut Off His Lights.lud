(game "Shut Off His Lights" 
    (players 2) 
    (equipment { 
        (board (celtic 4)) 
        (piece "Light" Each
            (move
                Leap
                "KnightWalk"
                (to if:(is Empty (to)))
                (then
                    (forEach Site (sites Around (last To) includeSelf:True)
                        (if (is Occupied (site))
                            (remove (site))
                            (add (piece (mover)) (to (site)))
                        )
                    )		
                )
            ) 
        )
    }) 
    (rules
        (start { 
            (place Random {"Light1"} count:12)
            (place Random {"Light2"} count:12)
        })
        (play (forEach Piece))
        
        (end 
            (if (no Pieces Next) (result Mover Win))
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Shut Off His Lights is a multiplayer version of the electronic game Lights Out.")
        (rules "The game is played on a Celtic-inspired board. At the beginning of the game, each player controls 12 lights, which are randomly placed on the board. Players take turns moving their own lights as Knights in Chess. This move will extinguish all adjacent lights and activate any unlit adjacent cells. The objective of the game is to strategically move your lights to turn off all of your opponentâ€™s lights, thereby winning the game.")
        (id "4296")
        (version "1.3.14")
        (classification "board/space/blocking")
        (author "Eric Piette")
        (credit "Eric Piette")
        (date "2024")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
        (board Colour Phase0 (colour 0 102 0))
        (board Colour OuterEdges (colour Black))
        (board Colour InnerEdges (colour Black))
    })
)
