(game "Janes Soppi (Symmetrical)" 
    (players 2) 
    (equipment { 
        (board
            (remove
                ("ThreeMensMorrisGraphWithLeftAndRightTriangles")
                edges:{{6 4} {4 2} {0 4} {4 8}}
            )
            use:Vertex
        )
        (hand Each)
        (piece "Hound" P1 "StepToEmpty")
        (piece "Hare" P2 "StepToEmpty")
    }) 
    
    (rules 
        (start {
            (place "Hound1" (handSite P1) count:3)
            (place "Hare2" (handSite P2) count:3)
        })
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        ("PhaseMovePiece" "Movement")
        }
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "In this version of the blocking game known as Jänes Soppi  both players have three pieces. It was played by children in Kärla, Estonia.")
(aliases {"Jänes Soppi (Symmetrical)"})
(rules "3x3 intersecting lines, with triangles extending on opposite ends of the large square and the central line extending to the apices. One player plays as three notched sticks, called hares, and the other as three sharp sticks, known as the hounds. Players alternate turns placing a piece on the board. When the players have placed all of their pieces on the board, they move one of their pieces to an empty adjacent spot along the lines. The first player to block their opponent from being able to move wins.")
(source "Michaelsen  2014: 207-208.")
(id "1073")
(version "1.3.7")
(classification "board/space/blocking")
(credit "Eric Piette")
(origin "This game was played in Estonia, around 1973.")
}
)
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    
)
