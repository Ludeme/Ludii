(game "Co Chan Cho" 
    (players 2) 
    (equipment { 
        (board 
            (square 2 diagonals:Solid)
            use:Vertex
        )
        (piece "Disc" Each
            (move Step 
                (to 
                    if:(and {
                        (or {
                            (and (!= (from) (coord "A1")) (!= (from) (coord "C1")))
                            (and (= (from) (coord "C1")) (!= (to) (coord "A1")))
                            (and (= (from) (coord "A1")) (!= (to) (coord "C1")))
                        }) 
                        (is Empty (to))
                    })
                )
            )
        )
    }) 
    
    (rules 
        (start {
            (place "Disc1" (sites Top)) 
            (place "Disc2" (sites Bottom))
        })
        (play (forEach Piece))
        (end ("NoMoves" Loss)) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Co Chan Cho is a blocking game that was played by children in the Tonkin region of Vietnam  in the mid twentieth century.  The game was usually drawn on the ground, and broken  pieces of pottery, paper, or straw were used as pieces.")
        (rules "The board is a square with diagonals drawn, with the bottom side of the square marked with an X. Two pieces per player, which begin on opposite corners of the square, one player on the side with the X and the other to the opposite side. Players alternate  turns moving  one of their pieces along the lines of the board to an empty adjacent point. Players cannot move along the line marked with an X. When one player  cannot move, the opponent wins.")
        (source "Son 1985: 44.")
        (id "1929")
        (version "1.3.12")
        (classification "board/space/blocking")
        (origin "This game was played in Vietnam, from around 1940 to 1941.")
        }
    )
    
    (graphics {
        (show Symbol "cross" Edge 0 fillColour:(colour Black) edgeColour:(colour Black) scale:0.2)
        (piece Scale "Disc" 0.5)
    })
    
    (ai
        "Co Chan Cho_ai"
    )
)
