// Trisect / Shred
//

(define "ConnectionSites" (difference (sites Occupied by:Mover) #1))

(define "IsConnected"
    (and 
        {
        (< 0
            (count Sites
                in:(sites Around #1 Orthogonal if:(is Empty (to)))
        ))
        (< 0
            (count Sites
                in:(sites Around ("ConnectionSites" #1) Orthogonal if:(is Empty (to)))
        ))
        (!= Infinity
            (count Steps Orthogonal
                (step Orthogonal (to if:(is Empty (to))))
                #1
                (sites Around ("ConnectionSites" #1) Orthogonal if:(is Empty (to)))
        ))
        }
))

(game "Shred"
    <Board:players>
    (equipment
        {
        (board <Board:size> use:Cell)
        (piece "Disc" Each)
        (piece "Hex" Neutral)
        }
    )
    (rules
        <Board:start>
        (play  
            (forEach Piece
                (move Step
                    (from if:(and (< 0 (state at:(from))) ("IsConnected" (from))))
                    Orthogonal
                    (to if:(is Empty (to)))
                    (then
                        (add
                            (piece "Hex0")
                            (to (last From))
                            (then
                                (forEach Site 
                                    (sites Occupied by:Mover) 
                                    (if
                                        (and
                                            (< 0 (state at:(site))) 
                                            (not ("IsConnected" (site)))  
                                        )
                                        (set State at:(site) 0)
        ))))))))
        (end
            {
            (if
                (no Moves Next)  // State requires a move to be set to 0
                (result Next Loss)
            )
            // Multi-player games should not allow suicide to give a ranking advantage.
            (if
                (no Pieces Mover in:(sites State 1)) // Suicide test 
                (result Mover Loss)
            )
            }
)))

(option "Size" <Board> args:{<players> <size> <start>}
    {
    (item "3 player Hex 11"
        <(players 3)> 
        <(hex 11)> 
        <(start      
            {
            (place "Disc1" {87 93 200 206 212 299 305} state:1)
            (place "Disc2" {27 33 120 126 132 239 245} state:1)
            (place "Disc3" {52 58 157 163 169 268 274} state:1)
            }
        )>
        "Size Hex 11"
    )
    (item "Hex 11"
        <(players 2)> 
        <(hex 11)> 
        <(start      
            {
            (place "Disc1" {86 92 199 205 211 298 304} state:1)
            (place "Disc2" {26 32 119 125 131 238 244} state:1)
            }
        )>
        "Size Hex 11"
    )*
    (item "Trisect: Hex 11" 
        <(players 2)> 
        <(hex 11)> 
        <(start      
            {
            (place "Disc1" {57 219 228} state:1)
            (place "Disc2" {102 111 273} state:1)
            }
        )>
        "Size Hex 11"
    )
    (item "Trisect: Hex 7"
        <(players 2)> 
        <(hex 7)> 
        <(start      
            {
            (place "Disc1" {19 84 90} state:1)
            (place "Disc2" {36 42 107} state:1)
            }
        )>
        "Size Hex 7"
    )
    (item "Trisect: Hex 6"
        <(players 2)> 
        <(hex 6)> 
        <(start      
            {
            (place "Disc1" {16 58 63} state:1)
            (place "Disc2" {27 32 74} state:1)
            }
        )>
        "Size Hex 6"
    )
    (item "Square 10"
        <(players 2)> 
        <(square 10)> 
        <(start      
            {
            (place "Disc1" {22 46 53 77} state:1)
            (place "Disc2" {27 34 65 72} state:1)
            }
        )>
        "Size Square 10"
    )
    (item "Square 12"
        <(players 2)> 
        <(square 12)> 
        <(start      
            {
            (place "Disc1" {7 68 75 136} state:1)
            (place "Disc2" {41 48 95 102} state:1)
            (place "Disc1" {14 129} state:1)
            (place "Disc2" {34 109} state:1)
            }
        )>
        "Size Square 12"
    )*
    }
)

(metadata
    (info
        {
        (description "Shred is a blocking/isolation game where live pieces require a path to friends, and a piece's steps dissolve the board.
            
            It can be played on a variety of boards and starting positions with 2 or more players.
            
        The original idea was for 2 players on a hex board with 3 pieces per player, and called Trisect.")
        (rules "Goal: Last survivor.
            
            To 'live', a piece needs a path of empty board cells leading to another piece of its own kind.
            
            Play:
            Move one of your living pieces a single step and remove the cell of the board that it used to occupy.
            
            A player with no live pieces at the end of their turn, loses.
        The winner is the last to survive.")
        (id "4316")
        (version "1.3.14")
        (classification "board/space/blocking")
        (author "Dale W. Walton")
        (credit "Dale W. Walton")
        (date "08-11-2024")
        }
    )
    
    (graphics 
        {
        (player Colour P1 (colour DarkGrey))
        (player Colour P2 (colour Green))
        (player Colour P3 (colour Blue))
        (region Colour Neutral (sites Occupied by:Neutral) (colour White))
        (board Colour InnerEdges (colour White))
        (piece Colour Neutral
            fillColour:(colour 0 0 0 0) 
            strokeColour:(colour 0 0 0 0)
        )
        //   (piece Foreground P1 "Disc" state:0 text:"X" scale:.5)
        //   (piece Foreground P2 "Disc" state:0 text:"X" scale:.5)
        //   (piece Foreground P3 "Disc" state:0 text:"X" scale:.5)
        //   (show Piece State Middle)
        }
))

