(game "Migration"
    (players {(player E) (player N)})
    (equipment {
        (board (square 8) use:Vertex)	
        (piece "Ball" Each (move Step (directions Forwards of:All) (to if:(is Empty (to)))))
    })
    (rules 
        (start {
            (place "Ball1" (sites {48 40..41 32..34 24..26 16..17 8}))
            (place "Ball2" (sites {1..6 10..13 19..20}))
        })
        (play (forEach Piece))
        (end (if (no Moves Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Move all your stones to the other edge, but as slow as possible...")
        (rules "On each turn, each player moves one stone forward (Black moves north, White moves east) to an empty cell. A player can only pass if there is no move option. The first player to pass, loses.")
        (id "4284")
        (source "<a href=\"https://www.di.fc.ul.pt/~jpn/gv/migration.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />The World of Abstract Games</a>")
        (version "1.3.14")
        (classification "board/space/blocking")
        (author "Greg van Patten")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Board)
        (board Colour InnerEdges (colour 160 140 100))
        (board Colour OuterEdges (colour Hidden))
        (board Colour Phase0 (colour 255 230 150))
        (show Edges Diagonal Hidden)
    })
)
