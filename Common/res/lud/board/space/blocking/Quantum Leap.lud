(define "DistanceToMove"
    (count Sites 
        in:(sites Around 
            (from) 
            Orthogonal 
            if:("IsFriendAt" (to))
        )
    )
)

//------------------------------------------------------------------------------

(game "Quantum Leap" 
    (players 2) 
    (equipment { 
        (board (hex 5))
        (piece "Marker" Each 
            (move Hop 
                Orthogonal 
                (between 
                    (exact (- "DistanceToMove" 1))
                    if:True
                )
                (to 
                    if:("IsEnemyAt" (to)) 
                    (apply (remove (to)))
                )
            )
        )
    }) 
    (rules 
        (start {
            (place "Marker2" (centrePoint))
            (place Random {"Marker2"} count:29) 
            (place Random {"Marker1"} count:30) 
        })
        phases:{
        (phase "Swap" 
            (play
                (or
                    (move Pass)
                    (move Select
                        (from (sites Occupied by:Mover))
                        (to (sites Occupied by:Next))
                        (then
                            (swap Pieces (last From) (last To))
                        )
                    )
                )
            )
            (nextPhase "Play")		
        )
        (phase "Play" (play (forEach Piece)))
        }
        (end ("NoMoves" Loss)) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Quantum Leap was invented in 2013 by Néstor Romeral Andrés. It can be played on a hexagonal grid of any size.")
        (rules "Play begins with the supply of pieces (30 of each colour) randomly distributed on the board, so each cell contains only one stone and there is one free space left. This free space can be anywhere except the centre space. Each player has an allocated colour. Before the game starts, Black can swap the positions of any two stones on the board. White then starts the game. Players alternate turns during the game until one of them cannot make a valid move, thereby losing the game. On a turn, a player must make one capture. A player makes a capture by leaping in a straight line in any of the 6 directions exactly as many spaces as friendly pieces surround its original position, and landing on an enemy piece, which is removed from the game. The player who cannot make a capture loses.")
        (source "<a href=\"https://boardgamegeek.com/boardgame/140782/quantum-leap\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (id "449")
        (version "1.3.4")
        (classification "board/space/blocking")
        (author "Néstor Romeral Andrés")
        (publisher "<a href=\"https://www.nestorgames.com\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />nestorgames</a> (used with permission)")
        (credit "Eric Piette")
        (date "2013")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
    })
    
    (ai 
        "Quantum Leap_ai"
    )
    
)
