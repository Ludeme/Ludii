(define "Score"
    (+
        (results
            from:(sites {(from)})
            to:(sites Around #1 Orthogonal)
            (if (= (from) (to))
                0
                (if ("IsFriendAt" (to)) 
                    1 
                    (if ("IsEnemyAt" (to))
                        -1 
                        0
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Jostle"
    (players 2)
    (equipment {
        <Board:board>
        (piece "Marker" Each
            (move Step Orthogonal
                (to if:(and 
                        (is Empty (to))
                        (< ("Score" (from)) ("Score" (to)))
                    )
                )
            )
        )
    })
    (rules
        <Board:start>
        (play (forEach Piece))
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <board> <start>} {
    (item "Square 10x10" <(board (square 10))> 
        <
        (start {
            (place "Marker1" (sites {23 25 27 32 34 36 43 47 52 63 72 74 76 65 67 56}))
            (place "Marker2" (sites {22 24 26 33 35 37 42 46 53 57 62 64 66 73 75 77}))
        })
        >
    "The game is played on a 10x10 square board.")* 
    (item "Hex 6x6" 	 <(board (hex 6))> 
        <
        (start {
            (place "Marker1" (sites {16 18 37 58 75 73 63 42 23 55 46 34}))
            (place "Marker2" (sites {15 17 27 48 67 74 72 53 32 56 35 44}))
        })
        >
    "The game is played on a 6x6 hex board.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Jostle is a two player game. The 10x10 board is initially set up with 16 red and 16 blue checkers. Red andBlue, take turns moving checkers of their own color, one move per turn, starting with Red.")
        (rules "OBJECT OF THE GAME The last player to make a move wins.
            
            FRIENDLY AND ENEMY CONNECTIONS A connection, i.e. an orthogonal
            adjacency between two checkers, is friendly if the two checkers are the same color and enemy if the two checkers are oppositely colored. 
            
            CHECKER VALUE A checker’s value is its number of friendly connections minus
            its number of enemy connections.
            
            MOVES A checker’s value must increase when it‘s moved. Move a checker to an
            unoccupied, orthogonally adjacent square.
            
        There is no capturing in Jostle, only jostling.")
        (id "1766")
        (source "<a href=\"http://www.marksteeregames.com/Jostle_Go_rules.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.marksteeregames.com</a>")
        (version "1.3.11")
        (classification "board/space/blocking")
        (author "Mark Steere")
        (publisher "<a href=\"http://www.marksteeregames.com/Jostle_Go_rules.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.marksteeregames.com</a>")
        (credit "Eric Piette")
        (date "2010")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
    })
    
    (ai
        "Jostle_ai"
    )
)
