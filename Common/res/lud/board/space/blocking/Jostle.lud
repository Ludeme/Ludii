(define "Score"
    (+
        (results
            from:(sites {(from)})
            to:(sites Around #1 Orthogonal)
            (if (= (from) (to))
                0
                (if (is Friend (who at:(to))) 
                    1 
                    (if (is Enemy (who at:(to)))
                        -1 
                        0
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Jostle"
    (players 2)
    (equipment {
        <Board:board>
        (piece "Marker" Each
            (move Step Orthogonal
                (to if:(and 
                        (is Empty (to))
                        (< ("Score" (from)) ("Score" (to)))
                    )
                )
            )
        )
    })
    (rules
        <Board:start>
        
        (play (forEach Piece))
        (end (if (no Moves Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <board> <start>} {
    (item "Square 10x10"    <(board (square 10))>   
        <
        (start {
            (place "Marker1" (sites {23 25 27 32 34 36 43 47 52 63 72 74 76 65 67 56}))
            (place "Marker2" (sites {22 24 26 33 35 37 42 46 53 57 62 64 66 73 75 77}))
        })
        >
    "The game is played on a 10x10 square board.")*   
    (item "Hex 6x6"    	  <(board (hex 6))>   
        <
        (start {
            (place "Marker1" (sites {16 18 37 58 75 73 63 42 23 55 46 34}))
            (place "Marker2" (sites {15 17 27 48 67 74 72 53 32 56 35 44}))
        })
        >
    "The game is played on a 6x6 hex board.")   
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
    })
    
)
