(game "Snailtrail"
    (players 2)
    (equipment {
        (board (rectangle <Board:size>))
        (piece "Pawn" Each (move Step (to if:(is Empty (to))) (then (moveAgain))))
        (piece "Square" Neutral)
    })
    (rules
        (start {
            <Board:placement>
        })
        (play 
            (if (is Even (count Moves))
                (forEach Piece)
                (move Add (piece "Square0") (to (last From)))
            )
        )
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <placement> } {
    (item "7x7" <7 7> <(place "Pawn1" {"A1"}) (place "Pawn2" {"G7"})> "The game is played on a 7x7 board.")*
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Blocking game played on a 6x8 or 7x7 board.")
        (rules "Each player has a pawn, that can move to any neighboring cell. After moving, the player places a block to the cell he came from. First player who cannot move loses.")
        (id "3960")
        (source "<a href=\"https://boardgamegeek.com/boardgame/37135/snailtrail\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.11")
        (classification "board/space/blocking")
        (credit "Michael Taktikos")
        }
    )
)
