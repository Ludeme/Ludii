(game "Gopher"
    (players 2)
    (equipment {
        (board (hex 6))
        (piece "Disc" Each)
    })
    (rules
        (play
            (if 
                (= 0 (count Moves))
                (move Add (to (sites Empty)))
                (move Add
                    (to
                        (sites Empty)
                        if:(and 
                            (= 0
                                (count Sites
                                    in:(sites Around
                                        (to)
                                        Own
                                    )
                                )
                            )
                            (= 1
                                (count Sites
                                    in:(sites Around
                                        (to)
                                        Enemy
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        (end 
            (if 
                (no Moves Next) 
                (result Next Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Gopher is a simple game made by Mark Steere to be easy to program.")
        (rules "The first player begins the game by placing a stone anywhere on the board. Then, starting with the second player, players take turns placing a stone orthogonally adjacent to exactly one enemy stone and no friendly stones.")
        (id "1506")
        (source "<a href=\"http://www.marksteeregames.com/Gopher_hex_rules.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />http://www.marksteeregames.com/</a>")
        (version "1.3.4")
        (classification "board/space/blocking")
        (author "Mark Steere")
        (publisher "<a href=\"http://www.marksteeregames.com/\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Mark Steere Games</a> (used with permission)")
        (credit "Michael Amundsen")
        (date "2021")
        }
    )
    (ai
        "Gopher_ai"
    )
)
