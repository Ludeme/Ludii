(game "Dodo"
    (players {(player N) (player S)})
    (equipment {
        (board (rotate 30 (hex 4)))
        (piece "Disc" Each
            (move Step Forwards (to if:(is Empty (to))))
        )
    })
    (rules
        (start {
            (place "Disc1" {0 1 2 3 4 5 6 7 9 10 11 15 16})
            (place "Disc2" {20 21 25 26 27 29 30 31 32 33 34 35 36 })
        })
        (play
            (forEach Piece)
        )
        (end 
            (if 
                (no Moves Mover) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Dodo is a dead simple game where the goal is to run out of moves.")
        (rules "On your turn you move one of your pieces one step onto an empty cell in one of the three forwards directions. Black's pieces move towards the bottom of the board, while White's pieces move towards the top. If, at the beginning of your turn, none of your pieces can move, you win.")
        (id "1550")
        (version "1.3.4")
        (classification "board/space/blocking")
        (author "Mark Steere")
        (publisher "Mark Steere Games")
        (credit "Michael Amundsen")
        (date "2021")
        }
    )
)
