(game "Orthokon"
    (players 2)
    (equipment {
        (board (square 4) use:Vertex)	
        (piece "Ball" Each 
            (move Slide All
                (then
                    (forEach Site 
                        (sites Around (last To) Enemy)
                        (and
                            (remove (site))
                            (add (piece (mover)) (to (site)))
                        )
                    )
                )
            )	
        )
    })
    (rules 
        (start {
            (place "Ball1" (sites Bottom))
            (place "Ball2" (sites Top))
        })
        (play (forEach Piece))
        (end (if (no Moves Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Slide stones, change enemies to friends, stalemate your opponent.")
        (rules "A stone slides (orthogonal or diagonal) through empty cells, stopping only when encountering another piece or an edge of the board. It then changes into its own color, all enemy stones orthogonally adjacent to it. A players looses if he is stalemated.")
        (id "4289")
        (source "<a href=\\"https://www.di.fc.ul.pt/~jpn/gv/orthokon.htm\\" target=\\"_blank\\" class=\\"style1\\" style=\\"color: #0000EE\\" />The World of Abstract Games</a>")
        (version "1.3.14")
        (classification "board/space/blocking")
        (author "L. Lynn Smith")
        (credit "Eric Piette")
        (date "2001")
        }
    )
    
    (graphics {
        (board Style Board)
        (board Colour InnerEdges (colour 160 140 100))
        (board Colour OuterEdges (colour Hidden))
        (board Colour Phase0 (colour 255 230 150))
        (show Edges Diagonal Hidden)
    })
)
