(define "AllCowsInBottom" (all Sites (difference (sites Occupied by:P2) (sites Bottom)) if:(= 0 (count at:(site)))))

//------------------------------------------------------------------------------

(game "Gurvan Xudag"
    (players 2)
    (equipment {
        (board 
            (remove (square 3) edges:{{0 1}{1 2}})
            use:Vertex
        )
        (piece "Human" P1 "StepToEmpty")
        (piece "Cow" P2 "StepToEmpty")
    })
    (rules 
        (start {
            (place "Human1" (sites Left))
            (place "Cow2" (sites Right))
        })
        (play (forEach Piece))
        (end {
            (if (no Moves P1) (result P2 Win))
            (if (and ("AllCowsInBottom") (no Moves P2)) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Gurvan Xudag is a blockade game played in Mongolia. Three guardians attempt to herd three calves into their pens.")
        (aliases {"Tugul"})
        (rules "Three vertical lines, and two horizontal lines intersecting them along their top end points and their midpoints. The ends of the lines which are not connected to one another are the calves' pens. Three pieces per player, one playing as calves (black), and the other as guardians (white), arranged on the intersections of opposite vertical lines. Players alternate turns moving one of their pieces to an adjacent spot. The guardians attempt to trap the calves into the three pens, the calves attempt to block the guardians from being able to move.")
        (source "Popova 1974: 16, 19.")
        (id "1010")
        (version "1.3.5")
        (classification "board/space/blocking")
        (credit "Eric Piette")
        (origin "This game was played in Mongolia, from around 1963 to 1991.")
        }
    )
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    (ai
        "Gurvan Xudag_ai"
    )
)
