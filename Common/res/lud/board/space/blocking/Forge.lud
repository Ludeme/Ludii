(game "Forge" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (rectangle 8 6) use:Vertex)
        (piece "Ball" Each (move Slide (directions Forwards of:All))) 
    }) 
    (rules 
        (start {
            (place "Ball1" (expand (sites Bottom)))
            (place "Ball2" (expand (sites Top)))
        })
        phases:{
        (phase "Opening"
            (play (forEach Piece))
            (nextPhase "Main")
        )
        (phase "Main"
            (play
                (priority {
                    (forEach Piece
                        (move Slide 
                            (from (from) if:(is In (from) (sites Around (last To) All)))
                            (directions Forwards of:All)
                        )
                    )
                    (forEach Piece)
                })	
            )
        )
        }
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Forge is a two-player game played on an NxM square board. I will consider here an 8x6 board.")
        (rules "MOVEMENT - A stone must move as far as it can (no jumps) in a forward direction (orthogonal or diagonal). 
            - At the first move, a player may choose any of his stones to move.
            - After that, a player must move a stone that is adjacent to the last moved enemy stone. If that is not possible, the player may choose any unblocked of his stones to move. 
            - There is no capturing.
            
        GOAL  - Wins the player that moved the last stone.")
        (id "1808")
        (source "<a href=\"http://www.di.fc.ul.pt/~jpn/gv/forge.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.di.fc.ul.pt</a>")
        (version "1.3.6")
        (classification "board/space/blocking")
        (author "Michael Howe")
        (credit "Eric Piette")
        (date "2001")
        }
    )
    
    (graphics {
        (show Edges Diagonal (colour Hidden))
        (board Background image:"square.svg" fillColour:(colour 223 178 110) edgeColour:(colour 223 178 110) scale:1.25)
        (board Colour OuterEdges (colour Black))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterVertices (colour Hidden))
        (board Colour InnerVertices (colour Hidden))
        (show Symbol "disc" (sites Corners) fillColour:(colour Black) edgeColour:(colour Black) scale:0.3)
    })
    
)
