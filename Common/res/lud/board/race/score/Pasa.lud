(define "Row"
    (- (/ (score Mover) 10) 1)
)

(define "Column" (- (mover) 1))

//------------------------------------------------------------------------------

(game "Pasa"
    (players 2)
    (equipment
        {
        (board (rectangle 10 2))
        (piece "Disc" Each)
        (dice d:7 from:0 num:1)
        (map {(pair 0 -10) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4) (pair 5 5) (pair 6 6) (pair 7 10)})
        }
    )
    (rules
        (play
            (do
                (roll (then (addScore Mover (mapEntry (count Pips)))))
                next:(move Pass
                    (then
                        (and
                            (remove (sites Column "Column"))
                            (if (>= (score Mover) 10)
                                (add
                                    (piece (id "Disc" Mover))
                                    (to (intersection (sites Column "Column") (sites Row "Row")))
                                )
                            )
                        )
                    )
                )
            )
        )
        (end (if (>= (score Mover) 100) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Pasa is a dice game that was played in the Andes since at least the eighteenth century. It involves a special seven-sided die, and includes rolls where players may lose points. The highest roll is called \"Huayru,\" which is a word for dice known to be used by the Inka.")
        (rules "The board is a double-headed eagle, with two lines of ten holes. One seven-sided die. One side has a special mark, one is blank, and the others are numbered 1-5. The special mark scores 10, the blank side scores -10. Players roll the die, and add up their scores as they go, placing a peg in a hole to mark ten points. The player who reaches 100 points first wins.")
        (source "Juan and Ulloa 1748: 549; Bollaert 1860: 168; Depaulis 1998: 32.")
        (version "1.3.0")
        (classification "board/race/score")
        (credit "Eric Piette")
        (origin  "This game was played in Andes, from around 1735 to 1854.")
        }
    )

    (graphics {
        (piece Foreground "Die" state:6 image:"star" fillColour:(colour White) scale:0.2)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })

    (ai
        "Pasa_ai"
    )
)
