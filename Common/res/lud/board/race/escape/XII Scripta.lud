(define "SiteToMoveOnTrack" ("NextSiteOnTrack" (pips) from:#1))

(define "RemoveAPiece" (move Select (from #1) (then (if (= (count at:(last To)) 1) (remove (last To)) (set Count at:(last To) (- (count at:(last To)) 1))))))

(define "PieceMove" 
    (forEach Die 
        combined:True 
        if:(and 
            (!= (pips) 0) 
            ("IsNotOffBoard" ("SiteToMoveOnTrack" #2))
        )
        (if ("IsEndTrack" ("SiteToMoveOnTrack" #2))
            ("RemoveAPiece" #1)
            (move 
                (from #1) 
                (to 
                    ("SiteToMoveOnTrack" #2) 
                    if:(not 
                        (and 
                            ("IsEnemyAt" (to)) 
                            (> (count at:(to)) 1)
                        )
                    ) 
                    (apply 
                        if:(and 
                            ("IsEnemyAt" (to)) 
                            (= (count at:(to)) 1)
                        ) 
                        (fromTo 
                            (from (to)) 
                            (to (handSite Next 1))
                        ) 
                    )
                ) 
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "XII Scripta"
    (players 2)  
    (equipment { 
        (board 
            (add
                (merge (rectangle 3 6) (shift 7 0 (rectangle 3 6))) 
                edges:{{5 18} {11 24} {17 30}}
            )
            {    
            (track "Track1" "36,6,E,N1,W,0,E,End" P1 directed:True)
            (track "Track2" "38,6,E,S1,W,12,E,End" P2 directed:True)
            } 
            use:Vertex
        )
        (dice num:3)
        (hand Each size:2)
        (piece "Disc" Each ("PieceMove" (from) (from))) 
    })  
    
    (rules 
        (start { 	
            (place "Disc1" (handSite P1) count:15)
            (place "Disc2" (handSite P2) count:15)
        })	
        
        (play 
                ("RollEachNewTurnMove"
                        (if (!= (what at:(handSite Mover 1)) 0)
                    ("PieceMove" (handSite Mover 1) (handSite Mover))
                    (or (forEach Piece) (forEach Piece container:(mover)))
                    (then (if (can Move 
                                (if (!= (what at:(handSite Mover 1)) 0)
                                    ("PieceMove" (handSite Mover 1) (handSite Mover))
                                    (or (forEach Piece) (forEach Piece container:(mover)))
                                )
                            )
                            (moveAgain)
                        )
                    )
                )
            )
        )
        
        (end ("EscapeWin"))
    )
)

//--------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <type> }
    {
    (item "Murray"   <>       "The rules are describing with the Murray ruleset.")   
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Murray (Suggested)" {
        "Variant/Murray"
    })*
    
    (ruleset "Ruleset/Historical Information (Incomplete)" {
        
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "XII scripta (\"twelve signs\") or ludus duodecim scrtiptorum (\"game of twelve signs\" ) or, later, alea (\"dice\"), is a Roman game dating to the first cnturies CE that appears to be an ancestor of backgammon. Game boards that the form of 36 symbols, placed in three rows of twelve but divided in the middle by either a gap or other, larger signs, such as circles or rosettes. Sometimes the 36 symbols are letters, forming six six-letter words that form a poem or phrase, often relating to game play. One board, from Ostia, may indicate that game play started in the left hand side of the center row of signs, progressed to the right, went from right to left in the top row, and then left to right in the bottom row. A late epigram of Agathias recounts a game of tabula (generic Roman term for \"board game;\" backgammon in the Greek world is today known as tavli) that clearly describes a game of backgammon.")
        (aliases {"Duodecim Scripta" "Ludus Duodecim Scriptorum" "Alea"})
        (useFor "Ruleset/Murray (Suggested)" (rules "Play begins in the central row and continues from left to right, then down the furthest row from right to left, then on the closest row from left to right. Played using three dice; throws can be used singly or combined but could not be divided. No second throw with doubles or triples. More than one piece of the same player can occupy one point, Single pieces can be removed from the board when the opponent lands on it; it must then be re-entered to the board before the player can move their other pieces. The goal is to bear off all of the player's pieces, which can be done one they are all in the closest row."))
        (useFor "Ruleset/Murray (Suggested)" (source "Murray 1951:30-31."))
        (useFor "Ruleset/Murray (Suggested)" (id "33"))
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "3x12 board divided in half. Track goes down the center line from left to right, then right to left in the top line, then left to right in the bottom line. "))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP evidence. "))
        (useFor "Ruleset/Historical Information (Incomplete)" (id "533"))
        (source "Murray 1951: 30-31")
        (id "33")
        (version "1.3.4")
        (classification "board/race/escape")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (show Edges Hidden) 
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:1.1 scaleY:0.01 offsetX:0 offsetY:0.13)
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:1.1 scaleY:0.01 offsetX:0 offsetY:-0.13)
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:0.007 scaleY:0.423 offsetX:-0.55 offsetY:0)
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:0.007 scaleY:0.423 offsetX:0.55 offsetY:0)
        (board Background image:"flower.svg" fillColour:(colour Black) edgeColour:(colour Black) scale:0.06)
        (board Background image:"FlowerHalf1.svg" fillColour:(colour Black) edgeColour:(colour Black) scale:0.06 offsetY:0.075)
        (board Background image:"FlowerHalf2.svg" fillColour:(colour Black) edgeColour:(colour Black) scale:0.06 offsetY:-0.075)
    })
    
    (ai 
        "XII Scripta_ai"
    )
    
)
