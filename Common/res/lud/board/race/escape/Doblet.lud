(define "DieNotUsed" (!= (pips) 0))

(define "SiteToSelect" (trackSite Move from:(mapEntry "StartTrack" (mover)) steps:(- (pips) 1)))

(define "SiteToMoveOnTrack" (trackSite Move steps:(pips)))

(define "RemoveAPiece" (move Remove (from) ) )

(define "PieceStacked" (= (state at:#1) 1) )
(define "Unstack" (set State at:(last To) 0))

(define "StackSizeLessThan2" (< (size Stack at:"SiteToMoveOnTrack") 2))

//------------------------------------------------------------------------------

(game "Doblet"
    (players 2)
    (equipment {
        (board
            (merge {
                (rectangle 1 6)
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {0..5 6..11 23..18 17..12 End} P1 directed:True)
            (track "Track2" {12..17 18..23 11..6 5..0 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:3)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (= "SiteToMoveOnTrack" End)
                    "RemoveAPiece"
                    (move
                        (from)
                        (to
                            "SiteToMoveOnTrack"
                            if:"StackSizeLessThan2"
                            (apply (if (is Enemy (who at:(to))) (remove (to))))
                        )
                    )
                )
            )
        )
        (map "StartTrack" {(pair P1 0) (pair P2 12)})
    })
    (rules
        (start {
            (place Stack "Disc1" (sites {0..5}) count:2 state:1)
            (place Stack "Disc2" (sites {12..17}) count:2 state:1)
        })

        phases:{
        (phase "UnStacking"
            (play
                (do
                    (if (not "SameTurn") (roll))
                    next:
                    (forEach Die
                        if:("DieNotUsed")
                        (if ("PieceStacked" "SiteToSelect")
                            (move Select (from ("SiteToSelect"))
                                (then
                                    (and {
                                        ("Unstack")
                                        (if (not (all DiceUsed))
                                            (moveAgain)
                                        )
                                    })
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (= 0 (count Sites in:(forEach (sites Occupied by:Mover) if:("PieceStacked" (site))))) "BearingOff")
        )
        (phase "BearingOff"
            (play
                (do
                    (if (not "SameTurn") (roll))
                    next:(forEach Piece top:True)
                    (then (if (and (not (was Pass)) (not (all DiceUsed))) (moveAgain)))
                )
            )
        )
        }

        (end (if (no Pieces Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Doblet is a race game from medieval Spain that is related to other European Tables games. It is documented in Alfonso X's Libro de los Juegos.")
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Twelve pieces per player. Pieces begin on one half of a player's side of the board (the half to the left of one player, and to the right of the other player), two per space, stacked on top of one another. Three dice. To begin, a player must first unstack their pieces, by rolling the number of the space on which the stack is located, and thus removing the stack, but keeping the piece on the same space. Once complete, pieces move in a track the long way around the board toward the portion where the opponent begins. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. No more than two pieces can occupy a space at a time. When a piece lands on a space occupied by a single piece of the opponent, the opponent's piece is removed from the board. Players attempt to bear off their pieces by rolling the exact number of spaces left on the board, plus one. When unstacking the pieces in the beginning or when bearing off at the end, if the player cannot play their roll but the opponent is able, the opponent may use the roll. The first player to bear off all of their pieces wins.
        ")
        (source "Libro de los Juegos 74.")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Spain, around 1283.")
        }
    )

    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType state:0 Backgammon)
    })

)

