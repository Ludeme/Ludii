(define "StepMove" ("StepToEmpty" (directions { Forward Leftward Rightward })))

//------------------------------------------------------------------------------

(game "Dodgem"
    (players 2)
    (equipment {
        (board (square <Board>))
        (piece "Car" P1 E 
            (if (is In (from) (sites Right))
                (move Remove (from))
                ("StepMove")
            )
        )
        (piece "Car" P2 N 
            (if (is In (from) (sites Top))
                (move Remove (from))
                ("StepMove")
            )
        )
    })
    (rules
        (start {
            (place "Car1" (difference (sites Left) (sites {"A1"})))
            (place "Car2" (difference (sites Bottom) (sites {"A1"})))
        })
        (play (forEach Piece))
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size > } {
    (item "3x3" <3> "The game is played on a 3x3 board.")*
    (item "4x4" <4> "The game is played on a 4x4 board.")
    (item "8x8" <8> "The game is played on a 8x8 board.")
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Invented by Colin Vout in 1972 as described in the book Winning Ways for Your Mathematical Plays.")
        (rules "Played on a nxn board. Each player has n-1 cars, that can move either forward or up and down. Cars can leave the board once they have reached the opposite edge. First player to run out of legal moves wins.")
        (id "1260")
        (source "<a href=\"https://en.wikipedia.org/wiki/Dodgem\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.6")
        (classification "board/race/escape")
        (author "Colin Vout")
        (credit "Fabio Barbero")
        (date "1972")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Blue))
        (player Colour P2 (colour Red))
        (piece Rotate P2 "Car" degrees:270)
    })
    (ai
        "Dodgem_ai"
    )
)
