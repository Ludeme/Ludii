(define "Triple" (all DiceEqual))
(define "Die1" (face 97))
(define "Die2" (face 98))
(define "Die3" (face 99))
(define "HasDouble" 
    (or {
        (= ("Die1") ("Die2"))
        (= ("Die1") ("Die3"))
        (= ("Die2") ("Die3"))
    })
)

(define "CountPieceTypeInStack"
    (count Pieces of:#1 in:(sites {#2}))
)

(define "HasASixThreeOrTwo"
    (or {
        (is AnyDie 6)
        (is AnyDie 3)
        (is AnyDie 2)
    })
)

(define "PieceMove"
    (or
        (if (and { (= 0 (count MovesThisTurn)) ("Triple") ("HasASixThreeOrTwo") (= 3 ("CountPieceTypeInStack" (mover) (from))) })
            (move 
                (from (from) level:(level))
                (to ("NextSiteOnTrack" (* 2 ("Die1")) from:(from) #1))
                (then 
                    (forEach Level (last From) FromTop
                        (if (is Mover (who at:(last From) level:(level)))
                            (fromTo 
                                (from (last From) level:(level))
                                (to (last To))
                            )
                        )
                    )
                )
            )
        )
        (forEach Die
            if:("DieNotUsed")	
            (if ("IsEndTrack" ("NextSiteOnTrack" (pips) from:(from) #1))
                (move Remove (from) level:(level))
                (move 
                    (from (from) level:(level))
                    (to ("NextSiteOnTrack" (pips) from:(from) #1))
                )
            )
            (then ("ReplayNotAllDiceUsed"))
        )
        (then
            (forEach Level (last To) FromTop
                (if (and 
                        ("IsEnemyAt" (last To) level:(level))
                        (= 1 ("CountPieceTypeInStack" (who at:(last To) level:(level)) (last To)))
                    )
                    (fromTo 
                        (from (last To) level:(level))
                        (to (mapEntry "Start" (who at:(last To) level:(level))))
                    )
                )
            )
        )
    )
)

(define "Move" 
    (if (no Pieces Mover)
        (if (is Mover P1)
            (forEach Piece ("PieceMove" "Track3") P3)
            (if (is Mover P3)
                (forEach Piece ("PieceMove" "Track1") P1)
                (if (is Mover P4)
                    (forEach Piece ("PieceMove" "Track2") P2)
                    (forEach Piece ("PieceMove" "Track4") P4)
                )
            )
        )
        (forEach Piece)
    )		
)

//------------------------------------------------------------------------------

(game "Chaupar"  
    (players 4)  
    (equipment { 
        ("PachisiBoard" 
            {
            (track "Track1" "61,S,E,N,11,E,N,W,53,N,W,S,42,W,S,E,8,54,E1,N7,End" P1 directed:True)
            (track "Track2" "21,W,S,E,8,S,E,N,11,E,N,W,53,N,W,S,42,W,S1,E7,End"  P2 directed:True)
            (track "Track3" "88,N,W,S,42,W,S,E,8,S,E,N,11,E,N,W,53,N,W1,S7,End"  P3 directed:True)
            (track "Track4" "32,E,N,W,53,N,W,S,42,W,S,E,8,S,E,N,11,E,N1,W7,End"  P4 directed:True)
            }
        )
        (dice d:4 faces:{1 2 5 6} num:3)
        (map "Start" {(pair P1 61) (pair P2 21) (pair P3 88) (pair P4 32)})
        (piece "Pawn" Each ("PieceMove" "Track"))
    })  
    (rules 
        (start {
            (set Team 1 {P1 P3})
            (set Team 2 {P2 P4})
            
            // The normal pawns.
            (place Stack "Pawn1" (sites {61 58 59 56}))
            (place Stack "Pawn2" (sites {21 20 0 1}))
            (place Stack "Pawn3" (sites {91 88 93 90}))
            (place Stack "Pawn4" (sites {33 32 49 50}))
        })
        
        (play ("RollEachNewTurnMove" ("Move")))
        
        (end ("EscapeTeamWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Chaupar is a very popular race game from South Asia that is related to similar games from the region. It has been documented since at least the sixteenth century, when the first description of the rules has been documented.")
        (rules "Four 3x8 rectangles arranged in a cross with a large empty square in the center. Two teams of two players, or by two players playing with two sets of pieces. The pieces move along the outer track of the board according to the throw of three four-sided dice with values of 1, 2, 5 and 6. The values of each die must be played individually, except in certain cases. Each player has four pieces, which begin on the sixth and seventh space of the central row and the seventh and eight space in the right hand row of the arm of the board belonging to the player. If a piece lands on a space occupied by an opponent, the opponent's piece is sent back to the starting position. If two of a player's pieces are on the same space, they cannot be sent to the beginning. In addition, when three pieces are on the same spaces, if triple sixes are thrown the pieces may move twelve spaces together. The same rule applies for threes and twos, moving six and four, respectively. After completing a circuit of the board, the pieces then move into the central row of squares in the arm where the player began. The player must then move off all of their pieces by an exact roll. If a player has removed all of their pieces from the board and their partner is still playing, the player rolls on what would be their turn and the partner moves according to these rolls in addition to their own turn. The player or team to remove all of their pieces from the board first wins.
        ")
        (source "Abu'l Fazl 1590: 303-304.")
        (id "200")
        (version "1.3.4")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, around 1590.")
        }
    )
    
    (graphics {
        (stackType 0 Ground)
        (stackType 1 Ground)
        (stackType 2 Ground)
        (stackType 3 Ground)
        (stackType 4 Ground)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Scale "Pawn" 0.5)
    })
    
)
