(define "NextSiteFrom"
    (trackSite Move
        from:#1
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"
    (or
        (and
            (is Enemy (who at:(to)))
            (= (topLevel at:(to)) 0)
        )
        (not (is Enemy (who at:(to))))
    )
)

(define "AllPiecesInFinalQuadrant"
    (no Pieces Mover in:(difference (sites Board) (sites Mover "FinalQuadrant")))
)

//------------------------------------------------------------------------------

(game "Verquere"
    (players 2)
    (equipment {
        (board (rectangle 2 13)
            {
            (track "Track1" {6 25..20 18..13 0..5 7..12 End} P1 directed:True)
            (track "Track2" {19 0..5 7..12 25..20 18..13 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                replayDouble:True
                if:("DieNotUsed")
                (if (= End ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move
                        (from (from))
                        (to
                            ("NextSiteFrom" (from) (pips))
                            if:(and
                                ("NotEnemyOrOnlyOne")
                                (if (not (is In (to) (sites Mover "StartingSide")))
                                    True
                                    (is Empty (to))
                                )
                            )
                            (apply
                                (if (is Enemy (who at:(to)))
                                    (fromTo
                                        (from (to))
                                        (to (mapEntry "Bar" Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then
                    (if (not (all DiceUsed))
                        (moveAgain)
                    )
                )
            )
        )
        (regions "FinalQuadrant" P1 (sites {7..12}))
        (regions "FinalQuadrant" P2 (sites {13..18}))
        (regions "StartingSide" P1 (sites {25..20 18..13}))
        (regions "StartingSide" P2 (sites {0..5 7..12}))
        (map "Bar" {(pair P1 6) (pair P2 19)})
    })
    (rules
        (start {
            (place Stack "Disc1" 25 count:15)
            (place Stack "Disc2" 0 count:15)
        })
        (play
            (do (if (not "SameTurn") (roll))
                next:(forEach Piece top:True)
            )
        )

        (end
            (if (no Pieces Mover) (result Mover Win))
        )
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Verquere is a European Tables game known since at least the eighteenth century. It was said to have been invented in Holland, but was also played in England.")
        (aliases {"Verkehren" "Verkeer" "Revertier"})
        (rules "2x12 board, with the spaces rendered as points, divided into half. Fifteen pieces per player. Two six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. A throw of doubles forces the player to play the throw twice. Each player's pieces begin in three stacks of five, on the leftmost point (with respect to the opponent) on the opponent's side of the board. Play proceeds (with respect to the player) from right to left on the opponent's side of the board, and then from left to right on the player's side of the board. A player cannot place two pieces on a single point on any of the first twelve points of the board, except for those pieces in the starting position. A player cannot move a piece onto a point containing two or more pieces belonging to the opponent. When a piece lands on a point occupied by a single piece belonging to the opponent, it is removed from the board and must be entered again, and can only do so using the value of one die, not both, with the starting point considered to be point 1, the next point 2, etc. Opponent's pieces can be removed from the board in this way when reentering the board. If a player cannot reenter pieces on the board they lose their turn. When all of a player's pieces are on their final 6 points, they may start removing pieces from the board. They can do so by rolling a 6 to move from the 6th point, and so on down to 1. Players must use all available moves presented by the dice. The first player to remove all of their pieces wins.")
        (source "Cotton 1725: 99-102.")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Netherlands, around 1725.")
        }
    )

    (graphics {
        (board Style backgammon)
        (stackType Backgammon limit:3)
    })

)

