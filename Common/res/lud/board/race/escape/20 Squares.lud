(define "CountPips"
    (if (= 0 (face 23))
        (face 22)	
        (if (= 1 (face 22))
            (+ (face 22) 5)
            (if (= 2 (face 22))
                (+ (face 22) 6)
                (if (= 3 (face 22))
                    (+ (face 22) 7)
                    (+ (face 22) 10)
                )
            )
        )
    )
)

(define "RemoveAPiece" (move Remove (from)))

(define "MoveTo" ("NextSiteOnTrack" ("CountPips")))

//------------------------------------------------------------------------------

(game "20 Squares"
    (players 2) 
    (equipment { 
        (board 
            (merge 
                (rectangle 3 4) 
                (shift 0 1 (rectangle 1 12))
            )
            { 
            (track "Track1" "20,3,W,N1,E,End" P1 directed:True)
            (track "Track2" "21,12,W,S1,E,End" P2 directed:True)
            }
        )
        (dice facesByDie:{{1 2 3 4} {0 1}} num:2)
        (hand Each)
        (regions "Replay" (sites {"A1" "A3" "D2" "H2" "L2"}))
        
        (piece "UrPiece" Each
            (if ("IsEndTrack" ("MoveTo"))
                "RemoveAPiece"
                (if ("IsNotOffBoard" ("MoveTo"))	 			
                    (if (or
                            (is Empty ("MoveTo"))
                            (and 
                                (not ("IsFriendAt" ("MoveTo"))) 
                                (not (is In ("MoveTo") (sites "Replay")))
                            )
                        )
                        (move 
                            (from) 
                            (to 
                                ("MoveTo")
                                ("HittingCapture" (handSite (who at:(to))))
                            )
                            (then ("ReplayInMovingOn" (sites "Replay")))
                        )
                    )
                )
            )
        ) 
        
    }) 
    
    (rules 
        (start { 	
            (place "UrPiece1" (handSite P1) count:7)
            (place "UrPiece2" (handSite P2) count:7)
        })	
        
        (play ("RollMove" (or (forEach Piece) (forEach Piece container:(mover)))))
        
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <seleucid> }
    {
    (item "Seleucid" <> "Rules from Seleucid.")* 
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Seleucid (Scholarly)" 
        {
        "Variant/Seleucid"
        }
    )
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        }
    )
    
    (graphics {
        (show Symbol "flower" "Replay" scale:0.9)
        (board Colour InnerEdges (colour 120 190 240))
        (board Colour OuterEdges (colour Black))
        (board Colour Symbols (colour 130 50 50))
        (board Colour Phase0 (colour 210 230 255))
    })
    
    (ai 
        "20 Squares_ai"
    )
    
)

