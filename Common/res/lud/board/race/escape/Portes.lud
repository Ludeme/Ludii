(define "IfEnemyBlotIt"
    (apply
        if:(is Enemy (who at:(to)))
        (fromTo
            (from (to))
            (to (mapEntry (next)))
        )
    )
)

(define "NotEnemyOrOnlyOne"
    (or
        (and
            (is Enemy (who at:(to)))
            (= (topLevel at:(to)) 0)
        )
        (not (is Enemy (who at:(to))))
    )
)

(define "BarEmpty" (= (what at:"Bar") 0))

(define "NextSiteFrom"
    (trackSite Move
        from:#1
        steps:(pips)
    )
)

(define "Bar" (mapEntry (mover)))

(define "DieNotUsed" (!= (pips) 0))

(define "AllPieceInHome"
    ("NoSites"
        (difference
            (sites Occupied by:Mover)
            (sites Mover)
        )
    )
)

(define "RemoveAPiece"
    (move
        Remove
        (site)
    )
)

(define "DieNotUsedAndNoEscapeAndNotOffTheTrack"
    (and {
        ("DieNotUsed")
        (!= ("NextSiteFrom" (from)) Off)
        (!= ("NextSiteFrom" (from)) End)
    })
)

(define "MoveAPiece"
    (move
        (from #1)
        (to
            ("NextSiteFrom" #1)
            if:("NotEnemyOrOnlyOne")
            ("IfEnemyBlotIt")
        )
    )
)

(define "MoveAPieceIf"
    (forEach Die
        replayDouble:True
        if:#1
        ("MoveAPiece" #2)
    )
)

(define "AllPieceEscaped" (no Pieces Mover))

(define "NumPiece" (size Stack in:(sites Occupied by:#1)))

(define "CanEscape" (= ("NextSiteFrom" (site)) End))

(define "NotEmptyAndNotOffTheBoard"
    (and
        (!= (what at:(site)) 0)
        (!= ("NextSiteFrom" (site)) Off)
    )
)

(define "SetScoreOf"
    (score
        #1
        (if (is Mover #1)
            (if
                (= ("NumPiece" #2) 15)
                2
                1
            )
            0
        )
    )
)

(define "HaveAPieceAndCanEscape"
    (and
        (is Friend (who at:(site)))
        (< (trackSite Move from:(site) steps:(pips)) 0)
    )
)

//-----------------------------------------------------------------------------

(game "Portes"
    (players 2)
    (equipment {
        (board (rectangle 2 13)
            {
            (track "Track1" {6 12..7 5..0 13..18 20..25 End} P1 directed:True)
            (track "Track2" {19 25..20 18..13 0..5 7..12 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice num:2)
        (regions P1 { 20..25 }) // P1 Home
        (regions P2 { 7..12 })  // P2 Home
        (map {(pair 1 6 ) (pair 2 19)}) // Bar
        (piece "Disc" Each ("MoveAPieceIf" "DieNotUsedAndNoEscapeAndNotOffTheTrack" (from)))
    } )

    (rules
        (start {
            (place Stack "Disc1"  0 count:5) (place Stack "Disc1" 12 count:2)
            (place Stack "Disc1" 17 count:3) (place Stack "Disc1" 20 count:5)
            (place Stack "Disc2"  4 count:3) (place Stack "Disc2"  7 count:5)
            (place Stack "Disc2" 13 count:5) (place Stack "Disc2" 25 count:2)
        })

        (play
            (do
                (if (not "SameTurn") (roll))
                next:(if "AllPieceInHome"
                    (forEach Die
                        replayDouble:True
                        if:("DieNotUsed")
                        (forEach Site
                            (sites Mover)
                            (if ("NotEmptyAndNotOffTheBoard")
                                (if ("CanEscape")
                                    ("RemoveAPiece")
                                    ("MoveAPiece" (site))
                                )
                            )
                            noMoveYet:(firstMoveOnTrack "Track" Mover
                                (if "HaveAPieceAndCanEscape"
                                    "RemoveAPiece"
                                )
                            )
                            (then
                                (if (not (all DiceUsed))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                    (max Distance "Track" Mover
                        (if "BarEmpty"
                            (forEach Piece top:True)
                            ("MoveAPieceIf" ("DieNotUsed") ("Bar"))
                            (then
                                (if (not (all DiceUsed))
                                    (if (can Move
                                            (if "BarEmpty"
                                                (forEach Piece top:True)
                                                ("MoveAPieceIf" ("DieNotUsed") ("Bar"))
                                            )
                                        )
                                        (moveAgain)
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )

        (end
            (if ("AllPieceEscaped")
                (byScore {
                    ("SetScoreOf" P1 P2)
                    ("SetScoreOf" P2 P1)
                } )
            )
        )
    )
)

//-------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Portes is a game that is closely similar to Backgammon played in the eastern Mediterranean. It is frequently played as part of Tavli, where it is played in succession with Plakoto and Fevga.")
        (rules "The game is played on a board with twelve points on either side. The points form a continuous track in a horseshoe shape; each player progresses in opposite directions (one from their bottom right to the top right, the other from their bottom left to their top left. Each player has 15 pieces. The starting position is as such, numbering the points from the origin of each player's track: Point six: five pieces. Point 8: three pieces. Point 13: five pieces. Point 24: two pieces. Play begins by each player rolling one die; the player who rolls the highest plays first, and then rolls again to begin play. Players move according to the number on each die by moving one piece the number on one die and another the number on the other die, or by moving one piece twice (once for each die). If doubles are rolled, the player must play the number on each die twice. Players cannot end their move on a point with multiple opposing pieces. If a player ends the turn on a point with one opposing piece, that piece is placed in the middle of the board (not on a point) and must reenter the board according the the next die roll, counting the origin point as a move of one. They cannot reenter on a point with two or more pieces. No other pieces can move until all of the pieces belonging to that player are removed from the center. When all of a player's pieces are on their final 6 points, they may start removing pieces from the board. They can do so by rolling a 6 to move from the 6th point, and so on down to 1. Players must use all available moves presented by the dice. The first player to remove all of their pieces wins. The winner gains one point, or two points if the opponent has pieces in their starting quadrant.
            ")
            (source "<a href=\"https://bkgm.com/variants/Portes.html\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />bkgm.com</a>")
            (version "1.3.0")
            (classification "board/race/escape")
            (credit "Eric Piette")
            }
        )
        (graphics {
            (board Style backgammon)
            (stackType Backgammon)
        })

        (ai
            "Portes_ai"
        )
    )
