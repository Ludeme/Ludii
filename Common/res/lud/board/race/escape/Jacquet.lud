(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:(pips)
    )
)

(define "StartPos" 
    (trackSite Move 
        from:("Bar" (mover)) 
        steps:1
    )
)

(define "NextSiteFromDistDoubleDice" 
    (trackSite Move 
        from:#1 
        steps:(/ (count Pips) 2)
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "RemoveAPiece" 
    (move
        Remove
        (site) 
    )		
)

(define "AllPieceEscaped" (no Pieces Mover))

(define "CanEscape" (= #1 Off))

(define "PieceInOpponentQuadrantOrNoPieceInTheBoard" 
    (or {
        (= 
            (count Sites 
                in:(intersection 
                    (sites Occupied by:Mover) 
                    (difference 
                        (sites Board) 
                        "StartPos"
                    )
                )
            ) 
            0
        )
        (not (is Empty ("Bar" (mover))))
        (< (size Stack at:("StartPos")) 14)
        (!= 
            (count Sites in:(intersection (sites Occupied by:Mover) (sites Mover "OppositeQuadrant"))) 
            0
        ) 
    })
)

(define "AllPieceInOpponentSide"
    (= 0 
        (count Sites 
            in:(difference 
                (sites Occupied by:Mover) 
                (sites Mover "OppositeQuadrant")
            )
        )
    )
)

(define "Bar" (mapEntry #1))

(define "NotEnemyOrOnlyOne"   
    (or 
        (and 
            (is Enemy (who at:(to))) 
            (= (topLevel at:(to)) 0)
        ) 
        (not (is Enemy (who at:(to))))
    )
)

//-----------------------------------------------------------------------------

(game "Jacquet"  
    (players 2)  
    (equipment { 
        (board (rectangle 2 13) 
            {
            (track "Track1" {6 13..18 20..25 12..7 5..0} P1 directed:True)
            (track "Track2" {19 12..7 5..0 13..18 20..25} P2 directed:True)
            } 
            use:Vertex
        ) 
        (dice num:2)
        (piece "Disc" Each) 
        (map {(pair 1 6 ) (pair 2 19)}) // Bar
        (regions "Home" P1 { 13..18 }) // P1 Home (first quadrant)
        (regions "Home" P2 { 12..7 }) // P2 Home (first quadrant)
        (regions "OppositeQuadrant" P1 { 0..5 }) // Opposite Quadrant of P1
        (regions "OppositeQuadrant" P2 { 20..25 }) // Opposite Quadrant of P2
    } )
    
    (rules
        (start { 
            (place Stack "Disc1" 13 count:15)
            (place Stack "Disc2" 12 count:15)
        })
        
        (play 
            (do 
                (if (not "SameTurn") (roll))
                next:(forEach Die
                    replayDouble:True 
                    if:("DieNotUsed")
                    (forEach Site
                        (sites Occupied by:Mover)
                        (if (and ("CanEscape" ("NextSiteFrom" (site))) ("AllPieceInOpponentSide"))
                            ("RemoveAPiece")
                            (if (or 
                                    (!= (site) "StartPos")
                                    (and 
                                        (= (site) "StartPos")
                                        ("PieceInOpponentQuadrantOrNoPieceInTheBoard")
                                    )
                                )
                                (move 
                                    (from (site))
                                    (to ("NextSiteFrom" (site))
                                        if:("NotEnemyOrOnlyOne")
                                        (apply 
                                            if:(is Enemy (who at:(to))) 
                                            (fromTo 
                                                (from (to)) 
                                                (to ("Bar" (next)))
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (then 
                        (if 
                            (not (all DiceUsed)) 
                            (moveAgain)
                        )
                    )
                )
            )
        )
        
        (end 
            (if ("AllPieceEscaped")
                (result Mover Win)
            )
        )
    )
)

//-------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Jacquet is a European Tables game played in nineteenth century France.")
        (rules "2x12 board, divided in half, where the spaces are rendered as points. Fifteen pieces per player. Pieces begin on the leftmost point on their opponent's side of the board. Two six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. When doubles are thrown, the player plays the throw twice. Each player progresses in a clockwise direction around the board, to bear off upon reaching the final point on the opponent's side of the board. One piece must reach the opposite quadrant of the board from where it entered before the player may place more than one piece on a point. When a piece lands on a point occupied by a single opponent's piece, the opponent's piece is removed from the board and must enter the board again. When all of the player's pieces have entered the opposite quadrant from the one in which they entered, they may begin to bear off. The first player to bear off all their pieces wins.")
        (source "Moulidars 1840: 183-184.")
        (id "1122")
        (version "1.3.4")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in France, around 1840.")
        }
    )
    
    (graphics {
        (board Style backgammon)
        (stackType 0 Backgammon)
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)
