(define "NotAnEnemy" (not ("IsEnemyAt" (to))))

(define "NextSiteFrom" ("NextSiteOnTrack" (pips) from:#1))

(define "AllPieceInHome" ("AllOwnedPiecesIn" (sites Mover "Home")))

(define "RemoveAPiece" (move Remove (site)))

(define "DieNotUsedAndNoEscapeAndNotOffTheTrack" 
    (and { 
        ("DieNotUsed") 
        ("IsNotOffBoard" ("NextSiteFrom" (from))) 
        ("IsNotEndTrack" ("NextSiteFrom" (from)))
    })		
)

(define "MoveAPiece" 
    (move 
        (from #1)
        (to 
            ("NextSiteFrom" #1) 
            if:("NotAnEnemy") 
        )
    )
)

(define "MoveAPieceIf" 
    (forEach Die 
        replayDouble:True 
        if:#1
        (if (or {
                (not ("InTheStartingPosition"))
                (and ("InTheStartingPosition") ("Not14PiecesInThePosition"))
                ("PieceInOpponentSide")
            })
            ("MoveAPiece" #2)
        )
    )		
)

(define "AllPieceEscaped" (no Pieces Mover))

(define "NumPiece" (size Stack in:(sites Occupied by:#1)))

(define "CanEscape" ("IsEndTrack" ("NextSiteFrom" (site))))

(define "NotEmptyAndNotOffTheBoard" 
    (and 
        (is Occupied (site))
        ("IsNotOffBoard" ("NextSiteFrom" (site)))
    )
)

(define "SetScoreOf" 
    (score 
        #1 
        (if (is Mover #1) 
            (if 
                (= ("NumPiece" #2) 15) 
                2 
                1
            ) 
            0
        )
    )
)

(define "HaveAPieceAndCanEscape" 
    (and 
        ("IsFriendAt" (site)) 
        (< ("NextSiteOnTrack" (pips) from:(site)) 0)
    )
)

(define "PieceInOpponentSide" (not ("AllOwnedPiecesIn" (sites Mover "Side"))))

(define "InTheStartingPosition" (= (from) (mapEntry (mover))))

(define "Not14PiecesInThePosition" (!= (topLevel at:(from)) 13))

//-----------------------------------------------------------------------------

(game "Fevga"  
    (players 2)  
    (equipment { 
        ("BackgammonBoard" ("BackgammonTracksSameDirectionOppositeCorners" End)) 
        (dice num:2)
        (regions "Home" P1 { 20..25 }) 
        (regions "Home" P2 { 7..12 }) 
        (regions "Side" P1 { 0..12 }) 
        (regions "Side" P2 { 13..25 }) 
        (map {(pair 1 0) (pair 2 25)}) // The starting positions of each player.
        (piece "Disc" Each ("MoveAPieceIf" "DieNotUsedAndNoEscapeAndNotOffTheTrack" (from))) 
    })
    
    (rules
        (start { 
            (place Stack "Disc1" 0 count:15)
            (place Stack "Disc2" 25 count:15)
        })
        
        (play 
            ("RollEachNewTurnMove"
            	(if "AllPieceInHome"
                    (forEach Die 
                        replayDouble:True 
                        if:("DieNotUsed")
                        (forEach Site 
                            (sites Mover "Home")
                            (if ("NotEmptyAndNotOffTheBoard") 
                                (if ("CanEscape")
                                    ("RemoveAPiece")
                                    ("MoveAPiece" (site))
                                )
                            )
                            noMoveYet:(firstMoveOnTrack "Track" Mover
                                (if "HaveAPieceAndCanEscape"
                                    "RemoveAPiece"
                                )
                            )
                            (then "ReplayNotAllDiceUsed")
                        )
                    )
                    (max Distance "Track" Mover
                        (forEach Piece top:True
                            (then "ReplayNotAllDiceUsed")
                        )
                    )
                )
            )
        )
        
        (end 
            (if ("AllPieceEscaped") 
                (byScore { 
                    ("SetScoreOf" P1 P2)
                    ("SetScoreOf" P2 P1)
                })
            )
        )
    )
)

//-------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Fevga is a game similar to Backgammon that is played in Greece and the eastern Mediterranean. It is typically played along with Portes and Plakoto as Tavli.")
        (aliases {"Moultezim"})
        (rules "Played on a standard Backgammon board. Each player has fifteen pieces, which all begin on the leftmost point on each player's side of the board. Both players move anti-clockwise around the board. Both players roll one die, and the player with the higher number plays first. If it is played after another game, the winner of the previous game goes first. The player rolls two die and begins play with that roll. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. When doubles are thrown, they are played twice. If a player can only play one number but not both, they must play the higher number, if possible. To begin, each player may only move one piece, until it passes the opponent's starting point, after which any pieces can be moved. Pieces may only be moved to an empty point or one occupied by another piece belonging to the player. Players may not block all six points in either player's starting quadrant. If a player has blocked six points in a row, and the opponent has moved all of their pieces to the point adjacent to the blocked points, the player must move to unblock one of the points. Once a player has moved all of their pieces into the final six points of their track, they may begin bearing off. They do so by rolling a number equal to the number of points left in the track, plus one. If they cannot bear off, they must make a legal move with a piece that cannot bear off. If no such move is available, then they must bear off the piece which is furthest from the goal. The first player to bear off all their pieces wins the game and gets one point. If the winner bears off all their pieces before the opponent bears off any, they get two points. ")
        (source "<a href=\"https://archive.org/details/fevga/mode/2up\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />archive.org</a>")
        (id "175")
        (version "1.3.4")
        (classification "board/race/escape")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style backgammon)
        (stackType Backgammon)
    })
    
    (ai
        "Fevga_ai"
    )
)
