(define "SiteToMoveOnTrack" (trackSite Move steps:(pips)))

(define "RemoveAPiece" (move Remove (from) ) )

(define "CaptureEnemyPiece"
    (apply
        if:(is Enemy (who at:(to)))
        (fromTo (from (to)) (to (handSite Next)))
    )
)

(define "DieNotUsed" (!= (pips) 0))

//------------------------------------------------------------------------------

(game "Pareia de Entrada"
    (players 2)
    (equipment {
        (board
            (merge {
                (rectangle 1 6)
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {24 0..5 6..11 23..18 17..12} P1 directed:True)
            (track "Track2" {25 0..5 6..11 23..18 17..12} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:<Dice:num>)
        (hand Each)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (= "SiteToMoveOnTrack" Off)
                    "RemoveAPiece"
                    (if (or
                            (not (is Enemy (who at:"SiteToMoveOnTrack")))
                            (and
                                (is Enemy (who at:"SiteToMoveOnTrack"))
                                (= 1 (size Stack at:"SiteToMoveOnTrack"))
                            )
                        )
                        (move
                            (from)
                            (to
                                "SiteToMoveOnTrack"
                                "CaptureEnemyPiece"
                            )
                        )
                    )
                )
            )
        )
    })
    (rules
        (start {
            (place Stack "Disc1" (handSite P1) count:15)
            (place Stack "Disc2" (handSite P2) count:15)
        })
        (play
            (do (if (or (is Pending) (not "SameTurn")) (roll))
                next:(or (forEach Piece top:True) (forEach Piece container:(mover) top:True)
                    (then
                        (and
                            (if (not (all DiceUsed))
                                (moveAgain)
                            )
                            (if (and (all DiceUsed) (all DiceEqual)) (and (set Pending) (moveAgain)))
                        )
                    )
                )
            )
        )
        (end (if (no Pieces Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Dice" <Dice> args:{ <num>}
    {
    (item "2" <2> "The game involves 2 dice")
    (item "3" <3> "The game involves 3 dice")*
    }
)

//------------------------------------------------------------------------------

(rulesets {
    (ruleset "Ruleset/Pareia de Entrada (Described)" {
        "Dice/3"
        }
        variations:{"Dice/2"}
    )*
})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Pareia de Entrada is a race game from medieval Spain that is related to other European Tables games. It is one of the earliest forms of Tables that employs a second turn when doubles are rolled on the dice.")
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Fifteen pieces per player. Two or three dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. Both players enter their pieces into the same quadrant of the board, and move pieces along a horseshoe-shaped track around the board toward the quadrant on the opposite side of the board.  When a piece lands on a space occupied by a single piece of the opponent, the opponent's piece is removed from the board and must enter again as before. When all of the dice present the same number, the player receives another turn. The player to bear off all of their pieces first wins.
        ")
        (source "Libro de los Juegos 76-77.")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Spain, around 1283.")
        }
    )

    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType 0 Backgammon)
        (stackType 1 Count)
        (stackType 2 Count)
    })

)

