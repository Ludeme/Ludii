(define "NextSiteFrom" ("NextSiteOnTrack" #2 from:#1))

(define "NoGoBack" ("NoSites" (difference (sites #1 "StartQuadrant") (union (sites {(to)}) (sites Occupied by:Mover)))))

(define "GoBack" 
    (if (and (!= (to) (regionSite (sites #1 "StartQuadrant") index:0))
            (not ("IsFriendAt" (regionSite (sites #1 "StartQuadrant") index:0)))
        )
        (regionSite (sites #1 "StartQuadrant") index:0)
        (if (and (!= (to) (regionSite (sites #1 "StartQuadrant") index:1))
                (not ("IsFriendAt" (regionSite (sites #1 "StartQuadrant") index:1)))
            )
            (regionSite (sites #1 "StartQuadrant") index:1)
            (if (and (!= (to) (regionSite (sites #1 "StartQuadrant") index:2))
                    (not ("IsFriendAt" (regionSite (sites #1 "StartQuadrant") index:2)))
                )
                (regionSite (sites #1 "StartQuadrant") index:2)
                (if (and (!= (to) (regionSite (sites #1 "StartQuadrant") index:3))
                        (not ("IsFriendAt" (regionSite (sites #1 "StartQuadrant") index:3)))
                    )
                    (regionSite (sites #1 "StartQuadrant") index:3)
                    (if (and (!= (to) (regionSite (sites #1 "StartQuadrant") index:4))
                            (not ("IsFriendAt" (regionSite (sites #1 "StartQuadrant") index:4)))
                        )
                        (regionSite (sites #1 "StartQuadrant") index:4)
                        (if (and (!= (to) (regionSite (sites #1 "StartQuadrant") index:5))
                                (not ("IsFriendAt" (regionSite (sites #1 "StartQuadrant") index:5)))
                            )
                            (regionSite (sites #1 "StartQuadrant") index:5)
                            (regionSite (sites #1 "StartQuadrant") index:6)
                        )
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Fallas"
    (players 2)
    (equipment {
        ("TableBoard" "TableTracksOpposite")
        (dice d:6 num:3)
        (piece "Disc" Each
            (forEach Die 
                if:("DieNotUsed")
                (if ("IsOffBoard" ("NextSiteFrom" (from) (pips)))
                    (move Remove (from))
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NoEnemyOrOnlyOne" (to))
                            (apply 
                                (if ("IsEnemyAt" (to))
                                    (if ("NoGoBack" Next)
                                        (and
                                            (remove (to))
                                            (trigger "StartQuadrantFull" Next)
                                        )
                                        (fromTo 
                                            (from (to))
                                            (to ("GoBack" Next))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
                (then "ReplayNotAllDiceUsed")
            )		
        )
        (regions "StartQuadrant" P1 (sites {6..11}))
        (regions "StartQuadrant" P2 (sites {18..23}))
    })
    (rules 
        (start {
            (place Stack "Disc1" 7 count:13)
            (place Stack "Disc1" 11 count:2)
            (place Stack "Disc2" 19 count:13)
            (place Stack "Disc2" 23 count:2)
        })
        (play ("RollEachNewTurnMove" (forEach Piece)))
        (end {
            ("EscapeWin")
            (if (is Triggered "StartQuadrantFull" P1) (result P2 Win))
            (if (is Triggered "StartQuadrantFull" P2) (result P1 Win))
            (if (and ("NewTurn") (no Moves Mover)) (result Mover Loss))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Fallas is a race game from Spain that is related to other European Tables games. It is documented in Alfonso X's Libro de los Juegos.")
        (aliases {"Faille" "Fails"})
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Fifteen pieces per player. Play begins with thirteen pieces on the seventh point of the track, and two pieces on the twelfth point. Three six-sided dice. Players move pieces according to the value of each individual die. Play moves around the board through starting from the quadrant where the pieces begin, through the one where the opponent's pieces begin, and then through the remaining quadrant on the opponent's side of the board, where the pieces are borne off the board. When a piece lands on a spot occupied by a single piece of the opponent, the opponent's piece is sent back to its starting quadrant. If a player's piece is sent back to start, but all of the spaces in their starting quadrant are occupied by the opponent's pieces, the player loses automatically. Also, any roll that allows the player to only move to spaces occupied by the opponent results in an automatic loss for the player. Otherwise, the first player to bear off all their pieces wins.")
        (source "Libro de los Juegos 74-75.")
        (id "727")
        (version "1.3.13")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Spain, from around 1283 to 1399.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType Backgammon)
    })
    
)
