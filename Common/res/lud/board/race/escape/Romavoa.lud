(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #1))

(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "RemoveAPiece" (move Remove (from) level:(level)))

(define "Movement" 
    (if (if 
            (not (is In (from) (sites Mover "LastSide")))
            True
            (or {
                (= ("ThrowValue") 1)
                (= ("ThrowValue") 2)
                (= ("ThrowValue") 3)
            })
        )
        (or
            (if (= 14 ("ThrowValue"))
                (if ("IsEndTrack" ("SiteToMoveOnTrack" 1))
                    "RemoveAPiece"
                    (move 
                        (from (from) level:(level))
                        (to 
                            ("SiteToMoveOnTrack" 1) 
                            ("HittingStackCapture" (handSite (who at:(to) level:(level))))
                        )
                    )
                )
            )
            (if ("IsEndTrack" ("SiteToMoveOnTrack" ("ThrowValue")))
                "RemoveAPiece"
                (move 
                    (from (from) level:(level))
                    (to 
                        ("SiteToMoveOnTrack" ("ThrowValue")) 
                        ("HittingStackCapture" (handSite (who at:(to) level:(level))))
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Romavoa"
    (players 2)
    (equipment {
        ("KintsBoard"
            {
            (track "Track1" "46,5,SSE,N,E,S,W,NNW2,End" P1 directed:True)
            (track "Track2" "47,41,NNW,W,S,E,N,SSE2,End" P2 directed:True)
            }
        )
        (regions "AllSites" (sites Board))
        (regions "LastSide" P1 (sites {6..9 23..27}))
        (regions "LastSide" P2 (sites {27 33..40}))
        (dice d:2 facesByDie:{{0 4} {0 6} {0 14} {0 15}} num:4)
        (piece "Marker" Each ("Movement"))
        (hand Each)
        (map "Throw" {
            (pair 0 10) (pair 39 5) (pair 24 3) (pair 25 3) (pair 35 3) (pair 33 3)
            (pair 10 2) (pair 18 2) (pair 19 2) (pair 20 2) (pair 21 2) (pair 29 2)
        })
    })
    (rules 
        (start {
            (place Stack "Marker1" (handSite P1) count:2)
            (place Stack "Marker2" (handSite P2) count:2)
        })
        (play ("RollMove" (or (forEach Piece) (forEach Piece container:(mover)))))
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Romavoa is a race game that seems to have been attested among the Tarahumara people of the Sierra Madre Occidental in Mexico since the eighteenth century. ")
        (aliases {"Romavóa" "Quinze" "Romayá" "Quince" "Domabóa" "Deétali" "Domaláka"})
        (rules "Played on a board with 36 holes arranged in a square, with a gap in the center of each side. There are two semi circles of five holes on the outside of two opposite corners of the board. Two pieces per player. Four stick dice are used, each with one marked side. Each die is marked differently for a different value: 4, 6, 14, 15. The throws are as follows: All marked sides up = 5; three marked sides up = 3; two marked sides up = 2, one marked side up = the value of the marked side; zero marked sides up = 10. Throws of 14 may instead be played as a throw of 1, whenever the player chooses. Players’ pieces enter on the branching arcs. Players move in opposite directions around the board, and then back down the arc in which they started, moving their pieces off the board. If a player lands on the same spot as the opponent, the opponent’s piece is removed from the board and must re-enter. A player's piece is allowed to occupy the same space as that player's other piece. On the last side of the rectangle before completing a circuit of the board, only throws of 1, 2, or 3 are used. Players must make an exact throw to get a piece off the board. The first player to remove their pieces from the board wins.
        ")
        (source "Bennett and Zingg 1935: 343-344.")
        (id "173")
        (version "1.3.5")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Mexico, from around 1791 to 1931.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (show Symbol "disc" "AllSites" Vertex fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
        (show Symbol "stick" Vertex 3 fillColour:(colour White) edgeColour:(colour Black) scale:0.3)
        (show Symbol "stick" Vertex 43 fillColour:(colour White) edgeColour:(colour Black) scale:0.3)
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (piece Scale "Marker" 0.5)
        (piece Rename piece:"Die1" "KintsDice1")
        (piece Rename piece:"Die2" "KintsDice2")
        (piece Rename piece:"Die3" "KintsDice3")
        (piece Rename piece:"Die4" "KintsDice4")
        (piece Style "Die" NativeAmericanDice)
    })
    
)
