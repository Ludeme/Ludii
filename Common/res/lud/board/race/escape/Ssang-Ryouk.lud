(define "NextSiteFrom" ("NextSiteOnTrack" #2 from:#1))

(define "EnterPieces"
    (if (is Occupied (handSite Mover))
        (forEach Die
            if:("DieNotUsed")
            (move 
                (from (handSite Mover))
                (to 
                    ("NextSiteFrom" (handSite Mover) (pips))
                    if:(< (size Stack at:(to)) 2)
                    ("HittingCapture" (handSite Next))
                )
            )
            (then ("ReplayNotAllDiceUsed"))
        )
    )
)

(define "AllPiecesInFinalQuadrant" ("AllOwnedPiecesIn" (sites Mover "FinalQuadrant")))

//------------------------------------------------------------------------------

(game "Ssang-Ryouk"
    (players 2)
    (equipment {
        (board 
            (add
                (remove
                    (merge (rectangle 3 6) (shift 7 0 (rectangle 3 6))) 
                    vertices:{6 7 8 9 10 11 24 25 26 27 28 29}
                )
                edges:{{11 18} {5 12} {0 6} {1 7} {2 8} {3 9} {4 10} {5 11} {12 18} {13 19} {14 20} {15 21} {16 22} {17 23}}
            )
            { 
            (track "Track1" "24,17,W,N,E,End" P1 directed:True)
            (track "Track2" "25,23,W,S,E,End" P2 directed:True)
            } 
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (= End ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NoEnemyOrOnlyOne" (to))
                            ("HittingCapture" (handSite Next))
                        )
                    )
                )
                (then ("ReplayNotAllDiceUsed"))
            )
        )
        (hand Each)
        (regions "FinalQuadrant" P1 (sites {18..23}))
        (regions "FinalQuadrant" P2 (sites {12..17}))
    })
    (rules 
        (start {
            (place Stack "Disc1" 6 count:5) (place Stack "Disc1" 4 count:3) 
            (place Stack "Disc1" 12 count:5) (place Stack "Disc1" 23 count:2)
            (place Stack "Disc2" 18 count:5) (place Stack "Disc2" 10 count:3) 
            (place Stack "Disc2" 0 count:5) (place Stack "Disc2" 17 count:2)
        })
        (play 
            ("RollEachNewTurnMove"
                (priority {
                    ("EnterPieces")
                    (if (is Empty (handSite Mover)) (max Distance (forEach Piece top:True)))
                })
            )
        )
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Ssang-Ryouk (\"double sixes\") is a race game played in Korea.")
        (rules "The game is played on a board with twelve points on either side. The points form a continuous track in a horseshoe shape; each player progresses in opposite directions (one from their bottom right to the top right, the other from their bottom left to their top left). Each player has 15 pieces. The starting position is as such, numbering the points from the origin of each player's track: Point six: five pieces Point 8: three pieces Point 13: five pieces Point 24: two pieces. Players move according to the number on each die by moving one piece the number on one die and other the number on the other die, or by moving one piece the total number of both die. Players cannot end their move on a point with multiple opposing pieces. If a player ends the turn on a point with one opposing piece, that piece is placed in the middle of the board (not on a point) and must reenter the board according the the next die roll, counting the origin point as a move of 1. They cannot reenter on a point with two or more pieces. No other pieces can move until all of the pieces belonging to that player are removed from the center. When all of a player's pieces are on their final 6 points, they may start removing pieces from the board. They can do so by rolling a 6 to move from the 6th point, and so on down to 1. Players must use all available moves presented by the dice. The first player to remove all of their pieces wins.
        ")
        (source "Culin 1895: 79-81.")
        (id "1045")
        (version "1.3.12")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Korea, around 1895.")
        }
    )
    
    (graphics {    
        (show Edges Hidden) 
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:1.1 scaleY:0.01 offsetX:0 offsetY:0.13)
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:1.1 scaleY:0.01 offsetX:0 offsetY:-0.13)
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:0.007 scaleY:0.423 offsetX:-0.55 offsetY:0)
        (board Background image:"rectangle.svg" fillColour:(colour Black) edgeColour:(colour Black) scaleX:0.007 scaleY:0.423 offsetX:0.55 offsetY:0)
        (board Background image:"flower.svg" fillColour:(colour Black) edgeColour:(colour Black) scale:0.06)
        (board Background image:"FlowerHalf1.svg" fillColour:(colour Black) edgeColour:(colour Black) scale:0.06 offsetY:0.075)
        (board Background image:"FlowerHalf2.svg" fillColour:(colour Black) edgeColour:(colour Black) scale:0.06 offsetY:-0.075)
        
    })
    
)
