(define "SiteToMoveOnTrack" (trackSite Move steps:(pips)))

(define "RemoveAPiece" (move Remove (from) ) )

(define "DieNotUsed" (!= (pips) 0))

(define "Bar" (handSite Mover))

(define "BarEmpty" (= (what Cell at:"Bar") 0))

(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:(pips)
    )
)

(define "CaptureEnemyPiece"    
    (apply 
        if:(is Enemy (who at:(to))) 
        (fromTo 
            (from (to)) 
            (to (handSite Next))
        )
    )
)

(define "NotEnemyOrOnlyOne"    
    (or 
        (and 
            (is Enemy (who at:(to))) 
            (= (topLevel at:(to)) 0)
        ) 
        (not (is Enemy (who at:(to))))
    )
)

(define "MoveAPiece" 
    (move 
        (from #1)
        (to 
            ("NextSiteFrom" #1) 
            if:("NotEnemyOrOnlyOne") 
            ("CaptureEnemyPiece")
        )
    )
)

(define "MoveAPieceIf" 
    (forEach Die 
        replayDouble:True 
        if:#1
        ("MoveAPiece" #2)
    )		
)

//------------------------------------------------------------------------------

(game "Quinze Tablas"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 6) 
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {24 0..5 6..11 23..18 17..12 End} P1 directed:True)
            (track "Track2" {25 12..17 18..23 11..6 5..0 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:3)
        (hand Each)
        (piece "Disc" Each
            (forEach Die 
                if:("DieNotUsed")
                (if (= "SiteToMoveOnTrack" End)
                    "RemoveAPiece"
                    (if (!= "SiteToMoveOnTrack" Off)
                        (if (or 
                                (is Empty "SiteToMoveOnTrack")
                                (and 
                                    (or 
                                        (is Friend (who at:"SiteToMoveOnTrack")) 
                                        (is Enemy (who at:"SiteToMoveOnTrack"))
                                    )
                                    (= 1 (size Stack at:"SiteToMoveOnTrack"))
                                )
                            )
                            (move 
                                (from) 
                                (to 
                                    "SiteToMoveOnTrack" 
                                    "CaptureEnemyPiece" 
                                )
                            )
                        )
                    )
                )
            )
        )
    })
    (rules 
        (start {
            (place Stack "Disc1" (sites {0..6}) count:2)
            (place Stack "Disc1" 7 count:1)
            (place Stack "Disc2" (sites {12..18}) count:2)
            (place Stack "Disc2" 19 count:1)
        })
        (play 
            (do 
                (if (not "SameTurn") (roll)) 
                next:(if ("BarEmpty")
                    (forEach Piece top:True)
                    ("MoveAPieceIf" ("DieNotUsed") ("Bar"))
                    (then 
                        (if (not (all DiceUsed))
                            (moveAgain)
                        )
                    )
                )
            )
        )
        (end {
            (if (no Pieces P1) (result P1 Win))
            (if (no Pieces P2) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Quinze Tablas is a race game related to other European Tables games. It is documented in the Libro de los Juegos of Alfonso X.")
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Fifteen pieces per player. Pieces begin on one half of a player's side of the board (the half to the left of one player, and to the right of the other player), two per space, then two on the next space and one on the space following that one. Three six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. Pieces move in a track the long way around the board toward the portion where the opponent begins. No more than two pieces can occupy a space at a time. When a piece lands on a space occupied by a single piece of the opponent, the opponent's piece is removed from the board. The opponent must then reenter this piece on the board onto an open point according to the throw of the dice, from the original starting quadrant on the board. Players attempt to bear off their pieces by rolling the exact number of spaces left on the board, plus one. The first player to bear off all their pieces wins.")
        (source "Libro de los Juegos 73-74.")
        (id "726")
        (version "1.3.3")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Spain, around 1283.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType 0 Backgammon)
        (stackType 1 Count)
        (stackType 2 Count)
    })
)
