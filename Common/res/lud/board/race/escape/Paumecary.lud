(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"    
    (or 
        (and 
            (is Enemy (who at:(to))) 
            (= (topLevel at:(to)) 0)
        ) 
        (not (is Enemy (who at:(to))))
    )
)

//------------------------------------------------------------------------------

(game "Paumecary"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 6) 
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {24..18 End} P1 directed:True)
            (track "Track2" {25 23..18 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (or
                (forEach Die 
                    replayDouble:True 
                    if:("DieNotUsed")
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NotEnemyOrOnlyOne")
                            (apply 
                                (if (is Enemy (who at:(to)))
                                    (fromTo 
                                        (from (to))
                                        (to (handSite Next))
                                    )
                                )
                            )
                        )
                    )
                    (then 
                        (if (not (all DiceUsed))
                            (moveAgain)
                        )
                    )
                )
                (if (is Empty (handSite Mover))
                    (forEach Die 
                        replayDouble:True 
                        if:(and
                            ("DieNotUsed")
                            (= End ("NextSiteFrom" (from) (pips)))
                        )
                        (move Remove (from))
                        (then 
                            (if (not (all DiceUsed))
                                (moveAgain)
                            )
                        )
                    )
                )
            )		
        )
        (hand Each)
    })
    (rules 
        (start {
            (place Stack "Disc1" (handSite P1) count:15)
            (place Stack "Disc2" (handSite P2) count:15)
        })
        (play 
            (do 
                (if (not "SameTurn") (roll))
                next:(or (forEach Piece) (forEach Piece container:(mover)))
            )
        )
        (end (if (no Pieces Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Paumecary is a European Tables game from fourteenth century England. Play is confined to one quarter of a typical board. Customarily, when a player wins, the game continues, with the winner helping the loser bear off their remaining pieces, smacking the loser's hands for each piece borne off.")
        (rules "Played on a board with 12 points per side, divided in half. Play occurs only in the top right quadrant of the board. Fifteen pieces per player. Two six-sided dice. A double throw grants the player another throw. Players enter their pieces according to the throws of the dice, and they must use the entire value of a die to place or to move a piece. When all of a player's pieces are on the board, they may bear off their pieces with throws equal to the number of remaining spaces, plus one. If a piece lands on a point with a single piece belonging to the opponent, the opponent's piece is removed from the board and must re-enter. The player who bears off all their pieces first wins.")
        (source "Murray 1951: 120.")
        (id "814")
        (version "1.3.4")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in England, from around 1300 to 1399.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType 0 Backgammon)
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)

