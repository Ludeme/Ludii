(define "NextSiteFrom" ("NextSiteOnTrack" (pips) from:#1))

(define "NextSiteFromDist6" ("NextSiteOnTrack" 6 from:#1))

(define "RemoveAPiece" (move Remove (site)))

(define "Bar" (mapEntry (mover)))

(define "NotEnemyOrOnlyOne"  
    (or 
        (and 
            ("IsEnemyAt" (to)) 
            ("IsSingletonStack" (to))
        ) 
        (and
            (if 
                (not (is In (to) (sites Mover))) 
                True
                (is Empty (to))
            )
            (not ("IsEnemyAt" (to)))
        )
    )
)

(define "AllPieceEscaped" (no Pieces Mover))

(define "CanEscape" ("IsOffBoard" #1))

//-----------------------------------------------------------------------------

(game "Ludus Anglicorum" 
    (players 2) 
    (equipment { 
        ("BackgammonBoard" 
            {
            (track "Track1" {6 0..5 7..12 25..20 18..12} P1 directed:True)
            (track "Track2" {19 12..18 20..25 12..7 5..0} P2 directed:True)
            } 
        ) 
        (dice num:3)
        (map {(pair 1 19) (pair 2 6)}) // Bar
        (piece "Disc" Each) 
        (regions P1 { 0..5 }) // P1 Home
        (regions P2 { 13..18 }) // P2 Home
    })
    
    (rules
        (start { 
            (place Stack "Disc1" 0 count:15)
            (place Stack "Disc2" 13 count:15)
        })
        
        (play 
            ("RollEachNewTurnMove"
                (forEach Die
                    if:("DieNotUsed")
                    (forEach Site
                        (sites Occupied by:Mover)
                        (if ("CanEscape" ("NextSiteFrom" (site))) 
                            ("RemoveAPiece")
                            (move 
                                (from (site))
                                (to ("NextSiteFrom" (site))
                                    if:("NotEnemyOrOnlyOne")
                                    ("HittingCapture" ("Bar"))
                                )
                            )
                        )
                    )
                    (then ("ReplayNotAllDiceUsed"))
                )
            )
        )
        
        (end 
            (if ("AllPieceEscaped")
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <ludus> }
    {
    (item "Ludus Anglicorum" <> "")* 
    }
)

//-------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Ludus Anglicorum (Described)" {
        "Variant/Ludus Anglicorum"
    })*
    
    (ruleset "Ruleset/Opponent's Choice (Described)" {
        
    })
    
    (ruleset "Ruleset/Player's Choice (Described)" {
        
    })
    
    (ruleset "Ruleset/Throw of 6 (Described)" {
        
    })
    
})

//-------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The Ludus Anglicorum was a European Table game played in medieval England. It was described in a fourteenth century manuscript describing other similar games.")
        (useFor "Ruleset/Ludus Anglicorum (Described)" (rules "2x12 board, divided in half. Spaces on each side take the form of points. Fifteen pieces per player. Three six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. One player begins with all of their pieces on the space furthest to the left on their side, the other with their pieces on the point directly opposite it. Pieces move in opposite directions around the board, toward the point where the opponent's pieces begin, and bearing off the board from there. A player cannot have two of their pieces on the same spot in the starting quadrant. When a piece lands on the same space as a single opponent's piece, the opponent's piece is sent back to where it began. The first person to bear off all their pieces wins. "))
        (useFor "Ruleset/Ludus Anglicorum (Described)" (source "Murray 1951: 124."))
        (useFor "Ruleset/Ludus Anglicorum (Described)" (id "1116"))
        (useFor "Ruleset/Throw of 6 (Described)" (rules "Two six-sided dice. Each player receives an automatic throw of 6 on each turn, in addition to the throw presented by the dice."))
        (useFor "Ruleset/Throw of 6 (Described)" (source "Murray 1951: 124."))
        (useFor "Ruleset/Throw of 6 (Described)" (id "1117"))
        (useFor "Ruleset/Player's Choice (Described)" (rules "Two dice. The player chooses a number from 1 to 6 in place of a third die."))
        (useFor "Ruleset/Player's Choice (Described)" (source "Murray 1951: 124."))
        (useFor "Ruleset/Player's Choice (Described)" (id "1118"))
        (useFor "Ruleset/Opponent's Choice (Described)" (rules "Two dice. The opponent chooses a value from 1 to 6 for the player to move, in place of a third die."))
        (useFor "Ruleset/Opponent's Choice (Described)" (source "Murray 1951: 124."))
        (useFor "Ruleset/Opponent's Choice (Described)" (id "1119"))
        (source "Murray 1951: 124.")
        (id "1116")
        (version "1.3.5")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in England, around 1300.")
        }
    )
    
    (graphics {
        (board Style backgammon)
        (stackType Backgammon)
    })
    
)
