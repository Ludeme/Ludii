(define "SiteToMoveOnTrack" ("NextSiteOnTrack" (count Pips)))

(define "RemoveAPiece" (move Remove (from)))

(define "CaptureEnemyPiece" ("HittingCapture" (handSite (who at:(to))))) 

//------------------------------------------------------------------------------

(game "Kawade Kelia" 
    (players <Player:num>)
    (equipment {
        (board 
            (merge {
                (rectangle 1 12)
                (shift 3.5 3.25 (scale 1 1.5 (rotate 90 (rectangle 1 4))))
                (shift 0 7 (rectangle 1 12))
                (shift -5.5 12.5 (rotate 90 (rectangle 1 12)))
                (shift 2.5 10.5 (rotate 90 (rectangle 1 6)))
                (shift 5.5 12.5 (rotate 90 (rectangle 1 12)))
                (shift 0 18 (rectangle 1 11))
                (shift 1 11 (rotate 45 (rectangle 1 4)))
                (shift 5 13
                    (graph
                        vertices:{ { 0 0 } {0 1} {-0.94 0.26 } {-0.23 -0.44} } 
                        edges:{ {0 1} {1 2} {2 3} {3 0}} 
                    )
                )
            })
            {
            (track "Track1" "71,0,E5,N5,E,N,W,S,E5,N,WSW,SW,End" P1 directed:True)
            (track "Track2" "72,11,W6,N5,W,N,E,S,W6,N,WSW,SW,End" P2 directed:True)
            <Player:trackP3P4>
        })
        (piece "Paddle" Each
            (if ("IsEndTrack" "SiteToMoveOnTrack")
                "RemoveAPiece"
                (if ("IsNotOffBoard" "SiteToMoveOnTrack") 	 			
                    (if (or {
                            (is Empty "SiteToMoveOnTrack")
                            ("IsFriendAt" "SiteToMoveOnTrack")
                            (and 
                                ("IsEnemyAt" "SiteToMoveOnTrack")
                                (not (is In "SiteToMoveOnTrack" (sites "Protect")))
                            )
                        })
                        (move 
                            (from (from) level:(level))
                            (to 
                                "SiteToMoveOnTrack" 
                                "CaptureEnemyPiece"
                            )
                        )
                    )
                )
            )
        )
        ("StickDice" 6)
        (hand Each)
        (regions "Protect" (sites {5 17 19 27 41 57 46 62 63 34 35 51 52}))
    })
    (rules
        (start {
            (place Stack "Paddle1" (handSite P1) count:3)
            (place Stack "Paddle2" (handSite P2) count:3)
            <Player:startP3P4>
        })
        (play 
            ("RollMove"
                (or (forEach Piece) (forEach Piece container:(mover) top:True))
            )
        )
        (end {
            <Player:end>
        })
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <num> <startP3P4> <trackP3P4> <end> }
    {
    (item "2" <2> <> <> 
        <
        (if (no Pieces P1) (result P1 Win))
        (if (no Pieces P2) (result P2 Win))
        > 
    "The game involves 2 players.") 
    (item "4" <4> 
        <
        (place Stack "Paddle3" (handSite P3) count:3)
        (place Stack "Paddle4" (handSite P4) count:3)
        (set Team 1 {P1 P3})
        (set Team 2 {P2 P4})
        > 
        <
        (track "Track3" "73,0,E5,N5,E,N,W,S,E5,N,WSW,SW,End" P3 directed:True)
        (track "Track4" "74,11,W6,N5,W,N,E,S,W6,N,WSW,SW,End" P4 directed:True)
        > 
        <
        ("EscapeTeamWin")
        > 
    "The game involves 4 players.")* 
    }
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Kawade Kelia is a race game that was played in Sri Lanka in the late nineteenth century.")
        (aliases {"Kawada Keliya"})
        (rules "One horizontal row of eleven squares, the central square marked with an X. From this central square, extending vertically, eleven more squares, making a row of twelve in total. The fifth square and the final square in this row are marked with an X. An arc of five squares extend left and down from the final square of the vertical row. From the fifth row of the vertical row, a square with twelve squares per side. The corner squares are marked with an X, the central two squares of the left, right, and top side are marked with one X across both squares. Two or four players; when there are four players they play on two teams of two. Three cowrie shells per player, used as pieces. Six cowrie shells used as dice; the throws = the number of mouths that land face up. Players move their pieces onto the board and around the circuit according to the throws of the cowries. Teams start on opposite ends of the bottom row of squares, up the vertical line to the square, in opposite directions around the square, then up the remaining part of the vertical row and down the arc, and off the board. When a piece lands on a space occupied by an opponent's piece, the opponent's piece is sent back and must enter the board again. Pieces are safe from being sent back if they rest on a marked square. The first team to remove all of their pieces from the board wins. ")
        (source "Culin 1898: 851.")
        (id "976")
        (version "1.3.5")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Sri Lanka, around 1898.")
        }
    )
    (graphics {
        (show Line {{5 19} {18 6} {29 50} {33 51} {90 85} {91 84} {34 46} {45 35} {43 57} {56 44} {112 102} {113 101} {79 67} {78 68} {126 119} {117 128} {72 63} {74 61} {95 108} {106 97}} (colour Black) scale:2)
        (piece Scale "Paddle" 0.5)
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
)
