(define "SiteToMoveOnTrack" (trackSite Move steps:(pips)))

(define "RemoveAPiece" (move Remove (from) ) )

(define "DieNotUsed" (!= (pips) 0))

//------------------------------------------------------------------------------

(game "Laquet"
    (players 2)
    (equipment {
        (board
            (merge {
                (rectangle 1 6)
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            (track "Track1" {11..6 5..0 12..17 18..23} directed:True)
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (= "SiteToMoveOnTrack" Off)
                    "RemoveAPiece"
                    (if (not (is Enemy (who at:"SiteToMoveOnTrack")))
                        (move
                            (from)
                            (to
                                "SiteToMoveOnTrack"
                            )
                        )
                    )
                )
            )
        )
    })
    (rules
        (start {
            (place Stack "Disc1" 11 count:14)
            (place Stack "Disc1" 23)
            (place Stack "Disc2" (sites {7..10}) count:3)
            (place Stack "Disc2" 6 count:2)
            (place Stack "Disc2" 12)
        })
        (play
            (do
                (if (and (not (is Pending)) (not "SameTurn")) (roll))
                next:(if (can Move (forEach Piece))
                    (forEach Piece
                        top:True
                        (then
                            (if (not (all DiceUsed))
                                (moveAgain)
                            )
                        )
                    )
                    (move Pass (then (set Pending)))
                )
            )
        )
        (end (if (no Pieces Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Laquet is a race game from medieval Spain that is similar to other European Tables games. It is documented in the Libro de los Juegos of Alfonso X.")
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Fifteen pieces per player. One player places fourteen of their pieces on the first point of the table to their right closest to them, with the other piece on the point opposite it on the other side of the board. The other player places three pieces on each of the four  spots following the spot with the other player's fourteen, two on the spot following this, and one on the opposite end of the row of spots where the opponent's single piece is located. Two six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. Players move along the spaces of the board toward the spot where the first player's single piece is located, and from there they bear off. Pieces cannot be hit as in similar games. If a player cannot play the value on a die (or on both dice), the opponent may play it. The first player to bear off all of their pieces wins.")
        (source "Libro de los Juegos 78.")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Spain, around 1283.")
        }
    )

    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType Backgammon)
    })

)

