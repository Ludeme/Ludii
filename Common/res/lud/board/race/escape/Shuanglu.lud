(define "NextSiteFrom"
    (trackSite Move
        from:#1
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"
    (or
        (and
            (is Enemy (who at:(to)))
            (= (topLevel at:(to)) 0)
        )
        (not (is Enemy (who at:(to))))
    )
)

(define "EnterPieces"
    (if (is Occupied (handSite Mover))
        (forEach Die
            if:("DieNotUsed")
            (move
                (from (handSite Mover))
                (to
                    ("NextSiteFrom" (handSite Mover) (pips))
                    if:(< (size Stack at:(to)) 2)
                    (apply
                        (if (is Enemy (who at:(to)))
                            (fromTo
                                (from (to))
                                (to (handSite Next))
                            )
                        )
                    )
                )
            )

            (then
                (if (not (all DiceUsed))
                    (moveAgain)
                )
            )
        )
    )
)

(define "AllPiecesInFinalQuadrant"
    (no Pieces Mover in:(difference (sites Board) (sites Mover "FinalQuadrant")))
)

//------------------------------------------------------------------------------

(game "Shuanglu"
    (players 2)
    (equipment {
        (board
            (merge {
                (rectangle 1 6)
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {24 11..0 12..23 End} P1 directed:True)
            (track "Track2" {25 23..12 0..11 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (= End ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move
                        (from (from))
                        (to
                            ("NextSiteFrom" (from) (pips))
                            if:("NotEnemyOrOnlyOne")
                            (apply
                                (if (is Enemy (who at:(to)))
                                    (fromTo
                                        (from (to))
                                        (to (handSite Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then
                    (if (not (all DiceUsed))
                        (moveAgain)
                    )
                )
            )
        )
        (hand Each)
        (regions "FinalQuadrant" P1 (sites {18..23}))
        (regions "FinalQuadrant" P2 (sites {6..11}))
    })
    (rules
        (start {
            (place Stack "Disc1"  6 count:5) (place Stack "Disc1" 4 count:3)
            (place Stack "Disc1" 12 count:5) (place Stack "Disc1" 23 count:2)
            (place Stack "Disc2"  18 count:5) (place Stack "Disc2" 16 count:3)
            (place Stack "Disc2" 0 count:5) (place Stack "Disc2" 11 count:2)
        })
        (play
            (do (if (not "SameTurn") (roll))
                next:(priority {
                    ("EnterPieces")
                    (if (is Empty (handSite Mover)) (max Distance (forEach Piece top:True)))
                })
            )
        )
        (end (if (no Pieces Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Shuanglu is a race game known from East Asia that is similar to European Backgammon. References to it in Chinese sources date to the first millennium CE.")
        (aliases {"Shwan-Liu" "Ssang-Ryouk" "Sunoroku"})
        (rules "The game is played on a board with twelve points on either side. The points form a continuous track in a horseshoe shape; each player progresses in opposite directions (one from their bottom right to the top right, the other from their bottom left to their top left). Each player has 15 pieces. The starting position is as such, number the points from the origin of each player's track: Point six: five pieces Point 8: three pieces Point 13: five pieces Point 24: two pieces. Players move according to the number on each die by moving one piece the number on one die and other the number on the other die, or by moving one piece the total number of both die. Players cannot end their move on a point with multiple opposing pieces. If a player ends the turn on a point with one opposing piece, that piece is placed in the middle of the board (not on a point) and must reenter the board according the the next die roll, counting the origin point as a move of 1. They cannot reenter on a point with two or more pieces. No other pieces can move until all of the pieces belonging to that player are removed from the center. When all of a player's pieces are on their final 6 points, they may start removing pieces from the board. They can do so by rolling a 6 to move from the 6th point, and so on down to 1. Players must use all available moves presented by the dice. The first player to remove all of their pieces wins.
        ")
        (source "Culin 1895a: 499-500.")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in East Asia, around 1895.")
        }
    )

    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 0 Backgammon)
    })

)

