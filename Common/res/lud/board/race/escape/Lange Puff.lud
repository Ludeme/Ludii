(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"    
    (or 
        (and 
            (is Enemy (who at:(to))) 
            (= (topLevel at:(to)) 0)
        ) 
        (not (is Enemy (who at:(to))))
    )
)

(define "DoubleDieValue" (face 28))

(define "MoveOppositeDice" 
    (forEach Value (values Remembered "Opposite")
        (if (= Off ("NextSiteFrom" (from) (value)))
            (move Remove (from))
            (move 
                (from (from))
                (to 
                    ("NextSiteFrom" (from) (value))
                    if:("NotEnemyOrOnlyOne")
                    (apply 
                        (and
                            (if (is Enemy (who at:(to)))
                                (fromTo 
                                    (from (to))
                                    (to (handSite Next))
                                )
                            )
                            (forget Value "Opposite" (value))
                        )
                    )
                )
            )
            (then 
                (if (!= 0 (size Array (values Remembered "Opposite")))
                    (moveAgain)
                    (and { 
                        (set Var "PlayOpposite" 0) 
                        (set Pending)
                        (moveAgain)
                    })
                )
            )
        )
    )	
)

//------------------------------------------------------------------------------

(game "Lange Puff"
    (players 2)
    (equipment {
        (board (rectangle 2 13) 
            {
            (track "Track1" {26 25..20 18..13 0..5 7..12} P1 directed:True)
            (track "Track2" {27 25..20 18..13 0..5 7..12} P2 directed:True)
            } 
            use:Vertex
        ) 
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                replayDouble:(= 1 (value Player Mover))  
                if:("DieNotUsed")
                (if (= Off ("NextSiteFrom" (from) (pips)))
                    (move Remove (from))
                    (move 
                        (from (from))
                        (to 
                            ("NextSiteFrom" (from) (pips))
                            if:("NotEnemyOrOnlyOne")
                            (apply 
                                (if (is Enemy (who at:(to)))
                                    (fromTo 
                                        (from (to))
                                        (to (handSite Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then 
                    (if (not (all DiceUsed))
                        (moveAgain)
                        (if (= 1 (value Player Mover))
                            (if (and (all DiceEqual) (= 0 (size Array (values Remembered)))) 
                                (and 
                                    (moveAgain) 
                                    (set Var "PlayOpposite" 1)
                                )
                            )
                            (set Value Mover 1)
                        )
                    )
                )
            )			
        )
        (hand Each)
    })
    
    (rules 
        (start {
            (place Stack "Disc1" (handSite P1) count:15)
            (place Stack "Disc2" (handSite P2) count:15)
        })
        
        (play 
            (do (if (or (is Pending) (not "SameTurn")) (roll))
                next:(if (= 1 (var "PlayOpposite"))
                    (or { 
                        (forEach Piece ("MoveOppositeDice") top:True) 
                        (forEach Piece container:(mover) ("MoveOppositeDice") top:True)
                        (move Pass 
                            (then 
                                (and { 
                                    (forget Value "Opposite" All) 
                                    (set Var "PlayOpposite" 0) 
                                    (set Pending) 
                                    (moveAgain)
                                })
                            )
                        )
                    })
                    //(max Distance "Track" Mover 
                        (do (if (and { 
                                    (= 0 (count MovesThisTurn)) 
                                    (all DiceEqual)
                                    (= 1 (value Player Mover))
                                })
                                (and 
                                    (remember Value "Opposite" (- 7 ("DoubleDieValue"))) 
                                    (remember Value "Opposite" (- 7 ("DoubleDieValue")))
                                )
                            )
                            next:(or (forEach Piece top:True) (forEach Piece container:(mover) top:True))
                        )
                    //)
                )
            )
        )
        
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Lange Puff is a European Tables game played in Germany. Its rules have been documented since the nineteenth century, but it may be related to other games with similar names that are much older. ")
        (rules "2x12 board, with spaces as points, divided in two. Fifteen pieces per player. Two six-sided die. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. The lowest value of the dice must be played; if it cannot, the player does not move. When a player throws doubles, they play the values on each die, as well as the values on the opposite side of the dice (I.e., double 6 if double 1 is thrown, 5 if 2, 3 if 4). However, the values on the bottom can only be played if all of the top values are played. The first double throw is only played once, but every subsequent double throw is played twice, and the top values must be played twice before using the bottom values. Bottom values do not have to be played, or only some of the bottom values may be played if the player wishes. In addition, an extra throw is granted for the second and subsequent throws of doubles. Players enter their pieces onto the same section of the board, and proceed around the board to the point opposite the starting point, and then off the board. A player may not land on a point with two or more of the opponent's pieces. A player's piece may land on a point with a single opponent's piece. The opponent's piece is removed from the board and must enter again. The first player to remove all of their pieces from the board wins.")
        (source "Alvenslebem 1853: 406-408.")
        (id "1077")
        (version "1.3.4")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Germany, around 1853.")
        }
    )
    
    (graphics {
        (board Style backgammon)
        (stackType 0 Backgammon)
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)

