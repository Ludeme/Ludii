(define "ThrowValue" (mapEntry "Throw" (count Pips)))
(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #2 #1))

(define "IsSpecialThrow" (is In ("ThrowValue") (sites {10 25 30})))

//------------------------------------------------------------------------------

(game "Pachesi" 
    (players 4) 
    (equipment { 
        ("PachisiBoard" 
            {
            (track "Track1" "96,9,S,W,N,7,W,N,E,51,N,E,S,43,E,S,W,10,S,W1,N7,End" P1 directed:True)
            (track "Track2" "96,26,W,N,E,51,N,E,S,43,E,S,W,10,S,W,N,7,W,N1,W7,End" P2 directed:True)
            (track "Track3" "96,52,N,E,S,43,E,S,W,10,S,W,N,7,W,N,E,51,N,E1,S7,End" P3 directed:True)
            (track "Track4" "96,27,E,S,W,10,S,W,N,7,W,N,E,51,N,E,S,43,E,S1,W7,End" P4 directed:True)
            }
        )
        ("StickDice" 7)
        (piece "Pawn" Each
            (if ("IsEndTrack" ("SiteToMoveOnTrack" from:(from) ("ThrowValue")))
                (move Remove (from) level:(level))
                (if (and ("IsEndTrack" ("SiteToMoveOnTrack" from:(from) 1)) ("IsSpecialThrow"))
                    (move Remove (from) level:(level))
                    (move 
                        (from (from) level:(level))
                        (to
                            ("SiteToMoveOnTrack" from:(from) ("ThrowValue"))
                            if:(not (and (is In (to) (sites "SafeSites")) ("IsEnemyAt" (to))))
                            ("HittingStackCapture" (centrePoint))
                        )
                    )
                )
            )
        )
        (hand Each)
        (regions "SafeSites" (sites {55 63 65 34 15 47 94 84 86 19 38 3}))
        (map "Throw" {(pair 0 7) (pair 1 10) (pair 2 2) (pair 3 3) (pair 4 4) (pair 5 25) (pair 6 30) (pair 7 12)})
    }) 
    (rules 
        (start {
            (place Stack "Pawn1" (handSite P1) count:4)
            (place Stack "Pawn2" (handSite P2) count:4)
            (place Stack "Pawn3" (handSite P3) count:4)
            (place Stack "Pawn4" (handSite P4) count:4)
        })
        
        (play 
            ("RollMove"
                (or
                    (if (and (is Occupied (handSite Mover)) ("IsSpecialThrow"))
                        (move
                            (from (handSite Mover))
                            (to (centrePoint))
                        )
                    )
                    (forEach Piece)
                )
            )
        )
        
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Pachesi is a race game played on a cross-shaped board, of which there are several versions in South Asia. This version is played near Chitrakoot Dham in central India.")
        (rules "Four 3x8 rectangles, arranged in a cross. The couting from the bottom of each arm, the spaces marked are the fourth square in the outer rows and the first in the central row. Four pieces per player. Seven cowrie shells, used as dice. The throws are as follows: one mouth up = 10; two mouths up = 2; three mouth up = 3; four mouths up = 4; five mouths up. = 25; six mouths up = 30, seven mouths up = 12, zero mouths up = 7. Pieces begin in the large central space of the board, and then proceed down the central row of their arm, then clockwise around the board, back to the central row, and then to the central space again. Pieces may enter the board on throws of 10, 25, or 30. When a player's piece lands on a space that is occupied by an opponent's piece, the opponent's piece is sent back to start. Pieces are safe from being sent to start when they rest on a marked space. To move off the board, a player must make an exact throw to arrive in the central square. When resting on the final square before the central one, a throw of 10, 25, or 30 is required. The first player to place all of their pieces in the central square wins. ")
        (source "Humphries 1906: 117-119.")
        (id "895")
        (version "1.3.4")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin "This game was played in India, from around 1904 to 1905.")
        }
    )
    
    (graphics {
        (stackType site:96 Ring)
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (show Symbol "thinCross" {55 63 65 34 15 47 94 84 86 19 38 3} scale:0.9)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Scale "Pawn" 0.5)
    })
    
)
