(define "SiteToSelect" ("NextSiteOnTrack" (- (pips) 1) from:(mapEntry "StartTrack" (mover))))

(define "SiteToMoveOnTrack" ("NextSiteOnTrack" (pips)))

(define "RemoveAPiece" (move Remove (from)))

(define "PieceUnstacked" (= (state at:#1) 1))
(define "PieceStacked" (= (state at:#1) 0))

(define "Unstack" 
    (forEach Level (last To)
        (set State at:(last To) level:(level) 1)
    )
)

(define "Stack" 
    (forEach Level (last To)
        (set State at:(last To) level:(level) 0)
    )
)

//------------------------------------------------------------------------------

(game "Ofanfelling"
    (players 2)
    (equipment {
        ("BackgammonBoard"
            {
            (track "Track1" {7..12} P1 directed:True)
            (track "Track2" {18..13} P2 directed:True)
            }
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die 
                if:("DieNotUsed")
                (if ("IsOffBoard" "SiteToMoveOnTrack")
                    "RemoveAPiece"
                    (move 
                        (from) 
                        (to "SiteToMoveOnTrack")
                    )
                )
            )
        )
        (map "StartTrack" {(pair P1 7) (pair P2 18)})
    })
    (rules 
        (start {
            (place Stack "Disc1" (sites {7..12}) count:2)
            (place Stack "Disc2" (sites {13..18}) count:2)
        })
        phases:{
        (phase "UnStacking" (play 
                (do 
                    (if (or (is Pending) ("NewTurn")) (roll)) 
                    next:
                    (forEach Die 
                        if:("DieNotUsed")
                        (if ("PieceStacked" "SiteToSelect")
                            (move Select (from ("SiteToSelect"))
                                (then 
                                    (and
                                        ("Unstack")
                                        ("ReplayNotAllDiceUsed")
                                    )
                                )
                            )
                        )
                    )
                    (then (if (all DiceEqual) (and (set Pending) (moveAgain))))
                )
            )
            (nextPhase Mover (all Sites (sites Occupied by:Mover) if:(not ("PieceStacked" (site)))) "Stacking")
        )
        (phase "Stacking" (play 
                (do 
                    (if (or (is Pending) ("NewTurn")) (roll)) 
                    next:
                    (forEach Die 
                        if:("DieNotUsed")
                        (if ("PieceUnstacked" "SiteToSelect")
                            (move Select (from ("SiteToSelect"))
                                (then 
                                    (and 
                                        ("Stack")
                                        ("ReplayNotAllDiceUsed")
                                    )
                                )
                            )
                        )
                    )
                    (then (if (all DiceEqual) (and (set Pending) (moveAgain))))
                )
            )
            (nextPhase Mover (all Sites (sites Occupied by:Mover) if:(not ("PieceUnstacked" (site)))) "BearingOff")
        )
        (phase "BearingOff" 
            (play 
                (do 
                    (if (or (all DiceEqual) ("NewTurn")) (roll)) 
                    next:(forEach Piece top:True)
                    (then (if (or (not (all DiceUsed)) (all DiceEqual)) (moveAgain)))
                )
            )
            (end ("EscapeWin"))
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Ofanfelling is a race game played in Iceland. Though it is played on the same board as other European Tables Games, it is different from most of them in that all of the game play for each player takes place in separate quadrants of the board, and the players' pieces never interact with one another.")
(aliases {"Ofanfellingartafl"})
(rules "2x12 board, with spaces as points, divided in two. Twelve pieces per player, which start stacked, two per point in the quadrant to the player's right. Two six-sided dice. Players unstack the pieces by moving pieces off of the stack, but remaining on the same point, according to the throws of the dice. The value of the throw indicates the number of the point which can be unstacked. For example, a throw of 1 and 4 allows the player to unstack the pieces on points 1 and 4. Doubles allow players to only move one piece, but grant the player another throw. When all of a player's pieces are unstacked, they must be restacked, in the same way. When they have been successfully restacked, the player then bears off the pieces from this quadrant of the board. When bearing off, players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. The first player to bear off all their pieces wins. ")
(source "Fiske 1905: 353.")
(id "815")
(version "1.3.7")
(classification "board/race/escape")
(credit "Eric Piette")
(origin "This game was played in Iceland, around 1905.")
}
)
    
    (graphics {
        (show Edges Hidden)
        (board Style backgammon)
        (stackType state:1 Backgammon)
    })
    
)
