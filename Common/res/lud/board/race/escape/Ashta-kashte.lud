(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "Grace" (is In (count Pips) (array {0 4})))

(define "SiteToMoveOnTrack" ("NextSiteOnTrack" ("ThrowValue")))

(define "CaptureEnemyPiece" 
    (apply 
        if:(and (not (is In (to) (sites "ProtectedSites"))) ("IsEnemyAt" (to))) 
        (and
            (fromTo 
                (from (to))
                (to (handSite (who at:(to))))
            )
            (moveAgain)
        )
    ) 
)

(define "Goal" 24)

(define "EntryPoint" (mapEntry #1))

//------------------------------------------------------------------------------

(game "Ashta-kashte"
    (players <Player:num>)
    (equipment {
        (board 
            (square 7)
            {
            (track "Track1" "27,N,W,S,E,N2,SW1,W4,N4,E4,S3,W3,N2,E2,S1,W1" P1 directed:True)
            (track "Track2" "21,S,E,N,W,S2,NE1,E4,S4,W4,N3,E3,S2,W2,N1,E1" P2 directed:True)
            <Player:trackP3>
            <Player:trackP4>
            }
        )
        (piece "Paddle" Each
            (if (and (is In (from) (sites Board)) ("IsNotOffBoard" "SiteToMoveOnTrack"))
                (move 
                    (from (from) level:(level))
                    (to 
                        "SiteToMoveOnTrack" 
                        "CaptureEnemyPiece"
                    )
                )
                (then 
                    (if (= "Goal" (last To))
                        (remove (last To))
                    )
                )
            )	
        )
        (hand Each)
        ("StickDice" 4)
        (map {(pair 1 "G4") (pair 2 "A4") (pair 3 "D1") (pair 4 "D7")})
        (map "Throw" {(pair 0 8)})
        (regions "ProtectedSites" (sites {0 3 6 21 27 3 45 42 48 24}))
    })
    (rules 
        (start {
            (place Stack "Paddle1" (handSite P1) count:4)
            (place Stack "Paddle2" (handSite P2) count:4)
            <Player:startP3>
            <Player:startP4>
        })
        (play 
            ("RollMove"
                (or {
                    (forEach Piece)
                    (move Pass)
                    (if (and ("Grace") (!= 0 (who at:(handSite Mover))))
                        (move 
                            (from (handSite Mover))
                            (to (mapEntry Mover))
                        )
                    )
                })
                (then 
                    (if ("Grace") (moveAgain))
                )
            )		
        )
        
        (end ("EscapeWin"))
    )
)
//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <num> <startP3> <trackP3> <startP4> <trackP4> }
    {
    (item "2" <2> <> <> <> <> "The game involves 2 players.")* 
    (item "3" <3> 
        <(place Stack "Paddle3" (handSite P3) count:4)> 
        <(track "Track3" "3,E,N,W,S,E2,NW1,N4,E4,S4,W3,N3,E2,S2,W1,N1" P3 directed:True)> 
        <> 
        <> 
    "The game involves 3 players.")
    (item "4" <4> 
        <(place Stack "Paddle3" (handSite P3) count:4)> 
        <(track "Track3" "3,E,N,W,S,E2,NW1,N4,E4,S4,W3,N3,E2,S2,W1,N1" P3 directed:True)> 
        <(place Stack "Paddle4" (handSite P4) count:4)> 
        <(track "Track4" "45,W,S,E,N,W2,SE1,S4,W4,N4,E3,S3,W2,N2,E1,S1" P4 directed:True)> 
    "The game involves 4 players.") 
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Falkener (Described)" {
        "Players/2"
        }
        variations:{"Players/3" "Players/4"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Ashta-kashte is a race game that was documented in India during the nineteenth century. The exact location of this version of the game was not recorded, but it is similar to other race games that mimic Pachisi and similar games, but on a square board.")
(rules "7x7 board. The central square, as well as the central square on each edge of the board, is marked with an X. Two to four players. Four pieces per player. Four cowries used as dice, the values are equal to the number of mouths which land face up; when all mouths are down the value = 8 and a \"grace,\" all mouths up = a grace. Throws giving a grace also allow the player to throw again. Players are not obliged to play their throws if they do not wish to. Pieces enter the board with a grace on the marked square on the edge closest to the player. Pieces move along the outer squares of the board in an anti-clockwise direction. Upon reaching the square before the marked square where the player enters their pieces, the track proceeds in the left corner (respective to the player) of the next concentric track of squares, and proceeds in a clockwise spiraling track toward the central marked square. When a player's piece lands on a square occupied by an opponent's piece, the opponent's piece is removed from the board, and the player gains another throw. Pieces cannot be removed from the board if they are resting on a marked square. Players may enter their pieces onto the board when an opponent's piece is resting on their starting place. This first player to move all of their pieces to the central square by exact throws wins.")
(source "Falkener 1892: 267-266.")
(id "198")
(version "1.3.7")
(classification "board/race/escape")
(credit "Eric Piette")
}
)
    
    (graphics {
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (piece Scale "Paddle" 0.5)
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (show Symbol "thinCross" {0 3 6 21 27 3 45 42 48 24} scale:0.9)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
