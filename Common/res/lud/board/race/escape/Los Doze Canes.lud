(define "SiteToMoveOnTrack" (trackSite Move steps:(pips)))

(define "RemoveAPiece" (move Remove (from) ) )

(define "CaptureEnemyPiece"
    (apply
        if:(is Enemy (who at:(to)))
        (fromTo (from (to)) (to (handSite Next)))
    )
)

(define "DieNotUsed" (!= (pips) 0))

//------------------------------------------------------------------------------

(game "Los Doze Canes"
    (players 2)
    (equipment {
        (board
            (merge {
                (rectangle 1 6)
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {24 0..5 6..11 23..18 17..12} P1 directed:True)
            (track "Track2" {25 12..17 18..23 11..6 5..0} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (or
                        (not (is Enemy (who at:"SiteToMoveOnTrack")))
                        (and
                            (is Enemy (who at:"SiteToMoveOnTrack"))
                            (= 1 (size Stack at:"SiteToMoveOnTrack"))
                        )
                    )
                    (move
                        (from)
                        (to
                            "SiteToMoveOnTrack"
                            "CaptureEnemyPiece"
                        )
                    )
                )
            )
        )
        (hand Each)
    })
    (rules
        (start {
            (place Stack "Disc1" (handSite P1) count:12)
            (place Stack "Disc2" (handSite P2) count:12)
        })
        (play
            (do
                (if (not "SameTurn") (roll))
                next:(or
                    (forEach Piece top:True)
                    (forEach Piece container:(mover) top:True)
                    (then
                        (if (not (all DiceUsed))
                            (moveAgain)
                        )
                    )
                )
            )
        )
        (end {
            (if (and {
                    (= 2 (size Stack at:<End:site1>))
                    (= 2 (size Stack at:<End:site2>))
                    (= 2 (size Stack at:<End:site3>))
                    (= 2 (size Stack at:<End:site4>))
                    (= 2 (size Stack at:<End:site5>))
                    (= 2 (size Stack at:<End:site6>))
                    (= 0 (count Sites in:(difference (sites Occupied by:Mover) (sites <End:sites>))))
                })
                (result Mover Win)
            )
            (if (and
                    (< 2 (size Stack at:12))
                    (< 2 (size Stack at:0))
                )
                (result Mover Draw)
            )
        })
    )
)

//------------------------------------------------------------------------------

(option "End Rules" <End> args:{ <site1> <site2> <site3> <site4> <site5> <site6> <sites> }
    {
    (item "Bottom Left Quadrant" <0> <1> <2> <3> <4> <5> <{0..5}> "The quadrant selected is the bottom left one.")
    (item "Bottom Right Quadrant" <6> <7> <8> <9> <10> <11> <{6..11}> "The quadrant selected is the bottom right one.")
    (item "Top Left Quadrant" <12> <13> <14> <15> <16> <17> <{12..27}> "The quadrant selected is the top right one.")
    (item "Top Right Quadrant" <18> <19> <20> <21> <22> <23> <{18..23}> "The quadrant selected is the top left one.")*
    }
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Los Doze Canes is a race game related to other European Tables games. It was played by children. It is described in the Libro de los Juegos of Alfonso X.")
        (aliases {"Los Doze Hermanos"})
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Twelve pieces per player. Two dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. Each player enters their pieces into their home section of the board (to the left of one player, to the right of the other player, and move pieces along a horseshoe-shaped track around the board toward the other player's home space. At the beginning of play, the first player will choose one quadrant of the board, and the goal of the game will be for one player to move two  pieces onto each of the six places in that quadrant. When a piece lands on a space occupied by a single piece of the opponent, the opponent's piece is removed from the board, and must be reentered.")
            (source "Libro de los Juegos 73-74.")
            (version "1.3.0")
            (classification "board/race/escape")
            (credit "Eric Piette")
            (origin  "This game was played in Spain, around 1283.")
            }
        )

        (graphics {
            (show Edges Hidden)
            (board Style Table)
            (stackType 0 Backgammon)
            (stackType 1 Count)
            (stackType 2 Count)
        })

    )

