(define "SiteToMoveOnTrack" ("NextSiteOnTrack" (count Pips)))

//------------------------------------------------------------------------------

(game "Had"
    (players 2) 
    (equipment { 
        (board (rectangle 3 14) 
            { 
            (track "Track1" "0,E,N1,W,N1,E" P1 directed:True)
            (track "Track2" "41,W,S1,E,S1,W" P2 directed:True)
            }
        )
        (dice d:4 num:2)
        (piece "Marker" Each 
            (if ("IsOffBoard" ("SiteToMoveOnTrack"))
                (move Remove (from))
                (move 
                    (from) 
                    (to 
                        ("SiteToMoveOnTrack") 
                        if:(is Empty (to)) 
                    )
                )
            )
        ) 
    }) 
    
    (rules 
        (start { 	
            (place "Marker1" (sites Bottom))
            (place "Marker2" (sites Top))
        })	
        
        (play ("RollMove" (forEach Piece)))
        
        (end 
            (if 
                (no Pieces Mover) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Had is a race game played in the Bordj Bou Arreridj region of Algeria in the early twentieth century. It resembles other games such as Tab which are popular in North Africa and Southwest Asia.")
(rules "3x14 board. Fourteen pieces per player, placed in the outer rows of the board. Two knucklebones used as dice. Players move their pieces according to the throws of the knucklebones. Pieces move in a boustrophedon path along the board. The first player to remove their pieces from the board wins.")
(source "Robert 1921: 66.")
(id "769")
(version "1.3.7")
(classification "board/race/escape")
(credit "Eric Piette")
(origin "This game was played in Algeria, around 1921.")
}
)
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Had_ai"
    )
)

