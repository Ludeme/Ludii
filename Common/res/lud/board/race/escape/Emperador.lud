// The capturing piece goes to the hand and have to enter again, because nothing is specified for capturing pieces which have to go back to the starting point if opponent pieces are already there.

(define "SiteToMoveOnTrack" (trackSite Move steps:(pips)))

(define "RemoveAPiece" (move Remove (from) ) )

(define "CaptureEnemyPiece" 
    (apply 
        if:(is Enemy (who at:(to))) 
        (fromTo (from (to)) (to (handSite Next)))
    ) 
)

(define "DieNotUsed" (!= (pips) 0))

//------------------------------------------------------------------------------

(game "Emperador"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 6) 
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {24 0..5 6..11 23..18 17..12} P1 directed:True)
            (track "Track2" {25 12..17 18..23 11..6 5..0} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:3)
        (hand Each)
        (regions "StartingQuadrant" P1 (sites {0..5}))
        (regions "StartingQuadrant" P2 (sites {12..17}))
        (piece "Disc" Each 
            (forEach Die 
                if:("DieNotUsed")
                (if (= "SiteToMoveOnTrack" Off)
                    "RemoveAPiece"
                    (if (or {
                            (is Empty ("SiteToMoveOnTrack"))
                            (and 
                                (is Friend (who at:"SiteToMoveOnTrack"))
                                (if (is In (to) (sites Mover "StartingQuadrant" )) (!= 1 (size Stack at:"SiteToMoveOnTrack")) True)
                            )
                            (and 
                                (is Enemy (who at:"SiteToMoveOnTrack"))
                                (= 1 (size Stack at:"SiteToMoveOnTrack"))
                            )
                        })
                        (move 
                            (from) 
                            (to 
                                "SiteToMoveOnTrack" 
                                "CaptureEnemyPiece" 
                            )
                        )
                    )
                )	
            )
        )
    })
    (rules 
        (start {
            (place Stack "Disc1" 0 count:15)
            (place Stack "Disc2" 12 count:15)
        })
        (play
            (do 
                (if (not "SameTurn") (roll)) 
                next:(or 
                    (forEach Piece top:True) 
                    (forEach Piece container:(mover) top:True)
                    (then 
                        (if (not (all DiceUsed))
                            (moveAgain)
                        )
                    )
                )
            )
        )     
        (end {
            (if (no Pieces Mover) (result Mover Win))
            (if (and (no Moves P1) (no Moves P2)) (result Mover Draw))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Emperador is a race game from medieval Spain that is related to other European Tables games. It is said, in the Libro de los Juegos of Alfonso X, to have been invented by \"the Emperor.\" It has different degrees of winning.")
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Fifteen pieces per player. Three six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. One player begins with all of their pieces on the space furthest to the left on their side, the other with their pieces on the point directly opposite it. Pieces move in opposite directions around the board, toward the point where the opponent's pieces begin, and bearing off the board from there. A player cannot have two of their pieces on the same spot in the starting quadrant. When a piece lands on the same space as a single opponent's piece, the opponent's piece is sent back to where it began. The first person to bear off all their pieces wins. If players move their pieces such that they fill up all the points in a quadrant, and the other fills up all of the points in an adjacent quadrant such that neither player can move, the game is a draw. It is a prime win when one player has twelve pieces arranged on consecutive points so that the other player cannot pass them. When the player sends four pieces back to the beginning while preserving this state, it is a prime win.")
        (source "Libro de los Juegos 75-76.")
        (id "729")
        (version "1.3.3")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Spain, around 1283.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType 0 Backgammon)
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)
