(define "NotEnemyOrOnlyOne"
    (or
        (and
            (is Enemy (who at:(to)))
            (= (topLevel at:(to)) 0)
        )
        (not (is Enemy (who at:(to))))
    )
)

(define "NextSiteFrom"
    (trackSite Move
        from:#1
        steps:(pips)
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "AllPieceInHome"
    ("NoSites"
        (difference
            (sites Occupied by:Mover)
            (sites Mover)
        )
    )
)

(define "RemoveAPiece"
    (move
        Remove
        (site)
    )
)

(define "DieNotUsedAndNoEscapeAndNotOffTheTrack"
    (and {
        ("DieNotUsed")
        (!= ("NextSiteFrom" (from)) Off)
        (!= ("NextSiteFrom" (from)) End)
    })
)

(define "MoveAPiece"
    (move
        (from #1)
        (to
            ("NextSiteFrom" #1)
            if:("NotEnemyOrOnlyOne")
        )
    )
)

(define "MoveAPieceIf"
    (forEach Die
        replayDouble:True
        if:#1
        ("MoveAPiece" #2)
    )
)

(define "AllPieceEscaped" (no Pieces Mover))

(define "NumPiece" (size Stack in:(sites Occupied by:#1)))

(define "CanEscape" (= ("NextSiteFrom" (site)) End))

(define "NotEmptyAndNotOffTheBoard"
    (and
        (!= (what at:(site)) 0)
        (!= ("NextSiteFrom" (site)) Off)
    )
)

(define "SetScoreOf"
    (score
        #1
        (if (is Mover #1)
            (if
                (= ("NumPiece" #2) 15)
                2
                1
            )
            0
        )
    )
)

(define "HaveAPieceAndCanEscape"
    (and
        (is Friend (who at:(site)))
        (< (trackSite Move from:(site) steps:(pips)) 0)
    )
)

(define "MotherCheckerP1Pinned"
    (and
        (= (id "Disc1") (what at:12 level:0))
        (= (id "Disc2") (what at:12 level:1))
    )
)

(define "MotherCheckerP2Pinned"
    (and
        (= (id "Disc2") (what at:25 level:0))
        (= (id "Disc1") (what at:25 level:1))
    )
)

//-----------------------------------------------------------------------------

(game "Plakoto"
    (players 2)
    (equipment {
        (board (rectangle 2 13)
            {
            (track "Track1" {12..7 5..0 13..18 20..25 End} P1 directed:True)
            (track "Track2" {25..20 18..13 0..5 7..12 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice num:2)
        (regions P1 { 20..25 }) // P1 Home
        (regions P2 { 7..12 })  // P2 Home
        (piece "Disc" Each ("MoveAPieceIf" "DieNotUsedAndNoEscapeAndNotOffTheTrack" (from)))
    } )

    (rules
        (start {
            (place Stack "Disc1" 12 count:15)
            (place Stack "Disc2" 25 count:15)
        })

        (play
            (do
                (if (not "SameTurn") (roll))
                next:(if "AllPieceInHome"
                    (forEach Die
                        replayDouble:True
                        if:("DieNotUsed")
                        (forEach Site
                            (sites Mover)
                            (if ("NotEmptyAndNotOffTheBoard")
                                (if ("CanEscape")
                                    ("RemoveAPiece")
                                    ("MoveAPiece" (site))
                                )
                            )
                            noMoveYet:(firstMoveOnTrack "Track" Mover
                                (if "HaveAPieceAndCanEscape"
                                    "RemoveAPiece"
                                )
                            )
                            (then
                                (if (not (all DiceUsed))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                    (max Distance "Track" Mover
                        (forEach Piece top:True
                            (then
                                (if (not (all DiceUsed))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                )
            )
        )

        (end {
            (if ("AllPieceEscaped")
                (byScore {
                    ("SetScoreOf" P1 P2)
                    ("SetScoreOf" P2 P1)
                } )
            )
            (if ("MotherCheckerP1Pinned")
                (byScore {
                    (score P1 0)
                    (score P2 2)
                } )
            )
            (if ("MotherCheckerP2Pinned")
                (byScore {
                    (score P1 2)
                    (score P2 0)
                } )
            )
        })
    )
)

//-------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Plakoto is a game related to Backgammon that is typically played as part of Tavli, along with Fevga and Portes, in the eastern Mediterranean.")
        (aliases {"Tsiliton" "Mahbooseh"})
        (rules "Played on a standard Backgammon board. Each player starts with fifteen pieces on the starting point of their track.  The pieces move around the board in opposite directions.
            The object of the game is to bring all of one's checkers around to one's own home board
            and then bear them off. The first player to bear off all of his or her checkers wins the game.
            Each player rolls one die. The player who rolls highest goes first. The player then rolls two die to begin play. After the first game, the winner of the previous game goes first. The roll of the dice indicates how many pips the player must move their pieces.
            The numbers on the two dice constitute separate moves. Doubles are played twice. A player must use both numbers of a roll if possible, or all four numbers in the case of doubles. A piece may not be moved to a point occupied by two or more of the opponent's pieces.. The player cannot move a piece to a point where the opponent has trapped another of the player's pieces.
            If a player lands on a point
            occupied by a single opposing piece, the opponentâ€™s piece is trapped until the trapping player removes their piece. If the last piece remaining in the starting position gets pinned by the opponent before
            it has left the starting position, the game is over and the player loses two points. The only exception is if
            the opponent still has pieces on their starting position. A game in which both players' respective
            pieces in the starting position are trapped is a draw.
            Once a player has moved all fifteen of their pieces into the player's home
            section of the board (that is, the six points at the end of the player's track), the player may begin bearing off. A player cannot bear off when the player has one or
            more pieces trapped inside the player's home section. A player bears off by rolling a number
            equal to the number of points left in the track of a piece, plus one. If there is no checker on the point indicated by the roll, then the player must make a legal move using a piece that will not remove it from the board. If there are no pieces available to fulfill this, the player must remove a piece that is furthest from the goal. The first player to bear off all fifteen pieces wins the game. If the losing player has
            borne off at least one piece, they lose only one point. If the losing player has not borne off a
        single piece by the time the opponent has borne off all of their pieces, then the player loses two points. ")
        (source "<a href=\"https://bkgm.com/articles/Mamoun/PlakotoBoardGameStrategy.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />bkgm.com</a>")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        }
    )

    (graphics {
        (board Style backgammon)
        (stackType Backgammon)
    })

    (ai
        "Plakoto_ai"
    )
)
