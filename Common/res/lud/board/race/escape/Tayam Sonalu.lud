(define "ThrowValue" (count Pips))
(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #2 #1))
(define "IsSpecialThrow" (is In ("ThrowValue") (sites {1 5})))
(define "Die1" (face 101))
(define "Die2" (face 102))
(define "Tayam" 
    (or
        (and (= 0 ("Die1")) (= 1 ("Die2")))
        (and (= 1 ("Die1")) (= 0 ("Die2")))
    )
)
(define "Sonal" 
    (or
        (and (= 0 ("Die1")) (= 4 ("Die2")))
        (and (= 4 ("Die1")) (= 0 ("Die2")))
    )
)

//------------------------------------------------------------------------------

(game "Tayam Sonalu" 
    (players 4) 
    (equipment {
        ("PachisiBoard"
            {
            (track "Track1" "96,9,S,E,N,11,E,N,W,53,N,W,S,42,W,S,E,8,S,E1,N7,End" P1 directed:True)
            (track "Track2" "96,26,W,S,E,8,S,E,N,11,E,N,W,53,N,W,S,42,W,S1,E7,End" P2 directed:True)
            (track "Track3" "96,52,N,W,S,42,W,S,E,8,S,E,N,11,E,N,W,53,N,W1,S7,End" P3 directed:True)
            (track "Track4" "96,27,E,N,W,53,N,W,S,42,W,S,E,8,S,E,N,11,E,N1,W7,End" P4 directed:True)
            }
        )
        (hand Each) 
        (dice d:4 faces:{0 1 2 4} num:2)
        (piece "Pawn" Each
            (if ("IsEndTrack" ("SiteToMoveOnTrack" from:(from) ("ThrowValue")))
                (move Remove (from) level:(level))
                (move 
                    (from (from) level:(level))
                    (to
                        ("SiteToMoveOnTrack" from:(from) ("ThrowValue"))
                        if:True
                        (apply
                            (if ("IsEnemyAt" (to))
                                (if ("IsSingletonStack" (to))
                                    (forEach Level (to) FromTop
                                        (fromTo
                                            (from (to) level:(level))
                                            (to (centrePoint))
                                        )
                                    )
                                    (forEach Level (to) FromTop
                                        (fromTo
                                            (from (to) level:(level))
                                            (to (handSite (who at:(to) level:(level))))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )		
        )
        (map "Start" {(pair P1 9) (pair P2 26) (pair P3 52) (pair P4 27)})
    }) 
    (rules 
        (start {
            (place Stack "Pawn1" (handSite P1) count:4)
            (place Stack "Pawn2" (handSite P2) count:4)
            (place Stack "Pawn3" (handSite P3) count:4)
            (place Stack "Pawn4" (handSite P4) count:4)
        })
        
        (play 
            ("RollMove"
                (if (!= 0 ("ThrowValue"))
                    (or
                        (if (and 
                                (is Occupied (handSite Mover)) 
                                (or
                                    (and (= Undefined (value Player Mover)) ("Tayam"))
                                    (and (= 1 (value Player Mover)) (or ("IsSpecialThrow") ("Sonal")))
                                )
                            )
                            (move
                                (from (handSite Mover))
                                (to (mapEntry "Start" Mover))
                                (then
                                    (if (= Undefined (value Player Mover))
                                        (set Value Mover 1)
                                    )
                                )
                            )
                        )
                        (forEach Piece)
                    )
                )
                (then
                    (if (and (not (no Pieces Mover)) (or ("IsSpecialThrow") ("Sonal")))	
                        (moveAgain)
                    )
                )
            )
        )
        
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Tayam Sonalu is a race game played on  a cross-shaped board played by Tamil people of Sri Lanka. It is closely related to other race games on a cross-shaped board played throughout South Asia.")
        (rules "Four 3x8 rectangles, arranged in a cross shape around large central square. Four players. Four pieces per player. Two rectangular four-sided die, each marked as follows: 0, 1, 2, 4. When the throw is 0 and 1, the throw is called Tayam, when it is 0 and 4 it is called Sonal. Pieces enter the board from the central square onto the central row of their arm of the board, proceed down the central row, and then in an anti-clockwise direction around the circumference of the board, and back up the central row of their arm of the board, having to enter the central square with an exact throw. The first counter of each player must be entered with a throw of Tayam. After this, pieces may be entered with a throw of 1, 5, or Sonal, each of which enter the piece on the first square of the central row of their arm. These three throws also grant the player another throw. The throws made after entering a piece are made at once, and then the pieces moved afterward, the undivided value of each throw being used to move a piece. When a piece lands on a space occupied by an opponent's piece, the opponent's piece is sent back to start. If the opponent has two pieces on such a space, they both are sent back to start, but both may reenter the board with a single throw of 1, 5, or Sonal. The first player to place all of their pieces in the central square wins. ")
        (source "Parker 1909: 617-618.")
        (id "750")
        (version "1.3.12")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Sri Lanka, around 1909.")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Green))
        (player Colour P3 (colour Yellow))
        (player Colour P4 (colour Black))
        (stackType site:96 Ring)
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Scale "Pawn" 0.5)
    })
    
)
