(define "NextSiteFrom" ("NextSiteOnTrack" (pips) from:#1))

(define "AllPiecesInLastQuadrant"
    (no Pieces Mover in:(union (sites Hand Mover) (difference (sites Board) (sites Mover "FinalQuadrant"))))
)

//------------------------------------------------------------------------------

(game "El Mundo"
    (players 4)
    (equipment {
        (board 
            (concentric {24})
            {
            (track "Track1" {24 0 2 4 6 8 10 12 14 16 18 20 22 23 21 19 17 15 13} P1 directed:True)
            (track "Track2" {25 12 14 16 18 20 22 23 21 19 17 15 13 11 9 7 5 3 1} P2 directed:True)
            (track "Track3" {26 23 21 19 17 15 13 11 9 7 5 3 1 0 2 4 6 8 10} P3 directed:True)
            (track "Track4" {27 11 9 7 5 3 1 0 2 4 6 8 10 12 14 16 18 20 22} P4 directed:True)
            }
            use:Vertex
        )
        (dice d:7 num:3)
        (regions "Mundo" P1 {23 21 19 17 15 13})
        (regions "Mundo" P2 {11 9 7 5 3 1})
        (regions "Mundo" P3 {0 2 4 6 8 10})
        (regions "Mundo" P4 {12 14 16 18 20 22})
        (piece "Marker" Each
            (forEach Die
                if:("DieNotUsed")
                (if (and ("AllPiecesInLastQuadrant") (= ("NextSiteFrom" (from)) Off))
                    (move Remove (from))
                    (move
                        (from (from))
                        (to
                            ("NextSiteFrom" (from))
                            if:("NoEnemyOrOnlyOne" (to))
                            ("HittingCapture" (handSite (who at:(to))))
                        )
                    )
                )
                (then ("ReplayNotAllDiceUsed"))
            )		
        )
        (map "Entry" {(pair P1 0) (pair P2 6) (pair P3 12) (pair P4 18)})
        (hand Each)
        (regions "FinalQuadrant" P1 (sites {13 15 17 19 21 23}))
        (regions "FinalQuadrant" P2 (sites {1 3 5 7 9 11}))
        (regions "FinalQuadrant" P3 (sites {10 8 6 4 2 0}))
        (regions "FinalQuadrant" P4 (sites {22 20 18 16 14 12}))
    })
    (rules 
        (start {
            (place Stack "Marker1" (handSite P1) count:12)
            (place Stack "Marker2" (handSite P2) count:12)
            (place Stack "Marker3" (handSite P3) count:12)
            (place Stack "Marker4" (handSite P4) count:12)
        })
        (play 
            ("RollEachNewTurnMove"
                (or (forEach Piece top:True) (forEach Piece container:(mover) top:True))
            )
        )
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "El Mundo is a four-player race game played in medieval Spain that is related to European Tables games. It is player on a circular board, and is conceptualized as a game of the four seasons.")
        (rules "Four players. Circular board, with 24 semi-circular sockets along the circumference, divided into four sections of six. Twelve pieces per player; the player's colors are green, red, black, and white, the sections to which they enter are in that order anti-clockwise around the board. Three seven-sided dice. Play proceeds in an anti-clockwise fashion around the board, as is the order of player turns. Players enter their pieces on the board according to the throws of the dice. The player must enter all of their pieces, and then attempt to move all of their pieces in the section of the board belonging to the player sitting opposite them. Once the player achieves this, they begin bearing their pieces off the board. When a piece lands on a spot occupied by a single piece belonging to the opponent, the opponent's piece is returned to the place where it started. When a player bears off all their pieces, they win.")
        (source "Libro de los Juegos 89.")
        (id "736")
        (version "1.3.14")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin "This game was played in Spain., around 1283.")
        }
    )
    
    (graphics {
        (show Edges Hidden)
        (player Colour P1 (colour Green))
        (player Colour P2 (colour Red))
        (player Colour P3 (colour Black))
        (player Colour P4 (colour White))
        (stackType 0 TowardsCenter)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (board Background image:"disc.svg" fillColour:(colour 223 178 110) edgeColour:(colour 223 178 110) scale:1.02)
        (show Symbol "disc" Vertex (sites Board) fillColour:(colour 223 178 110) edgeColour:(colour 223 178 110) scale:0.5)
    })
    
)
