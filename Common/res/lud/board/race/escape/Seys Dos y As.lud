(define "NextSiteFrom"
    (trackSite Move
        from:#1
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"
    (or
        (and
            (is Enemy (who at:(to)))
            (= (topLevel at:(to)) 0)
        )
        (not (is Enemy (who at:(to))))
    )
)

//------------------------------------------------------------------------------

(game "Seys Dos y As"
    (players 2)
    (equipment {
        (board
            (merge {
                (rectangle 1 6)
                (shift 7 0 (rectangle 1 6) )
                (shift 0 6 (rectangle 1 6) )
                (shift 7 6 (rectangle 1 6) )
            })
            {
            (track "Track1" {24 11..0 12..17} P1 directed:True)
            (track "Track2" {25 11..0 12..17} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:3)
        (piece "Disc" Each
            (forEach Die
                if:("DieNotUsed")
                (if (= Off ("NextSiteFrom" (from) (pips)))
                    (move Remove (from))
                    (move
                        (from (from))
                        (to
                            ("NextSiteFrom" (from) (pips))
                            if:("NotEnemyOrOnlyOne")
                            (apply
                                (if (is Enemy (who at:(to)))
                                    (fromTo
                                        (from (to))
                                        (to (handSite Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then
                    (if (not (all DiceUsed))
                        (moveAgain)
                    )
                )
            )
        )
        (hand Each)
    })
    (rules
        (start {
            (place Stack "Disc1"  11 count:8) (place Stack "Disc1" 6 count:3)
            (place Stack "Disc1" 7 count:4) (place Stack "Disc2" (sites {10..8})  count:5)
        })
        (play
            (do (if (not "SameTurn") (roll))
                next:(or (forEach Piece top:True) (forEach Piece container:(mover) top:True))
            )
        )

        (end (if (no Pieces Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Seys Dos y As is a race game from medieval Spain that is related to other European Tables games. It is documented in Alfonso X's Libro de los Juegos.")
        (rules "2x12 board, divided in half. Spaces on each side take the form of semi-circular sockets, into which the pieces fit. Fifteen pieces per player. Three six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. Both players begin in the same quadrant of the board. One player's pieces are arranged as follows: Eight on the first point of the quadrant to their left, four on the fifth, and three on the sixth. The other player places five on the second, third, and fourth space of the same quadrant. The track proceeds in a clockwise direction around the board. When a player's piece lands on a space occupied by a single piece of the opponent, it is sent back to the beginning of the quadrant to the right of the starting quadrant. Pieces bear off from the quadrant diagonal from the starting quadrant. The first player to bear off all their pieces wins.")
        (source "Libro de los Juegos 75.")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Spain, around 1283.")
        }
    )

    (graphics {
        (show Edges Hidden)
        (board Style Table)
        (stackType Backgammon)
    })

)

