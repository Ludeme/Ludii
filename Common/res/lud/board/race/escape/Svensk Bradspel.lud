(define "NextSiteFrom"
    (trackSite Move
        from:#1
        steps:#2
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "NotEnemyOrOnlyOne"
    (or
        (and
            (is Enemy (who at:(to)))
            (= (topLevel at:(to)) 0)
        )
        (not (is Enemy (who at:(to))))
    )
)

(define "AllPiecesInFinalQuadrant"
    (no Pieces Mover in:(difference (sites Board) (sites Mover "FinalQuadrant")))
)

(define "FiveOnThreeFinal"
    (if (is Mover P1)
        (and {
            (is Friend (who at:10))
            (is Friend (who at:11))
            (is Friend (who at:12))
            (= 5 (size Stack at:10))
            (= 5 (size Stack at:11))
            (= 5 (size Stack at:12))
        })
        (and {
            (is Friend (who at:13))
            (is Friend (who at:14))
            (is Friend (who at:15))
            (= 5 (size Stack at:13))
            (= 5 (size Stack at:14))
            (= 5 (size Stack at:15))
        })
    )
)

(define "ThreeOnFiveFinal"
    (if (is Mover P1)
        (and {
            (is Friend (who at:8))
            (is Friend (who at:9))
            (is Friend (who at:10))
            (is Friend (who at:11))
            (is Friend (who at:12))
            (= 3 (size Stack at:8))
            (= 3 (size Stack at:9))
            (= 3 (size Stack at:10))
            (= 3 (size Stack at:11))
            (= 3 (size Stack at:12))
        })
        (and {
            (is Friend (who at:13))
            (is Friend (who at:14))
            (is Friend (who at:15))
            (is Friend (who at:17))
            (is Friend (who at:18))
            (= 3 (size Stack at:13))
            (= 3 (size Stack at:14))
            (= 3 (size Stack at:15))
            (= 3 (size Stack at:17))
            (= 3 (size Stack at:18))
        })
    )
)

(define "SevenFiveThreeFinal"
    (if (is Mover P1)
        (and {
            (is Friend (who at:10))
            (is Friend (who at:11))
            (is Friend (who at:12))
            (= 3 (size Stack at:10))
            (= 5 (size Stack at:11))
            (= 7 (size Stack at:12))
        })
        (and {
            (is Friend (who at:13))
            (is Friend (who at:14))
            (is Friend (who at:15))
            (= 7 (size Stack at:13))
            (= 5 (size Stack at:14))
            (= 3 (size Stack at:15))
        })
    )
)

(define "FifteenFinal"
    (if (is Mover P1)
        (and
            (is Friend (who at:12))
            (= 15 (size Stack at:12))
        )
        (and
            (is Friend (who at:13))
            (= 15 (size Stack at:13))
        )
    )
)

//------------------------------------------------------------------------------

(game "Svensk Bradspel"
    (players 2)
    (equipment {
        (board (rectangle 2 13)
            {
            (track "Track1" {6 25..20 18..13 0..5 7..12 End} P1 directed:True)
            (track "Track2" {19 0..5 7..12 25..20 18..13 End} P2 directed:True)
            }
            use:Vertex
        )
        (dice d:6 num:2)
        (piece "Disc" Each
            (forEach Die
                replayDouble:True
                if:("DieNotUsed")
                (if (= End ("NextSiteFrom" (from) (pips)))
                    (if ("AllPiecesInFinalQuadrant")
                        (move Remove (from))
                    )
                    (move
                        (from (from))
                        (to
                            ("NextSiteFrom" (from) (pips))
                            if:(and
                                ("NotEnemyOrOnlyOne")
                                (if (not (is In (to) (sites Mover "StartingSide")))
                                    True
                                    (if (= (value Player Mover) 1) True (is Empty (to)))
                                )
                            )
                            (apply
                                (if (is Enemy (who at:(to)))
                                    (fromTo
                                        (from (to))
                                        (to (mapEntry "Bar" Next))
                                    )
                                )
                            )
                        )
                    )
                )
                (then
                    (and
                        (if (not (all DiceUsed))
                            (moveAgain)
                        )
                        (if (and
                                (= (last To) (mapEntry "12thPoint" Mover))
                                (!= 1 (value Player Mover))
                            )
                            (set Value Mover 1)
                        )
                    )
                )
            )
        )
        (regions "FinalQuadrant" P1 (sites {7..12}))
        (regions "FinalQuadrant" P2 (sites {13..18}))
        (regions "StartingSide" P1 (sites {25..20 18..14}))
        (regions "StartingSide" P2 (sites {0..5 7..11}))
        (map "12thPoint" {(pair P1 13) (pair P2 12)})
        (map "Bar" {(pair P1 6) (pair P2 19)})
    })
    (rules
        (start {
            (place Stack "Disc1" 25 count:15)
            (place Stack "Disc2" 0 count:15)
        })
        (play
            (do (if (not "SameTurn") (roll))
                next:(forEach Piece top:True)
            )
        )

        (end {
            (if (no Pieces Mover) (result Mover Win))
            (if  ("FiveOnThreeFinal" Mover) (result Mover Win))
            (if  ("ThreeOnFiveFinal" Mover) (result Mover Win))
            (if  ("SevenFiveThreeFinal" Mover) (result Mover Win))
            (if  ("FifteenFinal" Mover) (result Mover Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Svensk Brädspel is a European Tables game played in Sweden. It involves the possibility of winning by bringing the pieces to one of various winning positions.It was said to have been the most popular game in all parts of Sweden, in cities and in rural areas.")
        (aliases {"Svensk Brädspel"})
        (rules "2x12 board, with the spaces rendered as points, divided into half. Fifteen pieces per player. Two six-sided dice. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. A throw of doubles forces the player to play the throw twice. Each player's pieces begin in three stacks of five, on the leftmost point (with respect to the opponent) on the opponent's side of the board. Play proceeds (with respect to the player) from right to left on the opponent's side of the board, and then from left to right on the player's side of the board. A player cannot place two pieces on a single point on any of the first eleven points of the board, except for those pieces in the starting position, until one of the player's pieces has first landed on the twelfth point. However, a player may place two pieces on any point after the twelfth point, regardless of whether a piece has ever landed on the twelfth point. A player cannot move a piece onto a point containing two or more pieces belonging to the opponent. When a piece lands on a point occupied by a single piece belonging to the opponent, it is removed from the board and must be entered again, and can only do so using the value of one die, not both, with the starting point considered to be point 1, the next point 2, etc. Opponent's pieces can be removed from the board in this way when reentering the board. To win, a player must place their pieces in one of the following winning positions: five pieces on each of the final three points; three pieces on the final five points; seven on the final point, five on the penultimate point, and three on the antepenultimate point; fifteen on the final point. If none of these positions are possible, the player must bear off all of their pieces once all of their pieces have reached the final six points. The first player to reach a winning position or to bear off all of their pieces wins.")
        (source "Fiske 1905: 321-328.")
        (version "1.3.0")
        (classification "board/race/escape")
        (credit "Eric Piette")
        (origin  "This game was played in Sweden, around 1888.")
        }
    )

    (graphics {
        (board Style backgammon)
        (stackType Backgammon limit:3)
    })

)

