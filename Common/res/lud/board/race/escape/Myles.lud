(define "NextSiteFrom"
    (trackSite Move
        from:#1
        steps:(pips)
    )
)

(define "NextSiteFromDist6"
    (trackSite Move
        from:#1
        steps:6
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "RemoveAPiece"
    (move
        Remove
        (site)
    )
)

(define "Bar" (mapEntry (mover)))

(define "NotEnemyOrOnlyOne"
    (or
        (and
            (is Enemy (who at:(to)))
            (= (topLevel at:(to)) 0)
        )
        (not (is Enemy (who at:(to))))
    )
)

(define "AllPieceEscaped" (no Pieces Mover))

(define "CanEscape" (= #1 Off))

//-----------------------------------------------------------------------------

(game "Myles"
    (players 2)
    (equipment {
        (board (rectangle 2 13)
            {
            (track "Track1" {6 0..5 7..12 25..20 18..13} P1 directed:True)
            (track "Track2" {19 0..5 7..12 25..20 18..13} P2 directed:True)
            }
            use:Vertex
        )
        (dice num:2)
        (map {(pair 1 19 ) (pair 2 6)}) // Bar
        (piece "Disc" Each)
    } )

    (rules
        (start {
            (place Stack "Disc1" 12 count:5)
            (place Stack "Disc1" 20 count:4)
            (place Stack "Disc1" 21 count:4)
            (place Stack "Disc1" 14 count:2)
            (place Stack "Disc2" 25 count:3)
            (place Stack "Disc2" 24 count:3)
            (place Stack "Disc2" 23 count:3)
            (place Stack "Disc2" 22 count:3)
            (place Stack "Disc2" 21 count:3)
        })

        (play
            (do
                (if (not "SameTurn") (roll))
                next:(if (all DiceUsed)
                    (forEach Site
                        (sites Occupied by:Mover)
                        (if ("CanEscape" ("NextSiteFromDist6" (site)))
                            ("RemoveAPiece")
                            (move
                                (from (site))
                                (to ("NextSiteFromDist6" (site))
                                    if:("NotEnemyOrOnlyOne")
                                    (apply
                                        if:(is Enemy (who at:(to)))
                                        (fromTo
                                            (from (to))
                                            (to ("Bar"))
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (forEach Die
                        if:("DieNotUsed")
                        (forEach Site
                            (sites Occupied by:Mover)
                            (if ("CanEscape" ("NextSiteFrom" (site)))
                                ("RemoveAPiece")
                                (move
                                    (from (site))
                                    (to ("NextSiteFrom" (site))
                                        if:("NotEnemyOrOnlyOne")
                                        (apply
                                            if:(is Enemy (who at:(to)))
                                            (fromTo
                                                (from (to))
                                                (to ("Bar"))
                                            )
                                        )
                                    )
                                )
                            )
                        )
                        (then (moveAgain))
                    )
                )
            )
        )

        (end
            (if ("AllPieceEscaped")
                (result Mover Win)
            )
        )
    )
)

//-------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Myles is the name attributed to a fourteenth century English game. It is a European Tables game which is unnamed in the manuscript describing its rules. ")
        (rules "2x12 board, divided in half, where the spaces are rendered as points. Fifteen pieces per player. Two six-sided dice. Both players begin on the same side of the board, one player (who plays first) with five pieces on the rightmost point of the starting row, four on the fifth and sixth points and two in the eleventh point in the opposite row. The other player has three pieces each on the right five points in the second row. Players move according to the number on each die by moving one piece the value on one die then another piece the value on the other die, or by moving one piece the value of one die and then the value of the other. On each throw the player also plays a throw of 6 in addition to the throw presented by the dice. Pieces move in an anti-clockwise direction around the board. A piece cannot move to a point that is occupied by more than one of the opponent's pieces. If a piece lands on a point occupied by a single piece belonging to the opponent, the opponent's piece is removed from the board and must enter again from the leftmost point in the row where the pieces began. A piece may be borne off the board when a throw is greater than the number of points left on the board. The first player to bear all of their pieces off the board wins.")
        (source "Murray 1951: 123.")
        (version "1.3.0")
        (classification "board/race/escape")
        (author "Eric Piette")
        (origin  "This game was played in England, from around 1300 to 1350.")
        }
    )

    (graphics {
        (board Style backgammon)
        (stackType Backgammon)
    })

)
