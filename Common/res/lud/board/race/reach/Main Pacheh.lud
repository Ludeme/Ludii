(define "ThrowValue" (mapEntry "ThrowDiceValue" (count Pips)))

(define "SiteToMoveOnTrack" (trackSite Move steps:("ThrowValue")))

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (fromTo 
            (from (to))
            (to (mapEntry "StartingPoint" (who at:(to))))
        )
    ) 
)

(define "Goal" 12)

//------------------------------------------------------------------------------

(game "Main Pacheh"
    (players <Player:num>)
    (equipment {
        (board 
            (square 5)
            {
            (track "Track1" "2,E,N,W,S,E1,N3,E2,S2,W1,12" P1 directed:True)
            (track "Track2" "22,W,S,E,N,W1,S3,W2,N2,E1,12" P2 directed:True)
            <Player:trackP3>
            <Player:trackP4>
            }
        )
        (piece "Marker" Each
            (if (!= "SiteToMoveOnTrack" Off)
                (if (or
                        (is In "SiteToMoveOnTrack" (sites Empty))
                        (is Enemy (who at:"SiteToMoveOnTrack"))
                    )
                    (move 
                        (from (from) level:(level))
                        (to 
                            "SiteToMoveOnTrack" 
                            "CaptureEnemyPiece"
                        )
                    )
                )
                (then 
                    (if (= "Goal" (last To))
                        (remove (last To))
                    )
                )
            )
        )
        (map "ThrowDiceValue" {(pair 0 8) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4)})
        (map "StartingPoint" {(pair 1 2) (pair 2 22) (pair 3 14) (pair 4 10)})
        (dice d:2 from:0 num:4)
    })
    (rules 
        (start {
            (place Stack "Marker1" 2 count:4)
            (place Stack "Marker2" 22 count:4)
            <Player:startP3>
            <Player:startP4>
        })
        (play 
            (do  
                (roll) 
                next:(if (can Move (forEach Piece))
                    (forEach Piece (then (if (= ("ThrowValue") 8) (moveAgain))))
                    (move Pass (then (if (= ("ThrowValue") 8) (moveAgain))))
                )
            )
        )
        (end ("EscapeWin"))
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <num> <startP3> <trackP3> <startP4> <trackP4> }
    {
    (item  "2"  <2> <> <> <> <> "The game involves 2 players.")  
    (item  "3"  <3> <(place Stack "Marker3" 14 count:4)> <(track "Track3" "14,N,W,S,E,N1,W3,N2,E2,S1,12" P3 directed:True)> <> <> "The game involves 3 players.")  
    (item  "4"  <4> <(place Stack "Marker3" 14 count:4)> <(track "Track3" "14,N,W,S,E,N1,W3,N2,E2,S1,12" P3 directed:True)> <(place Stack "Marker4" 10 count:4)> <(track "Track4" "10,S,E,N,W,S1,E3,S2,W2,N1,12" P4 directed:True)> "The game involves 4 players.")*  
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Main Pacheh (Described)" {
        "Players/4"
        }
        variations:{"Players/2" "Players/3"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Main Pacheh is  race game played on a square board from the Penang region in Malaysia. Curiously, the name of the game is similar to the name for many cross-shaped games known throughout South, Southwest, and Southeast Asia.")
        (aliases {"Main Pachih"})
        (rules "5x5 board, with the central square on each side and the central square of the board marked with an X. Two to four players, each with four pieces, which start on one of the marked squares on the edge of the board. Four cowrie shells used as dice. The throws are as  follows, with the value of the throws equaling the number of mouths which land up, all mouths down = 8, and grants the player another throw. Pieces moves in a spiral track around the board in an anti-clockwise direction until they reach the space before the starting point, at which point they proceed around the inner circles in a clockwise direction, and then to the central space. When a player lands on a space occupied by an opponent's piece, the opponent's piece is sent back to their starting position. When a player reaches the central square by an exact throw, it is removed from the board. The first player to remove all four of their pieces from the board wins.")
        (source "Dussek 1919: 70.")
        (id "873")
        (version "1.3.4")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin  "This game was played in Malaysia, around 1919.")
        }
    )
    
    (graphics {
        (show Symbol "thinCross" {2 10 12 14 22} scale:0.9)
        (stackType 0 Ground)
        (piece Scale "Marker" 0.5)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
