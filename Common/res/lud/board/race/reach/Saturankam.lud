(define "ThrowValue" (count Pips))
(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #2 #1))

(define "Move"
    (or
        (if (and {
                (is Occupied (handSite Mover))
                (all DiceEqual) 
                (= 2 (value Player Mover))
            })
            (move
                (from (handSite Mover))
                (to (mapEntry "Entry" Mover))
                (then
                    (and
                        (moveAgain)
                        (set Var "Double" 1)
                    )
                )
            )
        )
        (forEach Piece)
    )		
)

(define "AllReachedCenter" 
    (= 2 (count Pieces Mover in:(sites Centre)))
)

//------------------------------------------------------------------------------

(game "Saturankam"
    (players <Players:num>)
    (equipment {
        (board (square 9)
            {
            <Players:tracks>
            }
        )
        (dice d:4 faces:{1 3 4 6} num:2)
        (piece "Marker" Each
            (if
                (and
                    (or { 
                        (= ("SiteToMoveOnTrack" from:(from) 1) (centrePoint))
                        (= ("SiteToMoveOnTrack" from:(from) 3) (centrePoint))
                        (= ("SiteToMoveOnTrack" from:(from) 4) (centrePoint))
                    })
                    (= (value Player Mover) ("ThrowValue"))
                )
                (if 
                    (and 
                        (all DiceEqual)
                        (= ("SiteToMoveOnTrack" from:(from) (/ ("ThrowValue") 2)) (centrePoint))
                    )
                    (move
                        (from (from))
                        (to (centrePoint))
                        (then
                            (and
                                (moveAgain)
                                (set Var "Double" 1)
                            )	
                        )
                    )
                )
                (forEach Value min:1 max:(value Player Mover)
                    (move 
                        (from (from) level:(level))
                        (to
                            ("SiteToMoveOnTrack" from:(from) (value))
                            if:True
                            (apply 
                                (and
                                    (if (and (not (is In (to) (sites "SafeSites"))) ("IsEnemyAt" (to)))
                                        (forEach Level (to) FromTop
                                            (fromTo
                                                (from (to) level:(level))
                                                (to (handSite (who at:(to) level:(level))))
                                            )
                                        )
                                    )
                                    (if (< 0 (- (value Player Mover) (value)))
                                        (and
                                            (moveAgain)
                                            (set Value Mover (- (value Player Mover) (value)))
                                        )
                                        (and
                                            (set Value Mover 0)
                                            (if (all DiceEqual)
                                                (and
                                                    (moveAgain)
                                                    (set Var "Double" 1)
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        (hand Each)
        (regions "SafeSites" (sites {4 44 76 36 40}))
        <Players:entries>
    })
    (rules 
        <Players:start>
        (play 
            (if (or (= 1 (var "Double")) ("NewTurn"))
                ("RollMove" 
                    (move Pass 
                        (then 
                            (and {
                                (set Value Mover ("ThrowValue"))
                                (if (= 1 (var "Double")) (set Var "Double" 0))
                                (moveAgain)
                            })
                        )
                    )
                )
                (if (can Move ("Move")) 
                    ("Move")
                    (move Pass (then (and (set Var "Double" 0) (set Value Mover 0))))
                )
            )
        )
        (end (if ("AllReachedCenter") (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Players" <Players> args:{ <num> <start> <tracks> <entries>} {
    (item "2" <2> 
        <
        (start {
            (place Stack "Marker1" 4 count:2)
            (place Stack "Marker2" 76 count:2)
        })
        >
        <
        (track "Track1" "81,4,E,N,W,S,E3,N1,W2,N6,E6,S5,W5,N4,E4,S3,W3,N2,E2,S1,W1" P1 directed:True)
        (track "Track2" "83,76,W,S,E,N,W3,S1,E2,S6,W6,N5,E5,S4,W4,N3,E3,S2,W2,N1,E1" P2 directed:True)
        >
        <(map "Entry" {(pair P1 4) (pair P2 76)})>
        "The game is played with 2 players."
    ) 
    (item "3" <3> 
        <
        (start {
            (place Stack "Marker1" 4 count:2)
            (place Stack "Marker2" 36 count:2)
            (place Stack "Marker3" 76 count:2)
        })
        >
        <
        (track "Track1" "81,4,E,N,W,S,E3,N1,W2,N6,E6,S5,W5,N4,E4,S3,W3,N2,E2,S1,W1" P1 directed:True)
        (track "Track2" "82,36,S,E,N,W,S3,E1,N2,E6,S6,W5,N5,E4,S4,W3,N3,E2,S2,W1,N1" P2 directed:True)
        (track "Track3" "83,76,W,S,E,N,W3,S1,E2,S6,W6,N5,E5,S4,W4,N3,E3,S2,W2,N1,E1" P3 directed:True)
        >
        <(map "Entry" {(pair P1 4) (pair P2 36) (pair P3 76)})>
        "The game is played with 3 players."
    ) 
    (item "4" <4> 
        <
        (start {
            (place Stack "Marker1" 4 count:2)
            (place Stack "Marker2" 36 count:2)
            (place Stack "Marker3" 76 count:2)
            (place Stack "Marker4" 44 count:2)
        })
        >
        <
        (track "Track1" "81,4,E,N,W,S,E3,N1,W2,N6,E6,S5,W5,N4,E4,S3,W3,N2,E2,S1,W1" P1 directed:True)
        (track "Track2" "82,36,S,E,N,W,S3,E1,N2,E6,S6,W5,N5,E4,S4,W3,N3,E2,S2,W1,N1" P2 directed:True)
        (track "Track3" "83,76,W,S,E,N,W3,S1,E2,S6,W6,N5,E5,S4,W4,N3,E3,S2,W2,N1,E1" P3 directed:True)
        (track "Track4" "84,44,N,W,S,E,N3,W1,S2,W6,N6,E5,S5,W4,N4,E3,S3,W2,N2,E1,S1" P4 directed:True)
        >
        <(map "Entry" {(pair P1 4) (pair P2 36) (pair P3 76) (pair P4 44)})>
        "The game is played with 4 players."
    )* 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Saturankam (Described)" {
        "Players/4"
        }
        variations:{"Players/2" "Players/3"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Saturankam is a race game played in South Asia. It is played by Sinhalese and Tamil people, and is similar to other games throughout the region. Though it is sometimes known as Siga or Chaturanga, it is distinct from the most widely-played games known by these names.")
        (aliases {"Siga" "Chaturanga"})
        (rules "9x9 board, with the central square of each side marked, as well as the central space of the board. Two to four players, each player with two pieces. Played with two four-sided dice, marked 1, 3, 4, 6. Throws may be divided as the players see fit between their pieces. Doubles allow another throw. Players start with their pieces on the marked square on their side of the board. Pieces move along a spiraling track, starting in an anti-clockwise direction around the outer edges of the board, then changing to a clockwise direction when reaching the 32nd space in the track, and continuing in this direction in a spiral pattern until reaching the central square. If a player lands on a space occupied by an opponent's piece, the opponent's piece is removed from the board, and the player must throw a double 1 to enter it again on their starting square. Pieces resting on a marked space are safe from being sent to the start. The goal is to reach the central square with both pieces, which must be reached by an exact throw. If a piece is 1, 3, or 4 spaces from the center, the player must throw doubles of that number to reach the goal.
        ")
        (source "Parker 1909: 605-607.")
        (id "197")
        (version "1.3.5")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin "This game was played in South Asia., around 1909.")
        }
    )
    
    (graphics {
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (board Colour Symbols (colour Black))
        (board Colour Phase0 (colour 222 173 123))
        (show Symbol "thinCross" {4 36 40 44 76})
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (piece Scale "Marker" 0.5)
    })
    
)

