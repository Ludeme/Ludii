(define "ThrowValue" (mapEntry "Throw" (count Pips)))
(define "SiteToMoveOnTrack" (trackSite Move #1 steps:#2))

(define "IsSpecialThrow" (is In ("ThrowValue") (sites {10 25 30})))

(define "AllPiecesOnCentre"
    (and
        (= 1 (count Sites in:(sites Occupied by:#1 top:False)))
        (is In (centrePoint) (sites Occupied by:#1 top:False))
    )
)

//------------------------------------------------------------------------------

(game "Pasit"
    (players 4)
    (equipment {
        (board
            (add
                (hole
                    (merge
                        (shift 0 8 (rectangle 3 19))
                        (shift 8 0 (rectangle 19 3))
                    )
                    (poly {{8 8} {8 11} {11 11} {11 8}})
                )
                cells:{ { 8 28 48 68 69 70 71 51 31 11 10 9 } }
            )
            {
            (track "Track1" "8,S,E,N,11,E,N,W,53,N,W,S,42,W,S,E,8,S,E1,N8" P1 directed:True)
            (track "Track2" "42,W,S,E,8,S,E,N,11,E,N,W,53,N,W,S,42,W,S1,E8" P2 directed:True)
            (track "Track3" "53,N,W,S,42,W,S,E,8,S,E,N,11,E,N,W,53,N,W1,S8" P3 directed:True)
            (track "Track4" "11,E,N,W,53,N,W,S,42,W,S,E,8,S,E,N,11,E,N1,W8" P4 directed:True)
            }
        )
        (hand Each)
        (dice d:2 from:0 num:6)
        (piece "Pawn" Each
            (if (= Off ("SiteToMoveOnTrack" from:(from) ("ThrowValue")))
                (move
                    (from (from) level:(level))
                    (to (centrePoint))
                )
                (move
                    (from (from) level:(level))
                    (to
                        ("SiteToMoveOnTrack" from:(from) ("ThrowValue"))
                        if:True
                        (apply
                            (if (and (not (is In (to) (sites "SafeSites"))) (is Enemy (who at:(to))))
                                (forEach Level (to) FromTop
                                    (fromTo
                                        (from (to) level:(level))
                                        (to (handSite (who at:(to) level:(level))))
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        (map "Throw" {(pair 0 6) (pair 1 10) (pair 2 2) (pair 3 3) (pair 4 5) (pair 5 25) (pair 6 12)})
        (map "Entry" {(pair P1 8) (pair P2 42) (pair P3 53) (pair P4 11)})
        (regions "SafeSites" (sites {63 65 15 47 84 86 38 3}))
    })
    (rules
        (start {
            (place Stack "Pawn1" (handSite P1) count:4)
            (place Stack "Pawn2" (handSite P2) count:4)
            (place Stack "Pawn3" (handSite P3) count:4)
            (place Stack "Pawn4" (handSite P4) count:4)
        })

        phases:{
        (phase "Opening"
            (play
                (do
                    (roll)
                    next:(if ("IsSpecialThrow")
                        (move
                            (from (handSite Mover))
                            (to (mapEntry "Entry" Mover))
                        )
                        (move Pass)
                        (then
                            (and
                                (if (!= 1 (value Player Mover)) (moveAgain))
                                (set Value Mover (+ 1 (value Player Mover)))
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (= 2 (value Player Mover)) "Moving")
        )
        (phase "Moving"
            (play
                (do
                    (roll)
                    next:(or
                        (if (and (is Occupied (handSite Mover)) ("IsSpecialThrow"))
                            (move
                                (from (handSite Mover))
                                (to (mapEntry "Entry" Mover))
                            )
                        )
                        (forEach Piece)
                    )
                )
            )
        )
        }

        (end (if ("AllPiecesOnCentre" Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Pasit is a race game played in Myanmar. In the nineteenth century, it was popular among children and people living in rural areas, and was said to not be popular among people living in cities.")
        (aliases {"Chuay Pyit-thee" "Ansah Pyit-thee"})
        (rules "Four 3x8 rectangles, arranged in a cross-shaped board. the fourth square, counting from the outer corners, in the outer rows of each arm are marked. Four pieces per player. Six cowrie shells are used as dice. The value of the throws is as follows: one mouth up = 10; two mouths up = 2, three mouths up = 3; four mouths up = 5; five mouths up = 25; six mouths up = 12; all mouths down = 6. On the first turn, players get three throws, and enter a piece for every throw of 10 or 25. On the top left square in their arm. Players move their pieces around the board in an anti-clockwise direction. When a piece lands on a space occupied by an opponent's piece, the opponent's piece is removed from the board and must enter again. Pieces resting on marked squares are safe from being removed from the board. When a piece completes a circuit of the board. It moves toward the central row in the player's arm, and progresses up the central row into the large square in the center of the board. The player who moves all of their pieces into the center first wine.")
        (source "Shway Yoe 1882: 83-85.")
        (version "1.3.0")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin  "This game was played in Myanmar, around 1882.")
        }
    )

    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Green))
        (player Colour P3 (colour Yellow))
        (player Colour P4 (colour Black))
        (stackType site:96 Ring)
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (show Symbol "thinCross" {63 65 15 47 84 86 38 3} scale:0.9)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Scale "Pawn" 0.5)
    })

)
