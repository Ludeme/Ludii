(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "Move" 
    (move
        (from (sites Occupied by:#1))
        (to 
            ("NextSiteOnTrack" ("ThrowValue") #2)
            if:(not ("IsEnemyAt" (to)))
        )
    )
)

//------------------------------------------------------------------------------

(game "Setichch"
    (players <Player:num>)
    (equipment {
        ("FortyStonesWithFourGapsBoard"
            {
            (track "TrackCW" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True)
            (track "TrackCCW" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True)
            }
        )
        (regions "AllSites" (sites Board))
        (piece "Stick" P1)
        (piece "Stick" P2)
        ("StickDice" 3)
        (hand Each)
        (map "Throw" {(pair 0 5) (pair 1 3) (pair 2 2) (pair 3 10)})
    })
    (rules 
        (start {
            (place Stack "Stick1" Edge 36 count:2)
            (place Stack "Stick2" Edge 38 count:2)
            <Player:teams>
        })
        (play 
            ("RollMove"
                (if (is In (mover) (players Team1))
                    ("Move" Team1 "TrackCW")
                    ("Move" Team2 "TrackCCW")
                )
                (then (if (= 10 ("ThrowValue")) (moveAgain)))
            )
        )
        (end {
            (if 
                (and {
                    (is In (mover) (players Team1)) 
                    (= 1 (count Sites in:(sites Occupied by:Team1))) 
                    (= 36 (where "Stick" Team1))
                })
                (result Team1 Win)
            )
            (if 
                (and {
                    (is In (mover) (players Team2)) 
                    (= 1 (count Sites in:(sites Occupied by:Team2))) 
                    (= 38 (where "Stick" Team2))
                })
                (result Team2 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <num> <teams>}
    {
    (item "4" <4> 
        <
        (set Team 1 {P1 P3})
        (set Team 2 {P2 P4})
        >
    "The game has 4 players.") 
    (item "6" <6> 
        <
        (set Team 1 {P1 P3 P5})
        (set Team 2 {P2 P4 P6})
        >
    "The game has 6 players.")* 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Setichch (Described)" {
        "Players/6"
        }
        variations:{"Players/4"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sétichch is a race game that was played by White Mountain Apache people in Fort Apache, Arizona in the early twentieth century.")
        (aliases {"Sétichch" "Tze-chis" "Zse-tilth" "Satill"})
        (rules "Forty stones arranged in a circle, with a larger gap after every tenth stone, the spaces between the stones are the playing spaces. Four or six players, played on two teams, with two sticks per team, starting in one of the gaps. Three sticks, round on one side and flat on the other, used as dice. The throws are as follows: three round sides = 10; two round sides = 2; one round side = 3; three flat sides = 5. A throw of 10 grants the player another throw. Pieces move according to the throws in opposite directions around the board. The team to get their pieces to the starting gap wins.")
        (source "Culin 1907: 90-91.")
        (id "839")
        (version "1.3.5")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin "This game was played in Arizona, around 1907.")
        }
    )
    
    (graphics {
        (show Edges (colour Hidden))
        (show Symbol "disc" "AllSites" Vertex fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
        (board Background image:"disc" edgeColour:(colour Black) scale:0.1)
    })
    
)

