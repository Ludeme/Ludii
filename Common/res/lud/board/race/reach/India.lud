(define "ReachWinIndia"
    (if
        (and (is In (last To) #1) (= (count at:(last To)) 1))
        (result #2 Win)
    )
)

//------------------------------------------------------------------------------

(game "India"
    (players 2)
    (equipment {
        (board (square 6))
        (hand Each)
        (piece "Marker" Each)
        (regions P1 (sites Top))
        (regions P2 (sites Bottom))
    })
    (rules
        (start {
            (place "Marker" "Hand" count:10)
            (place "Marker1" (expand (sites Bottom)))
            (place "Marker2" (expand (sites Top)))
        })
        (play 
            (or {
                (move 
                    (from (handSite Mover))
                    (to (difference (sites Occupied by:Mover) (handSite Mover)))
                )
                (forEach Piece 
                    (move Step 
                        (to if:(and (is Empty (to)) (> (count at:(from)) 0)))
                        (then
                            (fromTo (from (last From)) (to (last To)) count:(count at:(last From)))
                        )
                    )
                )
                (forEach Piece 
                    (move Step
                        (to
                            if:(and {
                                ("IsEnemyAt" (to))
                                (> (count at:(from)) 0)
                                (= (+ (count at:(to)) 1) (count at:(from)))
                            })
                            (apply (remove (to) count:(count at:(to))))
                        )
                        (then
                            (fromTo (from (last From)) (to (last To)) count:(count at:(last From)))
                        )
                    )
                )
            })
        )
        (end {
            ("ReachWinIndia" (sites Mover) Mover)
            (if (= (count Sites in:(forEach (intersection (sites Board) (sites Occupied by:Next)) if:(= (count at:(site)) 1))) 0)
                (result Next Loss)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "India is a strategic two-player game played on a 6x6 board, where players move and capture stacks of stones with the ultimate goal of moving a single stone to the opposite side of the board.")
        (rules "India is a two-player game played on a 6x6 board, where each player starts with 12 stones off the board and 12 stones on the board, with the first two rows on each player's side being full. On each turn, a player must either drop an off-board stone onto a friendly stack, move a friendly stack to an adjacent (orthogonal or diagonal) empty cell, or capture an adjacent (orthogonal or diagonal) enemy stack with exactly one less stone than the moved stack, with captured stones being removed from play. The goal of the game is to be the first player to move a stack of size one to the last row on the opposite side of the board, and a player who has no stacks of size one left on the board loses the game.")
        (id "4277")
        (source "<a href=\"https://www.di.fc.ul.pt/~jpn/gv/india.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />The world of Abstract Games</a>")
        (version "1.3.14")
        (classification "board/race/reach")
        (author "Martin Windischer ")
        (credit "Achille Morenville")
        (date "2003")
        }
    )
)
