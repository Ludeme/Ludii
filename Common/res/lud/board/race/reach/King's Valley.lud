(define "Move" (move (from) (to (sites LineOfSight Farthest at:(from)))))

//-------------------------------------------------------------------------

(game "King's Valley"
    (players 2)
    (equipment {
        (board (square 5))
        (piece "Disc" Each "Move")
        (piece "King" Each "Move")
    })
    (rules 
        (start {
            (place "King1" (intersection (sites Bottom) (sites Column 2)))
            (place "King2" (intersection (sites Top) (sites Column 2)))
            (place "Disc1" (difference (sites Bottom) (sites Column 2)))
            (place "Disc2" (difference (sites Top) (sites Column 2)))
        })
        (play (forEach Piece))
        (end (if (is In (where "King" Mover) (sites Centre)) (result Mover Win)))
    )
)

//-------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The game is played on a 5x5 board with the center square representing the King's Valley. Players start with 5 pieces on opposites sites of the board, with the center pieces being the kings.")
        (rules "King's Valley is a very simple and easy game to play. This is because all the pieces move the same. Any piece can move straight horizontal, vertical or diagonal, but always as far as possible. Pieces always stop their movement either at the sides of the board or before another piece in the same row, column, or diagonal.
        The winner is the first player that manages to move his king piece to the central square of the board, which represents the King's Valley.")
        (id "1160")
        (source "<a href=\"https://boardgamegeek.com/boardgame/86169/kings-valley\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.11")
        (classification "board/race/reach")
        (author "Mitsuo Yamamoto")
        (publisher "<a href=\"http://www.logygames.com/english/index.html\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Logy Games</a> (used with permission)")
        (credit "Eric Piette")
        (date "2006")
        }
    )
    
    (graphics {
        (board Style Chess)
        (board Colour Phase0 (colour White))
        (board Colour Phase1 (colour VeryLightBlue))
        (board Colour OuterEdges (colour Yellow))
        (show Symbol "sun" (sites Centre) fillColour:(colour Yellow)) 
    })
    
    (ai
        "King's Valley_ai"
    )
)
