(define "StepMove"
    (move Step 
        Orthogonal 
        (to 
            if:(and 
                (is In (to) (difference (union (sites Occupied by:Next) (sites Empty)) (union (sites "Water") (sites Mover)))) 
                (<= (state at:(to)) (state at:(from))) 
            ) 
            (apply (remove (to))) 
        )
    )
)

(define "HopTheWater"
    (move Hop 
        #1 
        (between 
            #2 
            if:(is In (between) (forEach (sites "Water") if:(is Empty (site)))) 
            (apply (remove (between)))
        ) 
        (to if:(and { 
                (not (is In (to) (sites "Water"))) 
                (not ("IsFriendAt" (to))) 
                (<= (state at:(to)) (state at:(from))) 
            })
        ) 
    )
)

(define "LosePower" (set State at:(last To) 0))

(define "RestoredPower" (set State at:(last To) (mapEntry (what at:(last To)))))

//------------------------------------------------------------------------------

(game "Jungle" 
    (players 2) 
    (equipment { 
        (board (rectangle 9 7))
        
        (piece "Elephant" Each "StepMove") 
        (piece "Lion" Each 
            (or {
                "StepMove" 
                ("HopTheWater" (directions {W E}) (max 2))
                ("HopTheWater" (directions {N S}) (max 3))
            })
        )  
        (piece "Tiger" Each 
            (or {
                "StepMove"
                ("HopTheWater" (directions {W E}) (max 2))
                ("HopTheWater" (directions {N S}) (max 3))
            })
        )  
        (piece "Leopard" Each "StepMove") 
        (piece "Dog" Each "StepMove") 
        (piece "Wolf" Each "StepMove") 
        (piece "Cat" Each "StepMove") 
        (piece "Rat" Each 
            (move Step 
                Orthogonal 
                (to 
                    if:(and 
                        (is In (to) (difference (union (sites Occupied by:Next) (sites Empty)) (sites Mover))) 
                        (or 
                            (<= (state at:(to)) (state at:(from))) 
                            // A rat can capture an elephant if it is not in the water first.
                            (and 
                                (not (is In (from) (sites "Water"))) 
                                ("IsPieceAt" "Elephant" Next (to))
                            )
                        ) 
                    ) 
                    (apply (remove (to)))
                )
            )
        ) 
        
        (regions "Water" (sites {"B4" "C4" "B5" "C5" "B6" "C6" "E4" "F4" "E5" "F5" "E6" "F6"})) 
        (regions "Trap" (sites {"C1" "E1" "D2" "D8" "C9" "E9"})) 
        (regions "Den" P1 (sites {"D1"})) // The goal of P1
        (regions "Den" P2 (sites {"D9"})) // The goal of P2
        
        // The original value of each piece (index/value)
        (map {(pair 1 8) (pair 2 8) (pair 3 7) (pair 4 7) (pair 5 6) (pair 6 6) (pair 7 5) (pair 8 5) (pair 9 4) (pair 10 4) (pair 11 3) (pair 12 3) (pair 13 2) (pair 14 2) (pair 15 1) (pair 16 1)}) 
        }
    ) 
    (rules 
        (start {
            (place "Rat1" coord:"G3" state:1) (place "Rat2" coord:"A7" state:1)
            (place "Cat1" coord:"B2" state:2) (place "Cat2" coord:"F8" state:2)
            (place "Wolf1" coord:"C3" state:3) (place "Wolf2" coord:"E7" state:3)
            (place "Dog1" coord:"F2" state:4) (place "Dog2" coord:"B8" state:4)
            (place "Leopard1" coord:"E3" state:5) (place "Leopard2" coord:"C7" state:5)
            (place "Tiger1" coord:"A1" state:6) (place "Tiger2" coord:"G9" state:6)
            (place "Lion1" coord:"G1" state:7) (place "Lion2" coord:"A9" state:7)
            (place "Elephant1" coord:"A3" state:8) (place "Elephant2" coord:"G7" state:8)
        })
        
        (play 
            (forEach Piece 
                (then 
                    (and 
                        (if
                            (is In (last From) (sites "Trap")) 
                            "RestoredPower"
                        ) 
                        (if 
                            (is In (last To) (sites "Trap")) 
                            "LosePower"
                        )
                    )
                )
            )
        )
        
        (end ("ReachWin" (sites Next) Mover))
    )
)

(metadata
    
(info
{
(description "Jungle is a modern game played in China and Southeast Asia and popular with children. Its origins are not well understood.")
(aliases {"The Jungle Game" "Children's Chess" "Animal Chess" "dòushòuqí"})
(rules "Each turn consists of moving a piece one square orthogonally in any direction. An animal may eat any animal smaller than itself by moving on to its square. The only exception to this rule is that the rat can kill the elephant. If the same animals meet, the animal moving on to a square eats the animal already there. Three pieces have special powers. When the rat reaches the river it can enter it and move along those squares squares as if it were any other. If it is in the river no other animal can attack it. The rat is unable to attack the elephant from the river. If both rats meet in the river the moving piece eats the other one. When a lion or a tiger reaches a square on the edge of the river, at the next move it can jump over the river in any orthogonal direction, landing on the nearest land square. It captures any smaller animal on that square: if, however, there is a rat in the river in the line of the jump, it blocks this move. Each side has three trap-squares and the player's own pieces may move on and off them without restriction, but if an enemy animal occupies a trap-square, it loses all its power and becomes weaker than any defending piece. As soon as it moves out of the trap it regains its full strength. A player may not move any of his animals on to his own den. The player who moves any of their pieces into the enemy's den wins.")
(source "<a href=\"https://en.wikipedia.org/wiki/Jungle_(board_game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(id "392")
(version "1.3.7")
(classification "board/race/reach")
(credit "Eric Piette")
}
)
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
        (piece Background image:"disc")
        (piece Colour fillColour:(colour White))
        (piece Scale 0.75)
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (board Colour Phase0 (colour White))
        (board Colour Symbols (colour Black))
        (show Symbol "square" {2 4 10 52 58 60} scale:0.9)
        (show Symbol "disc" {3 59} scale:0.9)
        (show Symbol "rat" {20 42} scale:0.65)
        (show Symbol "leopard" {18 44} scale:0.65)
        (show Symbol "wolf" {16 46} scale:0.65)
        (show Symbol "elephant" {14 48} scale:0.65)
        (show Symbol "cat" {8 54} scale:0.65)
        (show Symbol "dog" {12 50} scale:0.65)
        (show Symbol "lion" {6 56} scale:0.65)
        (show Symbol "tiger" {0 62} scale:0.65)
        (show Symbol "waves" {22 23 29 30 36 37 25 26 32 33 39 40} scale:0.95)
    })
    
    (ai 
        "Jungle_ai"
    )
    
)
