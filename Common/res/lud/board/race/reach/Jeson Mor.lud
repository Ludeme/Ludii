(define "CaptureToPieceAndCheckVictory"
    (apply
        (if ("IsEnemyAt" (to))
            (remove (to))
       )
   ) 
)

//------------------------------------------------------------------------------

(game "Jeson Mor"  
    (players 2)  
    (equipment { 
        (board (square 9)) 
        (piece "Knight" Each 
            (move Leap 
                "KnightWalk"
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPieceAndCheckVictory"
               ) 
           )
       )
    })  
    (rules 
        (start {
            (place "Knight1" (sites Bottom))
            (place "Knight2" (sites Top))
        })
        (play (forEach Piece))
        (end {
            (if (no Pieces Next) (result Mover Win))
            (if (is In <End:rule> (sites Centre)) (result Mover Win))
        })
   )
)

//------------------------------------------------------------------------------

(option "Variants" <End> args:{ <rule>} {
    (item "Original"  <(last To)>  "To win a knight as to reach the central square.")*   
    (item "Wikipedia" <(last From)> "To win a knight as to leave the central square.")   
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Jesön Mor' (the nine horses) is a game played in Mongolia. Two sides attempt to reach the central square on the board. ")
        (aliases {"Jesön Mor'"})
        (rules "9x9 board. Nine horse-shaped pieces per player. Pieces begin in the spaces along opposite sides of the board. Pieces move orthogonally one space and then diagonally another, jumping over any intervening pieces. A player may capture one of the opponent's pieces by moving onto a space occupied by the opponent's piece. The goal is to move to the central space on the board.")
        (source "Popova 1974: 32-35")
        (id "1035")
        (version "1.3.4")
        (classification "board/race/reach")
        (credit "Nicholas Bamber")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
   )
    
    (graphics {
        (board Style Chess)
    })
    
    (ai
        "Jeson Mor_ai"
   )
)
