(define "ThrowValue" (mapEntry "Throw" (count Pips)))
(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #2 #1))
(define "IsSpecialThrow" (is In ("ThrowValue") (sites {1 8})))
(define "Throw1" (= ("ThrowValue") 1))

(define "AllPiecesOnCentre"
    (and
        (= 1 (count Sites in:(sites Occupied by:#1 top:False)))
        (is In (centrePoint) (sites Occupied by:#1 top:False))
    )
)

//------------------------------------------------------------------------------

(game "Siga (Sri Lanka)"
    (players 2)
    (equipment {
        (board (square 5)
            {
            (track "Track1" "2,E,N,W,S,E1,N3,E2,S2,W1,N1" P1 directed:True)
            (track "Track2" "22,W,S,E,N,W1,S3,W2,N2,E1,S1" P2 directed:True)
            }
        )
        (piece "Marker" Each
            (move 
                (from (from) level:(level))
                (to
                    ("SiteToMoveOnTrack" from:(from) ("ThrowValue"))
                    if:True
                    (apply
                        (if (and (not (is In (to) (sites "SafeSites"))) ("IsEnemyAt" (to)))
                            (and
                                (forEach Level (to) FromTop
                                    (fromTo
                                        (from (to) level:(level))
                                        (to (handSite (who at:(to) level:(level))))
                                    )
                                )
                                (moveAgain)
                            )
                        )
                    )
                )
            )		
        )
        (hand Each)
        ("StickDice" 4)
        (map "Throw" {(pair 0 8) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4)})
        (map "Entry" {(pair P1 2) (pair P2 22)})
        (regions "SafeSites" (sites {2 10 12 14 22}))
    })
    (rules 
        (start {
            (place Stack "Marker1" (handSite P1) count:2)
            (place Stack "Marker2" (handSite P2) count:2)
        })
        (play 
            ("RollMove"
                (or
                    (if (and (is Occupied (handSite Mover)) ("Throw1"))
                        (move 
                            (from (handSite Mover))
                            (to (mapEntry "Entry" (mover)))
                        )
                    )
                    (forEach Piece)
                )
                (then (if ("IsSpecialThrow") (moveAgain)))
            )
        )
        (end (if ("AllPiecesOnCentre" Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Siga, as played in Sri Lanka, is a race game similar to Saturankam, which is also played there. Typically, it is played on a cloth with pawns, or drawn on the ground with sticks as the pawns.")
        (rules "5x5 board, with an X in the central square of each side of the board, as well as the central square of the board. Two players. Two pieces per player. Four cowrie shells used as dice. Throws have the value of the number of mouths that land face up, with no mouths up = 8. Players start on opposite sides of the board, the first point on their track being the marked square on that side. Players move in an anti-clockwise direction around the board, until reaching the space before the starting space, at which point they move into the space above it, and proceed around the inner part of the board in a clockwise direction, until below the central square. Players enter a piece with a throw of 1. Throws of 1 or 8 give the player another turn. When a player's piece lands on the same space as a space with an opponent's piece, the opponent's piece is removed from the board and must enter again, and the player causing this receives another throw. Pieces resting on a marked square cannot be sent back to start. To reach the central space, the player must throw the exact number required. The first player to move both pieces to the central space first wins.")
        (source "Parler 1909: 607-608.")
        (id "745")
        (version "1.3.9")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin "This game was played in Sri Lanka, around 1909.")
        }
    )
    
    (graphics {
        (show Symbol "thinCross" {2 10 12 14 22} scale:0.9)
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (piece Scale "Marker" 0.5)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
