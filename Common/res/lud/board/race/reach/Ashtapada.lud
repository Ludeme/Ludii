(define "SiteToMoveOnTrack" (trackSite Move steps:(mapEntry "DieValue" (count Pips))))

(define "MoveToNextSiteOnTrack" (trackSite Move steps:1))

//------------------------------------------------------------------------------

(game "Ashtapada"
    (players <Version:numPlayers>)
    (equipment
        {
        (board (square 8)
            {
            (track "Track1" "3,E,N,W,S,E1,N6,E5,S5,W4,N4,E3,S3,W1" 1 directed:True)
            (track "Track2" "60,W,S,E,N,W1,S6,W5,N5,E4,S4,W3,N3,E1" 2 directed:True)
            (track "Track3" "31,N,W,S,E,N1,W6,N5,E5,S4,W4,N3,E3,S1" 3 directed:True)
            (track "Track4" "32,S,E,N,W,S1,E6,S5,W5,N4,E4,S3,W3,N1" 4 directed:True)
            }
        )
        (dice d:2 from:0 num:4)
        (hand Each)
        (map "DieValue" { (pair 4 8) })
        <Version:map>
        (regions "Protect" (sites {"A1" "A4" "A5" "A8" "D1" "D4" "D5" "D8" "E1" "E4" "E5" "E8" "H1" "H4" "H5" "H8"}))

        (piece "Disc" Each
            (if (!= "SiteToMoveOnTrack" Off)
                (if
                    (if (is In "SiteToMoveOnTrack" (sites "Protect"))
                        (not (is Enemy (who at:"SiteToMoveOnTrack")))
                        True
                    )
                    (move
                        (from)
                        (to
                            "SiteToMoveOnTrack"
                            (apply
                                if:(is Enemy (who at:(to)))
                                (fromTo (from (to)) (to (handSite (who at:(to)))))
                            )
                        )
                    )
                )
            )
        )

        }
    )

    (rules
        <Version:start>

        (play
            (do (if (not "SameTurn") (roll))
                next:(if "SameTurn"
                    (or
                        (forEach Piece
                            (if (= (from) (mapEntry "Exit" Mover))
                                (move Remove (from))
                                (if (!= "MoveToNextSiteOnTrack" Off)
                                    (if
                                        (if (is In "MoveToNextSiteOnTrack" (sites "Protect"))
                                            (not (is Enemy (who at:"MoveToNextSiteOnTrack")))
                                            True
                                        )
                                        (move
                                            (from)
                                            (to
                                                "MoveToNextSiteOnTrack"
                                                (apply
                                                    if:(is Enemy (who at:(to)))
                                                    (fromTo
                                                        (from (to))
                                                        (to (handSite (who at:(to))))
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                        (if
                            (not ("HandEmpty" Mover))
                            (move
                                (from (handSite Mover))
                                (to
                                    (mapEntry "Entry" Mover)
                                    if:(not (is Enemy (who at:(to))))
                                )
                            )
                        )
                    )
                    (if (can Move (forEach Piece))
                        (forEach Piece (then (if (= (count Pips) 3) (moveAgain))))
                        (move Pass (then (if (= (count Pips) 3) (moveAgain))))
                    )
                )
            )
        )

        (end (if (no Pieces Mover) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Players" <Version> args:{ <numPlayers> <start> <map>}
    {
    (item "2"
        <2>
        <
        (start {
            (place "Disc1" (handSite P1) count:2)
            (place "Disc2" (handSite P2) count:2)
        })
        >
        <
        (map "Entry" { (pair P1 "D1") (pair P2 "E8")})
        (map "Exit" { (pair P1 "E3") (pair P2 "D6")})
        >
        "The version of the game played with 2 players."
    )*
    (item "4"
        <4>
        <
        (start {
            (place "Disc1" (handSite P1) count:2)
            (place "Disc2" (handSite P2) count:2)
            (place "Disc3" (handSite P3) count:2)
            (place "Disc4" (handSite P4) count:2)
        })
        >
        <
        (map "Entry" { (pair P1 "D1") (pair P2 "E8") (pair P3 "H4") (pair P4 "A5")})
        (map "Exit" { (pair P1 "E3") (pair P2 "D6") (pair P3 "F5") (pair P4 "C4")})
        >
        "The version of the game played with 4 players."
    )
})

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Kile (Suggested)" {
        "Players/2"
    })*

    (ruleset "Ruleset/Historical Information (Incomplete)" {

    })

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Ashtapada is a term referring to a board game that predates Chess in India. It is widely believed that the 8x8 Ashtapada board, which is marked in certain places, was appropriated and used for Chaturanga, since many Chaturanga boards in India maintain the Ashtapada markings, though they are meaningless for chess. In the Ashtapabada of Balabharata (thirteenth century), a dice game is described as being played on the Ashtapada, with dice determining the movement of pieces on the board.")
        (useFor "Ruleset/Kile (Suggested)" (rules "To be reconstructed. The rules implemented here are suggested by Jenny Kile as follows: Besides being an 8x8 squared board, the board consists of certain marked spaces which denote safety, entry, or exit for players. The four corners, the four central squares, and two center squares on each side are crosscut, like shown. Two, three, or four people can play the game. If two players are playing, each would sit on one side of the board and enter their two pieces onto the center crosscut squares along the outer side The object of the game is to be the first player to enter and exit both his pieces. A player exits his piece once reaching the center squares. "))
        (useFor "Ruleset/Kile (Suggested)" (source "<a href=\"https://allaboutfunandgames.com/the-ancient-game-of-ashtapada\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />allaboutfunandgames</a>"))
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "8x8 board. One die. Two players. One plays as black, the other as red. "))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP evidence."))
        (version "1.3.0")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin  "This game was played in India, from around 1200 to 1299.")
        }
    )

    (graphics {
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (board Colour Symbols (colour Black))
        (board Colour Phase0 (colour 222 173 123))
        (show Symbol "thinCross" "Protect")
        (player Colour P1 (colour Red))
    })

    (ai
        "Ashtapada_ai"
    )

)
