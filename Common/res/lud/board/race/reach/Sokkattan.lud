(define "ThrowValue" (mapEntry "Throw" (count Pips)))
(define "SiteToMoveOnTrack" (trackSite Move #1 steps:#2))

(define "IsSpecialThrow" (is In ("ThrowValue") (sites {10 25})))
(define "StopThrow" (is In ("ThrowValue") (sites {2 3 4})))

(define "Move"
    (if (= End ("SiteToMoveOnTrack" from:(from) #1))
        (move Remove (from) level:(level))
        (move
            (from (from) level:(level))
            (to
                ("SiteToMoveOnTrack" from:(from) #1)
                if:(not (and (is In (to) (sites "SafeSites")) (is Enemy (who at:(to)))))
                (apply
                    (if (is Enemy (who at:(to)))
                        (forEach Level (to) FromTop
                            (fromTo
                                (from (to) level:(level))
                                (to (handSite (who at:(to) level:(level))))
                            )
                        )
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Sokkattan"
    (players 4)
    (equipment {
        (board
            (add
                (hole
                    (merge
                        (shift 0 8 (rectangle 3 19))
                        (shift 8 0 (rectangle 19 3))
                    )
                    (poly {{8 8} {8 11} {11 11} {11 8}})
                )
                cells:{ { 8 28 48 68 69 70 71 51 31 11 10 9 } }
            )
            {
            (track "Track1" "96,9,S,E,N,11,E,N,W,53,N,W,S,42,W,S,E,8,S,E1,N7,End" P1 directed:True)
            (track "Track2" "96,26,W,S,E,8,S,E,N,11,E,N,W,53,N,W,S,42,W,S1,E7,End" P2 directed:True)
            (track "Track3" "96,52,N,W,S,42,W,S,E,8,S,E,N,11,E,N,W,53,N,W1,S7,End" P3 directed:True)
            (track "Track4" "96,27,E,N,W,53,N,W,S,42,W,S,E,8,S,E,N,11,E,N1,W7,End" P4 directed:True)
            }
        )
        (hand Each)
        (dice d:2 from:0 num:6)
        (piece "Pawn" Each ("Move" ("ThrowValue")))
        (regions "SafeSites" (sites {67 63 65 23 38 3 82 84 86 30 47 15}))
        (map "Throw" {(pair 0 6) (pair 1 10) (pair 2 2) (pair 3 3) (pair 4 4) (pair 5 25) (pair 6 12)})
        (map "Entry" {(pair P1 67) (pair P2 23) (pair P3 82) (pair P4 30)})
    })
    (rules
        (start {
            (set Team 1 {P1 P3})
            (set Team 2 {P2 P4})
            (place Stack "Pawn1" (handSite P1) count:3)
            (place Stack "Pawn2" (handSite P2) count:3)
            (place Stack "Pawn3" (handSite P3) count:3)
            (place Stack "Pawn4" (handSite P4) count:3)
        })

        (play
            (do (roll)
                next:(if (= (var "ExtraOne") 1)
                    (priority {
                        (if (is Occupied (handSite Mover))
                            (move
                                (from (handSite Mover))
                                (to (mapEntry "Entry" Mover))
                            )
                        )
                        (forEach Piece ("Move" 1))
                        }
                        (then
                            (and
                                (moveAgain)
                                (set Var "ExtraOne" 0)
                            )
                        )
                    )
                    (or
                        (if (and (is Occupied (handSite Mover)) ("IsSpecialThrow"))
                            (move
                                (from (handSite Mover))
                                (to (mapEntry "Entry" Mover))
                            )
                        )
                        (forEach Piece)
                    )
                )
                (then
                    (if (not ("StopThrow"))
                        (and
                            (if ("IsSpecialThrow") (set Var "ExtraOne" 1))
                            (moveAgain)
                        )
                    )
                )
            )
        )

        (end  (if (no Pieces TeamMover) (result TeamMover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Sokkattan is a race game played on a cross-shaped board played in Sri Lanka. It is closely related to similar games found throughout South Asia.")
        (aliases {"Sorkettan" "Sorkattan"})
        (rules "Four 3x8 rectangles, arranged in a cross shape around a large central square space. Squares marked with \"X\" (counting from top of each row): fifth in each outer row, fourth in central row. Four players playing on two teams. Three pieces per player. Play begins in the central row of each player's arm of the board. Six cowrie shells used as dice. Values of the cowries are: 0=6, 1=10, 2=2, 3=3, 4=4, 5=25, 6=12. A roll of 10 or 25 adds an extra move of 1. This extra move must be used to enter a new piece on the board if possible, if not possible then it can be assigned to any piece. Pieces on a space marked \"x\" are safe from being captured. Players play until they throw a 2, 3, or 4. The value of an individual roll can only move one piece, but multiple pieces can be moved in turns with multiple rolls. Pieces enter the board with a throw of 10 or 25. Pieces enter the board from the central square onto the central row of their arm of the cross, and then anti-clockwise around the board, then back up the central row of their arm, entering the central square only with an exact throw. The first team to move all of their pieces into the central square wins.")
        (source "Parker 1909: 619-621.")
        (version "1.3.0")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin  "This game was played in Sri Lanka, around 1906.")
        }
    )

    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Green))
        (player Colour P3 (colour Yellow))
        (player Colour P4 (colour Black))
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
        (stackType 3 Count)
        (stackType 4 Count)
        (show Symbol "thinCross" {67 63 65 23 38 3 82 84 86 30 47 15} scale:0.9)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Scale "Pawn" 0.5)
    })

)
