(define "ThrowValue" (mapEntry "Throw" (count Pips)))
(define "Throw1" (= ("ThrowValue") 1))
(define "SiteToMoveOnTrack" (trackSite Move #1 steps:#2))
(define "BankorOnTrack" (trackSite Move from:("WhereBankor") #1 steps:#2))
(define "BankorPhaseDone" (or 
        (= ("WhereBankor") ("Center"))
        (and
            (!= 0 (what at:("Center")))
            (>= 1 (count Sites in:(difference (sites Occupied by:All) ("WhereBankor"))))
        )
    )
)

(define "WhereBankor" (where "Bankor" Neutral))
(define "LevelBankor" (where Level "Bankor" Neutral at:("WhereBankor")))
(define "StateBankor" (state at:("WhereBankor") level:("LevelBankor")))

(define "Center" 0)

(define "MoveBankor"
    (move
        (from ("WhereBankor") level:("LevelBankor"))
        (to ("BankorOnTrack" "Track" ("ThrowValue")))
        (then
            (and
                (if (!= (mover) "StateBankor")
                    (set State at:("WhereBankor") level:("LevelBankor") (mover))
                )
                (if (= (last To) ("Center"))
                    (forEach Site (sites Track "Track" from:(last From) to:(last To))
                        (if (!= (site) (last To))
                            (forEach Level (site) FromTop
                                (remove (site) level:(level)) 
                            )
                        )
                    )
                    (forEach Site (sites Track "Track" from:(last From) to:(last To))
                        (if (!= (site) (last To))
                            (forEach Level (site) FromTop
                                (fromTo 
                                    (from (site) level:(level))
                                    (to (last To))
                                )
                            )
                        )
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Sik"
    (players <Player:number>)
    (equipment {
        (board 
            (spiral turns:5 sites:86) 
            {
            (track "Track" {85..0} directed:True)
            }
            use:Vertex
        )
        (dice d:2 from:0 num:4)
        (map "Throw" {(pair 0 4) (pair 1 3) (pair 2 0) (pair 3 1) (pair 4 6)})
        (piece "Stick" Each
            (move
                (from (from) level:(level)
                    if:(or 
                        (= 1 (state at:(from) level:(level)))
                        ("Throw1")
                    )
                )
                (to
                    ("SiteToMoveOnTrack" "Track" ("ThrowValue"))
                    if:True
                )
                (then
                    (if (!= 1 (state at:(last To) level:(last LevelTo)))
                        (set State at:(last To) level:(last LevelTo) 1)
                    )
                )
            )
        )
        (piece "Bankor" Neutral)
    })
    (rules 
        (start {
            (place Stack items:<Player:init> 85)
            (place Stack "Bankor0" 85)
        })
        
        (play 
            (do (roll) 
                next:(if (= ("Center") (where "Stick" Mover))
                    (if (< (value Player Mover) 2)
                        (move Pass (then (set Value Mover (+ 1 (value Player Mover)))))
                        (if (or (= (mover) ("StateBankor")) (= 0 ("StateBankor")))
                            (if (!= 0 ("ThrowValue")) ("MoveBankor"))
                        )
                    )
                    (if (!= 0 ("ThrowValue")) (forEach Piece))
                )
            )
        )
        (end {
            (forEach Player
                if:(no Pieces Player) 
                (result Player Loss)
            )
            (forEach Player
                if:("BankorPhaseDone")
                (result Player Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <number> <init>}
    {
    (item "2" <2> <{"Stick2" "Stick1"}> "The game has 2 players.") 
    (item "3" <3> <{"Stick3" "Stick2" "Stick1"}> "The game has 3 players.")  
    (item "4" <4> <{"Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 4 players.")*  
    (item "5" <5> <{"Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 5 players.") 
    (item "6" <6> <{"Stick6" "Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 6 players.") 
    (item "7" <7> <{"Stick7" "Stick6" "Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 7 players.") 
    (item "8" <8> <{"Stick8" "Stick7" "Stick6" "Stick5" "Stick4" "Stick3" "Stick2" "Stick1"}> "The game has 8 players.") 
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Sik (Observed)" {
        "Players/4"
        }
        variations:{"Players/2" "Players/3" "Players/5" "Players/6" "Players/7" "Players/8"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sik is a race game played in the Tidikelt region of Algeria by women and children. Played on a spiral board made in the sand, players race to the center and try not to be captures by the \"Bankor.\"")
        (aliases {"Sig"})
        (rules "Spiral board of any number of spaces, rendered as holes in the sand. The first space is a mound. Any number of players. One piece per player, and one piece, the \"bankor,\" all of which begin on the first space on the outer ring of the spiral. Four sticks, with one green and one white side, used as dice. The throws are as follows: One white side up = 1; two white sides up = 0, or 8 when playing as the bankor; three white sides up = 3; four white sides up = 4; four green sides up = 6. A player must throw a 1 to begin moving their piece. Pieces proceed along the spiral toward the central spot. The first player to reach the spot allows the other players three turns, and then begins playing as  the bankor. When the bankor overtakes another player's piece, it drags them with it. When the bankor reaches the penultimate spot, the players it is dragging with them loses. ")
        (source "Voinot 1909: 133-134.")
        (id "1189")
        (version "1.3.3")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin  "This game was played in Algeria, from around 1899 to 1906.")
        }
    )
    
    (graphics {
        (board Style Spiral)
        (stackType 0 Ground)
        (piece Rename piece:"Bankor" "Marker")
        (piece Colour "Die" state:1 fillColour:(colour Green))
        (piece Scale "Stick" 0.5)
        (piece Scale "Bankor" 0.5)
        (hand Placement P1 scale:0.2 offsetX:0.7 offsetY:0.9)
        (player Colour Neutral (colour Black))
    })
    
)

