(define "SameDirection" 
    (directions Cell from:#1 to:(where "Ball" Shared))
)

(define "KickerIsA" 
    (and ("IsPieceAt" #1 Mover #2) (is In (where "Ball" Shared) (sites Around #2 #3)))
)

(define "InBottomOrTop"
    (is In #1 (union (sites Bottom) (sites Top)))	
)

(define "NotInOrThroughGoal"
    (and (is Empty (between)) (not (is In (between) (sites "Goals"))))	
)

(define "KickTheBall"
    (if ("KickerIsA" "Knight" #1)
        (move
            (from (where "Ball" Shared))
            (to
                (sites To (leap (from (where "Ball" Shared)) "KnightWalk" (to if:(is Empty (to)))))
                if:(not (is In (to) (sites Around #1)))
            )
        )
        (if ("KickerIsA" "King" #1)
            (move
                (from (where "Ball" Shared))
                (to
                    (sites To (step (from (where "Ball" Shared)) ("SameDirection" #1) (to if:(is Empty (to)))))
                    if:(if ("InBottomOrTop" #1) (not (is In (to) (sites "Goals"))) True)
                )
            )
            (if ("KickerIsA" "Queen" #1)
                (move
                    (from (where "Ball" Shared))
                    (to
                        (sites To
                            (slide (from (where "Ball" Shared)) ("SameDirection" #1)
                                (between if:(if ("InBottomOrTop" #1) ("NotInOrThroughGoal") (is Empty (between))))
                            )
                        )
                    )
                )
                (if ("KickerIsA" "Rook" #1 Orthogonal)
                    (move
                        (from (where "Ball" Shared))
                        (to
                            (sites To
                                (slide (from (where "Ball" Shared)) ("SameDirection" #1)
                                    (between if:(if ("InBottomOrTop" #1) ("NotInOrThroughGoal") (is Empty (between))))
                                )
                            )
                        )
                    )
                    (if ("KickerIsA" "Bishop" #1 Diagonal)
                        (move
                            (from (where "Ball" Shared))
                            (to
                                (sites To
                                    (slide (from (where "Ball" Shared)) ("SameDirection" #1)
                                        (between if:(if ("InBottomOrTop" #1) ("NotInOrThroughGoal") (is Empty (between))))
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        #2
    )		
)

(define "KickBallAgainWithoutMoving" (is Pending))
(define "CanKickAgainWithoutMoving" (and (set Pending) (moveAgain)))

//------------------------------------------------------------------------------

(game "Football Chess" 
    (players 2) 
    (equipment { 
        (board (square 9)) 
        
        (piece "Rook" Each (move Slide Orthogonal (to (apply if:(not (is In (to) (sites "Goals")))))))
        (piece "King" Each (move Step (to if:(and (is Empty (to)) (not (is In (to) (sites "Goals")))))))
        (piece "Bishop" Each (move Slide Diagonal (to (apply if:(not (is In (to) (sites "Goals")))))))
        (piece "Knight" Each (move Leap "KnightWalk" (to if:(and (is Empty (to)) (not (is In (to) (sites "Goals")))))))
        (piece "Queen" Each (move Slide (to (apply if:(not (is In (to) (sites "Goals")))))))
        (piece "Ball" Shared)
        (regions "Goals" (sites {"E1" "E9"}))
        (map "Goal" {(pair P1 "E9") (pair P2 "E1")})
    }) 
    (rules 
        (meta (no Repeat PositionalInTurn))
        (start { 
            (place "Rook1" {"A1" "I1"}) (place "Knight1" {"B1" "H1"}) (place "Bishop1" {"C1" "G1"}) (place "Queen1" coord:"D1") (place "King1" coord:"F1") 
            (place "Rook2" {"A9" "I9"}) (place "Knight2" {"B9" "H9"}) (place "Bishop2" {"C9" "G9"}) (place "Queen2" coord:"D9") (place "King2" coord:"F9") 
            (place "Ball" {"E5"})
        })
        
        (play 
            (if ("SameTurn")
                (or
                    (if ("KickBallAgainWithoutMoving")
                        (forEach Site (sites Around (where "Ball" Shared)) ("KickTheBall" (site))
                            (then (if (can Move (forEach Site (sites Around (where "Ball" Shared)) ("KickTheBall" (site)))) ("CanKickAgainWithoutMoving")))		
                        )
                        ("KickTheBall" (last To) 
                            (then (if (can Move (forEach Site (sites Around (where "Ball" Shared)) ("KickTheBall" (site)))) ("CanKickAgainWithoutMoving")))
                        )
                    )
                    (move Pass)
                )
                (or 
                    (forEach Piece (then (if (can Move ("KickTheBall" (last To))) (moveAgain))))
                    (forEach Site 
                        (sites Around (where "Ball" Shared)) 
                        ("KickTheBall" (site))
                        (then (if (can Move (forEach Site (sites Around (where "Ball" Shared)) ("KickTheBall" (site)))) ("CanKickAgainWithoutMoving")))		
                    )
                )
            )
        )
        
        (end {
            (if (= (where "Ball" Shared) (mapEntry P1)) (result P1 Win))
            (if (= (where "Ball" Shared) (mapEntry P2)) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This variant of Chess was invented in 1951 by Joseph Boyer, one of the most productive inventors of chess variants.The game is played on a board of 9 by 9 squares, checkered, with black squares in the corners.Each player has a king, a queen, two rooks, two knights, and two bishops, i.e., the usual set of pieces without pawns. There is an additional piece, not owned by any player: the ball.The ball starts the game on e5. The squares e1 and e9 are the goals.")
        (rules "A move of a player consists of two parts: he first makes a normal move with one of his pieces (this is not obligatory), but no piece may move to a goal, i.e., to e1 or e9. (Pieces like rooks and queens however may move across it, e.g. from d1 to f1.) However, no captures are made, and check is disregarded. Then, after this move, the player may, if he can, have a piece kick the ball.
            
            A piece can kick the ball if the ball is adjacent to it (i.e., a kings move away.) The ball moves in the same way as the piece that kicks the ball, and the ball must be moved directly away from the piece. Also, the ball cannot be moved to or over occupied squares (except when a knight kicks the ball). For instance, a rook on a3 can kick a ball on a4 to squares a5, a6, a7, a8, a9, as long as the ball isn't moved to an occupied square; but a rook on a3 cannot kick a ball on b4.
            
            A knight can kick the ball to any square, a knight-move away from the square where the ball was before the kick, but not to a square, adjacent to the knight.
            
            When the ball is kicked to a square adjacent to another piece of the same player, the player may make another kick. This is called a `pass'. If after a second kick, a third kick is possible, the player may also make this third kick, and similar for successive kicks. Making an infinite number of passes, in order to draw the game, however is not allowed. Note that when kicking, only the ball moves but all normal pieces remain on the same places.
            
        The object of the game is to kick to ball to the goal at the opponents side of the board. However, it is not allowed to kick the ball into or across the goal from a square on the 1st or 9th row.")
        (id "1793")
        (source "<a href=\"https://www.chessvariants.com/crossover.dir/football.html\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.chessvariants.com</a>")
        (version "1.3.14")
        (classification "board/race/reach")
        (author "Joseph Boyer")
        (credit "Eric Piette")
        (date "1951")
        }
    )
    
    (graphics {
        (show Check "King")
        (piece Scale "Pawn" 0.825)
        (piece Families {"Defined" "Microsoft" "Pragmata" "Symbola"})
        (board Style Chess)
        (show Line {{14 4} {4 5} {5 15} {94 95} {94 84} {95 85}} (colour Black) scale:2)
    })
    
    (ai
        "Football Chess_ai"
    )
)
