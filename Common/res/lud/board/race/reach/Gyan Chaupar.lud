(game "Gyan Chaupar"
    (players 4)  
    (equipment { 
        (board (square 10) (track "Track" "0,E,N1,W,N1,E,N1,W,N1,E,N1,W,N1,E,N1,W,N1,E,N1,W" directed:True))
        (piece 
            "Pawn" 
            Each 
            (move 
                (from (from) level:(level)) 
                (to (mapEntry (trackSite Move steps:(count Pips) ) ) )
            )
        )
        (dice num:1)
        (map { 
            (pair "B1" "C4") (pair "D1" "G2") (pair "H1" "J4") (pair "A3" "B5") (pair "E4" "D5") (pair "H3" "D9") (pair "J8" "J10") (pair "A8" "A10") (pair "J6" "G7")
            (pair "B7" "C2") (pair "G5" "F3") (pair "E2" "F1") (pair "D7" "A6") (pair "C10" "C8") (pair "H10" "H8") (pair "E10" "E8") (pair "G9" "D3") (pair "H6" "E6") (pair "I5" "J2")
            }
        )
    })  
    
    (rules 
        (start (place Stack items:{"Pawn4" "Pawn3" "Pawn2" "Pawn1"} coord:"A1") )	
        
        (play (do (roll) next:(forEach Piece) ) )
        
        (end (if (= (last To) (trackSite EndSite)) (result Mover Win)))  
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Gyan Chaupar is a game originating in India at an unknown time in the past. Since its origin in India, the game had moralistic tones, with setback and boons in the game associated with immoral or moral behavior, respectively. There are different variations based on Jain, Hindu, Islamic, and other religious traditions. It came to England around 1890 and was commercialized in other places like the Unites States as \"Snakes (or Chutes) and Ladders.\"")
        (aliases {"Snakes and Ladders" "Chutes and Ladders"})
        (rules "10x10 board. Each player has one piece and move according to the roll of one die. Representations of snakes and ladders are scattered throughout the board, connecting two spaces. If a player lands at the bottom of the ladder at the end of their move, they advance to the space at the top of the ladder. If a player ends their turn on a space with the head of a snake, they move down to the space with the tail of the snake. The first player to move off the last space of the board wins.")
        (source "Topsfield 2006: 86.")
        (id "78")
        (version "1.3.4")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin  "This game was played in India, from around 1735 to 1899.")
        }
    )
    
    (graphics {
        (piece Scale "Pawn" 0.5)
        (stackType 0 Ground)
        (board Style SnakesAndLadders)
    })
    
    (ai 
        "Gyan Chaupar_ai"
    )
    
)
