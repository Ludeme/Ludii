(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "SiteToMoveOnTrack" ("NextSiteOnTrack" #1))

(define "NextSite" ("NextSiteOnTrack" 1 from:#1 #2))

(define "MadeACompleteCircuit" 
    (is In (mapEntry "Start" Mover) (sites Track Mover "Track" from:("NextSite" (last From) "Track") to:(last To)))
)

(define "Move" 
    (move
        (from (from) level:(level))
        (to 
            ("SiteToMoveOnTrack" ("ThrowValue"))
            if:True
            ("HittingCapture" (mapEntry "Start" (who at:(to))))
        )
        (then
            (forEach Site (sites Track Mover "Track" from:("NextSite" (last From) "Track") to:(last To))
                (if ("IsEnemyAt" (site))
                    (forEach Level (site)
                        (move
                            (from (site) level:(level))
                            (to (mapEntry "Start" (who at:(site) level:(level))))
                        )
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Set Dilth'"
    (players <Player:num>)
    (equipment {
        ("FortyStonesWithFourGapsBoard"
            <Player:track>
        )
        (regions "AllSites" (sites Board))
        (map "Throw" {(pair 0 10) (pair 1 2) (pair 2 3) (pair 3 5)})
        (map "Start" {(pair 1 37) (pair 2 38) (pair 3 39) (pair 4 36)})
        (piece "Stick" Each ("Move"))
        ("StickDice" 3)
    })
    (rules 
        <Player:start>
        (play 
            ("RollMove" 
                (forEach Piece)
                (then 
                    (do
                        (if ("MadeACompleteCircuit") 
                            (addScore Mover 1) 
                            (then 
                                (if (= (score Mover) 3) 
                                    (remove (last To))
                                )
                            )
                        )
                        next:(if (and (!= 3 (score Mover)) (= 10 ("ThrowValue"))) (moveAgain))
                    )
                )
            )
        )
        (end (if (>= (score Mover) 4) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <num> <start> <track>}
    {
    (item "2" <2>  
        <
        (start {
            (set Team 1 {P1})
            (set Team 2 {P2})
            (place Stack "Stick1" 37)
            (place Stack "Stick2" 38)
        })
        >
        <
        {
        (track "Track1" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True P1)
        (track "Track2" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True P2)
        }
        >
    "The game has 2 players.")  
    (item "3" <3>  
        <
        (start {
            (set Team 1 {P1 P3})
            (set Team 2 {P2})
            (place Stack "Stick1" 37)
            (place Stack "Stick2" 38)
            (place Stack "Stick3" 39)
        })
        >
        <
        {
        (track "Track1" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True P1)
        (track "Track2" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True P2)
        (track "Track3" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True P3)
        }
        >
    "The game has 3 players.") 
    (item "4" <4>  
        <
        (start {
            (set Team 1 {P1 P3})
            (set Team 2 {P2 P4})
            (place Stack "Stick1" 37)
            (place Stack "Stick2" 38)
            (place Stack "Stick3" 39)
            (place Stack "Stick4" 36)
        })
        >
        <
        {
        (track "Track1" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True P1)
        (track "Track2" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True P2)
        (track "Track3" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True P3)
        (track "Track4" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True P4)
        }
        >
    "The game has 4 players.")* 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Set Dilth' (Described)" {
        "Players/4"
        }
        variations:{"Players/2" "Players/3"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Set Dilth' is a race game played by White Mountain Apache women of Arizona. While it is similar to other race games in the region, in this game players must circle the board three times in order to win.")
    (rules "Forty stones, arranged in a circle, with a larger gap after every tenth stone. The gaps between the stones are the playing spaces. Two to four players. One stick per player. Three sticks, each with one flat marked side and one round side, used as dice. The throws are as follows: one marked side up = 2; two marked sides up = 3; all marked sides up = 5; no marked sides up =10. Throws of 10 grant the player another throw. Each player starts at one of the larger gaps, and any players facing each other play on a team. Teams play in opposite directions: The player starting from the left or right space moves in a clockwise direction; the player(s) sitting at the top or bottom play anticlockwise). When a player's piece moves past or onto the space occupied by a piece belonging to the opposing team, the opposing team's piece is sent back to the starting space. The first team to circle the board three times wins. ")
    (source "Culin 1907: 88-89.")
    (id "840")
    (version "1.3.4")
    (classification "board/race/reach")
    (credit "Eric Piette")
    (origin "This game was played in Arizona, around 1901.")
    }
)

(graphics {
    (show Edges (colour Hidden))
    (show Symbol "disc" "AllSites" Vertex fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
    (board Background image:"disc" edgeColour:(colour Black) scale:0.1)
    (stackType 0 Ground)
})

)

