(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "TrackMove" ("NextSiteOnTrack" ("ThrowValue") #1))

(define "Move" 
    (move
        (from (from) level:(level))
        (to 
            ("NextSiteOnTrack" ("ThrowValue") #1)
            if:(not ("IsFriendAt" (to)))
            ("HittingCapture" (mapEntry "Start" (who at:(to))))
        )
    )
)

(define "NextSite" ("NextSiteOnTrack" 1 from:#1 #2))

(define "MadeACompleteCircuit" 
    (if (or (is Mover P1) (is Mover P2))
        (is In (mapEntry "Start" Mover) (sites Track "TrackCCW" from:("NextSite" (last From) "TrackCCW") to:(last To)))
        (is In (mapEntry "Start" Mover) (sites Track "TrackCW" from:("NextSite" (last From) "TrackCW") to:(last To)))
    )
)

//------------------------------------------------------------------------------

(game "Tasholiwe"
    (players <Player:num>)
    (equipment {
        ("FortyStonesWithFourGapsBoard"
            {
            (track "TrackCW" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True)
            (track "TrackCCW" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True)
            }
        )
        (map "Throw" {(pair 0 10) (pair 1 3) (pair 2 2) (pair 3 5)})
        <Player:startPos>
        (regions "AllSites" (sites Board Vertex))
        (piece "Stick" Each)
        ("StickDice" 3)
        (hand Each)
    })
    (rules 
        (start {
            <Player:init>
        })
        (play 
            ("RollMove" 
                (if (or (is Mover P1) (is Mover P2))
                    (forEach Piece ("Move" "TrackCCW"))
                    (forEach Piece ("Move" "TrackCW"))
                )
                (then 
                    (do
                        (if ("MadeACompleteCircuit") 
                            (addScore Mover 1) 
                            (then 
                                (if (= (score Mover) 4) 
                                    (remove (last To))
                                )
                            )
                        )
                        next:(if (and (!= 4 (score Mover)) (= 10 ("ThrowValue"))) (moveAgain))
                    )
                )
            )
        )
        (end (if (>= (score Mover) 4) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <num> <startPos> <init>}
    {
    (item "2" <2> 
        <(map "Start" {(pair 1 39) (pair 2 37)})>
        <
        (place "Stick1" Edge (mapEntry "Start" P1))
        (place "Stick2" Edge (mapEntry "Start" P2))
        > 
    "The game has 2 players.") 
    (item "4" <4> 
        <(map "Start" {(pair 1 39) (pair 2 36) (pair 3 37) (pair 4 38)})>
        <
        (place "Stick1" Edge (mapEntry "Start" P1))
        (place "Stick2" Edge (mapEntry "Start" P2))
        (place "Stick3" Edge (mapEntry "Start" P3))
        (place "Stick4" Edge (mapEntry "Start" P4))
        > 
    "The game has 4 players.")* 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Tasholiwe (Observed)" {
        "Players/4"
        }
        variations:{"Players/2"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tasholiwe is a race game played by the Zuni people of the southwest United States. Two or four people play, each player playing a sa piece representing the four directions and seasons.")
        (rules "Forty stones, arranged in a circle, with a larger gap (called a door) after every tenth stone. Two or four players, each player playing with one piece. Each piece begins in one of the doors: north/winter is yellow, west/spring is blue, south/summer is red, east/autumn is white. North and west move anti-clockwise around the circle, south and east move clockwise. Three sticks, red on one side and black on the other, used as dice, the throws are as follows: three red = 10, three black = 5, two red and one black = 3, two black and one red = 2. A throw of 10 grants the player another throw. When a player lands on a spot occupied by an opponent, the opponent's piece is sent back to start. The first player to complete four circuits of the board wins. Circuits are usually counted with beans or corn.")
        (source "Culin 1898: 773-775.")
        (id "848")
        (version "1.3.7")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin "This game was played in Southwest United States, around 1898.")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Yellow))
        (player Colour P2 (colour Blue))
        (player Colour P3 (colour Red))
        (player Colour P4 (colour White))
        (piece Scale "Stick" 0.5)
        (show Edges (colour Hidden))
        (show Symbol "disc" "AllSites" Vertex fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
        (board Background image:"disc" edgeColour:(colour Black) scale:0.1)
        (piece Colour "Die" state:0 fillColour:(colour Red))
        (piece Colour "Die" state:1 fillColour:(colour Black))
    })
    
)

