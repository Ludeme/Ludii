(define "ThrowValue" (mapEntry "ThrowDiceValue" (count Pips)))

(define "SiteToMoveOnSelectedTrack"
    (if (= (value Player Mover) 2)
        (trackSite Move "TrackCCW" steps:("ThrowValue"))
        (trackSite Move "TrackCW" steps:("ThrowValue"))
    )
)

(define "NextSite" (trackSite Move from:#1 #2 steps:1))

(define "SiteToMoveOnTrackCW" (trackSite Move "TrackCW" steps:("ThrowValue")))
(define "SiteToMoveOnTrackCCW" (trackSite Move "TrackCCW" steps:("ThrowValue")))

(define "SelectCW"
    (set Value Mover 1)
)

(define "SelectCCW"
    (set Value Mover 2)
)

(define "RememberSelectedTrack"
    (then
        (if
            (= (value Player Mover) Off)
            #1
        )
    )
)

(define "CaptureEnemyPiece"
    (apply
        if:(is Enemy (who at:(to)))
        (fromTo (from (to)) (to (value Piece at:(to))))
    )
)

(define "MoveOn"
    (if (!= #1 Off)
        (if (or
                (is In #1 (sites Empty))
                (is Enemy (who at:#1))
            )
            (move
                (from (from) level:(level))
                (to
                    #1
                    <Play:capture>
                )
            )
        )
        #2
    )
)

(define "MadeACompleteCircuit"
    (if (= 1 (value Player Mover))
        (is In (value Piece at:(where "Marker" Mover)) (sites Track "TrackCW" from:("NextSite" (last From) "TrackCW") to:(last To)))
        (is In (value Piece at:(where "Marker" Mover)) (sites Track "TrackCCW" from:("NextSite" (last From) "TrackCCW") to:(last To)))
    )
)

//------------------------------------------------------------------------------

(game "Patol"
    (players <Player:num>)
    (equipment {
        (board
            (add
                (remove
                    (concentric {44})
                    vertices:{43 21 0 22}
                )
                edges:{{20 18} {0 1} {19 21} {38 39}}
            )
            {
            (track "TrackCW" {39 35 33 31 29 27 25 23 21 19 38 17 15 13 11 9 7 5 3 1 37 0 2 4 6 8 10 12 14 16 36 18 20 22 24 26 28 30 32 34} loop:True)
            (track "TrackCCW" {39 34 32 30 28 26 24 22 20 18 36 16 14 12 10 8 6 4 2 0 37 1 3 5 7 9 11 13 15 17 38 19 21 23 25 27 29 31 33 35} loop:True)
            }
            use:Edge
        )
        (regions "Gates" (sites {36..39}))
        (piece "Marker" Each
            (if (= Off (value Player Mover))
                (or
                    ("MoveOn" ("SiteToMoveOnTrackCW") ("RememberSelectedTrack" ("SelectCW")))
                    ("MoveOn" ("SiteToMoveOnTrackCCW") ("RememberSelectedTrack" ("SelectCCW")))
                )
                ("MoveOn" "SiteToMoveOnSelectedTrack")
            )
        )
        (dice d:2 from:0 num:3)
        (map "ThrowDiceValue" {(pair 0 5) (pair 1 3) (pair 2 2) (pair 3 10)})
        (hand Each)
    })
    (rules
        (start (place "Marker" "Hand"))
        phases:{
        (phase "Placement"
            (play
                (move
                    (from (handSite Mover))
                    (to (sites "Gates"))
                    stack:True
                    (then (set Value at:(last To) (last To)))
                )
            )
            (nextPhase Mover "Playing")
        )
        (phase "Playing"
            (play
                (do
                    (roll)
                    next:(forEach Piece)
                )
            )
            (end
                (if
                    ("MadeACompleteCircuit")
                    (result Mover Win)
                )
            )
        )
        }
    )
)

//------------------------------------------------------------------------------

(option "Players" <Player> args:{ <num>}
    {
    (item "2" <2>   "The game has 2 players.")*
    (item "3" <3>   "The game has 3 players.")
    (item "4" <4>   "The game has 4 players.")
    (item "5" <5>   "The game has 5 players.")
    (item "6" <6>   "The game has 6 players.")
    (item "7" <7>   "The game has 7 players.")
    (item "8" <8>   "The game has 8 players.")
    (item "9" <9>   "The game has 9 players.")
    (item "10" <10>   "The game has 10 players.")
    (item "11" <11>   "The game has 11 players.")
    (item "12" <12>   "The game has 12 players.")
    (item "13" <13>   "The game has 13 players.")
    (item "14" <14>   "The game has 14 players.")
    (item "15" <15>   "The game has 15 players.")
    (item "16" <16>   "The game has 16 players.")
})

(option "Play Rules" <Play> args:{ <capture>}
    {
    (item "Capture"    <"CaptureEnemyPiece"> "Captures are possible.")*
    (item "No Capture" <>   "No Capture.")
})

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Patol (Observed)" {
        "Play Rules/Capture" "Players/2"
        }
        variations:{"Players/3" "Players/4" "Players/5" "Players/6" "Players/7" "Players/8" "Players/9"
        "Players/10" "Players/11" "Players/12" "Players/13" "Players/14" "Players/15" "Players/16"}
    )*

    (ruleset "Ruleset/Captureless (Observed)" {
        "Play Rules/No Capture" "Players/2"
        }
        variations:{"Players/3" "Players/4" "Players/5" "Players/6" "Players/7" "Players/8" "Players/9"
        "Players/10" "Players/11" "Players/12" "Players/13" "Players/14" "Players/15" "Players/16"}
    )

    (ruleset "Ruleset/Fifteens (Observed)" {

    })

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Patol is a race game played by Pueblo peoples of New Mexico. Typically played by boys, any number of people can play, and it is a race game with three sticks used as dice.")
        (aliases {"Cuwee"})
        (useFor "Ruleset/Patol (Observed)" (rules "Forty stones are arranged in a circle, with a larger gap between every tenth and eleventh stone. The gaps between the stones are the playing spaces. One piece per player. Any number of players. Three sticks used as dice, marked on one side and blank on the other. Throws are as follows: One marked side up = 3, two marked sides up = 2, three marked sides up = 10; all blank sides up = 5. Players enter their pieces from one of the wider gaps in the board, and chooses to move either clockwise or anti-clockwise. When  a player's piece lands on a space occupied by the opponent, the opponent's piece is sent back to the space in which they entered. The first player to complete the circuit wins."))
        (useFor "Ruleset/Patol (Observed)" (source "Lummis 1891: 183-189."))
        (useFor "Ruleset/Fifteens (Observed)" (rules "Three sticks, marked on one side and blank on the other. Two are marked the same, the remaining is marked with an extra notch. Throws are as follows: two marked, with the extra notch up = 2, two marked without the extra notch = 3; one extra notch = 10, all three marked up = 15, zero marked up = 5."))
        (useFor "Ruleset/Fifteens (Observed)" (source "Lummis 1891: 186."))
        (useFor "Ruleset/Captureless (Observed)" (rules "Pieces are not sent back to start when an opponent's piece lands on the same space."))
        (useFor "Ruleset/Captureless (Observed)" (source "Lummis 1891: 189."))
        (source "Culin 1898: 763-765; 1907: 191; Murray 1951: 190-192.")
        (version "1.3.0")
        (classification "board/race/reach")
        (credit "Eric Piette")
        (origin  "This game was played in New Mexico, around 1891.")
        }
    )

    (graphics {
        (show Edges (colour Hidden))
        (show Symbol "disc" Vertex (sites Board) fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
        (board Background image:"disc" edgeColour:(colour Black) scale:0.1)
        (piece Foreground "Die" state:1 image:"star" fillColour:(colour White) scale:0.2)
        (stackType 0 Ground)
        (piece Scale "Marker" 0.5)

    })

)

