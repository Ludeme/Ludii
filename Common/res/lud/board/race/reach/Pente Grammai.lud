(define "SiteToMoveOnTrack" (trackSite Move steps:(count Pips)))

(define "EnterToBoard" (trackSite Move from:(handSite Mover) steps:(- (count Pips) 1)))

(define "SacredLine" 10)

//------------------------------------------------------------------------------

(game "Pente Grammai"
    (players 2)
    (equipment {
        (board
            (add
                (scale 1.5
                    (merge {
                        (shift 2 0 (rectangle 2 1))
                        (shift 1.5 0 (rectangle 2 1))
                        (shift 1 0 (rectangle 2 1))
                        (shift 0.5 0 (rectangle 2 1))
                        (rectangle 2 1)
                    })
                )
                vertices:{{1.5 0.75}}
            )
            {
            (track "Track1" "8,6,4,2,0,1,3,5,7,9" loop:True P1)
            (track "Track2" "1,3,5,7,9,8,6,4,2,0" loop:True P2)
            }
            use:Vertex
        )
        (piece "Marker" Each <Play:movePiece>)
        (hand Each)
        (dice d:6 num:1)
        (map "Goal" {(pair P1 5) (pair P2 4)})
    })
    (rules
        (start {
            (place Stack "Marker1" (handSite P1) count:5)
            (place Stack "Marker2" (handSite P2) count:5)
        })
        (play
            (do
                (roll)
                next:(priority {
                    <Play:enterBoard>
                    (forEach Piece)
                    }
                    (then
                        (if (= (mapEntry "Goal" Mover) (last To))
                            (fromTo (from (last To)) (to "SacredLine") stack:True)
                        )
                    )
                )
            )
        )
        (end
            (if
                (= 0 (count Sites in:(difference (sites Occupied by:Mover) "SacredLine" )))
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Play Rules" <Play> args:{ <enterBoard> <movePiece>}
    {
    (item "No Capture"
        <
        (if (and (not ("HandEmpty" Mover)) (is Empty ("EnterToBoard")))
            (move (from (handSite Mover)) (to ("EnterToBoard")))
        )
        >
        <
        (if (and (!= (from) "SacredLine") (is In "SiteToMoveOnTrack" (sites Empty)))
            (move
                (from)
                (to "SiteToMoveOnTrack")
            )
        )
        >
        "No capture."
    )*
    (item "Capture"
        <
        (if (and (not ("HandEmpty" Mover)) (or (is Enemy (who at:"EnterToBoard")) (is Empty ("EnterToBoard"))))
            (move
                (from (handSite Mover))
                (to
                    ("EnterToBoard")
                    (apply
                        if:(is Enemy (who at:"EnterToBoard"))
                        (fromTo
                            (from ("EnterToBoard"))
                            (to (handSite Next))
                            stack:True
                        )
                    )
                )
            )
        )
        >
        <
        (if (and (!= (from) "SacredLine") (or (is Enemy (who at:"SiteToMoveOnTrack")) (is In "SiteToMoveOnTrack" (sites Empty))))
            (move
                (from)
                (to "SiteToMoveOnTrack"
                    (apply
                        if:(is Enemy (who at:"SiteToMoveOnTrack"))
                        (fromTo
                            (from ("SiteToMoveOnTrack"))
                            (to (handSite Next))
                            stack:True
                        )
                    )
                )
            )
        )
        >
        "Captures are allowed."
    )
})

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Schadler (Scholarly)" {

    })

    (ruleset "Ruleset/Locos Ludi Race (Scholarly)" {
        "Play Rules/No Capture"
    })**

    (ruleset "Ruleset/Locus Ludi  Knock-Off Mode (Scholarly)" {
        "Play Rules/Capture"
    })

    (ruleset "Ruleset/Schadler (Reconstructed)" {

    })

    (ruleset "Ruleset/Historical Information (Incomplete)" {

    })

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Pente Grammai, or \"Five Lines,\" is a game known from Ancient Greek sources. Objects and graffiti have been found at Greek archaeological sites which come in the form of five parallel rows of lines, which have been identified as this game. Other versions appear that seem to have been doubled forms of the game. Other game boards in a 2x5 pattern that form square grids or rows of holes seem to be later versions of this game. ")
        (aliases {"Five Lines"})
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "2x5 board. Five pieces per player. Pieces played on the end of the lines. Two dice. Central line marked. Pieces move from the \"sacred line\" to win. Moving from the sacred line can be bad.

        "))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP Evidence."))
        (useFor "Ruleset/Schadler (Scholarly)" (rules "Board consists of five parallel lines. Each player has five pieces, one player’s pieces are placed at one end of each line, the other player on the opposite ends of the lines. Played using two cubic dice. Players move their pieces according to the roll of the dice. Pieces move in a counter-clockwise direction. The goal is to get all five pieces onto the “sacred line,” which is the central line. Perhaps they had to move several times around the board in order to land on the sacred line.
        "))
        (useFor "Ruleset/Schadler (Scholarly)" (source "Schädler 2009: 184-189."))
        (useFor "Ruleset/Locos Ludi Race (Scholarly)" (rules "Five pieces per player. One six-sided die. Players move their pieces onto the board using the throws of the dice. A piece must be brought onto the board if it is possible. Pieces move in an anti-clockwise direction around the board. Only one piece may be placed on a point at a time, except for the central line, which is the \"sacred line.\" The first person to collect all of their pieces on the sacred line by reaching it on the opponent's side of the board by an exact throw wins."))
        (useFor "Ruleset/Locos Ludi Race (Scholarly)" (source "<a href=\"https://locusludi.unifr.ch/pente-grammai/\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Locus Ludi</a>"))
        (useFor "Ruleset/Locus Ludi  Knock-Off Mode (Scholarly)" (rules "Five pieces per player. One die. Pieces are moved onto the board according to the throws of the die. Pieces move in an anti-clockwise direction. A piece must be moved onto the board with a throw if possible. Only one piece may occupy a space, except on the \"sacred line\" which can hold any number of pieces of both players. When a piece lands on a space occupied by a piece belonging to the opponent, the opponent's piece is sent back to start. Pieces on the sacred line cannot be sent back to start. The first person to move all of their pieces onto the sacred line by an exact throw on the opponent's side of the board wins."))
        (useFor "Ruleset/Locus Ludi  Knock-Off Mode (Scholarly)" (source "<a href=\"https://locusludi.unifr.ch/pente-grammai/\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Locus Ludi</a>"))
        (source "Schädler 2009.")
        (version "1.3.0")
        (classification "board/race/reach")
        (credit "Eric Piette")
        }
    )

    (graphics {
        (piece Scale "Marker" 0.3)
        (stackType Default 0.2)

    })

)
