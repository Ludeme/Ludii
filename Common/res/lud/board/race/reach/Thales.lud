(define "CaptureBySight"
    (forEach Site 
        (sites Occupied by:#1)
        (if
            (>= (count Sites in:(intersection (sites Occupied by:#2) (sites LineOfSight at:(site) All))) 3)
            (remove (site) at:EndOfTurn)
        )
    )
)

//-------------------------------------------------------------------------

(game "Thales" 
    (players 2)
    (equipment { 
        (board (hex  Diamond 8))
        (piece "Marker" Each)
    }) 
    (rules 
        (start {
            (place "Marker1" (sites {"A8" "A7" "A6" "A5" "B8" "B7" "B6" "B5" "C8" "C7" "C6" "D8" "D7"}))
            (place "Marker2" (sites {"E1" "F1" "G1" "H1" "E2" "F2" "G2" "H2" "F3" "G3" "H3" "G4" "H4"}))
        })
        (play 
            (forEach Piece
                (move Slide
                    All
                    (to
                        (sites Empty)
                    )
                )
                (then
                    (and
                        ("CaptureBySight" P1 P2)
                        ("CaptureBySight" P2 P1)
                    )
                )
            )
        )
        (end {
            (if 
                (and {
                    (= (who at:63) Next)
                    (= (who at:0) Next)
                })
                (result Next Win)
            )
        })
    )
)

//-------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Thales is an original reach game.")
        (rules "The game is played on an 8x8 hexagonal board shaped like a diamond. On each turn, a player can move one of their stones in any direction (horizontally, vertically, or diagonally) any number of spaces. Any stone that is located on a cell within the move range of three or more enemy stones is captured and removed from the board. The game is won by the player who successfully moves one of their stones into either of the two ports located on the two unused corners of the board, and the other player is unable to capture one of them on their next turn.")
        (id "4301")
        (version "1.3.14")
        (classification "board/race/reach")
        (author "Achille Morenville")
        (credit "Achille Morenville")
        (date "2024")
        }
    )
)
