(define "FromIsOnTheRegionToFill" (is In #1 (sites (player (mapEntry (mover))))))

(define "ToIsOnTheRegionToFill" (is In #1 (sites (player (mapEntry (mover))))))

(define "Hop" 
    (move Hop
        (from #1)
        (between if:(is Occupied (between)))
        (to 
            if:(and
                ("IsEmptyAndNotVisited" (to))
                (or
                    (not ("FromIsOnTheRegionToFill" #1))
                    ("ToIsOnTheRegionToFill" (to)) 
                )
            )
        )
        #2		
    )
) 

(define "HopSequence" ("Hop" #1 (then (if (can Move ("Hop" (last To))) (moveAgain)))))

(define "Dim" <Board:size>)

//------------------------------------------------------------------------------

(game "Chinese Checkers"
    (players <Players:num>) 
    (equipment { 
        (board (hex Star ("Dim")))
        (piece "Marker" Each) 
        <Players:home>
    }) 
    (rules 
        <Players:start>
        
        phases:{
        (phase "Movement" 
            (play 
                (forEach Piece 
                    (or 
                        ("HopSequence" (from)) 
                        (move Step
                            (to 
                                if:(and
                                    (is Empty (to))
                                    (or
                                        (not ("FromIsOnTheRegionToFill" (from)))
                                        ("ToIsOnTheRegionToFill" (to))
                                    )
                                )
                            )
                        )
                    )
                )
            ) 
            (nextPhase (is Mover (next)) "MultiJump")
        ) 
        (phase "MultiJump" (play 
                (or 
                    ("HopSequence" (last To)) 
                    (move Pass)
                )
            ) 
            (nextPhase (not (is Mover (next))) "Movement")
        ) 
        }
        
        (end ("FillWin" (sites (player (mapEntry (mover))))))
    )
)

//------------------------------------------------------------------------------

(option "Players" <Players> args:{ <num> <home> <start>} {
    (item "2 (a)" 
        <2> 
        <
        (regions "Home" P1 (expand origin:(coord row:0 column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P2 (expand origin:(coord row:(* ("Dim") 4) column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (map {(pair 1 2) (pair 2 1) })
        >
        <
        (start { 
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
        })
        >
        "Two players occupy home regions on opposite sides, four home regions are empty."
    ) 
    
    (item "2 (b)" 
        <2> 
        <
        (regions "Home" P1
            (union {
                (expand origin:(coord row:0 column:("Dim")) steps:(- ("Dim") 1))
                (expand origin:(coord row:(* ("Dim") 3) column:(* ("Dim") 4)) steps:(- ("Dim") 1))
                (expand origin:(coord row:(* ("Dim") 3) column:("Dim")) steps:(- ("Dim") 1))
            })
        )
        (regions "Home" P2 
            (union {
                (expand origin:(coord row:("Dim") column:(* ("Dim") 3)) steps:(- ("Dim") 1))
                (expand origin:(coord row:(* ("Dim") 4) column:(* ("Dim") 3)) steps:(- ("Dim") 1))
                (expand origin:(coord row:("Dim") column:0) steps:(- ("Dim") 1))
            })
        )
        (map {(pair 1 2) (pair 2 1) })
        >
        <
        (start { 
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
        })
        >
        "Two players occupy three regions each, alternating around board, i.e. all six home regions start full."
    ) 
    
    (item "3"  
        <3> 
        <
        (regions "Home" P1 (expand origin:(coord row:0 column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P6 (expand origin:(coord row:("Dim") column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (regions "Home" P2 (expand origin:(coord row:(* ("Dim") 3) column:(* ("Dim") 4)) steps:(- ("Dim") 1)))
        (regions "Home" P4 (expand origin:(coord row:(* ("Dim") 4) column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (regions "Home" P3 (expand origin:(coord row:(* ("Dim") 3) column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P5 (expand origin:(coord row:("Dim") column:0) steps:(- ("Dim") 1)))
        (map {(pair 1 4) (pair 2 6) (pair 3 6) (pair 4 1) (pair 5 2) (pair 6 2)})
        >
        <
        (start { 
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
            (place "Marker3" (sites P3))
        })
        >
        "Three players occupy home regions equally spaced around board, three home regions are empty."
    ) 
    
    (item "4"  
        <4> 
        <
        (regions "Home" P1 (expand origin:(coord row:("Dim") column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (regions "Home" P2 (expand origin:(coord row:(* ("Dim") 3) column:(* ("Dim") 4)) steps:(- ("Dim") 1)))
        (regions "Home" P3 (expand origin:(coord row:(* ("Dim") 3) column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P4 (expand origin:(coord row:("Dim") column:0) steps:(- ("Dim") 1)))
        (map {(pair 1 3) (pair 2 4) (pair 3 1) (pair 4 2)})
        >
        <
        (start { 
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
            (place "Marker3" (sites P3))
            (place "Marker4" (sites P4))
        })
        >
        "Four players occupy home regions, two opposite home regions are empty."
    ) 
    
    (item "5"  
        <5> 
        <
        (regions "Home" P1 (expand origin:(coord row:0 column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P2 (expand origin:(coord row:("Dim") column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (regions "Home" P3 (expand origin:(coord row:(* ("Dim") 3) column:(* ("Dim") 4)) steps:(- ("Dim") 1)))
        (regions "Home" P4 (expand origin:(coord row:(* ("Dim") 4) column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (regions "Home" P5 (expand origin:(coord row:(* ("Dim") 3) column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P6 (expand origin:(coord row:("Dim") column:0) steps:(- ("Dim") 1)))
        (map {(pair 1 4) (pair 2 5) (pair 3 6) (pair 4 1) (pair 5 2) (pair 6 3)})
        >
        <
        (start { 
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
            (place "Marker3" (sites P3))
            (place "Marker4" (sites P4))
            (place "Marker5" (sites P5))
        })
        >
        "Five players occupy home regions, one is empty."
    ) 
    
    (item "6"  
        <6> 
        <
        (regions "Home" P1 (expand origin:(coord row:0 column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P2 (expand origin:(coord row:("Dim") column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (regions "Home" P3 (expand origin:(coord row:(* ("Dim") 3) column:(* ("Dim") 4)) steps:(- ("Dim") 1)))
        (regions "Home" P4 (expand origin:(coord row:(* ("Dim") 4) column:(* ("Dim") 3)) steps:(- ("Dim") 1)))
        (regions "Home" P5 (expand origin:(coord row:(* ("Dim") 3) column:("Dim")) steps:(- ("Dim") 1)))
        (regions "Home" P6 (expand origin:(coord row:("Dim") column:0) steps:(- ("Dim") 1)))
        (map {(pair 1 4) (pair 2 5) (pair 3 6) (pair 4 1) (pair 5 2) (pair 6 3)})
        >
        <
        (start { 
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
            (place "Marker3" (sites P3))
            (place "Marker4" (sites P4))
            (place "Marker5" (sites P5))
            (place "Marker6" (sites P6))
        })
        >
        "Six players occupy home regions (standard game)."
    )** 
})

(option "Board Size" <Board> args:{ <size> } {
    (item "1" <1> "The game is played on a board of size 1.") 
    (item "2" <2> "The game is played on a board of size 2.") 
    (item "3" <3> "The game is played on a board of size 3.") 
    (item "4" <4> "The game is played on a board of size 4.")** 
    (item "5" <5> "The game is played on a board of size 5.") 
    (item "6" <6> "The game is played on a board of size 6.") 
    (item "7" <7> "The game is played on a board of size 7.") 
    (item "8" <8> "The game is played on a board of size 8.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Chinese Checkers, originally known as \"Sternhalma,\" was invented in Germany in 1892 as a variant of the older American game Halma. It was called Chinese Checkers as a marketing ploy in the United States in 1928.")
        (aliases {"Sternhalma"})
        (rules "The game can be played by two, three, four, or six people. It is played on a board that is shaped like a six-pointed star with 121 holes for the pieces. Each point of the star has ten colored spheres and the goal of the game is to move all of one's pieces into the opposite point of the star. Players can move a piece to one adjacent hole, or can jump either their own or opponent's pieces. This does not result in capturing, and jumping is not mandatory. When fewer than six people play, players can play with only one color of pieces, but if two or three people play they can use more than one color, but each player must use an equal number of pieces.")
        (source "https://en.wikipedia.org/wiki/Chinese_checkers")
        (id "71")
        (version "1.3.14")
        (classification "board/race/fill")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Colour InnerEdges (colour 120 190 240))
        (board Colour OuterEdges (colour 120 190 240))
        (board Colour Phase0 (colour 210 230 255))
        (board StyleThickness OuterEdges 2.0)
        (region Colour "Home" P1 (colour Green))
        (region Colour "Home" P2 (colour Blue))
        (region Colour "Home" P3 (colour Pink))
        (region Colour "Home" P4 (colour White))
        (region Colour "Home" P5 (colour Black))
        (region Colour "Home" P6 (colour Red))
    })
    
    (ai 
        "Chinese Checkers_ai"
    )
    
)
