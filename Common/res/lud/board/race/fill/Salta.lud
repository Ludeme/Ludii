(define "Hop" 
    (move Hop 
        (from #1)
        Diagonal
        (between if:("IsEnemyAt" (between)))
        (to if:("IsEmptyAndNotVisited" (to)))
        #2		
    )
) 

(define "HopSequence" ("Hop" #1 (then (if (can Move ("Hop" (last To))) (moveAgain)))))

(define "GoalOfP1Filled" 
    (and {
        ("IsPieceAt" "Salta1Star" P1 (coord "J10")) ("IsPieceAt" "Salta2Star" P1 (coord "H10")) ("IsPieceAt" "Salta3Star" P1 (coord "F10")) ("IsPieceAt" "Salta4Star" P1 (coord "D10")) ("IsPieceAt" "Salta5Star" P1 (coord "B10"))
        ("IsPieceAt" "Salta1Moon" P1 (coord "I9")) ("IsPieceAt" "Salta2Moon" P1 (coord "G9")) ("IsPieceAt" "Salta3Moon" P1 (coord "E9")) ("IsPieceAt" "Salta4Moon" P1 (coord "C9")) ("IsPieceAt" "Salta5Moon" P1 (coord "A9"))
        ("IsPieceAt" "Salta1Dot" P1 (coord "J8")) ("IsPieceAt" "Salta2Dot" P1 (coord "H8")) ("IsPieceAt" "Salta3Dot" P1 (coord "F8")) ("IsPieceAt" "Salta4Dot" P1 (coord "D8")) ("IsPieceAt" "Salta5Dot" P1 (coord "B8"))
    })
)

(define "GoalOfP2Filled" 
    (and { 
        ("IsPieceAt" "Salta1Star" P2 (coord "A1")) ("IsPieceAt" "Salta2Star" P2 (coord "C1")) ("IsPieceAt" "Salta3Star" P2 (coord "E1")) ("IsPieceAt" "Salta4Star" P2 (coord "G1")) ("IsPieceAt" "Salta5Star" P2 (coord "I1"))
        ("IsPieceAt" "Salta1Moon" P2 (coord "B2")) ("IsPieceAt" "Salta2Moon" P2 (coord "D2")) ("IsPieceAt" "Salta3Moon" P2 (coord "F2")) ("IsPieceAt" "Salta4Moon" P2 (coord "H2")) ("IsPieceAt" "Salta5Moon" P2 (coord "J2"))
        ("IsPieceAt" "Salta1Dot" P2 (coord "A3")) ("IsPieceAt" "Salta2Dot" P2 (coord "C3")) ("IsPieceAt" "Salta3Dot" P2 (coord "E3")) ("IsPieceAt" "Salta4Dot" P2 (coord "G3")) ("IsPieceAt" "Salta5Dot" P2 (coord "I3"))
    })
)

//------------------------------------------------------------------------------

(game "Salta" 
    (players 2) 
    (equipment { 
        (board (square 10)) 
        (piece "Salta1Dot" Each)
        (piece "Salta2Dot" Each)
        (piece "Salta3Dot" Each)
        (piece "Salta4Dot" Each)
        (piece "Salta5Dot" Each)
        (piece "Salta1Moon" Each)
        (piece "Salta2Moon" Each)
        (piece "Salta3Moon" Each)
        (piece "Salta4Moon" Each)
        (piece "Salta5Moon" Each)
        (piece "Salta1Star" Each)
        (piece "Salta2Star" Each)
        (piece "Salta3Star" Each)
        (piece "Salta4Star" Each)
        (piece "Salta5Star" Each)
    }) 
    
    (rules 
        (start { 
            (place "Salta1Star1" coord:"A1") (place "Salta2Star1" coord:"C1") (place "Salta3Star1" coord:"E1") (place "Salta4Star1" coord:"G1") (place "Salta5Star1" coord:"I1")
            (place "Salta1Moon1" coord:"B2") (place "Salta2Moon1" coord:"D2") (place "Salta3Moon1" coord:"F2") (place "Salta4Moon1" coord:"H2") (place "Salta5Moon1" coord:"J2")
            (place "Salta1Dot1" coord:"A3") (place "Salta2Dot1" coord:"C3") (place "Salta3Dot1" coord:"E3") (place "Salta4Dot1" coord:"G3") (place "Salta5Dot1" coord:"I3")
            
            (place "Salta1Star2" coord:"J10") (place "Salta2Star2" coord:"H10") (place "Salta3Star2" coord:"F10") (place "Salta4Star2" coord:"D10") (place "Salta5Star2" coord:"B10")
            (place "Salta1Moon2" coord:"I9") (place "Salta2Moon2" coord:"G9") (place "Salta3Moon2" coord:"E9") (place "Salta4Moon2" coord:"C9") (place "Salta5Moon2" coord:"A9")
            (place "Salta1Dot2" coord:"J8") (place "Salta2Dot2" coord:"H8") (place "Salta3Dot2" coord:"F8") (place "Salta4Dot2" coord:"D8") (place "Salta5Dot2" coord:"B8")
        })
        
        (play 
            (if "SameTurn"
                ("HopSequence" (last To))
                (priority { 
                    (forEach Piece ("HopSequence" (from)))
                    (forEach Piece "StepDiagonalToEmpty")
                }) 
            )
        ) 
        
        (end {
            (if "GoalOfP1Filled" 
                (result P1 Win)
            )
            (if "GoalOfP2Filled" 
                (result P2 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Salta was invented by Konrad Heinrich Büttgenbach in 1899. It is related to Halma.")
        (rules "Players alternate moving their pieces forward or backward diagonally. A piece may also jump over one adjacent piece of the opponent diagonally forward. This does not capture the opponent's piece. Jumps are compulsory. A player cannot block the other player's pieces such that he or she cannot perform a legal move. The player that can first bring each of their pieces seven rows forward is the winner. The pieces must retain their original order in each row.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Salta_(game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (id "417")
        (version "1.3.7")
        (classification "board/race/fill")
        (author "Konrad Büttgenbach")
        (credit "Eric Piette")
        (date "1899")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Green))
        (player Colour P2 (colour Red))
        (board Style Chess)
    })
    
    (ai 
        "Salta_ai"
    )
    
)
