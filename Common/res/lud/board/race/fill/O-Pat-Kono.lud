(define "RegionFilled" (= (sites Next) (sites Occupied by:Mover)))

//------------------------------------------------------------------------------

(game "O-Pat-Kono"
    (players 2)
    (equipment {
        (board (rectangle 5 5) use:Vertex)
        (piece "Marker" Each "StepDiagonalToEmpty")
        (regions "Home" P1 (union (sites Bottom) (sites {"A2" "E2"})))
        (regions "Home" P2 (union (sites Top) (sites {"A4" "E4"})))
        }
    )
    (rules
        (start {
            (place "Marker1" (sites P1))
            (place "Marker2" (sites P2))
        })
        (play (forEach Piece))

        (end (if "RegionFilled" (result Mover Win) ) )
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "O-Pat-Kono is a two-player game of alignment played since at least the late nineteenth century in Korea.")
        (aliases {"Five Field Kono"})
        (rules "Played on a 4x4 grid. Each player has seven pieces, placed on the intersections of the lines, five on the back row and one on each outer intersection of the second line. Pieces are moved diagonally across the squares. The object is to place one's pieces in the opponent's starting position. The first player to do so wins.")
        (source "Culin 1895: 102.")
        (version "1.3.0")
        (classification "board/race/fill")
        (credit "Eric Piette")
        (origin  "This game was played in Korea, from around 1893 to 1895.")
        }
    )

    (graphics {
        (board Style Graph)
        (show Edges Diagonal Hidden)

    })

    (ai
        "O-Pat-Kono_ai"
    )
)
