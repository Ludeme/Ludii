(game "Shanzak Kammar"
    (players 2) 
    (equipment { 
        (board 
            (add
                ("AlquerqueGraphWithBottomAndTopTriangles") 
                edges:{{5 1} {1 7} {7 3} {3 9} {13 9} {7 13} {11 5} {15 11} {11 17} {7 11} {17 13} {13 19} {23 19} {23 17} {17 21} {15 21}}
            )
            use:Vertex
        )
        (piece "Marker" Each (or "StepToEmpty" "HopCapture"))
        (regions "Goal" P1 (expand (sites Top)))
        (regions "Goal" P2 (expand (sites Bottom)))
    }) 
    (rules 
        (start {
            (place "Marker1" (expand (sites Bottom) steps:3))
            (place "Marker2" (expand (sites Top) steps:3))
        })
        
        (play (forEach Piece))
        
        (end (if (= 6 (count Pieces Mover in:(sites Mover "Goal"))) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Shanzak Kammar is a capturing game played in Central Asia. It was particularly popular in Samarkand in the nineteenth century, and was often played on a board drawn on the ground with clay pieces, coins, or pebbles.")
        (aliases {"Un Almy Tash"})
        (rules "5x5 intersecting lines. Two triangles on opposite sides of the square, with the apex intersecting with the midpoint of opposite sides. A line is drawn from the apex , bisecting the base, and another line bisecting this one and the opposite two sides of the triangle. Lines are drawn through the apex, extending the sides of the triangle to the midpoint of the adjacent side. Sixteen pieces per player, which begin on the points of the board closes to the player. Players alternate turns moving a piece to an empty adjacent spot orthogonally or diagonally, even when there are not lines connecting the adjacent points. A player's piece may hop over an opponent's piece to an empty space immediately on the opposite side of it to capture it. The player who occupies all of the points in the opponent's triangle wins. ")
        (source "Komarov 1893: 250.")
        (id "1202")
        (version "1.3.5")
        (classification "board/race/fill")
        (credit "Eric Piette")
        (origin  "This game was played in Central Asia, around 1893.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        (stackType Count)
        (show Line {
            {5 1} {1 7} {7 3} {3 9} {13 9} {7 13} {11 5} {15 11} {11 17} {7 11} {17 13} {13 19} 
            {23 19} {23 17} {17 21} {15 21} {20 16} {16 12} {12 8} {8 4} {0 6} {6 12} {12 18} {18 24}
        } (colour Hidden))
    })
    
)
