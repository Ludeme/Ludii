(define "PlaceStone"
    (move Add
        (to
            (union
                (sites Around (sites Occupied by:Mover) SSW)
                (sites Around (sites Occupied by:Mover) SSE)
            )
            if:(is Empty (to))
        )
        #1
    )
)

//------------------------------------------------------------------------------

(game "Cascades"
    (players 2)
    (equipment {
        (board (remove (hex Triangle 15) cells:{92..119} vertices:{223..285}))
        (piece "Ball" Each)
        (hand Each)
    })
    (rules
        (start {
            (place "Ball1" (forEach (sites Top) if:(is Even (site))))
            (place "Ball2" (forEach (sites Top) if:(is Odd (site))))
        })
        phases:{
        (phase "FirstMove" (play ("PlaceStone")) (nextPhase "Main"))
        (phase "Main"
            (play 
                (or 
                    ("PlaceStone" (then (if ("NewTurn") (moveAgain))))
                    (if ("SameTurn") (move Pass))
                )
            )
        )
        }
        (end 
            (if 
                (and (no Moves P1) (no Moves P2)) 
                (byScore {		
                    (score P1 (count Sites in:(forEach (sites Bottom) if:(= (who at:(site)) P1))))
                    (score P2 (count Sites in:(forEach (sites Bottom) if:(= (who at:(site)) P2))))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Cascades is an original game implying an original geometric pattern similar to cascades.")
(rules "DROP - Each stone is dropped on an empty cell, next to and below a stone of the same color

MOVE - First player drops one stone at the first turn, then each player drops up to two stones (he may pass).

GOAL - Wins the player with more stones on the bottom row.")
(id "1800")
(source "<a href=\"https://boardgamegeek.com/boardgame/27724/cascades\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
(version "1.3.9")
(classification "board/race/fill")
(author "William Chang")
(credit "Eric Piette")
(date "2001")
}
)
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
    })
    
    (ai
        "Cascades_ai"
    )
)
