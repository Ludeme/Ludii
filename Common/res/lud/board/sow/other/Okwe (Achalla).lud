(define "NextHoleFrom" ("NextSiteOnTrack" 1 from:#1 #2))

(define "PiecesOwnedBy" (count at:(mapEntry #1)))

//------------------------------------------------------------------------------

(game "Okwe (Achalla)"
    (players 2)
    (equipment {
        (board 
            (add
                (concentric {10}) 
                vertices:{{0.45 0.45} {-0.45 -0.45} {0.45 -0.45} {-0.45 0.45}}
            )
            (track "Track" {0 2 4 6 8 9 7 5 3 1} loop:True)
            use:Vertex
        )
        
        (regions P1 (sites {0..4}))  // P1 home
        (regions P2 (sites {5..9}))  // P2 home
        (map {(pair P1 11) (pair P2 10)}) // kalahs (storage pits)
        (piece "Seed" Shared)
    })
    (rules
        (start (set Count 10 to:(sites Track)))
        (play 
            (forEach Site 
                (forEach (sites Mover) if:(< 1 (count at:(site))))
                (forEach Value min:2 max:(count at:(site))
                    (do (set Var (value))
                        next:(move Select 
                            (from (site))
                            (then 
                                (sow 
                                    count:(var)
                                    if:True
                                    apply:(if (and (<= (count at:(to)) 9) (is Odd (count at:(to))))
                                        (fromTo 
                                            (from (to)) 
                                            (to (mapEntry (mover))) 
                                            count:(count at:(to))
                                        )
                                        (if (and (<= (count at:("NextHoleFrom" (to))) 9) (is Odd (count at:("NextHoleFrom" (to)))))
                                            (fromTo 
                                                (from ("NextHoleFrom" (to))) 
                                                (to (mapEntry (mover))) 
                                                count:(count at:("NextHoleFrom" (to)))
                                            )
                                        )
                                    )
                                    origin:True
                                    backtracking:(and (<= (count at:(to)) 9) (is Odd (count at:(to))))
                                )
                            )
                        )	
                    )
                )
            )
        )
        (end ("MancalaByScoreWhen" (and (no Moves P1) (no Moves P2))))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Okwe is the name of several mancala-style board games from West Africa. This particular game was played by the Ibo people in Achalla, Nigeria in the early twentieth century.")
(rules "Two rows of five holes arranged in a circle around four stores in the center. Each player has five holes on one half of the circle. Ten counters in each hole. Players sow from their holes, and can choose to sow any number of counters, but must leave at least one in the starting hole. Sowing occurs in an anti-clockwise direction. If sowing ends in a hole making its contents an odd number of counters, they are captured, but not more than nine. If the sowing ends and the last counter makes the contents of a hole an even number and the next hole has an odd number of counters, the contents of the odd hole are taken, but not more than nine. In both situations, if there is an unbroken sequence of holes with an odd number of counters, these are all taken, but not more than nine from each.")
(source "Murray 1951: 191-192.")
(id "235")
(version "1.3.7")
(classification "board/sow")
(credit "Eric Piette")
(origin "This game was played in Nigeria, around 1951.")
}
)
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Okwe (Achalla)_ai"
    )
)
