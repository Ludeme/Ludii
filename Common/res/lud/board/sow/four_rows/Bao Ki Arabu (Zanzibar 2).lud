(define "Columns" 8)

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2)) ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1 "Home"))))

//------------------------------------------------------------------------------

(game "Bao Ki Arabu (Zanzibar 2)"
    (players 2)

    (equipment {
        (mancalaBoard 4 "Columns" store:None
            {
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" "16,E,N1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))                              // P1 Home
        (regions "Home" P2 (sites Track "Track2"))                              // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top) ))   // P2 inner Home
        (piece "Seed" Shared)
        (hand Each)
        }
    )
    (rules

        (start (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home") ) ) )

        (play
            (move
                Select
                (from
                    (if
                        (is Mover Prev)
                        "PlayFromLastHole"
                        (sites Mover "Home")
                    )
                    if:(> (count at:(from)) 0)
                )
                (then
                    (sow
                        "Track"
                        owner:(mover)
                        apply:(if (= (count at:(to)) 1)
                            (if (is In (to) (sites Mover "Inner"))
                                (if (> (count at:"OppositePit") 0)
                                    (and
                                        (fromTo
                                            (from "OppositePit")
                                            (to (handSite Mover))
                                            count:(count at:"OppositePit")
                                        )
                                        (if
                                            (> (count at:"OppositeOuterPit") 0)
                                            (fromTo
                                                (from "OppositeOuterPit")
                                                (to (handSite Mover))
                                                count:(count at:"OppositeOuterPit")
                                            )
                                        )
                                    )
                                )
                            )
                            (moveAgain)
                        )
                    )
                )
            )
        )

        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1))
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Bao Ki Arabu is one of two mancala-style games by the same name is played by people in Zanzibar, where it is said to have been the original version that came to the island from Arabia. Indeed, it is quite similar to Hawalis, which has been documented in Oman. ")
        (aliases {"Bao" "Bao Kiarabu"})
        (rules "4x8 board. Play begins with two counters in each hole. Sowing occurs in an anti-clockwise direction. When the last counter falls into an occupied hole, the counters in it are picked up and sowing continues. Sowing ends when the last counter falls into an empty hole. When this hole is in the inner row, the counters in the opponent's inner row opposite it are captured; if there are also counters in the opponent's outer row opposite, these are also captured, but not if the inner row is empty. Play continues until one player has lost all of their counters.")
        (source "Ingrams 1931: 256-257.")
        (version "1.3.0")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Zanzibar, around 1931.")
        }
    )

    (graphics {
        (board Style Mancala)
    })

    (ai
        "Bao Ki Arabu (Zanzibar 2)_ai"
    )
)
