(define "LeftMost" 
    (trackSite FirstSite 
        from:(trackSite FirstSite if:(not (is Mover (who at:(to)))))
        if:(is Mover (who at:(to)))
    )
)
(define "NextHoleFrom" (trackSite Move from:#1 #2 #3))

//------------------------------------------------------------------------------

(game "Ngulungu"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 7 store:None
            {
            (track "Track" "0,E,27,W" loop:True)
            (track "TrackCorner" "0,6,27,21" loop:True)
            (track "ReverseTrack" "6,W,21,E" loop:True)
            }  		
        )
        (piece "Seed" Each)
    })  
    (rules 
        (start {
            (place "Seed1" (sites Bottom) counts:{3})
            (place "Seed2" (sites Top) counts:{3})
        })
        
        (play 
            (if (and (all Sites (sites Occupied by:Mover) if:(is In (sites Corners))) (= 1 (count Pieces Mover)))
                (move
                    (from ("LeftMost"))
                    (to 
                        (trackSite FirstSite "TrackCorner" from:(from) if:(is Enemy (who at:(to))))
                        (apply
                            (and
                                (remove (to) count:(count at:(to)))
                                (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                            )	
                        )	
                    )
                )
                (move
                    Select 
                    (from 
                        ("LeftMost") 
                    )
                    (then 
                        (sow
                            sowEffect:(if (and (!= (mover) (what at:(to))) (is Occupied (to)))
                                (and
                                    (remove (to) count:(count at:(to)))
                                    (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                                )
                            )
                            apply:(if (and (= 1 (count at:("NextHoleFrom" (to) "ReverseTrack" steps:1))) (= 1 (count at:(to))))
                                (moveAgain)
                            )
                        )
                    )
                )
            )		
        )
        
        (end
            (forEach NonMover
                if:(no Pieces Player)
                (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Ngulungu is a mancala-stlye board game played in the Katanga province of the Democratic Republic of Congo.")
        (rules "4x7 board. Two counters in each hole.")
        (source "DLP evidence.")
        (version "1.3.0")
        (classification "board/sow/four rows/reconstruction")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (player Colour P1 (colour White))
        (player Colour P2 (colour Black))
    })
)

