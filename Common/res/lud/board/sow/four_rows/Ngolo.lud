(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2)) ) )

(define "LastHole" (last To afterConsequence:True))

(define "NoPieceNext" (= (count in:(sites Next "Home")) 0))

(define "Columns" 9)

//------------------------------------------------------------------------------

(game "Ngolo"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" "18,E,N1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))                              // P1 Home
        (regions "Home" P2 (sites Track "Track2"))                              // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top) ))   // P2 inner Home
        (piece "Seed" Shared)
        }
    )  
    (rules 
        
        (start 
            (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home") ) )
        )
        
        (play 
            (or 
                (move Pass) // Forfeit move
                (move
                    Select
                    (from 
                        (sites Mover "Home") 
                        if:(< 0 (count at:(from)))
                    )
                    (then 
                        (sow
                            "Track"
                            owner:(mover)
                            apply:(if (and {
                                    (!= (count at:("OppositeOuterPit")) 0) 
                                    (!= (count at:("OppositePit")) 0) 
                                    (is In (to) (sites Mover "Inner"))
                                })
                                (and {
                                    (fromTo 
                                        (from ("OppositeOuterPit")) 
                                        (to (to)) 
                                        count:(count at:("OppositeOuterPit"))
                                    )
                                    (fromTo 
                                        (from ("OppositePit")) 
                                        (to (to)) 
                                        count:(count at:("OppositePit"))
                                    )
                                    (sow 
                                        (to)
                                        count:(+ (count at:"OppositePit") (count at:"OppositeOuterPit"))
                                        "Track"
                                        owner:(mover)
                                    )
                                    }
                                )
                            )
                        )
                    )
                )
            )
        )
        
        (end {
            (if ("NoPieceNext")
                (result Mover Win)
            )
            (if (was Pass)
                (result Next Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Ngolo is a four-row mancala-style board game played in the Democratic Republic of Congo.")
        (rules "4x9 board. Two counters in each hole. Players alternate turns sowing from one of the holes in their rows in an anti-clockwise direction. When a counter falls into a hole in their inner row, the player captures the counters from both of the opposite holes on the opponent's side of the board; if one of the two opposite holes is empty, no capture is made. Captures counters are sown on the player's side of the board. Play continues until one player captures all of the counters or one player forfeits.")
        (source "Klepzig 1972: 306.")
        (id "1366")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Democratic Republic of Congo, around 1971.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Ngolo_ai"
    )
)

