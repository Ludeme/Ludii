(define "Columns" <Board:size>)

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2)) ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

//------------------------------------------------------------------------------

(game "Muvalavala (Quioco)"
    (players 2)

    (equipment {
        (mancalaBoard 4 "Columns" store:None
            {
            (track "Track1" "0,E,N1,W" loop:True P1)
            <Board:track2>
            }
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Track "Track1"))                              // P1 Home
        (regions "Home" P2 (sites Track "Track2"))                              // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top) ))   // P2 inner Home
        (regions "Outer" P1 (sites Bottom))                                        // P1 outer Home
        (regions "Outer" P2 (sites Top) )                                          // P2 outer Home
        }
    )
    (rules

        (start {
            (set Count 1
                to:(union {
                    (difference (sites P1 "Home") (sites Left) )
                    (difference (sites P2 "Home") (sites Right) )
                })
            )
            (set Count (- (- (* 4 "Columns") 2) (* 2  (- "Columns" 1)))
                to:(union
                    (intersection (sites Left) (sites P1 "Outer"))
                    (intersection (sites Right) (sites P2 "Outer"))
                )
            )
        })

        (play
            (move
                Select
                (from
                    (if (is Mover Prev)
                        ("PlayFromLastHole")
                        (sites Mover "Home")
                    )
                    if:(> (count at:(from)) 1)
                )
                (then
                    (sow
                        "Track"
                        owner:(mover)
                        apply:(if (and {
                                (> (count at:(to)) 1)
                                (is In (to) (sites Mover "Inner"))
                                (> (count at:"OppositePit") 0)
                            })
                            (and
                                {
                                (fromTo
                                    (from "OppositePit")
                                    (to (to))
                                    count:(count at:"OppositePit")
                                )
                                (if (> (count at:"OppositeOuterPit") 0)
                                    (fromTo
                                        (from "OppositeOuterPit")
                                        (to (to))
                                        count:(count at:"OppositeOuterPit")
                                    )
                                )
                                (sow
                                    (to)
                                    count:(+ (count at:"OppositePit") (count at:"OppositeOuterPit"))
                                    "Track"
                                    owner:(mover)
                                )
                                }
                            )
                            (if (> (count at:(to)) 1) (moveAgain))
                        )
                    )
                )
            )
        )

        (end
            (if
                (no Moves Next)
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <track2>}
    {
    (item "6"   <6> <(track "Track2" "12,E,N1,W" loop:True P2)>   "Each row has 6 holes.")
    (item "7"   <7> <(track "Track2" "14,E,N1,W" loop:True P2)>   "Each row has 7 holes.")
    (item "8"   <8> <(track "Track2" "16,E,N1,W" loop:True P2)>   "Each row has 8 holes.")
    (item "9"   <9> <(track "Track2" "18,E,N1,W" loop:True P2)>   "Each row has 9 holes.")
    (item "10"   <10> <(track "Track2" "20,E,N1,W" loop:True P2)> "Each row has 10 holes.")
    (item "11"   <11> <(track "Track2" "22,E,N1,W" loop:True P2)> "Each row has 11 holes.")
    (item "12"   <12> <(track "Track2" "24,E,N1,W" loop:True P2)> "Each row has 12 holes.")
    (item "13"   <13> <(track "Track2" "26,E,N1,W" loop:True P2)> "Each row has 13 holes.")
    (item "14"   <14> <(track "Track2" "28,E,N1,W" loop:True P2)> "Each row has 14 holes.")
    (item "15"   <15> <(track "Track2" "30,E,N1,W" loop:True P2)> "Each row has 15 holes.")
    (item "16"   <16> <(track "Track2" "32,E,N1,W" loop:True P2)> "Each row has 16 holes.")
})

//------------------------------------------------------------------------------

(rulesets {
    (ruleset "Ruleset/Quiocos (Described)" {
        "Board Size/6"
        }
        variations:{
        "Board Size/7" "Board Size/8" "Board Size/9" "Board Size/10" "Board Size/11"
        "Board Size/12" "Board Size/13" "Board Size/14" "Board Size/15" "Board Size/16"
        }
    )*
})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Muvalavala is a four-row mancala-style board game that is played in different ways in what is now Angola. This version is played by the Quioco people.")
        (rules "4x6-16 board. Number of counters per player is four times the number of holes in a row minus two. Counters are distributed only in the outer row, with two counters in each hole, except the hole on the far left which contains the rest. The counters are then redistributed so that there is one in every hole, except the one on the extreme left in the inner and outer hole. The one on the left in the inner hole remains empty and the one in the outer row retains the same number initially placed there. Play begins from any of the player's holes, sowing anti-clockwise. When the final counter lands in an occupied hole, these are picked up and sowing continues, unless a capture can be made. Captures are made when the final counter falls into an occupied hole in the inner row, and the opponent's hole opposite contains counters. If it is, they are captured, and if the hole in to outer row opposite also contains counters, these are also captured. These are then sown from the hole following the one from which the capture occurred. If the final counter falls into an empty hole, the turn is over. Single counters cannot be sown. Play ends when one player cannot move.")
        (source "Silva 1995: 83-85.")
        (version "1.3.0")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        }
    )

    (graphics {
        (board Style Mancala)
    })

    (ai
        "Muvalavala (Quioco)_ai"
    )
)

