(define "Columns" <Board:rowSize>)
(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns")))
(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2))))
(define "OppositePitOuter" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2))))
(define "OppositeOuterPitOuter" (if (is Mover P1) (+ (to) (* "Columns" 3)) (- (to) (* "Columns" 3))))

//------------------------------------------------------------------------------

(game "Hus (Nama)"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" <Board:CCW2> loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))                                     // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))                                     // P2 home
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom)))        // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top)))          // P2 inner Home
        (regions "Outer" P1 (difference (sites Track "TrackCCW1") (sites P1 "Inner")))    // P1 outer Home
        (regions "Outer" P2 (difference (sites Track "TrackCCW2") (sites P2 "Inner")))   // P2 outer Home
        (regions "HalfRightInner" P1 (sites {<Board:HalfRightInnerP1>}))                 // P1 half right inner row
        (regions "HalfRightInner" P2 (sites {<Board:HalfRightInnerP2>}))                 // P2 half right inner row
        (piece "Seed" Shared)
        (hand Each)
    })  
    (rules 
        
        (start 
            (set Count 2 to:(union {(sites P1 "Outer") (sites P2 "Outer") (sites P1 "HalfRightInner")  (sites P2 "HalfRightInner") })) 
        )
        
        phases:{
        (phase "OpeningP1" P1
            (play
                (or {
                    (move Select
                        (from (- (/ ("Columns") 2) 2))
                        (then
                            (and
                                (fromTo
                                    (from (last To))
                                    (to (+ (/ ("Columns") 2) 1))
                                    count:2
                                )
                                (sow
                                    (+ (/ ("Columns") 2) 1)
                                    count:2
                                    "TrackCCW"
                                    owner:(mover)
                                )
                            )
                        )
                    )
                    
                    (move Select
                        (from (+ (/ ("Columns") 2) 2))
                        (then
                            (and
                                (fromTo
                                    (from (last To))
                                    (to (+ (+ ("Columns") (/ ("Columns") 2)) 1))
                                )
                                (fromTo
                                    (from (last To))
                                    (to (+ (+ ("Columns") (/ ("Columns") 2)) 2))
                                )
                            )
                        )
                    )
                    
                    (move Select
                        (from (+ ("Columns") (/ ("Columns") 2)))
                        (then
                            (and
                                (fromTo
                                    (from (last To))
                                    (to (+ (+ ("Columns") (/ ("Columns") 2)) 1))
                                )
                                (fromTo
                                    (from (last To))
                                    (to (+ (+ ("Columns") (/ ("Columns") 2)) 2))
                                )
                            )
                        )
                    )
                    (move Select
                        (from (- (/ ("Columns") 2) 1))
                        (then
                            (sow
                                "TrackCCW"
                                owner:(mover)
                            )
                        )
                    )
                })
            )
            (nextPhase Mover "Sowing")
        )
        
        (phase "OpeningP2" P2
            (play
                (or {
                    (move Select
                        (from (+ (- (* 4 ("Columns")) (/ ("Columns") 2)) 1))
                        (then
                            (and
                                (fromTo
                                    (from (last To))
                                    (to (- (- (* 4 ("Columns")) (/ ("Columns") 2)) 2))
                                    count:2
                                )
                                (sow
                                    (- (- (* 4 ("Columns")) (/ ("Columns") 2)) 2)
                                    count:2
                                    "TrackCCW"
                                    owner:(mover)
                                )
                            )
                        )
                    )
                    (move Select
                        (from (- (- (* 4 ("Columns")) (/ ("Columns") 2)) 3))
                        (then
                            (and
                                (fromTo
                                    (from (last To))
                                    (to (- (- (* 3 ("Columns")) (/ ("Columns") 2)) 2))
                                )
                                (fromTo
                                    (from (last To))
                                    (to (- (- (* 3 ("Columns")) (/ ("Columns") 2)) 3))
                                )
                            )
                        )
                    )
                    (move Select
                        (from (- (- (* 3 ("Columns")) (/ ("Columns") 2)) 1))
                        (then
                            (and
                                (fromTo
                                    (from (last To))
                                    (to (- (- (* 3 ("Columns")) (/ ("Columns") 2)) 2))
                                )
                                (fromTo
                                    (from (last To))
                                    (to (- (- (* 3 ("Columns")) (/ ("Columns") 2)) 3))
                                )
                            )
                        )
                    )
                    (move Select
                        (from (- (* 4 ("Columns")) (/ ("Columns") 2)))
                        (then
                            (sow
                                "TrackCCW"
                                owner:(mover)
                            )
                        )
                    )
                })
            )
            (nextPhase Mover "Sowing")
        )
        
        (phase "Sowing"
            (play
                (move Select
                    (from
                        (if ("SameTurn")
                            (sites {(var "Replay")})
                            (sites Mover "Home")
                        )
                        if:(< 1 (count at:(from)))
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (< 1 (count at:(to)))
                                (if (is In (to) (sites Mover "Inner"))
                                    (if (is Occupied ("OppositePit"))
                                        (and {
                                            (fromTo
                                                (from ("OppositePit"))
                                                (to (to))
                                                count:(count at:("OppositePit"))
                                            )
                                            (if (is Occupied ("OppositeOuterPit"))
                                                (and 
                                                    (fromTo
                                                        (from ("OppositeOuterPit"))
                                                        (to (to))
                                                        count:(count at:("OppositeOuterPit"))
                                                    )
                                                    (sow
                                                        (to)
                                                        count:(+ (count at:("OppositeOuterPitOuter")) (count at:("OppositePitOuter")))
                                                        "TrackCCW"
                                                        owner:(mover)
                                                    )
                                                    
                                                )
                                                (sow
                                                    (to)
                                                    count:(count at:("OppositePit"))
                                                    "TrackCCW"
                                                    owner:(mover)	
                                                )
                                            )
                                        })
                                        (and
                                            (moveAgain)	
                                            (set Var "Replay" (to))
                                        )
                                    )
                                    
                                    (if (is Occupied ("OppositePitOuter"))
                                        (and {
                                            (fromTo
                                                (from ("OppositePitOuter"))
                                                (to (to))
                                                count:(count at:("OppositePitOuter"))
                                            )
                                            (if (is Occupied ("OppositeOuterPitOuter"))
                                                (and
                                                    (fromTo
                                                        (from ("OppositeOuterPitOuter"))
                                                        (to (to))
                                                        count:(count at:("OppositeOuterPitOuter"))
                                                    )
                                                    (sow
                                                        (to)
                                                        count:(+ (count at:("OppositeOuterPitOuter")) (count at:("OppositePitOuter")))
                                                        "TrackCCW"
                                                        owner:(mover)	
                                                    )
                                                )
                                                (sow
                                                    (to)
                                                    count:(count at:("OppositePitOuter"))
                                                    "TrackCCW"
                                                    owner:(mover)	
                                                )
                                            )
                                        })
                                        (and
                                            (moveAgain)	
                                            (set Var "Replay" (to))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (end "ForEachNonMoverNoMovesLoss")
        )
        }
        
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <rowSize> <CCW2> <HalfRightInnerP1> <HalfRightInnerP2> }
    {
    (item "16"   <16> <"32,E,N1,W"> <24..31> <32..39> "Each player has 16 holes on each row.")*  
    (item "18"   <18> <"36,E,N1,W"> <27..35> <36..44> "Each player has 18 holes on each row.")  
    (item "20"   <20> <"40,E,N1,W"> <30..39> <40..49> "Each player has 20 holes on each row.")   
    (item "22"   <22> <"44,E,N1,W"> <33..43> <44..54> "Each player has 22 holes on each row.")   
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Schultze (Described)" {
        "Board Size/16"
        }
        variations:{"Board Size/18" "Board Size/20" "Board Size/22"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "||Hus is a common name for four-row mancala-style board games that are popular throughout Namibia. This version is played by the Nama people, and was documented in the early twentieth century.")
        (aliases {"||Hus (Nama)" "Huts"})
        (rules "4x16-22 board. Two counters in each player's outer row and the right half of the holes in the inner row. The opening move is stylized; each player must make one of the following moves on their first turn:
            
            Take counters from the hole that is two to the right of the center of the board in the outer row. Sow them to the right starting in the hole third from the center in the inner row;
            
            Take counters from the hole that is three to the right of the center of the board in the outer row. Sown them to the right starting from the hole second from the center in the inner row;
            
            Take counters from the hole next to the center of the board in the inner row. Sow them to the right;
            
            Take counters from the hole next to the left of the center of the board in the outer row. Sow them to the right starting from the hole next to the right of the center of the board.
            
        Players then sow from any of their holes in an anti-clockwise direction. If the last counter of a sowing lands in an empty hole, the turn ends. If the last counter falls into an occupied hole, the counters in that hole are picked up and sowing continues, unless the hole in the inner row on the opponent's side opposite the one the player last sowed into is occupied, in which case any counters in this hole and any in the opponent's opposite outer row hole are captured. The captured counters are then sown on the player's side of the board, starting with the hole following the one from which the capture was made. Sowing cannot occur from holes with single counters. The game ends when one player can no longer move.")
        (source "Schultze 1907: 313-315.")
        (id "254")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Namibia, around 1907.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)

