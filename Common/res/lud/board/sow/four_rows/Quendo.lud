(define "Columns" 8)
(define "NumSeed" <Sow:numSeed>)
(define "LeftMost" 
    (trackSite FirstSite 
        from:(trackSite FirstSite if:(not (is Mover (who at:(to)))))
        if:(is Mover (who at:(to)))
    )
)

//------------------------------------------------------------------------------

(game "Quendo" 
    (players 4) 
    
    (equipment { 
        <Board:board>
        (piece "Seed" Each)
        (hand Each)
    }) 
    
    (rules 
        
        <Board:start>
        
        (play 
            (if (!= 1 (count Pieces Mover))
                (move
                    Select 
                    (from 
                        ("LeftMost") 
                    )
                    (then
                        (sow
                            sowEffect:(if (and (!= (mover) (what at:(to))) (is Occupied (to)))
                                (and
                                    (remove (to) count:(count at:(to)))
                                    (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                                )
                            )		
                        )
                    )
                )
                (move
                    (from ("LeftMost"))
                    (to 
                        (trackSite FirstSite from:(from) if:("IsEnemyAt" (to)))
                        (apply
                            (and
                                (remove (to) count:(count at:(to)))
                                (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                            )	
                        )	
                    )
                )
            )
        )
        
        (end 
            (forEach NonMover 
                if:(no Pieces Player) 
                (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Start Rules" <Sow> args:{<numSeed>}
    {
    (item "1" <1> "1 seed per hole.")
    (item "2" <2> "2 seeds per hole.")
    (item "3" <3> "3 seeds per hole.")
    (item "4" <4> "4 seeds per hole.")**
    }
)

(option "Board" <Board> args:{<board> <start>}
    {
    (item "4x8" 
        <
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track" "0,E,N,W,S2" loop:True)
            }
        )
        > 
        <
        (start {
            (place "Seed1" (sites {0..4}) counts:{"NumSeed"})
            (place "Seed2" (sites {5..7 15 23}) counts:{"NumSeed"})
            (place "Seed3" (sites {31..27}) counts:{"NumSeed"})
            (place "Seed4" (sites {26..24 16 8}) counts:{"NumSeed"})
        })
        > 
        "The board is a 4x8."
    )**
    
    (item "Circular" 
        <
        (board 
            (concentric {20}) 
            { 
            (track "Track" "0,2,4,6,8,10,12,14,16,18,19,17,15,13,11,9,7,5,3,1" loop:True)
            }
            use:Vertex
        )
        > 
        <
        (start {
            (place "Seed1" (sites {0 2 4 6 8}) counts:{"NumSeed"})
            (place "Seed2" (sites {10 12 14 16 18}) counts:{"NumSeed"})
            (place "Seed3" (sites {19 17 15 13 11}) counts:{"NumSeed"})
            (place "Seed4" (sites {9 7 5 3 1}) counts:{"NumSeed"})
        })
        > 
        "The board is circular."
    )
    
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Quendo (Observed)" {
        "Start Rules/4" "Board/4x8"
        }
        variations:{"Start Rules/1" "Start Rules/2" "Start Rules/3"}
    )*
    
    (ruleset "Ruleset/Circular Board (Observed)" {
        "Start Rules/4" "Board/Circular"
        }
        variations:{"Start Rules/1" "Start Rules/2" "Start Rules/3"}
    )
    
})

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Quendo is a game played by children of the Lunda-Quioco people of Angola. It involves the sowing mechanism of mancala games, but differs from others in that the holes which a player controls are those in which their counters are located, rather than determined by the placement of the holes on the board.")
(useFor "Ruleset/Quendo (Observed)" (rules "4x8 board, played only along the perimeter. One to four counters per hole. Any number of players, holes are distributed equally among the players. Sowing occurs in an anti-clockwise direction. Each player owns the holes in which their counters are located at the beginning of their turn, and they must sow from the leftmost hole. When a counter falls into a hole containing counters belonging to the opponent, this hole and the counters in it are captured by the player, but remain in place and in play. When a player has a single counter and empty holes in from of it, this counter leaps to the next hole occupied by the opponent. The player who captures all of the opponent's holes wins. "))
(useFor "Ruleset/Quendo (Observed)" (source "Silva 1995: 106-109."))
(useFor "Ruleset/Quendo (Observed)" (id "273"))
(useFor "Ruleset/Circular Board (Observed)" (rules "Circular board with 10-30 holes."))
(useFor "Ruleset/Circular Board (Observed)" (source "Silva 1995: 106-109."))
(useFor "Ruleset/Circular Board (Observed)" (id "1176"))
(id "273")
(version "1.3.9")
(classification "board/sow/four rows")
(credit "Eric Piette")
(origin "This game was played in Angola, around 1995.")
}
)
    
    (graphics {
        (board Style Mancala)
        (player Colour P1 (colour White))
        (player Colour P2 (colour Black))
        (player Colour P3 (colour Red))
        (player Colour P4 (colour Green))
    })
    
)

