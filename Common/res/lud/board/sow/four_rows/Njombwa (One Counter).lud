(define "Columns" <Row:size>)
(define "NoPiece" (all Sites (sites Player) if:(= 0 (count at:(site)))))
(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))
(define "RightMostHole"
    (if (is Mover P1)
        (intersection (sites Mover "Outer") (sites Right))
        (intersection (sites Mover "Outer") (sites Left))
    )
)

(define "SecondToRightInnerRow"
    (if (is Mover P1)
        (intersection (sites Mover "Inner") (difference (expand (sites Right)) (sites Right)))
        (intersection (sites Mover "Inner") (difference (expand (sites Left)) (sites Left)))
    )
)

(define "AllSitesNoMoreThanOne"
    (all Sites (sites Mover "Home") if:(>= 1 (count at:(site))))
)

//------------------------------------------------------------------------------

(game "Njombwa (One Counter)" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" <Row:track2> loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))        // P1 Home
        (regions "Home" P2 (sites Track "Track2"))        // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top))) // P2 inner Home
        (regions "Outer" P1 (sites Bottom))          // P1 inner Home
        (regions "Outer" P2 (sites Top))          // P2 inner Home
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start 
            (set Count 1 to:(sites Board)) 
        )
        
        phases:{
        (phase "Opening1"
            (play
                (move Select
                    (from
                        (if 
                            ("SameTurn") 
                            "LastHoleSowed" 
                            ("RightMostHole")
                        ) 
                        if:(> (count at:(from)) 0)
                    )
                    (then
                        (sow
                            "Track"
                            owner:(mover)
                            apply:(if (and (not (is In ("NextHole" (to) 1) ("RightMostHole"))) (< 1 (count at:(to)))) (moveAgain))
                        )
                    )
                )
            )
            
            (nextPhase Mover (not (is Next Mover)) "Opening2")
        )
        
        (phase "Opening2"
            (play
                (move 
                    (from
                        (sites Mover "Home")
                        if:(is In ("NextHole" (from) 1) ("RightMostHole"))
                    )
                    (to
                        ("RightMostHole")	
                    )
                    count:2
                )
            )
            
            (nextPhase Mover "Opening3")
        )
        
        (phase "Opening3"
            (play
                (move Remove
                    ("SecondToRightInnerRow")
                )
            )
            
            (nextPhase Mover "Sowing")
        )
        (phase "Sowing"
            (play
                (if ("AllSitesNoMoreThanOne")
                    (move Select
                        (from (sites Mover "Home") if:(and (is Occupied (from)) (is Empty ("NextHole" (from) 1))))
                        (then 
                            (sow
                                "Track"
                                owner:(mover)		
                            )
                        )
                    )
                    (move Select
                        (from 
                            (if 
                                ("SameTurn") 
                                "LastHoleSowed"
                                (sites Mover "Home") 
                            ) 
                            if:(> (count at:(from)) 1)
                        )
                        (then
                            (sow
                                "Track"
                                owner:(mover)
                                apply:(if (< 1 (count at:(to)))
                                    (moveAgain)
                                    (if (and 
                                            (is In (to) (sites Mover "Inner"))
                                            (is Occupied ("OppositePit" (to)))
                                        )
                                        (and
                                            (remove ("OppositePit" (to)))
                                            (if (is Occupied ("OppositeOuterPit" (to)))
                                                (remove ("OppositeOuterPit" (to)))
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )		
            )
        )
        }
        
        (end
            (forEach NonMover
                if:("NoPiece")
                (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Row> args:{ <size> <track2> }
    {
    (item "8" <8> <"16,E,N1,W"> "Each row has 8 holes.")* 
    (item "9" <9> <"18,E,N1,W"> "Each row has 9 holes.") 
    (item "10" <10> <"20,E,N1,W"> "Each row has 10 holes.") 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Njombwa (One Counter) (Observed)" {
        "Board Size/8"
        }
        variations:{"Board Size/9" "Board Size/10"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Njombwa is a four-row mancala-style board game played by the Yao people of East Africa. In this version, players begin with one counter in each hole.")
        (rules "4x8 board, occasionally 4x9 or 10. One counter in each hole. Sowing occurs in an anti-clockwise direction. The players start by each making a stylised move. Sowing begins from the rightmost hole in the outer row. When the final counter lands in an occupied hole, these counters are picked up and sowing continues. When the sowing reaches the hole immediately before the one from which the sowing began (I.e,, the second-to-right hole in the outer row), these two counters are picked up and both placed in the rightmost hole in the outer row. The player then removes the two counters in the second-to-right hole in the inner row from the board. When both players complete this move, the main phase of the game begins. Players sow from any hole in their two rows. When the final counter lands in an occupied hole, these counters are picked up and sowing continues. When the final counter lands in an empty hole in the inner row, the counters in the opponent's opposite hole in their inner row are captured. If there are also counters in the opponent's opposite hole in the outer row, these are also captured, but only when a capture from the inner row was also made. Players cannot sow single counters, unless there are no holes with multiple counters left, in which case single counters may be sown into an empty hole. Play continues until one player has captured all of the opponent's counters, thus winning the game.")
        (source "Sanderson 1913: 732-733.")
        (id "1311")
        (version "1.3.13")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin "This game was played in East Africa, around 1913.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Njombwa (One Counter)_ai"
    )
)
