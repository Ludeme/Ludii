(define "Columns" <Board:size>)

(define "NextHole" ("NextSiteOnTrack" 1 from:(to) "Track"))

(define "PlayFromNextHole" (sites {(value Pending)}))

(define "MoveAgainAfterCapture" (is Pending))

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Hus (Damara)" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" <Board:track2> loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))        // P1 Home
        (regions "Home" P2 (sites Track "Track2"))        // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top))) // P2 inner Home
        (regions "Outer" P1 (sites Bottom))          // P1 outer Home
        (regions "Outer" P2 (sites Top))          // P1 outer Home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start 
            (set Count 2 to:(union { 
                    (expand (intersection (sites Row 1) (sites Right)) steps:3 W) 
                    (expand (intersection (sites Row 2) (sites Left)) steps:3 E) 
                    (sites P1 "Outer")
                    (sites P2 "Outer") 
                }) 
            ) 
        )
        
        (play 
            (move Select
                (from 
                    (if ("SameTurn") 
                        (if "MoveAgainAfterCapture" "PlayFromNextHole" "LastHoleSowed") 
                        (sites Mover "Home") 
                    ) 
                    if:(> (count at:(from)) <Sow:numSeed>)
                )
                (then 
                    (sow
                        "Track"
                        owner:(mover)
                        apply:(if (> (count at:(to)) 1)
                            (if (and { 
                                    (is In (to) (sites Mover "Inner")) 
                                    (> (count at:("OppositePit" (to))) 0) 
                                    (> (count at:("OppositeOuterPit" (to))) 0)
                                })
                                (and
                                    {
                                    (fromTo 
                                        (from (to)) 
                                        (to (handSite Mover)) 
                                        count:(count at:(to))
                                    ) 
                                    (fromTo 
                                        (from ("OppositePit" (to))) 
                                        (to (handSite Mover)) 
                                        count:(count at:("OppositePit" (to)))
                                    ) 
                                    (fromTo 
                                        (from ("OppositeOuterPit" (to))) 
                                        (to (handSite Mover)) 
                                        count:(count at:("OppositeOuterPit" (to)))
                                    )
                                    (if (> (count at:"NextHole") 1)
                                        (and
                                            (set Pending "NextHole") // We store the next hole to play in the pending value.
                                            (moveAgain)
                                        )
                                    )
                                    }
                                )
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <track2> }
    {
    (item "12" <12> <"24,E,N1,W"> "Each player has 12 holes.")* 
    (item "14" <14> <"28,E,N1,W"> "Each player has 14 holes.") 
    (item "16" <16> <"32,E,N1,W"> "Each player has 16 holes.") 
    (item "18" <18> <"36,E,N1,W"> "Each player has 18 holes.") 
    (item "20" <20> <"40,E,N1,W"> "Each player has 20 holes.") 
    (item "22" <22> <"44,E,N1,W"> "Each player has 22 holes.") 
    (item "24" <24> <"48,E,N1,W"> "Each player has 24 holes.") 
})

(option "Play Rules" <Sow> args:{<numSeed>}
    {
    (item "Sowing with 1 or more seeds" <0> "The player can sow from any hole with one or more seeds.")**
    (item "Sowing with 2 or more seeds" <1> "The player can sow from any hole with two or more seeds.")
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Damara (Observed)" {
        "Play Rules/Sowing with 1 or more seeds" "Board Size/12"
        }
        variations:{"Board Size/14" "Board Size/16" "Board Size/18" 
        "Board Size/20" "Board Size/22" "Board Size/24" 
        }
    )*
    
    (ruleset "Ruleset/Wagner (Observed)" {
        "Play Rules/Sowing with 2 or more seeds" "Board Size/12"
        }
        variations:{"Board Size/14" "Board Size/16" "Board Size/18" 
        "Board Size/20" "Board Size/22" "Board Size/24" 
        }
    )
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "||Hus is a four-row mancala-style game played among several groups in Southwest Africa, including the Damara. Hus is a word applied to several different games throughout the region, which were documented in the early twentieth century. ")
        (aliases {"||Hus (Damara)" "Huts" "Otjitoto"})
        (useFor "Ruleset/Damara (Observed)" (rules "4x12-24 (only even numbers allowed) board. Play begins with two counters in the outer row of each players' holes and two counter in each of the four holes on the right hand side of both players' inner row of holes. Player picks up contents of any hole containing two or more counters and sows them anti-clockwise. If the final counter of the sowing falls into an empty hole, the turn is over. If the last counter falls into an occupied hole, sowing continues by picking up the counters in that hole and continuing in the same direction. If the last counter falls into an occupied hole in the player's inner row, and the opponent's two holes opposite it are occupied, these are captured and sowing continues using these counters beginning at the next hole after the one which caused the capture. Play ends when one player cannot move; i.e. when they have only single counters in holes or all their counters have been captured."))
        (useFor "Ruleset/Damara (Observed)" (source "Vedder 1923: 95-96."))
        (useFor "Ruleset/Damara (Observed)" (id "375"))
        (useFor "Ruleset/Wagner (Observed)" (rules "Players cannot sow from holes with only a single counter. If a player only has holes with single counters or no counters, they are unable to play and lose the game."))
        (useFor "Ruleset/Wagner (Observed)" (source "Wagner 1918: 47-51."))
        (useFor "Ruleset/Wagner (Observed)" (id "613"))
        (source "Townshend 1977b: 88-89.")
        (id "375")
        (version "1.3.11")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin "This game was played in Southwest Africa, from around 1916 to 1923.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Hus (Damara)_ai"
    )
)
