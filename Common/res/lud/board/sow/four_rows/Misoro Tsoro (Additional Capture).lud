(define "Columns" <Row:size>)

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns")))

(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2))))

(define "NoPiece" (= (count in:(sites Player "Home")) 0))

(define "LastHoleSowed" (sites {(var)}))

(define "AHoleHasMoreThanOneCounter" 
    (!= 0 (count Sites in:(forEach (sites Mover) if:(< 1 (count at:(site))))))
)

(define "NextHoleFrom" (trackSite Move from:#1 #2 steps:1))

(define "NumCapture" (var "ExtraCapture"))

(define "HaveToMakeExtraCapture" (set Var "ExtraCapture" <ExtraCapture:num>))

//------------------------------------------------------------------------------

(game "Misoro Tsoro (Additional Capture)"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" <Row:track2> loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))                              // P1 Home
        (regions "Home" P2 (sites Track "Track2"))                              // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top)))   // P2 inner Home
        (regions "Outer" P1  (sites Bottom))                                    // P1 outer Home
        (regions "Outer" P2 (sites Top))                                        // P2 outer Home
        (piece "Seed" Shared)
        }
    )  
    (rules 
        
        <Start:rules>
        
        phases:{
        <Start:misoroPhase>
        (phase "Playing"
            (play 
                (if (< 0 ("NumCapture"))
                    (move 
                        Select
                        (from (forEach (sites Next) if:(and (= 0 (state at:(site))) (< 0 (count at:(site))))))
                        (then 
                            (and {
                                (if (< 1 ("NumCapture")) (moveAgain))
                                (set Var "ExtraCapture" (- ("NumCapture") 1))
                                (remove (last To))
                                (forEach Site (sites Board)
                                    (if (!= 0 (state at:(site))) (set State at:(site) (state at:(site))))
                                )
                            })
                        )
                    )
                    (or
                        (if (and (!= 0 (state at:(var))) (is Mover Prev))
                            (move Pass)
                        )
                        (move
                            Select
                            (from 
                                (if 
                                    (is Mover Prev)
                                    "LastHoleSowed" 
                                    (sites Mover "Home") 
                                ) 
                                if:(if "AHoleHasMoreThanOneCounter"
                                    (> (count at:(from)) 1)
                                    (and (= (count at:(from)) 1) (= 0 (count at:("NextHoleFrom" (from) Mover)))) 
                                )
                            )
                            (then 
                                (and
                                    (sow
                                        "Track"
                                        owner:(mover)
                                        apply:(if (= (count at:(to)) 1)
                                            (if (is In (to) (sites Mover "Inner"))
                                                (if (> (count at:"OppositePit") 0) 
                                                    (and {
                                                        (remove ("OppositePit")) 
                                                        (if 
                                                            (> (count at:"OppositeOuterPit") 0) 
                                                            (remove "OppositeOuterPit") 
                                                        )
                                                        (moveAgain)
                                                        ("HaveToMakeExtraCapture")
                                                    }) 
                                                ) 
                                            )
                                            (and 
                                                (moveAgain)
                                                (set Var (to))
                                            )
                                        )
                                    )
                                    (forEach Site (sites Board)
                                        (if (!= 0 (state at:(site))) (set State at:(site) (state at:(site))))
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end
            (forEach NonMover
                if:("NoPiece")
                (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Start Rules" <Start> args:{<rules> <misoroPhase>}
    {
    (item "Misoro Choice"   
        <
        (start (set Count 2 to:(union (sites Top) (sites Bottom ))))
        >
        <
        (phase "Misoro" 
            (play
                (move 
                    Select 
                    (from (forEach (sites Mover "Outer") if:(= 0 (state at:(site)))))
                    (then (set State at:(last To) (mover)))
                )
            )
            (nextPhase (= 4 (count Turns)) "Playing")
        )
        >  
        "Players choose the misoro holes."
    )*  
    (item "Fixed Misoro"   
        <
        (start {
            (place "Seed" (intersection (sites Bottom) (sites Left)) counts:{2} state:1)
            (place "Seed" (intersection (sites Top) (sites Right)) counts:{2} state:2)
            (place "Seed" (intersection (sites Top) (sites Left)) counts:{3} state:2)
            (place "Seed" (intersection (sites Bottom) (sites Right)) counts:{3} state:1)
            (set Count 2 to:(difference (union (sites Top) (sites Bottom )) (union (sites Left) (sites Right ))))
        })
        >
        <
        >  
        "The misoro holes are fixed."
    )*
    }
)

(option "Board Size" <Row> args:{ <size> <track2>}
    {
    (item "6"   <6>  <"12,E,N1,W">  "6 Holes per row.")   
    (item "7"   <7>  <"14,E,N1,W">  "7 Holes per row.")   
    (item "8"   <8>  <"16,E,N1,W">  "8 Holes per row.")**   
    (item "9"   <9>  <"18,E,N1,W">  "9 Holes per row.")   
    (item "10"  <10> <"20,E,N1,W"> "10 Holes per row.")   
    (item "11"  <11> <"22,E,N1,W"> "11 Holes per row.")   
    (item "12"  <12> <"24,E,N1,W"> "12 Holes per row.")*   
    (item "13"  <13> <"26,E,N1,W"> "13 Holes per row.")   
    (item "14"  <14> <"28,E,N1,W"> "14 Holes per row.")   
    (item "15"  <15> <"30,E,N1,W"> "15 Holes per row.")*   
    (item "16"  <16> <"32,E,N1,W"> "16 Holes per row.")  
    (item "17"  <17> <"34,E,N1,W"> "17 Holes per row.")  
    (item "18"  <18> <"36,E,N1,W"> "18 Holes per row.")*  
    (item "19"  <19> <"38,E,N1,W"> "19 Holes per row.")  
    (item "20"  <20> <"40,E,N1,W"> "20 Holes per row.")  
    (item "21"  <21> <"42,E,N1,W"> "21 Holes per row.")
})

(option "Play Rules" <ExtraCapture> args:{ <num> }
    {
    (item "1 extra capture" <1> "1 extra capture.")   
    (item "2 extra captures" <2> "2 extra captures.")   
    (item "3 extra captures" <3> "3 extra captures.")**   
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Misoro Tsoro (Additional Capture) (Observed)" {
        "Board Size/8" "Start Rules/Misoro Choice" "Play Rules/3 extra captures"
        }
        variations:{"Board Size/6" "Board Size/7" "Board Size/9" "Board Size/10" "Board Size/11"
        "Board Size/12" "Board Size/13" "Board Size/14" "Board Size/15" "Board Size/16" "Board Size/17"
        "Board Size/18" "Board Size/19" "Board Size/20" "Board Size/21" "Play Rules/2 extra captures" "Play Rules/1 extra capture"}
    )*
    
    (ruleset "Ruleset/Extra Counters (Observed)" {
        "Board Size/8" "Start Rules/Fixed Misoro" "Play Rules/3 extra captures"
        }
        variations:{"Board Size/6" "Board Size/7" "Board Size/9" "Board Size/10" "Board Size/11"
        "Board Size/12" "Board Size/13" "Board Size/14" "Board Size/15" "Board Size/16" "Board Size/17"
        "Board Size/18" "Board Size/19" "Board Size/20" "Board Size/21" "Play Rules/2 extra captures" "Play Rules/1 extra capture"}
    )
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tsoro is a name for sevela two-row mancala-style board games from Zimbabwe. In this version, multiple captures are allowed, as well as the option to end a sowing in certain holes.")
        (useFor "Ruleset/Misoro Tsoro (Additional Capture) (Observed)" (rules "4x6-21 board; 8 is most common, 12, 15, and 18 are also popular. Two counters in each hole in the players' outer rows. Two holes in each player's outer row are selected as misoro. Typically, the left two holes are chosen. At the beginning of the game, players choose whether to capture from one, two, or three extra holes. Sowing occurs in an anti-clockwise direction, only in the two rows belonging to the player. When the final counter lands in an occupied hole, these are picked up and sowing continues. If this final hole is one of the misoro, the player may choose to end their turn instead of continuing to sow. When the final counter lands in an empty hole in the inner row, any counters in the opposite hole in the opponent's inner row are captured. If there also are counters in the opposite hole in the opponent's outer row, these are also captured, but only if there was first a capture from the inner row hole. The player also captures the counters from the agreed-upon number of holes on the opponent's side of the board. Counters in misoro cannot be captured with one of these additional captures. Players cannot sow from a hole with a single counter unless there are no holes with multiple counters. Single counters can only be sown into an empty hole. Play continues until one player has captured all of the opponent's counters, thus winning the game."))
        (useFor "Ruleset/Misoro Tsoro (Additional Capture) (Observed)" (source "Matthews 1964: 65-66."))
        (useFor "Ruleset/Misoro Tsoro (Additional Capture) (Observed)" (id "1291"))
        (useFor "Ruleset/Extra Counters (Observed)" (rules "The leftmost and rightmost holes are misoro. Play starts with two counters in each hole in the outer row for each player, except their rightmost hole which contains three.
                
        "))
        (useFor "Ruleset/Extra Counters (Observed)" (source "Matthews 1964: 65-66."))
        (useFor "Ruleset/Extra Counters (Observed)" (id "1293"))
        (source "Matthews 1964: 65-66.")
        (id "1291")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Zimbabwe, around 1964.")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (show LocalStateHoles)
    })
    
    (ai
        "Misoro Tsoro (Additional Capture)_ai"
    )
)
