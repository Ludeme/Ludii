(define "Columns" 8)

(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2))))
(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns")))
(define "PiecesOwnedBy" (count in:(sites #1 "Home")))

//------------------------------------------------------------------------------

(game "Lisolo"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 ("Columns") store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" "16,E,N1,W" loop:True P2)
            }		
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Track "Track1"))  // P1 Home
        (regions "Home" P2 (sites Track "Track2"))  // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top)))   // P2 inner Home
        }
    )  
    (rules 
        
        (start (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home"))))
        
        phases:{
        (phase "Opening"
            (play
                (or
                    (forEach Site (sites Mover "Home")
                        (if (is Occupied (site))
                            (move 
                                (from (site))
                                (to 
                                    (forEach (sites Mover "Home")
                                        if:(!= (to) (site))
                                    )
                                )
                            )
                        )
                    )
                    (move Pass)
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        (phase "Sowing"
            (play
                (move
                    Select
                    (from 
                        (if 
                            ("SameTurn") 
                            "LastHoleSowed" 
                            (sites Mover) 
                        ) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "Track"
                            owner:(mover)
                            apply:(if (> (count at:(to)) 1)
                                (if  (not (is In (to) (sites Mover "Inner")))
                                    (moveAgain)
                                    (if (and 
                                            (= 0 (count at:"OppositePit"))
                                            (= 0 (count at:"OppositeOuterPit"))
                                        )
                                        (moveAgain)
                                        (and
                                            (if (!= 0 (count at:"OppositePit"))
                                                (remove 
                                                    ("OppositePit") 
                                                    count:(count at:"OppositePit" )
                                                ) 
                                            )
                                            (if (!= 0 (count at:"OppositeOuterPit"))
                                                (remove 
                                                    ("OppositeOuterPit") 
                                                    count:(count at:"OppositeOuterPit" )
                                                ) 
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )	  
            )
        )
        }
        
        (end 
            (forEach Player
                if:(= 0 ("PiecesOwnedBy" Player))
                (result Player Loss)
            )		
        )
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Lisolo is a four-row mancala-stly eboard game played by the Bemba people of central Africa.")
        (rules "4x8 board. Two counters in each hole. Before play, players may arrange their counters as they see fit in their holes. Players alternate turns sowing the counters from one of their holes in an anticlockwise direction. When the final counter lands in an empty hole, their turn is over. If the final counter lands in an occupied hole, the contents of the two opposite hole in the opponent's rows are captured. If both do not have counters, then the player picks up the counters from the hole the last counter fell into and continues sowing. The player who captures all of the opponent's counters wins.")
        (source "Edm√© 1946: 160.")
        (id "1384")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Democratic Republic of Congo, around 1946.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)

