(define "Columns" 8)
(define "NoPiecesInInner" (no Pieces All in:(sites #1 "Inner")))
(define "OppositePit" (if (is Mover P1) (+ #1 "Columns") (- #1 "Columns")) )
(define "Sowing"
    (sow
        #1
        owner:(mover)
        apply:(if (is In (to) (sites Mover "Inner"))
            (if (is Occupied ("OppositePit" (to)))
                (and {
                    (moveAgain)
                    (set Pending)
                    (if (is In (to) (expand (sites Right)))
                        (set Value Mover 2) // 2 = right
                        (if (is In (to) (expand (sites Left)))
                            (set Value Mover 1) // 1 = left
                            (set Value Mover 0) // 0 = choice
                        )
                    )
                })
                (if (< 1 (count at:( to)))
                    (and {
                        (moveAgain)
                        (set Var "Replay" (to))
                        (set Var "Direction" #2) // 1 = CCW, 2 = CW
                    })
                )
            )
            (if (< 1 (count at:( to)))
                (and {
                    (moveAgain)
                    (set Var "Replay" (to))
                    (set Var "Direction" #2) // 1 = CCW, 2 = CW
                })
            )
        )
    )
)

(define "SowingPhase2"
    (move Select
        (from 
            (sites Mover)
            if:(is Occupied (from))
        )
        (then
            (sow
                #1
                owner:(mover)
                apply:(if (< 1 (count at:( to)))
                    (and {
                        (moveAgain)
                        (set Var "Replay" (to))
                        (set Var "Direction" #2) // 1 = CCW, 2 = CW
                    })
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Bao Kiswahili (DR Congo)"  
    (players 2)  
    (equipment { 
        (mancalaBoard 4 8 store:None
            {
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "16,E,N1,W" loop:True P2)
            (track "TrackCW1" "7,W,N1,E" loop:True P1)
            (track "TrackCW2" "23,W,N1,E" loop:True P2)
            }
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions "Home" P1 (expand (sites Bottom)))                     // P1 home
        (regions "Home" P2 (expand (sites Top)))                        // P2 home
        (regions "Inner" P1 (sites Row 1))                     // P1 home
        (regions "Inner" P2 (sites Row 2))                        // P2 home
        (map "LeftMostInner" {(pair P1 8) (pair P2 23)})
        (map "RightMostInner" {(pair P1 15) (pair P2 16)})
        (map "AdjacentLeftMostInner" {(pair P1 9) (pair P2 22)})
        (map "AdjacentRightMostInner" {(pair P1 14) (pair P2 17)})
    })  
    (rules 
        (start {
            (set Count 24 at:(handSite P1))
            (set Count 24 at:(handSite P2))
            (set Count 1 to:(union (sites Row 1) (sites Row 2)) ) 
        })
        
        phases:{
        (phase "Placement"
            (play 
                (if ("SameTurn")
                    (if (is Pending) // If Pending = Seeds were captured.
                        (or
                            (if (or (= 1 (value Player Mover)) (= 0 (value Player Mover))) // Sow capture pieces from the left.
                                (do 
                                    (fromTo
                                        (from ("OppositePit" (last To afterConsequence:True)))
                                        (to (mapEntry "LeftMostInner" Mover))
                                        count:(count at:("OppositePit" (last To afterConsequence:True)))
                                    )
                                    next:(move Select
                                        (from (mapEntry "LeftMostInner" Mover))
                                        (then ("Sowing" "TrackCW" 1))
                                    )
                                )
                            )
                            (if (or (= 2 (value Player Mover)) (= 0 (value Player Mover))) // Sow capture pieces from the right.
                                (do 
                                    (fromTo
                                        (from ("OppositePit" (last To afterConsequence:True)))
                                        (to (mapEntry "RightMostInner" Mover))
                                        count:(count at:("OppositePit" (last To afterConsequence:True)))
                                    )
                                    next:(move Select
                                        (from (mapEntry "RightMostInner" Mover))
                                        (then ("Sowing" "TrackCCW" 2))
                                    )
                                )
                            )
                        )
                        
                        // Continue to sow.
                        (or
                            (if (or (= (var "Direction") 1) (= (var "Direction") 3))
                                (move Select
                                    (from (var "Replay"))
                                    (then ("Sowing" "TrackCW" 1))
                                )
                            )
                            (if (or (= (var "Direction") 2) (= (var "Direction") 3))
                                (move Select
                                    (from (var "Replay"))
                                    (then ("Sowing" "TrackCCW" 2))
                                )
                            )
                        )
                    )
                    (if (is Occupied (handSite Mover)) // Placement phase
                        (priority {
                            (move // Has to capture if possible
                                (from (handSite Mover))
                                (to (sites Mover "Inner") if:(is Occupied ("OppositePit" (to))))
                                count:1
                                (then
                                    (and {
                                        (moveAgain)
                                        (set Pending)
                                        (if (is In (last To) (expand (sites Right)))
                                            (set Value Mover 2) // 2 = right
                                            (if (is In (last To) (expand (sites Left)))
                                                (set Value Mover 1) // 1 = left
                                                (set Value Mover 0) // 0 = choice
                                            )
                                        )
                                    })
                                )
                            )
                            (move // If not possible just place then sow in any direction.
                                (from (handSite Mover))
                                (to (sites Mover "Inner"))
                                count:1
                                (then
                                    (and {
                                        (moveAgain)
                                        (set Var "Replay" (last To))
                                        (set Var "Direction" 3) // Right or Left
                                    })
                                )
                            )
                        })
                        // Sowing phase
                        (or // No capture in the first sowing in that phase of the game.
                            ("SowingPhase2" "TrackCW" 1)
                            ("SowingPhase2" "TrackCCW" 2)
                        )
                    )
                )
            )
        )
        }
        
        (end {
            (if ("NoPiecesInInner" P1) (result P2 Win))
            (if ("NoPiecesInInner" P2) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Bao Kiswahili is a four-row mancala-style board game played in the eastern part of the Democratic Republic of Congo. It was particularly popular among Banbubangu and Nonda people, as well as in some larger cities in surrounding regions.")
        (aliases {"Bao" "Bao Ki Swahili" "Lusole" "Busolo"})
        (rules "4x8 board. The fifth hole from the left in the inner row is rectangular, the rest are circular. 64 counters. Play begins with one counter in each hole in the inner row. Players take turns placing their remaining counters on the board in their inner rows. When they place a counter, they capture the hole facing it in the opponent's inner row, and sows the counters from either the leftmost or the rightmost hole of the player's inner row, sowing along the inner row. However, if the capture occurs from the rightmost, leftmost, or the next hole adjacent to each in the inner row, the captured pieces must be placed in the leftmost or rightmost hole, whichever is closer. The player may choose which hole to place captured counters in when captures are made from the central four holes in the inner row. The player must capture if possible, if not possible they place a counter into any of the holes in their inner row, pick up the contents of that hole, and sow in either direction. While sowing, if the final counter falls into an occupied hole and the opponent's hole opposite it has counters, the counters in the opponent's hole are captured. If the final counter falls into an occupied hole and the hole opposite it is empty, the counters are picked up and sowing continues in the same direction. If the final counter falls into an empty hole, the turn ends. If during this phase, the opponent's inner row is cleared of counters, the player wins. If all of the counters have been introduced and both players still have counters in their inner row, play proceeds to a second phase. Second Phase: Counters are picked up from any hole on the player's side and sown in either direction. The same capture, sowing, and victory rules apply as before. Captures, however, can only be made on subsequent sowings if there was a capture in the first round of a sowing.")
        (source "Townshend 1977: 41-43.")
        (id "142")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Central Africa, around 1977.")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (show AsHoles {12 19} Square)
    })
)
