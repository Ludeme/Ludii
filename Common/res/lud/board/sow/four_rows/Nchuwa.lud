(define "Columns" <Board:size>)

(define "PiecesOwnedBy" (count in:(sites #1 "Home")))

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2)) ) )

(define "FirstSiteToSow"
    (if (is Mover P1)
        (intersection (sites Right) (sites Row 1))
        (intersection (sites Left) (sites Row 2))
    )	
)

(define "NextSite" (trackSite Move #1 steps:1))

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

//------------------------------------------------------------------------------

(game "Nchuwa"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" <Board:CCW2> loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))                        // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))                        // P2 home
        (regions "Inner" P1 (sites Row 1))                                   // P1 Inner Home
        (regions "Inner" P2 (sites Row 2))                                   // P2 Inner Home
        (regions "Outer" P1 (sites Row 0))                                   // P1 Outer Home
        (regions "Outer" P2 (sites Row 3))                                   // P2 Outer Home
        (piece "Seed" Shared)
        }
    )  
    (rules 
        
        (start 
            (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home") ) )
        )
        
        phases:{
        (phase "Opening1"
            (play
                (move Select
                    (from ("FirstSiteToSow"))
                    (then 
                        (sow 
                            "TrackCCW"
                            owner:(mover)
                            apply:(set Value Mover ("NextSite" from:(to)))
                        )
                    )
                )
            )
            (nextPhase Mover "Opening2")
        )
        (phase "Opening2"
            (play
                (move Select
                    (from (value Player Mover))
                    (then 
                        (sow 
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (= (count at:("NextSite" from:(to))) 0) 
                                (set Value Mover Off)
                                (set Value Mover ("NextSite" from:(to)))
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (= Off (value Player Mover)) "Opening3")
        )
        (phase "Opening3"
            (play
                (move
                    (from (forEach (sites Mover "Inner") if:(!= 0 (count at:(site)))))
                    (to (forEach (sites Mover "Outer") if:(= 0 (count at:(site)))))
                    count:2
                )
            )
            (nextPhase Mover "Opening4")
        )
        (phase "Opening4"
            (play (move Remove (sites Next "Home")))
            (nextPhase Mover "Opening5")
        )
        (phase "Opening5"
            (play (move Remove (sites Next "Home")))
            (nextPhase Mover "Playing")
        )
        (phase "Playing"
            (play
                (move
                    Select 
                    (from 
                        (if 
                            (is Mover Prev) 
                            "PlayFromLastHole" 
                            (sites Mover "Home") 
                        ) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (= (count at:(to)) 1)
                                (if (is In (to) (sites Mover "Inner"))
                                    (if (> (count at:"OppositePit") 0) 
                                        (and
                                            (remove ("OppositePit")) 
                                            (if 
                                                (> (count at:"OppositeOuterPit") 0) 
                                                (remove ("OppositeOuterPit")) 
                                            ) 
                                        ) 
                                    ) 
                                )
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end {
            (if (= 0 ("PiecesOwnedBy" P1)) (result P2 Win)) 
            (if (= 0 ("PiecesOwnedBy" P2)) (result P1 Win)) 
        })
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <CCW2> }
    {
    (item "6"   <6>  <"12,E,N1,W">  "Each player has 6 holes by row.")*  
    (item "9"   <9>  <"18,E,N1,W">  "Each player has 9 holes by row.") 
    (item "12"  <12> <"24,E,N1,W"> "Each player has 12 holes by row.") 
    (item "15"  <15> <"30,E,N1,W"> "Each player has 15 holes by row.") 
})

//------------------------------------------------------------------------------

(rulesets { 
    (ruleset "Ruleset/Atonga (Described)" {
        "Board Size/6"
        }
        variations:{"Board Size/9" "Board Size/12" "Board Size/15"}
    )*
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Nchuwa is a four-row mancala-style board game played by the Atonga people of central Africa during the early twentieth century.")
        (rules "4x6, 9, 12, or 15. Two counters in each hole. Players begin by sowing from the rightmost hole in the inner row, placing one counter in each of the first two holes, then picking up the counters from the next hole and continuing to sow until the next hole after the sowing is empty (this should be the hole from which the sowing began). Each player then takes two counters from any of the holes in their inner row and places them in any empty hole in the outer row. The player then captures the counters in the opponent's opposite holes. The player then removes the contents of any one of the opponent's holes. Once both players have done this, the main phase begins. Players move by sowing from any of the holes on their side of the board in an anti-clockwise direction. When the final counter lands in an occupied hole, these are picked up and sowing continues. When they fall into an empty hole, the sowing ends. If this hole is in the inner row, the contents of the opponent's opposite hole in the inner row are also taken. If there are also counters in the opponent's outer row opposite, these are also taken. The player may also capture the contents of any other hole in the inner or outer row. Single counters may be moved. The player who captures all of their opponent's counters wins.")
        (source "Sanderson 1913: 734-735.")
        (id "255")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Nchuwa_ai"
    )
)

