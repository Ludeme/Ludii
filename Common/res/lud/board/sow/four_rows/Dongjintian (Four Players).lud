(define "Columns" 5)
(define "PiecesOwnedBy" (count Cell at:(handSite #1)))
(define "SitesSowed" (sites (values Remembered "SowedSites")))
(define "NextSiteExist"
    (!= (last To) ("NextSite" #1))
)
(define "NextSite"
    (ahead (last To) steps:#1 ("LastDirection" Vertex))
)
(define "SowAgainMove" 
    (move Select
        (from (last To))
        (to (difference (sites Around (last To)) ("SitesSowed")))
        (then
            (and
                (add (piece (id "Seed" Shared)) (to (last To)))
                (if (< 1 (var "NumToSow")) 
                    (and {
                        (set Var "NumToSow" (- (var "NumToSow") 1))
                        (moveAgain)
                        (remember Value "SowedSites" (last From))
                    })
                    (and {
                        (forget Value "SowedSites" All)
                        (set Var "NumToSow" 0)
                        (if ("NextSiteExist" 1)
                            (if (is Occupied ("NextSite" 1))
                                (moveAgain)
                                (if ("NextSiteExist" 2)
                                    (if (is Occupied ("NextSite" 2))
                                        (and
                                            (fromTo
                                                (from ("NextSite" 2))
                                                (to (handSite Mover))
                                                count:(count at:("NextSite" 2))
                                            )
                                            (if (and
                                                    ("NextSiteExist" 3)
                                                    ("NextSiteExist" 4)
                                                )
                                                (if (and
                                                        (is Occupied ("NextSite" 4))	
                                                        (is Empty ("NextSite" 3))
                                                    )
                                                    (fromTo
                                                        (from ("NextSite" 4))
                                                        (to (handSite Mover))
                                                        count:(count at:("NextSite" 4))
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    })
                )
            )
        )
    )	
)

//------------------------------------------------------------------------------

(game "Dongjintian (Four Players)" 
    (players 4) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None)
        (piece "Seed" Shared)
        (hand Each)
        (regions "ProtectingHoles" (sites {7 12}))
    }) 
    (rules 
        
        (start (set Count 5 to:(sites Board)))
        
        (play 
            (if (and ("SameTurn") (!= 0 (var "NumToSow")))
                (if (can Move ("SowAgainMove"))
                    ("SowAgainMove")
                    (do 
                        (and {
                            (add (piece (id "Seed" Shared)) (to (last To)) count:(var "NumToSow"))
                            (forget Value "SowedSites" All)
                            (set Var "NumToSow" 0)
                        })
                        next:(move Pass)
                    )
                )
                (if ("SameTurn")
                    (move 
                        (from ("NextSite" 1))
                        (to (sites Around (from)))
                        (then
                            (and {
                                (if (is Occupied (last From))
                                    (and {
                                        (moveAgain)
                                        (set Var "NumToSow" (count at:(last From)))
                                        (remember Value "SowedSites" (last From))
                                    })
                                    (if ("NextSiteExist" 1)
                                        (if (is Occupied ("NextSite" 1))
                                            (moveAgain)
                                            (if ("NextSiteExist" 2)
                                                (if (is Occupied ("NextSite" 2))
                                                    (and
                                                        (fromTo
                                                            (from ("NextSite" 2))
                                                            (to (handSite Mover))
                                                            count:(count at:("NextSite" 2))
                                                        )
                                                        (if (and
                                                                ("NextSiteExist" 3)
                                                                ("NextSiteExist" 4)
                                                            )
                                                            (if (and
                                                                    (is Occupied ("NextSite" 4))	
                                                                    (is Empty ("NextSite" 3))
                                                                )
                                                                (fromTo
                                                                    (from ("NextSite" 4))
                                                                    (to (handSite Mover))
                                                                    count:(count at:("NextSite" 4))
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                                (remove (last From) count:(count at:(last From)))
                            })
                        )
                    )
                    (move 
                        (from (sites Board) if:(is Occupied (from)))
                        (to (sites Around (from)))
                        (then
                            (and {
                                (if (is Occupied (last From))
                                    (and {
                                        (moveAgain)
                                        (set Var "NumToSow" (count at:(last From)))
                                        (remember Value "SowedSites" (last From))
                                    })
                                    (if ("NextSiteExist" 1)
                                        (if (is Occupied ("NextSite" 1))
                                            (moveAgain)
                                            (if ("NextSiteExist" 2)
                                                (if (is Occupied ("NextSite" 2))
                                                    (and
                                                        (fromTo
                                                            (from ("NextSite" 2))
                                                            (to (handSite Mover))
                                                            count:(count at:("NextSite" 2))
                                                        )
                                                        (if (and
                                                                ("NextSiteExist" 3)
                                                                ("NextSiteExist" 4)
                                                            )
                                                            (if (and
                                                                    (is Occupied ("NextSite" 4))	
                                                                    (is Empty ("NextSite" 3))
                                                                )
                                                                (fromTo
                                                                    (from ("NextSite" 4))
                                                                    (to (handSite Mover))
                                                                    count:(count at:("NextSite" 4))
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                                (remove (last From) count:(count at:(last From)))
                            })
                        )
                    )
                )
            )
        )
        
        (end 
            (if (<= (count Pieces All in:(sites Board)) 1)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                    (score P3 ("PiecesOwnedBy" P3))
                    (score P4 ("PiecesOwnedBy" P4))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Dongjintian is a mancala-style board game played in Yunnan Province, China. This four-player version features an orthogonal pattern of sowing not seen in other mancala-style games.")
        (rules "4x5 board. Five counters per hole. Players can sow from any hole. Sowing occurs orthogonally in any direction, and a player can change direction orthogonally at any point in the move, but can never double back. When the last counter falls into a hole, sowing continues if there are counters in the following holes; these are picked up and sowing continues from there. The hole from which the sowing continues will always be the next one in the opposite direction from the penultimate hole in which a counter was dropped. Therefore, if the sowing ends when a sowing hits an edge or corner, sowing does not continue. When the hole after the end of a sowing is empty, the counters in the next hole following it are captured, but captures cannot be made when the final counter falls into a hole from which a new direction must be chosen. Captures cannot be made from the central two holes of the board. The player who captures the most counters wins.
        ")
        (source "Eagle 1995: 57-58.")
        (id "1566")
        (version "1.3.5")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin "This game was played in China, around 1994.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)

