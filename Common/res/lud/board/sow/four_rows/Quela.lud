(define "Columns" <Board:size>)
(define "LeftMostCounters"
    (all Sites (difference (sites Mover "Home") (mapEntry "LeftMost" Mover))
        if:(> (count at:(mapEntry Mover)) (count at:(site)))
    )
)

(define "OnlyOneInInner"
    (all Sites (sites Mover "Inner") 
        if:(= 1 (count at:(site)))
    )
)

(define "SitesWithMaxCounter"
    (forEach (sites Mover "Home")
        if:(=
            (max
                (results
                    from:(sites Mover "Home")
                    to:(sites Mover "Home")
                    (count at:(to))
                )
            )
            (count at:(site))
        )
    )
)

//------------------------------------------------------------------------------

(game "Quela"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" <Board:CCW2> loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))                                     // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))                                     // P2 home
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom)))        // P1 Inner Home
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top)))           // P2 Inner home
        (map "LeftMost" {(pair P1 0) <Board:leftMostP2>})
        (piece "Seed" Shared)
        (hand Each)
    })  
    (rules 
        
        (start 
            (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home")))
        )
        
        phases:{
        (phase "Rearrangement"
            (play
                (or
                    (move 
                        (from (sites Mover "Home") if:(is Occupied (from)))
                        (to (difference (sites Mover) (from)))
                    )
                    (if (and ("LeftMostCounters") ("OnlyOneInInner"))
                        (move Pass)
                    )
                )
            )
            (nextPhase (all Passed) "InitialPhase")
        )
        (phase "InitialPhase"
            (play
                (move Select
                    (from 
                        (if 
                            ("SameTurn") 
                            "LastHoleSowed" 
                            (sites Mover "Home") 
                        ) 
                        if:(or ("SameTurn") 
                            (and 
                                (> 3 (count at:(from))) 
                                (is Occupied (from))
                            )
                        )
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (< 1 (count at:(to)))
                                (moveAgain)
                                (if (is In (to) (sites Mover "Inner"))
                                    (if (is Occupied ("OppositePit" (to)))
                                        (and
                                            (fromTo
                                                (from ("OppositePit" (to)))
                                                (to (handSite Mover))
                                                count:(count at:("OppositePit" (to)))
                                            )
                                            (if (is Occupied ("OppositeOuterPit" (to)))
                                                (fromTo
                                                    (from ("OppositeOuterPit" (to)))
                                                    (to (handSite Mover))
                                                    count:(count at:("OppositeOuterPit" (to)))
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            {
            (nextPhase P1 (< 1 (count Cell at:(handSite P2))) "MiddlePhase")
            (nextPhase P2 (< 1 (count Cell at:(handSite P1))) "MiddlePhase")
            }
        )
        
        (phase "MiddlePhase"
            (play
                (move Select
                    (from 
                        ("SitesWithMaxCounter") 
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (< 1 (count at:(to)))
                                (moveAgain)
                                (if (is In (to) (sites Mover "Inner"))
                                    (if (is Occupied ("OppositePit" (to)))
                                        (and
                                            (fromTo
                                                (from ("OppositePit" (to)))
                                                (to (handSite Mover))
                                                count:(count at:("OppositePit" (to)))
                                            )
                                            (if (is Occupied ("OppositeOuterPit" (to)))
                                                (fromTo
                                                    (from ("OppositeOuterPit" (to)))
                                                    (to (handSite Mover))
                                                    count:(count at:("OppositeOuterPit" (to)))
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase Mover "FinalPhase")
        )
        
        (phase "FinalPhase"
            (play
                (move Select
                    (from 
                        (if 
                            ("SameTurn") 
                            "LastHoleSowed" 
                            (sites Mover "Home") 
                        ) 
                        if:(is Occupied (from))
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (< 1 (count at:(to)))
                                (moveAgain)
                                (if (is In (to) (sites Mover "Inner"))
                                    (if (is Occupied ("OppositePit" (to)))
                                        (and
                                            (fromTo
                                                (from ("OppositePit" (to)))
                                                (to (handSite Mover))
                                                count:(count at:("OppositePit" (to)))
                                            )
                                            (if (is Occupied ("OppositeOuterPit" (to)))
                                                (fromTo
                                                    (from ("OppositeOuterPit" (to)))
                                                    (to (handSite Mover))
                                                    count:(count at:("OppositeOuterPit" (to)))
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (end ("BlockWin"))
        )
        }
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <CCW2> <leftMostP2>}
    {
    (item "6"   <6>  <"12,E,N1,W"> <(pair P2 23)> "Each player has 6 holes by row.")*  
    (item "7"   <7>  <"14,E,N1,W"> <(pair P2 27)> "Each player has 7 holes by row.") 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Quibala (Described)" {
        "Board Size/6"
        }
        variations:{"Board Size/7"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Quela is a four-row mancala-style board game played by the Quibala people of Angola.")
        (rules "4x6-7 board. Two counters in each hole; before beginning each player rearranges these as they wish, with the following rules: The leftmost hole in the outer row must have the most counters; holes in the inner row may only have one counter. Initial phase: In this phase, only holes with two or fewer counters may be used to sow. Sowing occurs in an anti-clockwise direction, and when a counter lands in an occupied hole the sowing continues. When the final counter lands in an empty hole, the turn ends. If the empty hole is in the player's inner row, counters in the opponent's inner row opposite are captured, and if there are also counters in the opponent's outer row hole opposite, these are also taken. The initial phase ends for a player when two of the player's counters have been taken. On the next turn they must play from the hole with the largest number of counters, and from that point they may move from any hole. The rules for play remain the same as in the initial phase otherwise. The player who captures all of the opponent's counters wins.")
        (source "Silva 1995: 97-102.")
        (id "270")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)

