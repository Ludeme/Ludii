(define "Columns" 8)
(define "PlayFromNextLastHole" (sites {("NextHoleFrom" (last To afterConsequence:True) "TrackCCW" 1)}))
(define "NextHoleFrom" ("NextSiteOnTrack" #3 from:#1 #2))

(define "FirstEmptyHole" 
    (trackSite FirstSite 
        Mover
        "TrackCW"
        from:#1
        if:(is Empty (to))
    )
)

//------------------------------------------------------------------------------

(game "Mangola" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "16,E,N1,W" loop:True P2)
            (track "TrackCW1" "7,W,N1,E" loop:True P1)
            (track "TrackCW2" "23,W,N1,E" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))         // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))         // P2 home
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom)))  // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top)))   // P2 inner Home
        (regions "LeftMost" P1 (intersection (sites Left) (sites Track "TrackCCW1"))) // P1 LeftMost
        (regions "LeftMost" P2 (intersection (sites Right) (sites Track "TrackCCW2"))) // P2 LeftMost
        (regions "EndHoles" P1 (intersection (union (sites Left) (sites Right)) (sites Track "TrackCCW1"))) // P1 End holes
        (regions "EndHoles" P2 (intersection (union (sites Left) (sites Right)) (sites Track "TrackCCW2"))) // P2 End holes
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start 
            (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home")))
        )
        
        phases:{
        (phase "Opening1"
            (play
                (move Select
                    (from 
                        (if ("SameTurn") 
                            "PlayFromNextLastHole" 
                            (sites Mover "LeftMost") 
                        ) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            numPerHole:2
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (is Occupied ("NextHoleFrom" (to) "TrackCCW" 1)) (moveAgain))
                        )
                    )
                )
            )
            (nextPhase Mover (not (is Next Mover)) "Opening2")
        )
        (phase "Opening2"
            (play
                (move Select
                    (from 
                        (if ("SameTurn") 
                            (sites {(var "Replay") })
                            (sites Mover "EndHoles") 
                        ) 
                        if:(> (count at:(from)) 1)
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (< 1 (count at:(to)))
                                (and {
                                    (moveAgain)
                                    (set Var "Replay" (to))
                                    (if 
                                        (and {
                                            (is In (to) (sites Mover "Inner"))
                                            (is Occupied ("OppositeOuterPit" (to)))
                                            (is Occupied ("OppositePit" (to)))
                                        })	
                                        (and {
                                            (fromTo
                                                (from ("OppositeOuterPit" (to)))
                                                (to ("NextHoleFrom" ("FirstEmptyHole" (to)) "TrackCCW" 1))
                                                count:(count at:("OppositeOuterPit" (to)))
                                            )
                                            (fromTo
                                                (from ("OppositePit" (to)))
                                                (to ("NextHoleFrom" ("FirstEmptyHole" (to)) "TrackCCW" 1))
                                                count:(count at:("OppositePit" (to)))
                                            )
                                            (sow
                                                ("NextHoleFrom" ("FirstEmptyHole" (to)) "TrackCCW" 1)
                                                count:(+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))))
                                                "TrackCCW"
                                                owner:(mover)	
                                            )
                                        })		
                                    )
                                })
                                (fromTo
                                    (from (to))
                                    (to ("NextHoleFrom" (to) "TrackCCW" 1))
                                    count:1
                                )
                            )
                        )
                    )
                )
            )
            
            (nextPhase Mover (not (is Next Mover)) "Sowing")
        )
        
        (phase "Sowing"
            (play
                (move Select
                    (from 
                        (if 
                            ("SameTurn") 
                            (sites {(var "Replay")})
                            (sites Mover "Home") 
                        ) 
                        if:(> (count at:(from)) 1)
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(if (< 1 (count at:(to)))
                                (and {
                                    (moveAgain)
                                    (set Var "Replay" (to))
                                    (if 
                                        (and {
                                            (is In (to) (sites Mover "Inner"))
                                            (is Occupied ("OppositeOuterPit" (to)))
                                            (is Occupied ("OppositePit" (to)))
                                        })	
                                        (and {
                                            (fromTo
                                                (from ("OppositeOuterPit" (to)))
                                                (to ("NextHoleFrom" ("FirstEmptyHole" (to)) "TrackCCW" 1))
                                                count:(count at:("OppositeOuterPit" (to)))
                                            )
                                            (fromTo
                                                (from ("OppositePit" (to)))
                                                (to ("NextHoleFrom" ("FirstEmptyHole" (to)) "TrackCCW" 1))
                                                count:(count at:("OppositePit" (to)))
                                            )
                                            (sow
                                                ("NextHoleFrom" ("FirstEmptyHole" (to)) "TrackCCW" 1)
                                                count:(+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))))
                                                "TrackCCW"
                                                owner:(mover)	
                                            )
                                        })		
                                    )
                                })
                                (fromTo
                                    (from (to))
                                    (to ("NextHoleFrom" (to) "TrackCCW" 1))
                                    count:1
                                )
                            )
                        )
                    )
                )
            )
            
        )
        }
        (end ("ForEachPlayerNoMovesLoss"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mangola is a four-row mancala-style board game from the Democratic Republic of Congo. It was documented in the mid-twentieth century by Suzanne Comhaire-Sylvain. Adults play it on boards, children, including girls, scoop holes out of the ground to play.")
        (rules "4x8 board. Two counters in each hole. Play begins with each player taking the two counters in the leftmost hole of either the inner or outer row and placing them both in the following hole in an anti-clockwise direction, and continuing with the two counters in the next hole, until there is an alternating pattern of holes with four counters followed by empty holes in all of the player's holes. On the next turn, the player sows from one of the holes on the end of one of their rows. Sowing occurs in an anti-clockwise direction. After this turn, sowing can be from any hole on the player's side of the board. Single counters cannot be sown.  When the final counter falls into an occupied hole, the player picks up these counters and continues to sow. When the final counter is supposed to fall into an empty hole, it is not placed there but placed in the next hole. When the final counter falls into an occupied hole in the inner row, any counters in the opponent's two holes opposite are captured. These are then sown on the player's side of the board, beginning with the first occupied hole immediately after an empty hole before the hole from which the capture was made. Play continues until one player can no longer play, and the opponent wins.")
        (source "Comhaire-Sylvain 1952: 356-361.")
        (id "277")
        (version "1.3.13")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Mangola_ai"
    )
)

