(define "Columns" <Board:size>)
(define "RightMost" 
    (trackSite FirstSite 
        from:(trackSite FirstSite if:(not (is Mover (who at:(to)))))
        if:(is Mover (who at:(to)))
    )
)
(define "NextHoleFrom" (trackSite Move from:#1 #2))

//------------------------------------------------------------------------------

(game "Mwendo"  
    (players 4)  
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            <Board:track>
        )
        (piece "Seed" Each)
        <Board:regions>
    })  
    (rules 
        (start {
            (place "Seed1" (sites P1) counts:{3})
            (place "Seed2" (sites P2) counts:{3})
            (place "Seed3" (sites P3) counts:{3})
            (place "Seed4" (sites P4) counts:{3})
        })
        
        (play 
            (if (!= 1 (count Pieces Mover))
                (or
                    (move
                        Select 
                        (from 
                            ("RightMost") 
                        )
                        (then 
                            (sow
                                numPerHole:(if (is Empty (to)) 2 1)
                                sowEffect:(if (and (!= (mover) (what at:(to))) (is Occupied (to)))
                                    (and
                                        (remove (to) count:(count at:(to)))
                                        (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                                    )
                                )
                                apply:(if (= 2 (count at:(to)))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                    (if (not ("SameTurn"))
                        (move
                            (from ("RightMost"))
                            (to ("NextHoleFrom" ("RightMost") steps:1)
                                (apply 
                                    (if (and (!= (mover) (what at:(to))) (is Occupied (to)))
                                        (and
                                            (remove (to) count:(count at:(to)))
                                            (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                                        )	
                                    )
                                )
                            )
                            count:(count at:("RightMost"))
                        )
                    )
                )
                (move
                    (from ("RightMost"))
                    (to 
                        (trackSite FirstSite from:(from) if:("IsEnemyAt" (to)))
                        (apply
                            (and
                                (remove (to) count:(count at:(to)))
                                (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                            )	
                        )	
                    )
                )
            )
        )
        
        (end 
            (forEach NonMover 
                if:(no Pieces Player) 
                (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <track> <regions>}
    {
    (item "4"   <4>   
        <(track "Track" "4,N,E,S,W" loop:True)>  
        <
        (regions P1 (sites {0..2}))
        (regions P2 (sites {3 7 11}))
        (regions P3 (sites {15..13}))
        (regions P4 (sites {12 8 4}))
        >
    "Each row has 4 holes.") 
    (item "6"   <6>   
        <(track "Track" "6,N,E,S,W" loop:True)>  
        <
        (regions P1 (sites {0..3}))
        (regions P2 (sites {4 5 11 17}))
        (regions P3 (sites {23..20}))
        (regions P4 (sites {19 18 12 6}))
        >
    "Each row has 6 holes.") 
    (item "8"   <8>   
        <(track "Track" "8,N,E,S,W" loop:True)>  
        <
        (regions P1 (sites {0..4}))
        (regions P2 (sites {5..7 15 23}))
        (regions P3 (sites {31..27}))
        (regions P4 (sites {26..24 16 8}))
        >
    "Each row has 8 holes.") 
    (item "10"   <10> 
        <(track "Track" "10,N,E,S,W" loop:True)> 
        <
        (regions P1 (sites {0..5}))
        (regions P2 (sites {6..9 19 29}))
        (regions P3 (sites {39..34}))
        (regions P4 (sites {33..30 20 10}))
        >
    "Each row has 10 holes.")* 
    (item "12"   <12> 
        <(track "Track" "12,N,E,S,W" loop:True)> 
        <
        (regions P1 (sites {0..6}))
        (regions P2 (sites {7..11 23 35}))
        (regions P3 (sites {47..41}))
        (regions P4 (sites {40..36 24 12}))
        >
    "Each row has 12 holes.") 
    (item "14"   <14> 
        <(track "Track" "14,N,E,S,W" loop:True)> 
        <
        (regions P1 (sites {0..7}))
        (regions P2 (sites {8..13 27 41}))
        (regions P3 (sites {55..48}))
        (regions P4 (sites {47..42 28 14}))
        >
    "Each row has 14 holes.")  
    (item "16"   <16> 
        <(track "Track" "16,N,E,S,W" loop:True)> 
        <
        (regions P1 (sites {0..8}))
        (regions P2 (sites {9..15 31 47}))
        (regions P3 (sites {63..55}))
        (regions P4 (sites {54..48 32 16}))
        >
    "Each row has 16 holes.")  
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Mwendo (Described)" {
        "Board Size/10"
        }
        variations:{"Board Size/4" "Board Size/6" "Board Size/8" "Board Size/12" "Board Size/14" "Board Size/16"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mwendo is a board game played among children of the Luenda and Bunda people of Angola. It is similar to mancala-style games, but the ownership of holes is determined by whether a player's counters are in the hole, rather than being fixed.")
        (rules "4x4-16 board, played only along the perimeter. Three counters per hole. Any number of players. Holes are distributed equally among the players. Sowing occurs in a clockwise direction. Counters are sown one per occupied hole, two per empty hole. Each player owns the holes in which their counters are located at the beginning of their turn, and they must sow from the rightmost hole. If the final counter of a sowing falls into an empty hole, the player sows again from the rightmost hole. Alternatively, a player may place all of the counters from the rightmost counter into the next hole, but this cannot be done after a move. When counters fall into holes belonging to an opponent, these hole and the counters in it are captured by the player, but remain in play. When a player has a single counter and empty holes in from of it, this counter leaps to the next hole occupied by the opponent. The player who captures all of the opponent's holes wins.")
        (source "Silva 1995: 111-112.")
        (id "274")
        (version "1.3.4")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Angola, around 1995.")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (player Colour P1 (colour White))
        (player Colour P2 (colour Black))
        (player Colour P3 (colour Red))
        (player Colour P4 (colour Green))
    })
    
)

