(define "Columns" <Board:size>)
(define "NextHoleFrom" ("NextSiteOnTrack" #3 from:#1 #2))
(define "LeftMost" 
    (trackSite FirstSite 
        from:(trackSite FirstSite if:(not (is Mover (who at:(to)))))
        if:(is Mover (who at:(to)))
    )
)

//------------------------------------------------------------------------------

(game "Chiana wa Kunja"
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            (track "Track" <Board:track> loop:True)
        )
        (piece "Seed" Each)
        (hand Shared)
    }) 
    
    (rules 
        (start {
            (place "Seed1" (sites Bottom) counts:{4})
            (place "Seed2" (sites Top) counts:{4})
        })
        
        (play 
            (or
                (move Select 
                    (from ("LeftMost"))
                    (then
                        (do
                            (and
                                (forEach Site
                                    (sites Track "Track" from:(last From) to:(trackSite Move from:(last From) steps:(count at:(last From))))
                                    (if (!= (mover) (who at:(site)))
                                        (and 
                                            (remove (site) count:(count at:(site)))
                                            (add (piece (id "Seed" Mover)) (to Cell (handSite Shared)) count:(count at:(site)))
                                        )
                                    )
                                )
                                (set Var "NumSowed" (count at:(last From)))
                            )
                            next:(sow)
                            (then
                                (and {
                                    (if (< 1 (var "NumSowed"))
                                        (if (and
                                                (= 1 (count at:("NextHoleFrom" (last From) "Track" (var "NumSowed"))))
                                                (= 2 (count at:("NextHoleFrom" (last From) "Track" (- (var "NumSowed") 1))))
                                            )
                                            (moveAgain)
                                        )
                                    )
                                    (if (!= 0 (what Cell at:(handSite Shared)))
                                        (fromTo
                                            (from Cell (handSite Shared))
                                            (to (last From))
                                            count:(count Cell at:(handSite Shared))
                                        )
                                    )
                                    (set Var "NumSowed" 0)
                                })
                            )
                        )
                    )
                )
                (if (and (= 1 (count Pieces Mover)) (is Empty ("NextHoleFrom" (from) "Track" 1)))
                    (move
                        (from ("LeftMost"))
                        (to 
                            ("NextHoleFrom" (from) "Track" 2)
                            (apply
                                (and
                                    (remove (to) count:(count at:(to)))
                                    (add (piece (id "Seed" Mover)) (to (to)) count:(count at:(to)))
                                )	
                            )	
                        )
                    )
                )
            )		
        )
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <track>}
    {
    (item "6" <6> <"0,E,23,W"> "Each row has 6 holes.") 
    (item "8" <8> <"0,E,31,W"> "Each row has 8 holes.") 
    (item "10" <10> <"0,E,39,W"> "Each row has 10 holes.") 
    (item "12" <12> <"0,E,47,W"> "Each row has 12 holes.") 
    (item "14" <14> <"0,E,55,W"> "Each row has 14 holes.") 
    (item "16" <16> <"0,E,63,W"> "Each row has 16 holes.")* 
    (item "18" <18> <"0,E,71,W"> "Each row has 18 holes.") 
    (item "20" <20> <"0,E,79,W"> "Each row has 20 holes.") 
    (item "22" <22> <"0,E,87,W"> "Each row has 22 holes.") 
    (item "24" <24> <"0,E,95,W"> "Each row has 24 holes.") 
    (item "26" <26> <"0,E,103,W"> "Each row has 26 holes.") 
    (item "28" <28> <"0,E,111,W"> "Each row has 28 holes.")
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Chiana wa Kunja (Observed)" {
        "Board Size/16" 
        }
        variations:{"Board Size/6" "Board Size/8" "Board Size/10" "Board Size/12" "Board Size/14" 
        "Board Size/18" "Board Size/20" "Board Size/22" "Board Size/24" "Board Size/26" "Board Size/28" 
        }
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Chiana wa Kunja is a four-row mancala-style board game played by the Nyanja people of East Africa. Typically, it is played by children.")
(aliases {"Chiana"})
(rules "4-row board, of any number of holes. Only the outer rows are used. Four counters in each hole in the outer row. Sowing occurs in an anti-clockwise direction. Players do not own rows, instead, over the course of their sowing, their counters are always grouped together, and sowing must occur from the rearmost counter in the player's series. If, after sowing, a player's series has one counter in the foremost hole and two in the next hole behind it, the player sows again from the rearmost hole. If the player overtakes the opponent's series, the player captures any counters in the opponent's series and places all of them in the hole immediately behind the player's series, now becoming the rearmost hole. If the rearmost hole contains a single counter, it is moved into the next hole which then becomes the rearmost hole for the next turn. If a player is reduced to a single counter, it can be moved two holes on the player's turn. The player who successfully captures all of the opponent's counters wins. ")
(source "Sanderson 1913: 735.")
(id "1314")
(version "1.3.9")
(classification "board/sow/four rows")
(credit "Eric Piette")
(origin "This game was played in East Africa, around 1913.")
}
)
    
    (graphics {
        (board Style Mancala)
        (player Colour P1 (colour White))
        (player Colour P2 (colour Black))
    })
    
)
