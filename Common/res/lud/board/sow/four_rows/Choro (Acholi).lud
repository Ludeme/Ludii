(define "Columns" 8)
(define "OuterPhaseOppositePit" (if (is Mover P1) (+ (to) (* 3 ("Columns"))) (- (to) (* ("Columns") 3))))
(define "NextHole" ("NextSiteOnTrack" #3 from:#1 #2))

//------------------------------------------------------------------------------

(game "Choro (Acholi)" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "16,E,N1,W" loop:True P2)
            (track "TrackCW1" "7,W,N1,E" loop:True P1)
            (track "TrackCW2" "24,E,S1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))        // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))        // P2 home
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top))) // P2 inner Home
        (regions "Outer" P1 (sites Bottom))          // P1 outer Home
        (regions "Outer" P2 (sites Top))           // P2 outer Home
        (piece "Seed" Shared)
        (map "LeftMostOuter" {(pair P1 0) (pair P2 31)})
        (map "SecondLeftInner" {(pair P1 9) (pair P2 22)})
    }) 
    (rules 
        
        (start (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home"))))
        
        phases:{
        (phase "OuterPhase"
            (play
                (move Select
                    (from 
                        (if ("SameTurn")
                            (sites {(var "Replay")})
                            (sites Mover "Outer") 
                        )
                        if:(if ("SameTurn")
                            (< 0 (count at:(from)))
                            (< 1 (count at:(from)))
                        )
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            owner:(mover)
                            apply:(and 
                                (if (and (!= 1 (value Player Mover)) (is In (to) (sites Mover "Inner"))) 
                                    (set Value Mover 1)
                                )
                                (if (< 1 (count at:(to)))
                                    (and
                                        (moveAgain)
                                        (if (= 1 (value Player Mover))
                                            (if (and {
                                                    (is Occupied ("OppositePit" (to)))
                                                    (is Occupied ("OppositeOuterPit" (to)))
                                                    (is In (to) (sites Mover "Inner"))
                                                })
                                                (and {
                                                    (fromTo
                                                        (from ("OppositePit" (to)))
                                                        (to (last From))
                                                        count:(count at:("OppositePit" (to)))
                                                    )
                                                    (fromTo
                                                        (from ("OppositeOuterPit" (to)))
                                                        (to (last From))
                                                        count:(count at:("OppositeOuterPit" (to)))
                                                    )
                                                    (set Var "Replay" (from))
                                                })
                                                (set Var "Replay" (to))
                                            )
                                            (if (is Occupied ("OuterPhaseOppositePit"))
                                                (and
                                                    (fromTo
                                                        (from ("OuterPhaseOppositePit"))
                                                        (to (last From))
                                                        count:(count at:("OuterPhaseOppositePit"))
                                                    )
                                                    (set Var "Replay" (from))
                                                )
                                                (set Var "Replay" (to))
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (= 1 (count at:(last To afterConsequence:True))) "MainPhase")
        )
        
        (phase "MainPhase"
            (play
                (or
                    (move Select
                        (from 
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (sites {(mapEntry "LeftMostOuter" Mover) (mapEntry "SecondLeftInner" Mover)}) 
                            )
                            if:(and {
                                (if ("SameTurn")
                                    (< 0 (count at:(from)))
                                    (< 1 (count at:(from)))
                                )
                                (is In (from) (sites {(mapEntry "LeftMostOuter" Mover) (mapEntry "SecondLeftInner" Mover)}))
                                (is In ("NextHole" (from) "TrackCW" (count at:(from))) (sites Mover "Inner"))
                                (is Occupied ("OppositePit" ("NextHole" (from) "TrackCW" (count at:(from)))))
                                (is Occupied ("OppositeOuterPit" ("NextHole" (from) "TrackCW" (count at:(from)))))
                            })
                        )
                        (then 
                            (sow
                                "TrackCW"
                                owner:(mover)
                                apply:
                                (if (< 1 (count at:(to)))
                                    (and
                                        (moveAgain)
                                        (if (and {
                                                (is Occupied ("OppositePit" (to)))
                                                (is Occupied ("OppositeOuterPit" (to)))
                                                (is In (to) (sites Mover "Inner"))
                                            })
                                            (and {
                                                (fromTo
                                                    (from ("OppositePit" (to)))
                                                    (to (last From))
                                                    count:(count at:("OppositePit" (to)))
                                                )
                                                (fromTo
                                                    (from ("OppositeOuterPit" (to)))
                                                    (to (last From))
                                                    count:(count at:("OppositeOuterPit" (to)))
                                                )
                                                (set Var "Replay" (from))
                                            })
                                            (set Var "Replay" (to))
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (move Select
                        (from 
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (sites Mover) 
                            )
                            if:(if ("SameTurn")
                                (< 0 (count at:(from)))
                                (< 1 (count at:(from)))
                            )
                        )
                        (then 
                            (sow
                                "TrackCCW"
                                owner:(mover)
                                apply:
                                (if (< 1 (count at:(to)))
                                    (and
                                        (moveAgain)
                                        (if (and {
                                                (is Occupied ("OppositePit" (to)))
                                                (is Occupied ("OppositeOuterPit" (to)))
                                                (is In (to) (sites Mover "Inner"))
                                            })
                                            (and {
                                                (fromTo
                                                    (from ("OppositePit" (to)))
                                                    (to (last From))
                                                    count:(count at:("OppositePit" (to)))
                                                )
                                                (fromTo
                                                    (from ("OppositeOuterPit" (to)))
                                                    (to (last From))
                                                    count:(count at:("OppositeOuterPit" (to)))
                                                )
                                                (set Var "Replay" (from))
                                            })
                                            (set Var "Replay" (to))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Choro is a four-row mancala-style board game played by the Didinga people in what is now South Sudan. It was documented by Driberg in the early twentieth century.")
        (aliases {"Coro"})
        (rules "4x8 board. Two counters in each hole. Opening play: Two players play simultaneously, lifting and sowing counters from their outer rows. Rules for sowing and capturing are the same as in the main phase of the game except that all holes in each player's outer rows are considered to be in opposition and back captures can be made from them. Once a counter has been sown into the inner row, this ceases and captures can only be made from the inner row. The opening ends when both players reach an empty hole, and the first player to do so begins play in the main phase. 
            
            Main phase: Play begins from any hole on the player's side of the board with counters in it. Singletons cannot move. Sowing happens in an anti-clockwise direction. If the last counter lands in an empty hole, the turn is over
            
            For capturing: Holes are in 'opposition' when one player has the front row hole occupied and the opponent has both of the holes opposite it occupied. If the last hole in a sowing is in opposition, the player takes the counters in both of the opponent's holes and places them in the empty hole from which the player lifted the counters. The player then sows the captured counters from this hole. Further captures in the sowing can occur in the same way. However, each player has two holes from which clockwise plays can be made: the leftmost hole in the outer row and the second from the left in the inner row. Clockwise moves can only be made from these holes if they immediately lead to a capture. When the captured counters are sown, starting from the same hole, they can also be sown clockwise if they lead to a capture. If they cannot lead to a capture, they are sown anti-clockwise. 
            
            Another alternative the player has is that, if the player plays clockwise from one of these holes and therefore makes a capture, the captured counters may be placed in the hole and left there, and the player may play instead from the other hole from which clockwise captures are allowed in a clockwise direction, if it leads to a capture. 
            
            The player may continue playing from this hole as above until the possibilities to move are exhausted, and then may move from any hole in an anti-clockwise direction. Multiple captures can only be made in a clockwise direction from these holes if it is made on the first sowing of the turn. Otherwise, only one clockwise capture can be made and sowing must proceed in an anti-clockwise direction. If the last counter lands on a hole that is occupied but not in opposition, these counters are picked up and sowing continues. 
            
        Play ends when one player captures all the opponent's counters or one player cannot play. The player who cannot play loses.")
        (source "Driberg 1927b: 188-189.")
        (id "260")
        (version "1.3.12")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Choro (Acholi)_ai"
    )
)

