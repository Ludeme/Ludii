(define "Columns" 8)
(define "CorrespondingOuterPit" (if (is Mover P1) (- #1 "Columns") (+ #1 "Columns")))
(define "NextHole" ("NextSiteOnTrack" #3 from:#1 #2))
(define "HolesWithPossibleCapture" 
    (forEach #1
        if:(and {
            (< 1 (count at:(site)))
            (is In ("NextHole" (site) "TrackCCW" (count at:(site))) (sites Mover "Inner")) 
            (is Occupied ("OppositePit" ("NextHole" (site) "TrackCCW" (count at:(site)))))
        })
    )
)

//------------------------------------------------------------------------------

(game "Choro (Lango)" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "TrackCCW1" "0,E,N1,W" loop:True P1)
            (track "TrackCCW2" "16,E,N1,W" loop:True P2)
            (track "TrackCW1" "7,W,N1,E" loop:True P1)
            (track "TrackCW2" "24,E,S1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))        // P1 Home
        (regions "Home" P2 (sites Track "TrackCCW2"))        // P2 home
        (regions "Inner" P1 (difference (sites Track "TrackCCW1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "TrackCCW2") (sites Top))) // P2 inner Home
        (regions "Outer" P1 (sites Bottom))          // P1 outer Home
        (regions "Outer" P2 (sites Top))           // P2 outer Home
        (regions "FourLeftMost" P1 (intersection (expand (sites Left)) (expand (sites Bottom)))) // P1 four left most
        (regions "FourLeftMost" P2 (intersection (expand (sites Right)) (expand (sites Top)))) // P2 four left most
        (regions "TwoRightMostOuter" P1 (intersection (expand (sites Right)) (sites Bottom))) // P1 two right most outer
        (regions "TwoRightMostOuter" P2 (intersection (expand (sites Left)) (sites Top)))  // P2 two right most outer
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home"))))
        
        phases:{
        (phase "OuterPhase"
            (play
                (move Select
                    (from 
                        (if ("SameTurn")
                            (sites {(var "Replay")})
                            (if (= 1 (value Player Mover))
                                (sites Mover "Outer") 
                                (sites Mover "TwoRightMostOuter") 
                            )
                        )
                        if:(if ("SameTurn")
                            (< 0 (count at:(from)))
                            (and
                                (if (= 0 (count Sites in:("HolesWithPossibleCapture" (sites Mover "Outer"))))
                                    True
                                    (is In (from) ("HolesWithPossibleCapture" (sites Mover "Outer")))
                                )
                                (< 1 (count at:(from)))
                            )
                        )
                    )
                    (then 
                        (sow
                            count:(if ("SameTurn") (var "Count") (count at:(last From)))
                            "TrackCCW"
                            owner:(mover)
                            apply:(and
                                (if (< 1 (count at:(to)))
                                    (and
                                        (moveAgain)
                                        (if (and 
                                                (is Occupied ("OppositePit" (to)))
                                                (is In (to) (sites Mover "Inner"))
                                            )
                                            (and {
                                                (fromTo
                                                    (from ("OppositePit" (to)))
                                                    (to ("CorrespondingOuterPit" (to)))
                                                    count:(count at:("OppositePit" (to)))
                                                )
                                                (if (is Occupied ("OppositeOuterPit" (to)))
                                                    (fromTo
                                                        (from ("OppositeOuterPit" (to)))
                                                        (to ("CorrespondingOuterPit" (to)))
                                                        count:(count at:("OppositeOuterPit" (to)))
                                                    )
                                                )
                                                (set Var "Replay" ("CorrespondingOuterPit" (to)))
                                                (if (= 1 (count at:("CorrespondingOuterPit" (to))))
                                                    (set Var "Count" (+ { 1 (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))) }))
                                                    (set Var "Count" (+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to)))))
                                                )
                                            })
                                            (and
                                                (set Var "Replay" (to))
                                                (set Var "Count" (count at:(to)))
                                            )
                                        )
                                    )
                                )
                                (if (!= 1 (value Player Mover)) (set Value Mover 1))
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (= 1 (count at:(last To afterConsequence:True))) "MainPhase")
        )
        
        (phase "MainPhase"
            (play
                (or
                    (move Select
                        (from 
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (sites Mover "FourLeftMost") 
                            )
                            if:(and {
                                if:(if ("SameTurn")
                                    (< 0 (count at:(from)))
                                    (< 1 (count at:(from)))
                                )
                                (is In (from) (sites Mover "FourLeftMost"))
                                (is In ("NextHole" (from) "TrackCW" (count at:(from))) (sites Mover "Inner"))
                                (is Occupied ("OppositePit" ("NextHole" (from) "TrackCW" (count at:(from)))))
                            })
                        )
                        (then 
                            (sow
                                count:(if ("SameTurn") (var "Count") (count at:(last From)))
                                "TrackCW"
                                owner:(mover)
                                apply:
                                (if (< 1 (count at:(to)))
                                    (and
                                        (moveAgain)
                                        (if (and 
                                                (is Occupied ("OppositePit" (to)))
                                                (is In (to) (sites Mover "Inner"))
                                            )
                                            (and {
                                                (fromTo
                                                    (from ("OppositePit" (to)))
                                                    (to ("CorrespondingOuterPit" (to)))
                                                    count:(count at:("OppositePit" (to)))
                                                )
                                                (if (is Occupied ("OppositeOuterPit" (to)))
                                                    (fromTo
                                                        (from ("OppositeOuterPit" (to)))
                                                        (to ("CorrespondingOuterPit" (to)))
                                                        count:(count at:("OppositeOuterPit" (to)))
                                                    )
                                                )
                                                (set Var "Replay" ("CorrespondingOuterPit" (to)))  
                                                (if (= 1 (count at:("CorrespondingOuterPit" (to))))
                                                    (set Var "Count" (+ { 1 (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))) }))
                                                    (set Var "Count" (+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to)))))
                                                )
                                            })
                                            (and
                                                (set Var "Replay" (to))
                                                (set Var "Count" (count at:(to)))
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (move Select
                        (from 
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (sites Mover) 
                            )
                            if:(if ("SameTurn")
                                (< 0 (count at:(from)))
                                (and
                                    (if (= 0 (count Sites in:("HolesWithPossibleCapture" (sites Mover))))
                                        True
                                        (is In (from) ("HolesWithPossibleCapture" (sites Mover)))
                                    )
                                    (< 1 (count at:(from)))
                                )
                            )
                        )
                        (then 
                            (sow
                                count:(if ("SameTurn") (var "Count") (count at:(last From)))
                                "TrackCCW"
                                owner:(mover)
                                apply:
                                (if (< 1 (count at:(to)))
                                    (and
                                        (moveAgain)
                                        (if (and {
                                                (is Occupied ("OppositePit" (to)))
                                                (is In (to) (sites Mover "Inner"))
                                            })
                                            (and {
                                                (fromTo
                                                    (from ("OppositePit" (to)))
                                                    (to ("CorrespondingOuterPit" (to)))
                                                    count:(count at:("OppositePit" (to)))
                                                )
                                                (if (is Occupied ("OppositeOuterPit" (to)))
                                                    (fromTo
                                                        (from ("OppositeOuterPit" (to)))
                                                        (to ("CorrespondingOuterPit" (to)))
                                                        count:(count at:("OppositeOuterPit" (to)))
                                                    )
                                                )
                                                (set Var "Replay" ("CorrespondingOuterPit" (to)))
                                                (if (= 1 (count at:("CorrespondingOuterPit" (to))))
                                                    (set Var "Count" (+ { 1 (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))) }))
                                                    (set Var "Count" (+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to)))))
                                                )
                                            })
                                            (and
                                                (set Var "Replay" (to))
                                                (set Var "Count" (count at:(to)))
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Choro is a four-row mancala-style board game played by the Lango people of northern Uganda. It was documented in the early twentieth century.")
        (rules "4x8 board. Two counters in each hole. Opening play: Two players play simultaneously, lifting and sowing counters from their outer rows. The game must begin from one of the rightmost two holes. Rules for sowing and capturing are the same as in the main phase of the game. The opening ends when both players reach an empty hole, and the first player to do so begins play in the main phase. Main phase: Play begins from any hole on the player's side of the board with counters in it. Singletons cannot move. Sowing happens in an anti-clockwise direction. If the last counter lands in an empty hole, the turn is over. For capturing: Holes are in 'opposition' when one player has the inner row hole occupied and the opponent has at least the opposite hole in the inner row occupied; if the outer row hole is also occupied it is also in opposition. However, if the opponent's inner row hole is empty and the outer row is occupied, it is not in opposition. If the last hole in a sowing falls into a hole that is is in opposition, the player takes the counters in the opponent's holes in opposition and places them in the outer row hole next to the hole from which the capture occurred. The player then sows the captured counters from this hole. If the capture is only of one counter, the contents of the appropriate outer row hole on the player's side are sown along with the one captured counter. Further captures in the sowing can occur in the same way. If a player can make a capture on the first sowing they must. Otherwise, they can choose any hole on their side to sow. However, each player has four holes from which clockwise plays can be made: the leftmost two holes in both the inner and outer rows. Clockwise moves can only be made from these holes if they immediately lead to a capture. When the captured counters are sown, they may, starting from the same hole, also be sown clockwise as long as they lead to a capture. If they cannot lead to a capture, they are sown anti-clockwise in the normal way from the outer row hole opposite the hole from which the capture was made. A player is not required to capture in a clockwise direction. If the last counter lands on a hole that is occupied but not in opposition, these counters are picked up and sowing continues. Play ends when one player captures all the opponent's counters or one player cannot play. The player who cannot play loses.")
        (source "Driberg 1927b: 186-187.")
        (id "265")
        (version "1.3.7")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin "This game was played in Uganda, around 1927.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)

