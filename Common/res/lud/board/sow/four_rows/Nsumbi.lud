(define "Columns" 8)

//------------------------------------------------------------------------------

(game "Nsumbi" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" "16,E,N1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))          // P1 Home
        (regions "Home" P2 (sites Track "Track2"))          // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom)))  // P1 Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top)))   // P2 home
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home"))))
        
        phases:{
        (phase "Opening" 
            (play 
                (move Select
                    (from (sites Mover))
                    (then 
                        (sow
                            "Track"
                            owner:(mover)
                        )
                    )
                )		
            )
            (nextPhase Mover "Playing")
        )
        (phase "Playing"
            (play 
                (move Select
                    (from 
                        (if 
                            ("SameTurn") 
                            "LastHoleSowed" 
                            (sites Mover) 
                        ) 
                        if:(< 1 (count at:(from)))
                    )
                    (then 
                        (sow
                            "Track"
                            owner:(mover)
                            apply:(if
                                (and { 
                                    (< 1 (count at:(to)))
                                    (is In (to) (sites Mover "Inner")) 
                                    (> (count at:("OppositePit" (to))) 0) 
                                    (> (count at:("OppositeOuterPit" (to))) 0) 
                                })
                                (and {
                                    (fromTo 
                                        (from ("OppositePit" (to))) 
                                        (to (last From)) 
                                        count:(count at:("OppositePit" (to)))
                                    ) 
                                    (fromTo 
                                        (from ("OppositeOuterPit" (to))) 
                                        (to (last From)) 
                                        count:(count at:("OppositeOuterPit" (to)))
                                    ) 
                                    (sow
                                        (last From)
                                        count:(+ (count at:("OppositePit" (to))) (count at:("OppositeOuterPit" (to))))
                                        "Track"
                                        owner:(mover)
                                        origin:True
                                    )
                                })
                                (if (< 1 (count at:(to)))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                )		
            )		
        )
        }
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Nsumbi is a four-row mancala-style board game played by the Taabwa people of central Africa.")
        (aliases {"Sombi"})
        (rules "4x8 board. Two counters in each hole. Players sow in an anti-clockwise direction from any of their holes. Singletons may not be sowed. Players capture when the sowing ends in a player's inner row and the opponent's inner and outer row holes opposite contain counters; these are captured and sown in the player's rows starting from the same hole that the previous sowing began. If the final counter lands in an occupied hole but capture is not possible, the counters in this hole are picked up and sowing continues. If the final counter ends in an empty hole, the turn ends. Captures cannot be made on each player's first turn. Play continues until one player has no holes with two or more counters, and the opponent wins.")
        (source "Townshend 1977a: 23.")
        (id "371")
        (version "1.3.8")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Nsumbi_ai"
    )
)

