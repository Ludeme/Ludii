(define "Columns" <Board:size>)
(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )
(define "OppositeOuterPit" (if (is Mover P1) (+ (to) (* "Columns" 2)) (- (to) (* "Columns" 2)) ) )
(define "PiecesOwnedBy" (count in:(sites #1 "Home")))
(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))
(define "NextSite" (trackSite Move #1 steps:1))

//------------------------------------------------------------------------------

(game "Spreta"
    (players 2)

    (equipment {
        (mancalaBoard 4 "Columns" store:None
            {
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" <Board:CCW2> loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))                                     // P1 Home
        (regions "Home" P2 (sites Track "Track2"))                                     // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom)))        // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top) ))          // P2 inner Home
        (regions "Outer" P1  (sites Bottom))                                           // P1 outer Home
        (regions "Outer" P2  (sites Top) )                                             // P2 outer Home
        (piece "Seed" Shared)
        }
    )
    (rules

        (start {
            (set Count 1
                to:(union {
                    (sites P1 "Outer")
                    (sites P2 "Outer")
                    (difference (sites P1 "Inner") (sites { <Board:thirdHoleInnerP1> <Board:rightMostInnerHoleP1>}))
                    (difference (sites P2 "Inner") (sites { <Board:thirdHoleInnerP2> <Board:rightMostInnerHoleP2>}))
                } )
            )
            (set Count 2 to:(sites { <Board:thirdHoleInnerP1> <Board:thirdHoleInnerP2>}))

        })

        phases:{
        (phase "Opening"
            (play
                (move
                    Select
                    (from
                        (sites Mover)
                        if:(= 2 (count at:(from)))
                    )
                    (then
                        (sow
                            "Track"
                            owner:(mover)
                        )
                    )
                )
            )
            (nextPhase Mover "Playing")
        )
        (phase "Playing"
            (play
                (if (or (= 1 (var "ExtraCapture")) (= 2 (var "ExtraCapture")))
                    (move Remove
                        (difference (sites Next "Home") (sites Empty))
                        count:(count at:(to))
                        (then
                            (if (= 1 (var "ExtraCapture"))
                                (and
                                    (moveAgain)
                                    (set Var "ExtraCapture" 2)
                                )
                                (set Var "ExtraCapture" 0)
                            )
                        )
                    )
                    (priority {
                        (move
                            Select
                            (from
                                (if
                                    (is Mover Prev)
                                    "PlayFromLastHole"
                                    (sites Mover)
                                )
                                if:(< 1 (count at:(from)))
                            )
                            (then
                                (sow
                                    "Track"
                                    owner:(mover)
                                    apply:(if (< 1 (count at:(to)))
                                        (moveAgain)
                                        (if (is In (to) (sites Mover "Inner"))
                                            (and {
                                                (if (> (count at:"OppositePit") 0)
                                                    (remove "OppositePit" count:(count at:"OppositePit"))
                                                )
                                                (if (> (count at:"OppositeOuterPit") 0)
                                                    (remove "OppositeOuterPit" count:(count at:"OppositeOuterPit"))
                                                )
                                                (set Var "ExtraCapture" 1)
                                                (moveAgain)
                                            })
                                        )
                                    )
                                )
                            )
                        )
                        (move
                            Select
                            (from
                                (if
                                    (is Mover Prev)
                                    "PlayFromLastHole"
                                    (sites Mover)
                                )
                                if:(and
                                    (= 1 (count at:(from)))
                                    (= 0 (count at:("NextSite" from:(from))))
                                )
                            )
                            (then
                                (sow
                                    "Track"
                                    owner:(mover)
                                    apply:(if (< 1 (count at:(to)))
                                        (moveAgain)
                                        (if (is In (to) (sites Mover "Inner"))
                                            (and {
                                                (if (> (count at:"OppositePit") 0)
                                                    (remove "OppositePit" count:(count at:"OppositePit"))
                                                )
                                                (if (> (count at:"OppositeOuterPit") 0)
                                                    (remove "OppositeOuterPit" count:(count at:"OppositeOuterPit"))
                                                )
                                                (set Var "ExtraCapture" 1)
                                                (moveAgain)
                                            })
                                        )
                                    )
                                )
                            )
                        )
                    })
                )
            )
        )
        }

        (end
            (if  (= 0 ("PiecesOwnedBy" Next))
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <CCW2> <thirdHoleInnerP1> <rightMostInnerHoleP1> <thirdHoleInnerP2> <rightMostInnerHoleP2>}
    {
    (item "10"   <10>  <"20,E,N1,W"> <12> <19> <27> <20> "Each player has 10 holes by row.")*
    (item "12"   <12>  <"24,E,N1,W"> <14> <23> <33> <24> "Each player has 12 holes by row.")
})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Spreta is a four-row mancala-style game board played by the Chikunda people of Malawi. ")
        (rules "4x10-12 board (even numbers only). One counter in each hole, except for the third hole from the left of the inner row, which contains two, and the rightmost hole of the inner row, which is empty. Play must begin from the hole containing two counters. Sowing occurs in an anti-clockwise direction. When the final counter lands in an occupied hole. These are picked up and sowing continues. When the final counter falls in an empty hole, the contents of the hole in the opponent's  inner row opposite this hole are captured; if there are also counters in the opponent's outer row, these are also captured, along with the contents of any other two holes on the opponent's side of the board. Single counters can be moved only when there are no holes with multiple counters and they may only be moved to an empty hole. The player who captures all of the opponent's counters wins.")
        (source "Sanderson 1913: 736.")
        (version "1.3.0")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin  "This game was played in Malawi, around 1913.")
        }
    )

    (graphics {
        (board Style Mancala)
    })

)

