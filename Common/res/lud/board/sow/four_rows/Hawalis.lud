(define "Columns" 7)

(define "Sow" 
    (move Select
        (from (sites Mover "Home") if:#1)
        (then 
            (sow
                "Track"
                owner:(mover)
                if:(and { 
                    (= (count at:(to)) 1) 
                    (is In (to) (sites Mover "Inner")) 
                    (> (count at:("OppositePit" (to))) 0) 
                })
                apply:(fromTo 
                    (from ("OppositePit" (to))) 
                    (to (handSite Mover)) 
                    count:(count at:("OppositePit" (to)))
                ) 
            )
        )
    )
)

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1 "Home"))))

//------------------------------------------------------------------------------

(game "Hawalis" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 4 "Columns" store:None
            { 
            (track "Track1" "0,E,N1,W" loop:True P1)
            (track "Track2" "14,E,N1,W" loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "Track1"))        // P1 Home
        (regions "Home" P2 (sites Track "Track2"))        // P2 home
        (regions "Inner" P1 (difference (sites Track "Track1") (sites Bottom))) // P1 inner Home
        (regions "Inner" P2 (difference (sites Track "Track2") (sites Top))) // P2 inner Home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 2 to:(union (sites P1 "Home") (sites P2 "Home"))))
        
        (play 
            (priority
                ("Sow" (> (count at:(from)) 1))
                ("Sow" (= (count at:(from)) 1))
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Hawalis is a mancala-style game played in Oman that closely mirrors Bao Ki Arabu in Zanzibar. It has been observed being played outside in Muscat, and the players were Arab-speaking Omani men. The board has four rows of seven holes, and is often played as holes dug out in the sand.")
        (rules "Play begins with two seeds in each hole. Each player owns the two rows closest to them. Play proceeds by taking the seeds from one hole and sowing them one by one in consecutive holes in an anti-clockwise direction. If the last seed falls in a hole in the inner row, and the opponent's hole opposite it is not empty, then the opponent's seeds in that hole are captured. Players can only play from holes with single counters when there is no other option, and they may not sow into another hole that already contains a counter. Play continues until one player no longer has any seeds.")
        (source "de Voogt 2003: 95-97.")
        (id "127")
        (version "1.3.13")
        (classification "board/sow/four rows")
        (credit "Eric Piette")
        (origin "This game was played in Oman? Zanzibar?, around 2003.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Hawalis_ai"
    )
)
