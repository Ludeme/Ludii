(define "Columns" 6)

(define "APlayerCanNotMove"
    (and (not (is Next Mover))
        (or
            (all Sites (sites P1) if:(or (!= 0 (state at:(site))) (is Empty (site))))
            (all Sites (sites P2) if:(or (!= 0 (state at:(site))) (is Empty (site))))
        )
    )
)

(define "PiecesOwnedBy" (count Cell at:(handSite #1)))

//------------------------------------------------------------------------------

(game "Leyla Gobale (Somaliland)"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 <Board:size> store:None
            (track "Track" <Board:track> loop:True)
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (hand Each)        
    })  
    (rules 
        
        (start (set Count 4 to:(sites Track)))
        
        (play
            (or {
                (move Select
                    (from
                        (if (= 0 (count Moves)) 
                            (sites {5})
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (sites Mover) 
                            )
                        )
                        if:(and 
                            (is Occupied (from)) 
                            (= 0 (state at:(from)))
                        )
                    )
                    (then
                        (sow
                            apply:(if (< 1 (count at:(to)))
                                (if (= 0 (state at:(to)))
                                    (and
                                        (moveAgain)
                                        (set Var "Replay" (to))
                                    )
                                )
                                (if (is In (to) (sites Mover))
                                    (if (!= 3 (count at:("OppositePitTwoRows" (to))))
                                        (if (!= 0 (count at:("OppositePitTwoRows" (to))))
                                            (fromTo
                                                (from ("OppositePitTwoRows" (to)))
                                                (to (handSite Mover))
                                                count:(count at:("OppositePitTwoRows" (to)))
                                            )
                                        )
                                        (and {
                                            (fromTo
                                                (from ("OppositePitTwoRows" (to)))
                                                (to (to))
                                                count:1
                                            )
                                            (set State at:(to) (mover))
                                            (set State at:("OppositePitTwoRows" (to)) (mover))
                                        })
                                    )
                                )
                            )
                        )
                    )
                )
                }
                (then
                    (if ("APlayerCanNotMove")
                        (and { 
                            (forEach Site 
                                (sites P1)
                                (if (= 0 (state at:(site)))
                                    (fromTo
                                        (from (site))
                                        (to (handSite P1))
                                        count:(count at:(site))
                                    )
                                )
                            )
                            (forEach Site 
                                (sites P2)
                                (if (= 0 (state at:(site)))
                                    (fromTo
                                        (from (site))
                                        (to (handSite P2))
                                        count:(count at:(site))
                                    )
                                )
                            )
                            (forEach Site 
                                (sites Board)
                                (if (= (state at:(site)) P1)
                                    (fromTo
                                        (from (site))
                                        (to (handSite P1))
                                        count:(count at:(site))
                                    )
                                    (if (= (state at:(site)) P2)
                                        (fromTo
                                            (from (site))
                                            (to (handSite P2))
                                            count:(count at:(site))
                                        )
                                    )
                                )
                            )
                        })
                    )
                )
            )
        )	

        (end ("MancalaByScoreWhen" ("NoPieceOnBoard")))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <track> }
    {
    (item "6"   <6> <"5,W,N,E"> "Each player has 6 holes.")  
    (item "8"   <8> <"7,W,N,E"> "Each player has 8 holes.")   
    (item "12"  <12> <"11,W,N,E"> "Each player has 12 holes.")   
})

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Leyla Gobale is a name for mancala-style games in the Horn of Africa. This game was played in Somaliland in the early twentieth century and is a two-row game.")
        (aliases {"Leyla-g√≤bale"})
        (rules "2x6, 8, or 12 holes. Four counters in each hole. Play begins from a player's righthand hole and counters are sown clockwise. After this, a player may begin sowing from any hole in their row. When the final counter falls into an occupied hole, the counters in that hole are picked up and sowing continues. If the final counter falls into an empty hole, the turn ends; if this hole is in the player's own row, the contents of the opponent's hole are captured if they contain one, two, four, or more counters. If it contains three counters, one of the opponent's counters is taken and placed into the hole where sowing ended, so that each now has two counters. These holes cannot be sown from for the rest of the game and are owned by the player whose turn created them. Play ends when one player can no longer play. The opponent then takes all of the counters in their own row, and each player takes the counters that have accumulated in their captured holes. The player with the most counters wins.")
        (source "Marin 1931: 5036-507.")
        (id "242")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)

