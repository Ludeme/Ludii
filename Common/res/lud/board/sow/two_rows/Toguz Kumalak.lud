(define "Columns" 9)

(define "Tuz" (value Player #1))

(define "CaptureFromTuzOf" 
    (if (and (> ("Tuz" #1) 0) (> (count at:("Tuz" #1)) 0))
        (fromTo (from ("Tuz" #1)) (to (mapEntry #1)) count:(count at:("Tuz" #1)))
    )
)

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Toguz Kumalak" 
    (players 2) 
    
    (equipment { 
        (board 
            (merge {
                (rectangle 1 9)
                (shift 2.5 1 (rectangle 1 1))
                (shift 5.5 1 (rectangle 1 1))
                (shift 0 2 (rectangle 1 9))
            })
            (track "Track" "0,E,19,W" loop:True)
            use:Vertex
        )
        (regions P1 (sites Bottom))    // P1 home
        (regions P2 (sites Top))       // P2 home
        (map {(pair P1 9) (pair P2 10)}) // kalahs (storage pits)
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start (set Count 9 to:(sites Track)))
        
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow 
                        apply:(if 
                            (and 
                                (is In (to) (sites Next)) 
                                (is Even (count at:(to)))
                            ) 
                            (fromTo 
                                (from (to)) 
                                (to (mapEntry (mover))) 
                                count:(count at:(to))
                            )
                            (if (and { 
                                    (is In (to) (sites Next)) 
                                    (= (count at:(to)) 3) 
                                    (< ("Tuz" Mover) 0) 
                                    (if (< ("Tuz" Next) 0)
                                        True
                                        (!= (+ (to) ("Tuz" Next)) 19)
                                    )
                                }) 
                                (and
                                    (set Value Mover (to))
                                    (fromTo 
                                        (from (to)) 
                                        (to (mapEntry (mover))) 
                                        count:(count at:(to))
                                    )
                                )
                            )
                        )
                        origin:(!= (count at:(last From)) 1)
                        (then
                            (and
                                (and
                                    ("CaptureFromTuzOf" P1)
                                    ("CaptureFromTuzOf" P2)
                                )
                                (and
                                    (set Score 
                                        P1 
                                        ("PiecesOwnedBy" P1)
                                    )
                                    (set Score 
                                        P2
                                        ("PiecesOwnedBy" P2) 
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Toguz Kumalak is a two-row mancala-style game board from Central Asia, and particularly popular in Kazakhstan and Kyrgyzstan. In Kyrgyzstan, in the early twentieth century, it was mostly played by older people, and was particularly popular during the warm season, when people would gather to play it in the hills. It could be played either on wooden boards or in holes dug into the ground. It is a well-known game throughout the region, and tournament play is becoming increasingly popular. ")
        (aliases {"Toguz Korgool"})
        (rules "2x9 board, with two stores in between the holes. Players own the store to their right. Nine counters in each hole. A player takes all of the counters from one of the holes in their row and sows them anti-clockwise. The first stone is dropped into the hole that was just emptied, except when there is only one stone, in which case it is moved to the next hole. If the last counter falls into an opponent's hole making this hole have an even number of counters, these counters are captured and placed in the player's store. Otherwise, the turn ends. When the last counter falls into an opponent's hole, making it contain three counters, it is made into a \"tuzduk.\" A player can only create one tuzduk per game. When a player creates a tuzduk, the opponent cannot create a tuzduk in the same hole in the player's row, with respect to the index of the hole in the respective player's hole. Stones that fall into a tuzduk are captured by its owner. The game ends when a player cannot move because all of the holes, except the tuzduk, are empty. The other player claims the remaining counters and the player with the most counters wins.")
        (source "Pantusov 1906: 149-152.")
        (id "282")
        (version "1.3.12")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Central Asia, from around 1500 to 2000.")
        }
    )
    
    (graphics {
        (show PlayerHoles)
        (board Style Mancala)
        (show AsHoles {9 10} Oval)
    })
    
    (ai 
        "Toguz Kumalak_ai"
    )
    
)
