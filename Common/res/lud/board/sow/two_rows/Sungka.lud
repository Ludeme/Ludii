(define "Columns" 7)
(define "PiecesCaptured" (count at:(mapEntry #1)))
(define "WasInStore" (is Pending))
(define "LastCounterInTheStore" (set Pending))
(define "SowingEndInMyStore" (= (to) (mapEntry Mover)))
(define "LastHoleSowed" (sites {(var)}))
(define "StoreLastHoleSowed" (set Var (to)))
(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns")))

//------------------------------------------------------------------------------

(game "Sungka" 
    (players 2)  
    (equipment { 
        (mancalaBoard 2 "Columns"
            (track "Track" "7,W,WNW,ENE,E,ESE" loop:True)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
        (piece "Seed" Shared)
    })  
    
    (rules 
        (start (set Count "Columns" to:(union (sites P1) (sites P2))))		
        
        phases:{
        (phase "Sowing"
            (play 
                (move Select 
                    (from 
                        (if 
                            (and 
                                ("SameTurn") 
                                (not ("WasInStore"))
                            ) 
                            "LastHoleSowed" 
                            (sites Mover)
                        )
                        if:(< 0 (count at:(from)))
                    )
                    (then 
                        (sow 
                            apply:(if ("SowingEndInMyStore")
                                (and
                                    (moveAgain)
                                    ("LastCounterInTheStore")
                                )
                                (if (and {(is In (to) (sites Mover)) (= (count at:(to)) 1) (< 0 (count at:"OppositePit"))})
                                    (fromTo 
                                        (from "OppositePit") 
                                        (to (mapEntry (mover))) 
                                        count:(count at:"OppositePit")
                                    )
                                    (if (> (count at:(to)) 1)
                                        (and
                                            (moveAgain)
                                            ("StoreLastHoleSowed")
                                        )
                                    )
                                )
                            )
                            skipIf:(= (to) (mapEntry Next))
                        )
                    )
                )
            )
            (end (if (all Passed)
                    {
                    (if (> 7 (count at:(mapEntry P1))) (result P2 Win))
                    (if (> 7 (count at:(mapEntry P2))) (result P1 Win))
                    }
                )
            )
            (nextPhase (all Passed) "BetweenRounds")
        )
        (phase "BetweenRounds"
            (play
                (if (<= 7 (count at:(mapEntry Mover)))
                    (move
                        (from (mapEntry Mover))
                        (to (intersection (sites Empty) (sites Mover)))
                        count:7
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Sungka is a two-row mancala-style board game from the Philippines. It is similar to many other games throughout Southeast Asia, which have similar playing mechanisms and similar names.")
        (rules "2x7 board, with two stores. Each player owns the store to their left. Seven counters per hole. Players take counters from one hole and sow them in a clockwise direction starting in the next hole from which the counters were taken. Players include their own store when sowing but ignore their opponent's store. If last counter falls into an empty hole, move ends. If it is on the player's side, the counters in the opponent's hole opposite are taken and added to the player's store. If the last counter falls into the player's store, the player may sow counters from another hole on their side. If the last counter drops into an occupied hole on either side of the board, the counters are picked up and sowing continues until the last counter drops into an empty hole. When all of the counters are in the players' stores, a new round begins. Players fill their holes with the counters in their store. Any holes which cannot be filled with seven counters are out of play for this round; any extra counters go back in the store. Play continues until one player cannot fill any holes, and the opponent wins.")
        (source "Bernardo 1937: 1-36.")
        (id "143")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Sungka_ai"
    )
)
