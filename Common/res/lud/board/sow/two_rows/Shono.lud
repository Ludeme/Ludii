(define "Columns" 6)

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Shono"
    (players 2) 
    (equipment { 
        (mancalaBoard 2 "Columns"
            (track "Track" "1,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)}) // kalahs (storage pits)
        (piece "Seed" Shared)
    }) 
    
    (rules 
        (start (set Count 5 to:(sites Track)))
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(< 0 (count at:(from)))
                )
                (then 
                    (sow 
                        if:(and 
                            (is In (to) (sites Next)) 
                            (or 
                                (= (count at:(to)) 2) 
                                (= (count at:(to)) 3)
                            )
                        ) 
                        apply:(fromTo 
                            (from (to)) 
                            (to (mapEntry (mover))) 
                            count:(count at:(to))
                        )
                        includeSelf:False
                        backtracking:True
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )	
)

//------------------------------------------------------------------------------

(option "Size Seeds" <Scale> args:{ <value> }
    {
    (item "Normal" 
        <1>  
        "The seeds have a normal size."
    )*
    (item "Small" 
        <0.8>  
        "The seeds are small."
    )
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Shono (Observed)" {
        "Size Seeds/Normal"
    })*
    
    (ruleset "Ruleset/Children (Observed)" {
        "Size Seeds/Small"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
(info
{
(description "Shono is a two-row mancala-style board game played by the Fang people of Gabon, Equatorial Guinea, and Cameroon.")
(useFor "Ruleset/Shono (Observed)" (rules "2x6 board with two stores. Five counters in each hole. Sowing occurs in an anti-clockwise direction. When the final counter lands in a hole in the opponent's row containing one or two counters, thus making it contain two or three counters, they are captured. Any other holes in the opponent's row which also contain two or three counters, in an unbroken sequence preceding the hole where the first capture was made, are captured. When a player sows around the board, the hole from which the coutners were taken is left empty. Play ends when one player cannot play from their row, and players capture any remaining counters in their holes. The player with the most counters wins."))
(useFor "Ruleset/Shono (Observed)" (source "Tessmann 1912: 269-270."))
(useFor "Ruleset/Shono (Observed)" (id "1308"))
(useFor "Ruleset/Children (Observed)" (rules "Four counters per hole."))
(useFor "Ruleset/Children (Observed)" (source "Tessmann 1912: 269."))
(useFor "Ruleset/Children (Observed)" (id "1309"))
(id "1308")
(version "1.3.9")
(classification "board/sow/two rows")
(credit "Eric Piette")
(origin "This game was played in Central Africa, around 1912.")
}
)
    
    (graphics {
        (board Style Mancala)
        (piece Scale <Scale:value>)
    })
    
    (ai
        "Shono_ai"
    )
)
