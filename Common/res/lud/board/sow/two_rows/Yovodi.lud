(define "Columns" 6)

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

(define "OppositePit" (if (is In (to) (sites P1)) (+ (to) "Columns") (- (to) "Columns")))

//------------------------------------------------------------------------------

(game "Yovodi"
    (players 2)  
    (equipment { 
        (mancalaBoard 2 "Columns"
            (track "Track" "6,W,N,E" loop:True)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
        (piece "Seed" Shared)
    })  
    
    (rules 
        (start (set Count 4 to:(sites Track)))
        (play 
            (move
                Select 
                (from 
                    (if 
                        (is Mover Prev) 
                        ("LastHoleSowed") 
                        (sites Mover) 
                    )  
                    if:(< 0 (count at:(from)))
                )
                (then 
                    (sow  
                        apply:(if (> (count at:(to)) 1)
                            (moveAgain)
                            (if (is In (to) (sites Mover))
                                (fromTo 
                                    (from ("OppositePit")) 
                                    (to (mapEntry (mover))) 
                                    count:(count at:("OppositePit"))
                                )
                                (fromTo 
                                    (from ("OppositePit")) 
                                    (to (mapEntry (next))) 
                                    count:(count at:("OppositePit"))
                                )
                            )
                        )
                    )
                )
            )
        )
        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )	
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Yovodi is a two-row mncala-style board game played by the Ewe people of Togo and Ghana.")
        (aliases {"Dzedodekase"})
        (rules "2x6 board with two stores. Four counters in each hole. Sowing occurs in a clockwise direction. When the final counter of a sowing lands in an occupied hole, these are picked up and sowing continues. When the final counter lands in an empty hole in the player's own row, the contents of the hole opposite it in the opponent's row are captured. If the final counter lands in an empty hole in the opponent's row, the opponent captures the counters in the hole opposite it in the player's row. The player who captures the most counters wins.")
        (source "Schönhärl 1909: 147-148.")
        (id "1306")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in West Africa, from around 1908 to 1909.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Yovodi_ai"
    )
)
