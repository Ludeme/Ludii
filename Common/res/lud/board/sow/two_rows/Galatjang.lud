(define "Columns" 6)

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "CaptureEnoughSeed" (> 7 (count in:(difference (sites Board) (mapEntry Mover)))))

(define "PlayableSites"
    (forEach (sites Mover "Home")
        if:(is In (site) (values Remembered))
    )
)

(define "AllPossibleHolesFilled"
    (or
        (and (> 7 (count at:(mapEntry P2))) (= 42 (count in:(sites Bottom))))
        (and (> 7 (count at:(mapEntry P1))) (= 42 (count in:(sites Top))))
    )
)

(define "NoLegalMoves"
    (= 0
        (count Sites
            in:(forEach
                (difference (union (sites Bottom) (sites Top)) (sites Empty))
                if:(is In (site) (values Remembered))
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Galatjang"
    (players 2)

    (equipment {
        (mancalaBoard 2 "Columns"
            {
            (track "Track1" "1,E,ENE,WNW,W" loop:True P1)
            (track "Track2" "12,W,WSW,ESE,E" loop:True P2)
            }
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Bottom))              // P1 Home
        (regions "Home" P2 (sites Top))                 // P2 home
        (map {(pair P1 LastSite) (pair P2 FirstSite)})  // kalahs (storage pits)
        }
    )
    (rules

        (start {
            (set Count 7 to:(union (sites Bottom) (sites Top)))
            (forEach Value min:1 max:12 (set RememberValue (value)))
        })

        phases:{
        (phase "Round"
            (play
                (move
                    Select
                    (from
                        (if
                            (and (not (is Pending)) (is Mover Prev))
                            ("PlayFromLastHole")
                            ("PlayableSites")
                        )
                        if:(> (count at:(from)) 0)
                    )
                    (then
                        (sow
                            "Track"
                            owner:(mover)
                            apply:
                            (if (= (to) (mapEntry Mover))
                                (and (moveAgain) (set Pending))
                                (if (> (count at:(to)) 1)
                                    (moveAgain)
                                    (if (and (is In (to) (sites Mover "Home")) (> (count at:("OppositePit")) 0))
                                        (fromTo
                                            (from "OppositePit")
                                            (to (mapEntry Mover))
                                            count:(count at:"OppositePit")
                                        )
                                    )
                                )
                            )
                            (then
                                (if ("NoLegalMoves")
                                    (and
                                        (forEach Site
                                            (difference (sites P1 "Home") (sites Empty) )
                                            (fromTo (from (site)) (to (mapEntry P1)) count:(count at:(site)))
                                        )
                                        (forEach Site
                                            (difference (sites P2 "Home") (sites Empty))
                                            (fromTo (from (site)) (to (mapEntry P2)) count:(count at:(site)))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase ("NoLegalMoves") "BetweenRound")
        )
        (phase "BetweenRound"
            (play
                (if (>= (count at:(mapEntry Mover)) 7)
                    (move
                        (from (mapEntry Mover))
                        (to (intersection (sites Empty) (sites Mover "Home")))
                        count:7
                    )
                    (then
                        (if ("AllPossibleHolesFilled")
                            (and
                                (forget Value All)
                                (forEach Site (sites Board)
                                    (if (= 7 (count at:(site)))
                                        (remember Value (site))
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase ("AllPossibleHolesFilled") "Round")
        )
        }

        (end
            (if ("CaptureEnoughSeed")
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Galatjan is a two-row mancala-style board game from the island of Celebes in Indonesia. It was played by the Macassar people in the middle of the nineteenth century. Kaudern states that it is played throughout Indonesia. It was customary to play this game during times of mourning.")
        (rules "2x6 board with two stores. Seven counters in each hole. Play moves in an anti-clockwise direction and players sow into the store on their right hand side. A move may begin from any of the player's holes except their store. If the last counter falls into the store, they can sow again from any of the holes in their row. If it lands in a hole with counters in it, these are picked up and sowing continues. If the last counter falls into an empty hole the move ends, but if the hole is in the player's own row, any counters in the hole opposite it are captured and placed in the store. When no more moves can be made, a new round begins and each player fills as many holes as they can by putting seven in each. Surplus counters are placed in the store. Any unfilled holes are excluded from play. Play continues until one player cannot fill a hole with seven counters, and the opponent wins.")
        (source "Matthes 1859: 84, 898. ")
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Indonesia, around 1859.")
        }
    )

    (graphics {
        (board Style Mancala)
    })

    (ai
        "Galatjang_ai"
    )
)
