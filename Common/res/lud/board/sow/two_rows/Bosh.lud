(define "NextHoleFrom" (trackSite Move from:#1 #2))

(define "PlayFromNextHole" (sites {("NextHoleFrom" (last To afterConsequence:True) steps:1)}))

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Bosh"  
    // THIS IS JUST FOR ONE ROUND FOR NOW
    (players 2)  
    (equipment { 
        (mancalaBoard 2 5 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))  // P1 home
        (regions P2 (sites Top))     // P2 home
        (piece "Seed" Shared)
        (hand Each)
        }
    )  
    (rules 
        
        (start (set Count 4 to:(sites Track)) )
        
        (play 
            (move 
                Select 
                (from 
                    (if 
                        (is Mover Prev) 
                        "PlayFromNextHole" 
                        (sites Mover) 
                    ) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        apply:(if 
                            (and 
                                (= (count at:("NextHoleFrom" (to) steps:1)) 0) 
                                (> (count at:("NextHoleFrom" (to) steps:2)) 0)
                            )
                            (fromTo 
                                (from ("NextHoleFrom" (to) steps:2)) 
                                (to (handSite Mover)) 
                                count:(count at:("NextHoleFrom" (to) steps:2))
                            )
                            (if
                                (> (count at:("NextHoleFrom" (to) steps:1)) 0) 
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        
        (end
            (if (no Moves Mover) 
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This two-row mancala-style board is played by the Darod people in Somalia, and is related to other games in the Horn of Africa. The board takes the form of two rows with five holes.")
        (rules "Play begins with four counters in each hole. Players move by picking up all of the counters in any of the holes in their row and sowing them anti-clockwise. If the next hole after the one in which the final counter is dropped contains counters, these are picked up and sowing continues. If the next hole after the one in which the last counter was sown is empty, the counters in the hole after this one are taken and the turn is over. If that hole is empty, none are taken. When all of the holes in one row are empty, the player whose row still contains counters captures these counters. A new game begins. Each player fills as many of the holes in their row with four counters. The player which cannot fill all of their holes with four counters removes from play all of the holes that cannot be filled, and sets aside the extra counters. They play another round with the same rules as before, but without the eliminated hole. The game ends when one player must close all of the holes in their row, thus being unable to play.")
        (source "Marin 1931: 507.")
        (version "1.2.1")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Horn of Africa, around 1931.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Bosh_ai"
    )
)
