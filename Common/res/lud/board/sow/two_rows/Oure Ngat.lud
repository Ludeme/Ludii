(define "NumPiecesOnBoard" (count in:(sites Track)))

(define "PiecesOwnedBy" (count Cell at:(handSite #1)))

//------------------------------------------------------------------------------

(game "Oure Ngat"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 4 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Bottom)) // P1 Home
        (regions "Home" P2 (sites Top))    // P2 home
        (hand Each)
        }
    )  
    (rules 
        (start (set Count 6 to:(sites Track) ) )
        
        (play 
            (move
                Select 
                (from 
                    (sites Mover "Home") 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        origin:True
                        (then
                            (forEach Site (sites Track)
                                (if (is In (count at:(site)) (sites {2 4 6}))
                                    (fromTo 
                                        (from (site))
                                        (to (handSite Mover))
                                        count:(count at:(site))
                                    )
                                )
                            )
                        )
                    )
                )
            )		
        )
        
        (end 
            (if (= 0 ("NumPiecesOnBoard"))
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata  
    
    (info
        {
        (description "Ouré Ngat is a two-row mancala-style board game played in Senegal during the early twentieth century. It is not typically played on a board; instead, the counters are arranged in heaps on the ground.")
        (aliases {"Ouré Ngat" "Uré Ngat"})
        (rules "Two rows of four piles. Six counters in each pile. A player picks up the counters in one of her piles and sows them anti-clockwise, beginning in the place from which the pile originated. Any piles containing 2, 4, or 6 counters at the end of sowing are captured. Play continues until all of the counters have been captured. The player who captures the most counters wins.")
        (source "Béart 1955: 511.")
        (id "427")
        (version "1.3.3")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Oure Ngat_ai"
    )
)

