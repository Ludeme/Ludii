(define "NextHoleFrom" (trackSite Move from:#1 #2 #3))
(define "NoSeedInHome"
    (all Sites (sites #1 "Home") if:(is Empty (site)))
)
(define "OneSeedInEachHole"
    (all Sites (sites #1 "Home") if:(= 1 (count at:(site))))
)

//------------------------------------------------------------------------------

(game "Lontu-Holo"
    (players 2)

    (equipment {
        (mancalaBoard 2 6
            {
            (track "TrackCCW1" {1 2 3 9 8 7} loop:True P1)
            (track "TrackCCW2" {4 5 6 12 11 10} loop:True P2)
            }
        )
        (regions "Home" P1 (sites Track "TrackCCW1"))          // P1 home
        (regions "Home" P2 (sites Track "TrackCCW2"))          // P2 home
        (regions "TwoHolesClosestNotCentre" P1 (sites {1 3}))
        (regions "TwoHolesClosestNotCentre" P2 (sites {10 12}))
        (regions "TwoHolesFurtherNotCentre" P1 (sites {7 9}))
        (regions "TwoHolesFurtherNotCentre" P2 (sites {4 6}))
        (regions "LeftMostHoles" P1 (intersection (expand (sites Left)) (sites Track "TrackCCW1")))
        (regions "LeftMostHoles" P2 (intersection (expand (sites Right)) (sites Track "TrackCCW2")))
        (regions "RightMostHoles" P1 (intersection (sites Centre) (sites Track "TrackCCW1")))
        (regions "RightMostHoles" P2 (intersection (sites Centre) (sites Track "TrackCCW2")))
        (regions "ClosestRow" P1 (intersection (sites Bottom) (sites Track "TrackCCW1")))
        (regions "ClosestRow" P2 (intersection (sites Top) (sites Track "TrackCCW2")))
        (map "CentreFurtherRow" {(pair P1 8) (pair P2 5)})
        (map "CentreClosestRow" {(pair P1 2) (pair P2 11)})
        (map "RightFurtherRow" {(pair P1 9) (pair P2 4)})
        (map "RightClosestRow" {(pair P1 3) (pair P2 10)})
        (map "LeftFurtherRow" {(pair P1 7) (pair P2 6)})
        (map "LeftClosestRow" {(pair P1 1) (pair P2 12)})
        (piece "Seed" Shared)
        (map "Store" {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
    })
    (rules
        (start (set Count 4 to:(union (sites Bottom) (sites Top)) ) )

        phases:{
        <Variant:opening>

        (phase "Sowing"
            (play
                (if (is Mover Prev)
                    (if ("OneSeedInEachHole" Next)
                        (move
                            (from
                                (sites Next "Home")
                            )
                            (to (sites Next "Home") if:(!= (from) (to)))
                            count:1
                            (then
                                (moveAgain)
                            )
                        )
                        (move
                            (from
                                (var "Replay")
                            )
                            (to (sites Next "Home") if:(< 1 (count at:(to))))
                            count:1
                            (then
                                (if (is Occupied (last From))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                    (move Select
                        (from
                            (sites Mover "Home")
                            if:(is Occupied (from))
                        )
                        (then
                            (sow
                                "TrackCCW"
                                owner:(mover)
                                apply:(if (= 4 (count at:(to)))
                                    (and
                                        (moveAgain)
                                        (set Var "Replay" (to))
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (end
                (if ("NoSeedInHome" Mover) (result Mover Win))
            )
        )
        }
    )
)

//------------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <opening> }
    {
    (item "Lontu Holo"
        <
        (phase "Opening1"
            (play
                (move
                    (from
                        (sites Mover "TwoHolesClosestNotCentre")
                        if:(is Occupied (from))
                    )
                    (to (mapEntry "CentreFurtherRow" Mover))
                    count:(count at:(from))
                )
            )
            (nextPhase Mover (all Sites (sites Mover "TwoHolesClosestNotCentre") if:(is Empty (site))) "Opening2")
        )
        (phase "Opening2"
            (play
                (move Select
                    (from
                        ("NextHoleFrom" (mapEntry "CentreFurtherRow" Mover) "TrackCCW" steps:1)
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                        )
                    )
                )
            )
            (nextPhase Mover "Opening3")
        )
        (phase "Opening3"
            (play
                (move Select
                    (from
                        (mapEntry "CentreFurtherRow" Mover)
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                        )
                    )
                )
            )
            (nextPhase Mover "Opening4")
        )
        (phase "Opening4"
            (play
                (move
                    (from
                        (sites Mover "ClosestRow")
                        if:(is Occupied (from))
                    )
                    (to (mapEntry "Store" Mover))
                    count:(count at:(from))
                )
            )

            (nextPhase Mover (all Sites (sites Mover "ClosestRow") if:(is Empty (site))) "Opening5")
        )

        (phase "Opening5"
            (play
                (move
                    (from
                        (mapEntry "Store" Mover)
                    )
                    (to (sites Mover "Home") if:(< 1 (count at:(to))))
                    count:1
                )
            )

            (nextPhase Mover (is Empty (mapEntry "Store" Mover)) "Sowing")
        )
        >
    "The opening rules of the variant Lontu Holo.")*

    (item "Alternate Opening 1"
        <
        (phase "Opening1"
            (play
                (move
                    (from
                        (sites Mover "TwoHolesClosestNotCentre")
                        if:(= 4 (count at:(from)))
                    )
                    (to (mapEntry "CentreClosestRow" Mover))
                    count:2
                )
            )
            (nextPhase Mover (all Sites (sites Mover "TwoHolesClosestNotCentre") if:(= 2 (count at:(site)))) "Opening2")
        )
        (phase "Opening2"
            (play
                (move
                    (from
                        (sites Mover "TwoHolesFurtherNotCentre")
                        if:(= 4 (count at:(from)))
                    )
                    (to (mapEntry "CentreFurtherRow" Mover))
                    count:2
                )
            )
            (nextPhase Mover (all Sites (sites Mover "TwoHolesFurtherNotCentre") if:(= 2 (count at:(site)))) "Opening3")
        )
        (phase "Opening3"
            (play
                (move Select
                    (from
                        (sites {(mapEntry "CentreClosestRow" Mover) (mapEntry "CentreFurtherRow" Mover) })
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                        )
                    )
                )
            )
            (nextPhase Mover "Opening4")
        )
        (phase "Opening4"
            (play
                (move
                    (from
                        (sites Mover "LeftMostHoles")
                        if:(is Occupied (from))
                    )
                    (to (mapEntry "Store" Mover))
                    count:(count at:(from))
                )
            )

            (nextPhase Mover (all Sites (sites Mover "LeftMostHoles") if:(is Empty (site))) "Opening5")
        )

        (phase "Opening5"
            (play
                (move
                    (from
                        (mapEntry "Store" Mover)
                    )
                    (to (sites Mover "Home") if:(< 1 (count at:(to))))
                    count:1
                )
            )

            (nextPhase (and (is Empty (mapEntry "Store" P1)) (is Empty (mapEntry "Store" P2)) ) "Sowing")
        )
        >
    "The opening rules of the variant Alternate Opening 1.")

    (item "Alternate Opening 2"
        <
        (phase "Opening1"
            (play
                (move
                    (from
                        (sites { (mapEntry "CentreClosestRow" Mover) (mapEntry "RightClosestRow" Mover)})
                        if:(= 4 (count at:(from)))
                    )
                    (to (mapEntry "LeftClosestRow" Mover))
                    count:2
                )
            )
            (nextPhase Mover (all Sites (sites { (mapEntry "CentreClosestRow" Mover) (mapEntry "RightClosestRow" Mover)}) if:(= 2 (count at:(site)))) "Opening2")
        )

        (phase "Opening2"
            (play
                (move
                    (from
                        (sites { (mapEntry "CentreFurtherRow" Mover) (mapEntry "RightFurtherRow" Mover)})
                        if:(= 4 (count at:(from)))
                    )
                    (to (mapEntry "LeftFurtherRow" Mover))
                    count:2
                )
            )
            (nextPhase Mover (all Sites (sites { (mapEntry "CentreFurtherRow" Mover) (mapEntry "RightFurtherRow" Mover)}) if:(= 2 (count at:(site)))) "Opening3")
        )
        (phase "Opening3"
            (play
                (move Select
                    (from
                        (mapEntry "LeftClosestRow" Mover)
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                        )
                    )
                )
            )
            (nextPhase Mover "Opening4")
        )

        (phase "Opening4"
            (play
                (move Select
                    (from
                        (mapEntry "CentreClosestRow" Mover)
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                        )
                    )
                )
            )
            (nextPhase Mover "Opening5")
        )
        (phase "Opening5"
            (play
                (move
                    (from
                        (sites Mover "Home")
                        if:(= 4 (count at:(from)))
                    )
                    (to (mapEntry "Store" Mover))
                    count:(count at:(from))
                )
            )

            (nextPhase Mover (all Sites (sites Mover "Home") if:(!= 4 (count at:(site)))) "Opening6")
        )

        (phase "Opening6"
            (play
                (move
                    (from
                        (mapEntry "Store" Mover)
                    )
                    (to (sites Mover "Home") if:(< 1 (count at:(to))))
                    count:1
                )
            )

            (nextPhase (and (is Empty (mapEntry "Store" P1)) (is Empty (mapEntry "Store" P2)) ) "Sowing")
        )
        >
    "The opening rules of the variant Alternate Opening 2.")

    (item "Alternate Opening 3"
        <
        (phase "Opening1"
            (play
                (move
                    (from
                        (mapEntry "RightClosestRow" Mover)
                    )
                    (to (mapEntry "CentreFurtherRow" Mover))
                    count:3
                )
            )
            (nextPhase Mover "Opening2")
        )

        (phase "Opening2"
            (play
                (move
                    (from
                        (mapEntry "RightFurtherRow" Mover)
                    )
                    (to (mapEntry "CentreFurtherRow" Mover))
                    count:1
                )
            )
            (nextPhase Mover "Opening3")
        )

        (phase "Opening3"
            (play
                (move
                    (from
                        (mapEntry "LeftFurtherRow" Mover)
                    )
                    (to (mapEntry "CentreClosestRow" Mover))
                    count:3
                )
            )
            (nextPhase Mover "Opening4")
        )

        (phase "Opening4"
            (play
                (move
                    (from
                        (mapEntry "LeftClosestRow" Mover)
                    )
                    (to (mapEntry "CentreClosestRow" Mover))
                    count:1
                )
            )
            (nextPhase Mover "Opening5")
        )

        (phase "Opening5"
            (play
                (move Select
                    (from
                        (sites Mover "Home")
                        if:(= 1 (count at:(from)))
                    )
                    (then
                        (sow
                            "TrackCCW"
                            owner:(mover)
                        )
                    )
                )
            )
            (nextPhase Mover (all Sites (sites Mover "Home") if:(!= 1 (count at:(site)))) "Opening6")
        )
        (phase "Opening6"
            (play
                (move
                    (from
                        (sites Mover "Home")
                        if:(= 4 (count at:(from)))
                    )
                    (to (mapEntry "Store" Mover))
                    count:(count at:(from))
                )
            )

            (nextPhase Mover (all Sites (sites Mover "Home") if:(!= 4 (count at:(site)))) "Opening7")
        )

        (phase "Opening7"
            (play
                (move
                    (from
                        (mapEntry "Store" Mover)
                    )
                    (to (sites Mover "Home") if:(< 1 (count at:(to))))
                    count:1
                )
            )

            (nextPhase (and (is Empty (mapEntry "Store" P1)) (is Empty (mapEntry "Store" P2)) ) "Sowing")
        )
        >
    "The opening rules of the variant Alternate Opening 3.")
})

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Lontu Holo (Observed)" {
        "Variant/Lontu Holo"
    })*

    (ruleset "Ruleset/Alternate Opening 1 (Observed)" {
        "Variant/Alternate Opening 1"
    })

    (ruleset "Ruleset/Alternate Opening 2 (Observed)" {
        "Variant/Alternate Opening 2"
    })

    (ruleset "Ruleset/Alternate Opening 3 (Observed)" {
        "Variant/Alternate Opening 3"
    })

})

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Lontu-Holo is a two-row mancala-style board game played by the Ndyuka people of Suriname. It is one of the only recorded games of this type where the goal is to capture the fewest number of counters.")
        (useFor "Ruleset/Lontu Holo (Observed)" (rules "2x6 board, with a store hole on either end. Four counters in each hole. Each player owns the six holes, three holes in each row, to their right. Sowing occurs in an anti-clockwise direction, and only in the holes belonging to the player. Play begins with a stylized opening move. The first player takes the counters from the holes in each of the row of holes closest to them, and places them in the central row of their holes in the further row. The counters from the hole next in the direction of sowing are then sown. The counters from the central hole in the further of the player's rows are then sown. When this is completed, the player takes the counters from the holes in the row closest to them, and holds them until the opponent performs the same sequence of moves on their side of the board. At this point, each player places the counters they are holding into any hole on the opponent's side of the board which contains more than one counter. Players then alternate turns sowing pieces around their holes of the board. Sowing always skips the hole from which sowing began. When the final counter of a sowing causes a hole to contain four counters, these counters are placed on any hole on the opponent's side of the board that contains more than one counter. When a player has only single seeds in their holes, and the opponent is about to place counters in the player's holes, the opponent moves one of the player's counters into another hole containing one counter, thus making two, and places the counters from the opponent's side are placed there. The player who is able to place all of their counters on the opponent's side of the board wins."))
        (useFor "Ruleset/Lontu Holo (Observed)" (source "Herskovits 1932: 27-28."))
        (useFor "Ruleset/Alternate Opening 1 (Observed)" (rules "The player takes two counters each for the left and right holes in the row closest to them, and places them in the central hole in that row. The player does the same in the further row. The player then sows from one of the central holes. The player then takes the counters from the leftmost holes in each row and holds them until after the opponent performs the same stylized move. Then the players place the counters they are holding into one of the holes containing one or more counters on the opponent's side of the board."))
        (useFor "Ruleset/Alternate Opening 1 (Observed)" (source "Herskovits 1932: 27."))
        (useFor "Ruleset/Alternate Opening 2 (Observed)" (rules "The player takes two counters from the central and right holes in both rows, and places them in the left hole in the further row. The player then takes the counters in the left hole in the closest row and sows them. Then the player takes the counters in the central hole in the closest row and sows them. The player then takes all of the counters from the holes which contain four counters, and holds them until after the opponent performs the same moves. Then, the player takes two counters from the opponent's hole with twelve counters, and places these two along with the counters the player is holding into any hole on the opponent's side of the board. The opponent does the same."))
        (useFor "Ruleset/Alternate Opening 2 (Observed)" (source "Herskovits 1932: 27-28."))
        (useFor "Ruleset/Alternate Opening 3 (Observed)" (rules "The player takes three counters from the rightmost hole in the closest row, one counter from the rightmost hole in the further row, and places them in the central hole of the further row. The player then takes three from the leftmost hole in the further row, one from the leftmost hole in the closest row, and places them in the central hole of the closest row. The player then sows the single counters into the next holes. The player then takes the contents of the holes with four counters, and holds them until after the opponent performs the same moves. The player then places the counters into one of the holes on the opponent's side of the board which contains counters. The opponent does the same."))
        (useFor "Ruleset/Alternate Opening 3 (Observed)" (source "Herskovits 1932: 28."))
        (source "Murray 1951: 203-204.")
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Suriname, around 1930.")
        }
    )

    (graphics {
        (board Style Mancala)
    })
)

