(define "Columns" 12)

(define "PlayFromLastHole" (last To afterConsequence:True))

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Intotoi"
    (players 2)

    (equipment {
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))                    // P1 home
        (regions P2 (sites Top))                       // P2 home
        }
    )
    (rules

        (start (set Count 3 to:(sites {1 2 4 5 7 8 10 11 22 21 19 18 16 15 13 12}) ) )

        phases:{

        (phase "Opening1"
            (play
                (move Select
                    (from 10)
                    (then (sow count:2))
                )
            )
            (nextPhase "Opening2")
        )

        (phase "Opening2"
            (play
                (if
                    (is Mover Prev)
                    (move Select
                        (from
                            (sites {"PlayFromLastHole"})
                            if:(> (count at:(from)) 0)
                        )
                        (then
                            (sow
                                apply:(if (!= 1 (count at:(to)))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                    (move Select
                        (from
                            (sites {13 15})
                            if:(> (count at:(from)) 0)
                        )
                        (then
                            (sow
                                count:2
                                apply:(if (!= 1 (count at:(to)))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase (not (is Next Mover)) "Play")
        )

        (phase "Play"
            (play
                (move Select
                    (from
                        (sites From
                            (do
                                (move Select
                                    (from
                                        (sites Mover)
                                        if:(> (count at:(from)) 0)
                                    )
                                    (then (sow))
                                )
                                ifAfterwards:(or
                                    (and (= 1 (count at:("PlayFromLastHole"))) (is In ("PlayFromLastHole") (sites Mover)))
                                    (is In ("PlayFromLastHole") (sites Next))
                                )
                            )
                        )
                        if:(> (count at:(from)) 0)
                    )
                    (then
                        (sow
                            apply:(if (= 1 (count at:(to)))
                                (if (and (not (is Empty ("OppositePit"))) (is In (to) (sites Mover)))
                                    (fromTo
                                        (from ("OppositePit"))
                                        (to (handSite Mover))
                                        count:(count at:("OppositePit"))
                                    )
                                )
                                (moveAgain)
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (is Next Mover) "Replay")
        )

        (phase "Replay"
            (play
                (move Select
                    (from
                        (sites {"PlayFromLastHole"})
                        if:(> (count at:(from)) 0)
                    )
                    (then
                        (sow
                            apply:(if (= 1 (count at:(to)))
                                (if (and (not (is Empty ("OppositePit"))) (is In (to) (sites Mover)))
                                    (fromTo
                                        (from ("OppositePit"))
                                        (to (handSite Mover))
                                        count:(count at:("OppositePit"))
                                    )
                                )
                                (moveAgain)
                            )
                        )
                    )
                )
            )
            (nextPhase Mover (not (is Next Mover)) "Play")
        )
        }

        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1))
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Intotoi is a two-row mancala-style board game played by the Samburu people of Kenya. It was documented in the early twentieth century.")
        (rules "2x12 board. Starting position, from the left hand hole, for each player: 0-3-3-0-3-3-0-3-3-0-3-3 In the opening play, one player must sow two counters from the second hole from their right. The opponent then makes the same play from their row, or from the fourth hole from their right. When the final counter lands in an occupied hole, these are picked up and sowing continues. A player's turn ends when the final counter falls into an empty hole. Players may now begin their move from any hole in their row, but only if it ends in an empty hole in their row or if it enters the opponent's row. If the final counter lands in an occupied hole in the opponent's row, these are picked up and sowing continues. If the final counter lands in an occupied hole in the player's row, the contents of the hole in the opponent's hole opposite it are captured. Sowing ends when a capture is made or when a counter falls into an empty hole.")
        (source "Murray 1951: 200.")
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Kenya, around 1951.")
        }
    )

    (graphics {
        (board Style Mancala)
    })
    (ai
        "Intotoi_ai"
    )
)

