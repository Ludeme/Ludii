(define "PiecesOwnedBy" (count Cell at:(handSite #1)))

//------------------------------------------------------------------------------

(game "Mankala" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    
    (rules 
        (start (set Count 6 to:(sites Track)))		 
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(< 0 (count at:(from)))
                )
                (then 
                    (sow 
                        if:(or {
                            (= (count at:(to)) 2) 
                            (= (count at:(to)) 4)
                            (= (count at:(to)) 6)
                        })
                        apply:(fromTo 
                            (from (to)) 
                            (to (handSite Mover)) 
                            count:(count at:(to))
                        )
                        backtracking:True
                    )
                )
            )
        )
        (end ("MancalaByScoreWhen" (and (no Moves Next) (no Moves Mover))))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Mankala is a two-row manca;a-style game, inwhich sowing is the primary movement mechanism in the game. It was documented in Egypt during the eighteenth century. ")
        (rules "2x6 board. Six counters in each hole. Players take the contents of one of their holes and sow in an anti-clockwise direction. When the final counter of a sowing lands in a hole, making it contain two, four, or six counters,  these counters are taken. If the second to last hole also contains two, four, or six counters, these are also taken, continuing in an unbroken line until a hole with containing a number of counters other than two, four, or six. The game continues until all of the counters have been captured. The player who captured the most counters wins.")
        (source "Niebuhr 1774: 173.")
        (id "1056")
        (version "1.3.7")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Egypt, around 1762.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Mankala_ai"
    )
)
