(define "NextHole" (trackSite Move from:#1 #2 steps:#3))
(define "NumBigPiecesCaptured" (count Cell in:(sites {(handSite P1 1) (handSite P2 1)})))

(define "LeftMostEmpty" 
    (trackSite FirstSite 
        "TrackCCW"
        from:(mapEntry Mover)
        if: (is Empty (to))
    )
)

(define "NoPieceOnBoard" (no Pieces All in:(sites Board)))

(define "NoPieceOnOwnedHoles"  (no Pieces All in:(sites #1)))

//------------------------------------------------------------------------------

(game "Laomuzhu"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 5 store:None largeStack:True
            {
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "4,W,N,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (piece "Ball" P1)
        (piece "Ball" P2)
        (hand Each size:2)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))         // P2 home
        (map {(pair P1 0) (pair P2 9)})  // left most holes
    })  
    (rules 
        
        (start {
            (place Stack "Seed" (union (sites Bottom) (sites Top)) count:5)
            (place Stack "Ball1" (handSite P1 1))
            (place Stack "Ball2" (handSite P2 1))
        })
        
        phases:{
        (phase "Placement"
            (play
                (if (is Occupied Cell (handSite Mover 1))
                    (move
                        (from Cell (handSite Mover 1) level:(topLevel Cell at:((handSite Mover 1))))
                        (to (sites Mover))
                        stack:True
                    )
                )
            )
            
            (nextPhase Mover (is Empty Cell (handSite Mover 1)) "Sowing")
        )
        
        (phase "Sowing"
            (play
                (or {
                    (if (!= (var "Direction") 2)
                        (move Select
                            (from 
                                (if ("SameTurn")
                                    (sites {(var "Replay")})
                                    (sites Mover) 
                                )
                                if:(!= 0 (size Stack at:(from)))
                            )
                            (then
                                (and {
                                    (forEach Value min:1 max:(size Stack at:(last From))
                                        (if (= (id "Seed" Shared) (what at:(last From) level:(- (size Stack at:(last From)) (value))))
                                            (fromTo
                                                (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                (to ("NextHole" (last From) "TrackCCW" (value)))
                                                stack:True
                                            )
                                            (and
                                                (if (and (is Occupied ("NextHole" (last From) "TrackCCW" (value))) (!= ("NextHole" (last From) "TrackCCW" (value)) (last From)))
                                                    (if (= (who at:(last From) level:(- (size Stack at:(last From)) (value))) P1)
                                                        (forEach Level ("NextHole" (last From) "TrackCCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P1))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P1 1))
                                                                )
                                                            )
                                                        )
                                                        (forEach Level ("NextHole" (last From) "TrackCCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P2))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P2 1))
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                                (fromTo
                                                    (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                    (to ("NextHole" (last From) "TrackCCW" (value)))
                                                    stack:True
                                                )
                                            )
                                        )
                                    )
                                    (if (< (var "Direction") 1) (set Var "Direction" 1))
                                    (if (< (size Stack at:(last From)) 8)
                                        (if (!= 0 (size Stack at:("NextHole" (last From) "TrackCCW" (+ 1 (size Stack at:(last From))))))
                                            (and 
                                                (moveAgain)
                                                (set Var "Replay" ("NextHole" (last From) "TrackCCW" (+ 1 (size Stack at:(last From)))))
                                            )
                                            (if (is Occupied ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))))
                                                (forEach Level ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) FromTop
                                                    (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) level:(level)))
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover))
                                                        )
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover 1))
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                })
                            )
                        )
                    )
                    (if (!= (var "Direction") 1)
                        (move Select
                            (from 
                                (if ("SameTurn")
                                    (sites {(var "Replay")})
                                    (sites Mover) 
                                ) 
                                if:(!= 0 (size Stack at:(from)))
                            )
                            (then
                                (and {
                                    (forEach Value min:1 max:(size Stack at:(last From))
                                        (if (= (id "Seed" Shared) (what at:(last From) level:(- (size Stack at:(last From)) (value))))
                                            (fromTo
                                                (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                (to ("NextHole" (last From) "TrackCW" (value)))
                                                stack:True
                                            )
                                            (and
                                                (if (and (is Occupied ("NextHole" (last From) "TrackCW" (value))) (!= ("NextHole" (last From) "TrackCW" (value)) (last From)))
                                                    (if (= (who at:(last From) level:(- (size Stack at:(last From)) (value))) P1)
                                                        (forEach Level ("NextHole" (last From) "TrackCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P1))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P1 1))
                                                                )
                                                            )
                                                        )
                                                        (forEach Level ("NextHole" (last From) "TrackCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P2))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P2 1))
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                                (fromTo
                                                    (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                    (to ("NextHole" (last From) "TrackCW" (value)))
                                                    stack:True
                                                )
                                            )
                                        )
                                    )
                                    (if (< (var "Direction") 1) (set Var "Direction" 2))
                                    (if (< (size Stack at:(last From)) 8)
                                        (if (!= 0 (size Stack at: ("NextHole" (last From) "TrackCW" (+ 1 (size Stack at:(last From))))))
                                            (and 
                                                (moveAgain)
                                                (set Var "Replay" ("NextHole" (last From) "TrackCW" (+ 1 (size Stack at:(last From)))))
                                            )
                                            (if (is Occupied ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))))
                                                (forEach Level ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) FromTop
                                                    (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) level:(level)))
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover))
                                                        )
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover 1))
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                })
                            )
                        )
                    )
                    }
                    (then
                        (if ("NoPieceOnOwnedHoles" P1)
                            (forEach Site
                                (sites Board)
                                (if (is Occupied (site))
                                    (forEach Level (site) FromTop
                                        (if (= (id "Seed" Shared) (what at:(site) level:(level)))
                                            (fromTo
                                                (from (site) level:(level))
                                                (to (handSite P1))
                                                stack:True
                                            )
                                            (fromTo
                                                (from (site) level:(level))
                                                (to (handSite P1 1))
                                                stack:True
                                            )
                                        )
                                    )
                                )
                            )
                            (if ("NoPieceOnOwnedHoles" P2)
                                (forEach Site
                                    (sites Board)
                                    (if (is Occupied (site))
                                        (forEach Level (site) FromTop
                                            (if (= (id "Seed" Shared) (what at:(site) level:(level)))
                                                (fromTo
                                                    (from (site) level:(level))
                                                    (to (handSite P2))
                                                    stack:True
                                                )
                                                (fromTo
                                                    (from (site) level:(level))
                                                    (to (handSite P2 1))
                                                    stack:True
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            
            (end (if ("NoPieceOnBoard")
                    {
                    (if (and (> 5 (size Stack Cell at:(handSite P1))) (= 0 (size Stack Cell at:(handSite P1 1))))
                        (result P2 Win)
                    )
                    (if (and (> 5 (size Stack Cell at:(handSite P2))) (= 0 (size Stack Cell at:(handSite P2 1))))
                        (result P1 Win)
                    )
                    }
                )
            )
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play 
                (or
                    (if (<= 5 (size Stack Cell at:(handSite Mover)))
                        (move Select
                            (from Cell (handSite Mover))
                            (to ("LeftMostEmpty"))
                            (then
                                (forEach Value min:1 max:5
                                    (fromTo
                                        (from Cell (last From) level:(- (size Stack Cell at:(last From)) (value)))
                                        (to (last To))
                                        stack:True
                                    )
                                )
                            )
                        )
                    )
                    (if (!= 0 (size Stack Cell at:(handSite Mover 1)))
                        (move Select
                            (from Cell (handSite Mover 1))
                            (to (sites Mover))
                            (then
                                (fromTo
                                    (from Cell (last From) level:(topLevel Cell at:(last From)))
                                    (to (last To))
                                    stack:True
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Laomuzhu is a two-row mancala-style board game from the Yunnan region of China. It is similar to other mancala games from Asia, but it has extra capturing rules determined by the movement of specialized stones.")
        (rules "2x5 board. Five counters per hole. Each player has a larger stone, which they place in any hole on their side of the board. Sowing proceeds in either direction, the first player chooses which and all sowing for the rest of the game proceeds in that direction. Players may sow from any hole on the board.  If the last counter falls into a hole with counters, the counters in the next hole are picked up and sowing continues, and if this hole is empty, the counters in the hole following the empty hole are captured. If there is an unbroken sequence of alternating empty and occupied holes, the counters in the occupied holes are captured until there are two empty or two occupied holes. If the final counter falls into an empty hole the turn ends. Also, if a large stone is dropped into an occupied hole during sowing, the contents of that hole are captured by the owner of the large stone, even if it is not the current player. If a player captures the other player's large stone, it is kept by the capturing player in subsequent rounds until it is captured. The game ends when one player's holes are empty, the opponent claiming the remainder of the counters. The players then attempt to fill all of the holes on their side of the board with either five counters or the large stone. Both large stones must be used to fill holes. Any holes that cannot be filled with five counters or a large stone are left empty, but may be sown into and played from as normal. Play continues in this fashion until one player cannot fill any holes. ")
        (source "Eagle 1995: 56.")
        (id "207")
        (version "1.3.3")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Yunnan, around 1995.")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (piece Scale "Ball" 1.3)
        (player Colour P1 (colour White))
        (player Colour P2 (colour White))
        (stackType Count)
    })
)
