(define "Columns" 8)
(define "OppositePit" (if (is In (to) (sites Bottom)) (+ (to) "Columns") (- (to) "Columns")) )
(define "PlayableSites" (sites (values Remembered "Playable")))
(define "NoPieceOnBoard" (no Pieces All in:(union (sites Top) (sites Bottom))))
(define "Stores" (sites {(mapEntry P1) (mapEntry P2)}))

//------------------------------------------------------------------------------

(game "Ovalhu"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 "Columns"
            {
            (track "Track1" "1,E,17,16,W" loop:True P1)
            (track "Track2" "1,E,N,W,0" loop:True P2)
            }
        )
        (piece "Seed" Shared)
        (map {(pair P1 LastSite) (pair P2 FirstSite)})  // kalahs (storage pits)
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
    })  
    (rules 
        
        (start {
            (set RememberValue "Playable" (union (sites Top) (sites Bottom)))
            (set Count 8 to:(union (sites Bottom) (sites Top))) 
        })
        
        phases:{
        (phase "Sowing"
            (play
                (or {
                    (move Select
                        (from 
                            (if (and (is Pending) (is Mover Prev))
                                (sites {(var "Replay")})
                                (sites Mover) 
                            )
                            if:(and (is Occupied (from)) (is In (from) ("PlayableSites")))
                        )
                        (then
                            (sow
                                "Track"
                                owner:(mover)
                                apply:(if (or (is In (to) ("Stores")) (< 1 (count at:(to))))
                                    (and
                                        (moveAgain)
                                        (if (not (is In (to) ("Stores")))
                                            (and
                                                (set Pending)
                                                (set Var "Replay" (to))
                                            )
                                        )
                                    )
                                    (if (is Occupied ("OppositePit"))
                                        (fromTo
                                            (from ("OppositePit"))
                                            (to (mapEntry Mover))
                                            count:(count at:("OppositePit"))
                                        )
                                    )
                                )
                                skipIf:(and (not (is In (to) ("Stores"))) (not (is In (to) ("PlayableSites"))))
                            )
                        )
                    )
                    }
                    (then
                        (if ("NoPieceOnBoard")
                            (forget Value "Playable" All)
                        )
                    )
                )
            )
            
            (end (if ("NoPieceOnBoard")
                    {
                    (if (> 8 (count at:(mapEntry P1))) (result P2 Win))
                    (if (> 8 (count at:(mapEntry P2))) (result P1 Win))
                    }
                )
            )
            
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (not (all Sites (sites Mover) if:(is Occupied (site))))
                    (if (<= 8 (count at:(mapEntry Mover)))
                        (move
                            (from (mapEntry Mover))
                            (to (sites Mover) if:(is Empty (to)))
                            count:8
                            (then (remember Value "Playable" (last To)))
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        
        }
        
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Ovalhu is a two-row mancala-style board game played in the Maldives.")
        (rules "2x8 board, with a store on either end. Each player owns the store to their right. Eight counters in each hole. Players alternate turns sowing in an anti-clockwise direction from one of the holes on their side of the board. Sowing always includes the player's store (but not the opponent's store), and sowing cannot begin from the store. When the final counter lands in an occupied hole, the contents of that hole are picked up and sowing continues. If the last counter lands in the player's store, they may choose any of the holes on their side of the board and continue sowing from it. When the final counter lands in an empty hole, the counters in the opposite hole are captured. Play continues until all of the counters are captured. A new round begins. Players fill the holes on their side of the board with their captured counters. If a player cannot fill a hole with the required number of counters, the hole is closed and is out of play for that round. Play continues as before. Play continues in successive rounds until one player closes all of their opponent's holes and wins. ")
        (source "de Voogt 2000: 178-179.")
        (id "1254")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Maldives, around 2000.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)

