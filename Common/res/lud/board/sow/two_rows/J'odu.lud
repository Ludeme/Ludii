(define "Columns" 6)

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "J'odu"
    (players 2)
    (equipment {
        (mancalaBoard 2 "Columns"
            (track "Track" "1,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
        (piece "Seed" Shared)
    })

    (rules
        (start (set Count 4 to:(sites Track)))
        (play
            (do
                (move
                    Select
                    (from
                        (if
                            (is Mover Prev)
                            "PlayFromLastHole"
                            (sites Mover)
                        )
                        if:(< 0 (count at:(from)))
                    )
                    (then
                        (sow
                            apply:(if (> (count at:(to)) 1)
                                (moveAgain)
                                (if (is In (to) (sites Mover))
                                    (fromTo
                                        (from "OppositePit")
                                        (to (mapEntry (mover)))
                                        count:(count at:"OppositePit")
                                    )
                                )
                            )
                        )
                    )
                )
                ifAfterwards:(< 0 (count in:(if (is Mover P1) (sites P2) (sites P1))))
            )
        )
        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1))
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "J'odu is a two-row mancala-style board game played by the Yoruba people of Nigeria and also in Ghana. It is closely related to other games similar to Oware, but the method of capture is slightly different.")
        (aliases {"Jodorakase"})
        (rules "2x6 board, which two stores. Four counters in each hole. Sowing proceeds in an anti-clockwise direction. When the final counter of a sowing falls in to an occupied hole, these counters are picked up and sowing continues. When the final hole lands in an empty hole, if the hole is in the player's row, the contents of the opponent's opposite hole are captured. If the empty hole is in the opponent's row, the turn ends. A player must play so that the opponent is able to play on their next turn, if possible. The player who captures the most counters wins. ")
        (source "Murray 1951: 181.")
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in West Africa, around 1951.")
        }
    )

    (graphics {
        (board Style Mancala)
    })

    (ai
        "J'odu_ai"
    )
)
