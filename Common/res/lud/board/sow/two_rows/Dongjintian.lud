(define "PiecesOwnedBy" (count Cell at:(handSite #1)))
(define "LastHoleSowed" (last To afterConsequence:True))
(define "NextHoleFrom" (trackSite Move from:#1 #2 #3))
(define "CaptureMove"
    (and
        (fromTo 
            (from ("NextHoleFrom" (to) #1 steps:2))
            (to (handSite Mover))
            count:(count at:("NextHoleFrom" (to) #1 steps:2))
        )
        (if 
            (and
                (is Empty ("NextHoleFrom" (to) #1 steps:3))	
                (is Occupied ("NextHoleFrom" (to) #1 steps:4))		
            )
            (and
                (fromTo 
                    (from ("NextHoleFrom" (to) #1 steps:4))
                    (to (handSite Mover))
                    count:(count at:("NextHoleFrom" (to) #1 steps:4))
                )
                (if 
                    (and
                        (is Empty ("NextHoleFrom" (to) #1 steps:5))	
                        (is Occupied ("NextHoleFrom" (to) #1 steps:6))		
                    )
                    (and
                        (fromTo 
                            (from ("NextHoleFrom" (to) #1 steps:6))
                            (to (handSite Mover))
                            count:(count at:("NextHoleFrom" (to) #1 steps:6))
                        )
                        (if 
                            (and
                                (is Empty ("NextHoleFrom" (to) #1 steps:7))	
                                (is Occupied ("NextHoleFrom" (to) #1 steps:8))		
                            )
                            (fromTo 
                                (from ("NextHoleFrom" (to) #1 steps:8))
                                (to (handSite Mover))
                                count:(count at:("NextHoleFrom" (to) #1 steps:8))
                            )
                        )
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Dongjintian"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 5 store:None
            {
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "4,W,N,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
    })  
    (rules 
        
        (start (set Count 5 to:(sites Track)))
        
        (play 
            (or
                (if (or (>= 0 (var "Direction")) (= (var "Direction") 1))
                    (move Select
                        (from 
                            (if 
                                ("SameTurn") 
                                (sites {("NextHoleFrom" "LastHoleSowed" "TrackCCW" steps:1) })
                                (sites Mover) 
                            ) 
                            if:(is Occupied (from))
                        )
                        (then
                            (sow
                                "TrackCCW"
                                apply:(if (is Occupied ("NextHoleFrom" (to) "TrackCCW" steps:1))
                                    (and
                                        (moveAgain)
                                        (set Var "Direction" 1)
                                    )
                                    (if 
                                        (and
                                            (is Empty ("NextHoleFrom" (to) "TrackCCW" steps:1))	
                                            (is Occupied ("NextHoleFrom" (to) "TrackCCW" steps:2))		
                                        )
                                        (and
                                            ("CaptureMove" "TrackCCW")	
                                            (set Var "Direction" 0)
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
                (if (or (>= 0 (var "Direction")) (= (var "Direction") 2))
                    (move Select
                        (from 
                            (if 
                                ("SameTurn") 
                                (sites {("NextHoleFrom" "LastHoleSowed" "TrackCW" steps:1) })
                                (sites Mover) 
                            ) 
                            if:(is Occupied (from))
                        )
                        (then
                            (sow
                                "TrackCW"
                                apply:(if (is Occupied ("NextHoleFrom" (to) "TrackCW" steps:1))
                                    (and
                                        (moveAgain)
                                        (set Var "Direction" 2)
                                    )
                                    (if 
                                        (and
                                            (is Empty ("NextHoleFrom" (to) "TrackCW" steps:1))	
                                            (is Occupied ("NextHoleFrom" (to) "TrackCW" steps:2))		
                                        )
                                        (and
                                            ("CaptureMove" "TrackCW")	
                                            (set Var "Direction" 0)
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )

        (end ("MancalaByScoreWhen" (<= (count Pieces All in:(sites Board)) 1)))
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Dongjintian is a two-row mancala-style board game played in China. It was described to Vernon Eagle by Wang Hongbin. ")
        (rules "2x5 board. Five counters per hole. Sowing can happen in either direction. When the last counter falls into a hole, sowing continues if there are counters in the following holes; these are picked up and sowing continues from there. When the hole after the end of a sowing is empty, the counters in the next hole following it are captured. If a pattern of alternating empty and occupied holes continues after this, the counters in the occupied holes are captured, until there are two occupied or two empty holes in a row. The player who captures the most counters wins.")
        (source "Eagle 1995: 57-58.")
        (id "209")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in China, around 1994.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
