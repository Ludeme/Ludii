(define "PiecesOwnedBy" (+ (count in:(sites #1)) (count at:(mapEntry #1))))

(define "PiecesHandOwnedBy" (+ (count in:(sites #1)) (count Cell at:(handSite #1))))

(define "NextHole" ("NextSiteOnTrack" 1 from:(to) #1))

//------------------------------------------------------------------------------

(game "Kpo" 
    (players <Players:num>) 
    
    (equipment { 
        (mancalaBoard 2 6
            <Players:track>
        )
        (piece "Seed" Shared)
        <Players:homes>
        <Players:store> // kalahs (storage pits)
    }) 
    (rules 
        
        (start (set Count <Players:init> to:(sites Track)))
        
        <Players:Play>
        
        <Players:end>
    )
)

//------------------------------------------------------------------------------

(option "Players" <Players> args:{ <num> <store> <homes> <track> <init> <Play> <end>}
    {
    (item "2"  
        <2> 
        <(map {(pair P1 FirstSite) (pair P2 LastSite)})>
        <
        (regions P1 (sites Bottom))          
        (regions P2 (sites Top))             
        >
        <
        (track "Track" "1,E,N,W" loop:True)
        >
        <4>
        <
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        apply:(if (or 
                                (= (count at:(to)) 2)
                                (= (count at:(to)) 3)
                            )
                            (fromTo 
                                (from (to))
                                (to (mapEntry Mover)) 
                                count:(count at:(to))
                            ) 
                        )
                    )
                )
            )			
        )
        >
        <
        (end ("MancalaByScoreWhen" (no Moves Next)))
        >
        "The game is played with 2 players."
    )*  
    
    (item "3"  
        <3> 
        <(hand Each)>
        <
        (regions P1 (sites {"B1" "C1" "B2" "C2"}))          
        (regions P2 (sites {"D1" "E1" "D2" "E2"}))           
        (regions P3 (sites {"F1" "G1" "F2" "G2"}))          
        >
        <
        {
        (track "TrackCCW" "1,E,N,W" loop:True)
        (track "TrackCW" "6,W,N,E" loop:True)
        }
        >
        <3>
        <
        (play 
            (or
                (move
                    Select 
                    (from 
                        (sites Mover) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            apply:(if (and 
                                    (or 
                                        (= (count at:(to)) 2)
                                        (= (count at:(to)) 3)
                                    )
                                    (!= (count at:("NextHole" "TrackCCW")) 0)
                                )
                                (fromTo 
                                    (from ("NextHole" "TrackCCW"))
                                    (to (handSite Mover)) 
                                    count:(count at:("NextHole" "TrackCCW"))
                                ) 
                            )
                        )
                    )
                )		
                (move
                    Select 
                    (from 
                        (sites Mover) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "TrackCW"
                            apply:(if (and 
                                    (or 
                                        (= (count at:(to)) 2)
                                        (= (count at:(to)) 3)
                                    )
                                    (!= (count at:("NextHole" "TrackCW")) 0)
                                )
                                (fromTo 
                                    (from ("NextHole" "TrackCW"))
                                    (to (handSite Mover)) 
                                    count:(count at:("NextHole" "TrackCW"))
                                ) 
                            )
                        )
                    )
                )	
            )
        )
        >
        <
        (end
            (if (or { 
                    (and (no Moves P1) (no Moves P2))
                    (and (no Moves P1) (no Moves P3))
                    (and (no Moves P2) (no Moves P3))
                })
                (byScore {
                    (score P1 ("PiecesHandOwnedBy" P1)) 
                    (score P2 ("PiecesHandOwnedBy" P2))
                    (score P3 ("PiecesHandOwnedBy" P3)) 
                })
            )
        )
        >
        "The game is played with 4 players."
    ) 
    
    (item "4"  
        <4> 
        <(hand Each)>
        <
        (regions P1 (sites {"B1" "C1" "D1"}))          
        (regions P2 (sites {"E1" "F1" "G1"}))           
        (regions P3 (sites {"G2" "F2" "E2"}))          
        (regions P4 (sites {"B2" "C2" "D2"}))            
        >
        <
        {
        (track "TrackCCW" "1,E,N,W" loop:True)
        (track "TrackCW" "6,W,N,E" loop:True)
        }
        >
        <3>
        <
        (play 
            (or
                (move
                    Select 
                    (from 
                        (sites Mover) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "TrackCCW"
                            apply:(if (and 
                                    (or 
                                        (= (count at:(to)) 2)
                                        (= (count at:(to)) 3)
                                    )
                                    (!= (count at:("NextHole" "TrackCCW")) 0)
                                )
                                (fromTo 
                                    (from ("NextHole" "TrackCCW"))
                                    (to (handSite Mover)) 
                                    count:(count at:("NextHole" "TrackCCW"))
                                ) 
                            )
                        )
                    )
                )		
                (move
                    Select 
                    (from 
                        (sites Mover) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            "TrackCW"
                            apply:(if (and 
                                    (or 
                                        (= (count at:(to)) 2)
                                        (= (count at:(to)) 3)
                                    )
                                    (!= (count at:("NextHole" "TrackCW")) 0)
                                )
                                (fromTo 
                                    (from ("NextHole" "TrackCW"))
                                    (to (handSite Mover)) 
                                    count:(count at:("NextHole" "TrackCW"))
                                ) 
                            )
                        )
                    )
                )	
            )
        )
        >
        <
        (end
            (if (or { 
                    (and {(no Moves P1) (no Moves P2) (no Moves P3)})
                    (and {(no Moves P1) (no Moves P3) (no Moves P4)})
                    (and {(no Moves P2) (no Moves P3) (no Moves P4)})
                })
                (byScore {
                    (score P1 ("PiecesHandOwnedBy" P1)) 
                    (score P2 ("PiecesHandOwnedBy" P2))
                    (score P3 ("PiecesHandOwnedBy" P3)) 
                    (score P4 ("PiecesHandOwnedBy" P4))
                })
            )
        )
        >
        "The game is played with 4 players."
    ) 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Momolu Massaquoi (Observed)" {
        "Players/2"
    })*
    
    (ruleset "Ruleset/Three players (Observed)" {
        "Players/3"
    })
    
    (ruleset "Ruleset/Four players (Described)" {
        "Players/4"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Kpo is a two-row mancala-style board game played by the Vei people in the late nineteenth century. It was described by Prince Momolu Massaquoi to Stewart Culin. It can be played by two, three, or four players.")
        (aliases {"Poo"})
        (useFor "Ruleset/Momolu Massaquoi (Observed)" (rules "Two rows of six holes with stores on either end. Four counters in each hole. Players sow in an anti-clockwise direction. When the last counter falls into a hole, and it now contains two or three counters, these are captured."))
        (useFor "Ruleset/Momolu Massaquoi (Observed)" (source "Culin 1896: 603-604."))
        (useFor "Ruleset/Momolu Massaquoi (Observed)" (id "280"))
        (useFor "Ruleset/Three players (Observed)" (rules "Three players. One player owns the first two holes in both rows, another the middle two holes in both rows, the third the last two holes in both rows. Three counters in each hole. Captures are made from the next hole in the direction of the sowing when the last counter causes a hole to contain two or three counters. Sowing may also occur in any direction."))
        (useFor "Ruleset/Three players (Observed)" (source "Culin 1896: 603-604."))
        (useFor "Ruleset/Three players (Observed)" (id "624"))
        (useFor "Ruleset/Four players (Described)" (rules "Four players. Three counters in each hole. Captures are made from the next hole in the direction of the sowing when the last counter causes a hole to contain two or three counters. Sowing may also occur in any direction."))
        (useFor "Ruleset/Four players (Described)" (source "Culin 1896: 603-604."))
        (useFor "Ruleset/Four players (Described)" (id "626"))
        (source "Silva 1995: 228.")
        (id "280")
        (version "1.3.5")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Kpo_ai"
    )
)
