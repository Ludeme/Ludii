(define "PiecesOwnedBy" (count Cell at:(handSite #1)))

//------------------------------------------------------------------------------

(game "Alitev" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 3 to:(sites Track)))
        
        (play 
            (move Select 
                (from 
                    (sites Mover) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        if:(or (= (count at:(to)) 1) (= (count at:(to)) 3))
                        apply:(fromTo (from (to)) 
                            (to (handSite Mover)) 
                            count:(count at:(to))
                        )
                        backtracking:True
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" ("NoPieceOnBoard")))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Alıtev is a two-row mancala-style board game played in Turkey. It is typically played in holes dug out of the earth.")
        (aliases {"Alıtev" "Mangala"})
        (rules "2x6 board. Three counters in each hole. Sowing occurs in an anti-clockwise direction from one of the holes in the player's row. If the final counter lands in a hole containing one or three counters, these are taken, as well as an unbroken sequence behind this containing one or three (after a counter has been placed there), these are taken. The game ends when all of the counters have been captured. The player who has captured the most counters wins.")
        (source "And 1979: 52.")
        (id "285")
        (version "1.3.5")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Alitev_ai"
    )
)
