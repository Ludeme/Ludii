(define "Columns" 6)

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "NoFreeSeed"
    (no Pieces All in:(forEach (sites Board) if:(= 0 (state at:(site)))))
)

(define "PiecesOwnedBy"
    (+
        (count Cell at:(handSite #1))
        (count in:(forEach (sites Board) if:(= #1 (state at:(site)))))
    )
)

//------------------------------------------------------------------------------

(game "Gamacha (Nobility)"
    (players 2)

    (equipment {
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
        (hand Each)
        }
    )
    (rules

        (start (set Count 4 to:(sites Track) ) )

        (play
            (move Select
                (from
                    (if (is Mover Prev)
                        ("PlayFromLastHole")
                        (sites Mover)
                    )
                    if:(and (< 0 (count at:(from))) (= 0 (state at:(from))))
                )
                (then
                    (sow
                        apply:(if (and
                                (is In (to) (sites Mover))
                                (= 1 (count at:(to)))
                            )
                            (if (= 1 (count at:("OppositePit")))
                                (fromTo
                                    (from ("OppositePit"))
                                    (to (handSite Mover))
                                    count:1
                                )
                            )
                            (if (< 1 (count at:(to)))
                                (and
                                    (moveAgain)
                                    (if (and { (is In (to) (sites Next)) (= 0 (state at:(to))) (= 4 (count at:(to))) })
                                        (set State at:(to) (mover))
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )

        (end
            (if  ("NoFreeSeed")
                (byScore {
                    (score P1 ("PiecesOwnedBy" 1))
                    (score P2 ("PiecesOwnedBy" 2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Gamacha is a term used by Hadiya and Kambati people of Ethiopia for mancala-style games. This is a two-row version that was typically played by male nobility of the Hadiya people.")
        (rules "2x6 board. Four counters in each hole. Sowing occurs in an anti-clockwise direction. When the final counter of a sowing lands in an empty hole on the player's own row, the contents of the opponent's opposite hole are captured only if it contains one counter. If the final counter lands in an occupied hole, the contents of it are picked up and sowing continues. If the final counter lands in a hole in the opponent's row, causing it to contain four counters, the hole becomes a qasamo, and it can no longer be sown from. Play continues until all of the counters are captured or are in a qasamo. The player with the most counters captured and in all qasamo belonging to them wins.")
        (source "Pankhurst 1971: 185.")
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Ethiopia, around 1971.")
        }
    )

    (graphics {
        (board Style Mancala)
        (show LocalStateHoles)
    })
)

