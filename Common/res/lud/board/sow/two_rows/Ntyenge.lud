(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Ntyenge" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 8 store:None
            (track "Track" "0,E,N,W" loop:True)		
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom)) 
        (regions P2 (sites Top)) 
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 4 to:(sites Track)))
        
        (play 
            (move Select
                (from (sites Mover) if:(> (count at:(from)) 0))
                (then
                    (sow
                        apply:(if (or (= 2 (count at:(to))) (= 4 (count at:(to))))
                            (fromTo
                                (from (to))
                                (to (handSite Mover))
                                count:(count at:(to))
                            )
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" ("NoPieceOnBoard")))
    )
)

//------------------------------------------------------------------------------

(metadata 
(info
{
(description "Ntyéngé is a two-row mancala-style board game played by the Mbenge people in Gabon. It is sometimes played on a board, but more frequently is made in the ground and played with seeds or pebbles.")
(aliases {"Ntyéngé"})
(rules "2x8 board. Four counters per hole. Players alternate turns taking the counters from one of the holes in their row and sowing them in an anticlockwise direction. When the final counter falls into a hole containing one or three counters, causing it to contain two or four, these counters are captured. Play continues until all of the counters have been captured. The player who captured the most counters wins.")
(source "Trilles 1932: 399-400.")
(id "1346")
(version "1.3.7")
(classification "board/sow/two rows")
(credit "Eric Piette")
(origin "This game was played in Central Africa, from around 1899 to 1907.")
}
)
    
    (graphics {
        (board Style Mancala)
    })
)

