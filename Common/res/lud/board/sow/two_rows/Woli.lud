(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Woli" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))           // P1 home
        (regions P2 (sites Top))            // P2 home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    
    (rules 
        (start (set Count 6 to:(sites Track)))		 
        (play 
            (if (is Proposed "End")
                (or (move Vote "End") (move Vote "No"))
                (or
                    (if (is Cycle) (move Propose "End")) 
                    (do 
                        (move Select 
                            (from 
                                (sites Mover) 
                                if:(< 0 (count at:(from)))
                            )
                            (then 
                                (sow 
                                    if:(and 
                                        (is In (to) (sites Next)) 
                                        (or 
                                            (= (count at:(to)) 2) 
                                            (= (count at:(to)) 3)
                                        )
                                    ) 
                                    apply:(fromTo 
                                        (from (to)) 
                                        (to (handSite Mover)) 
                                        count:(count at:(to))
                                    )
                                    includeSelf:False
                                    backtracking:True
                                )
                            )
                        )
                        ifAfterwards:(< 0 (count in:(sites Next)))
                    )
                )
            )
        )
        (end ("MancalaByScoreWhen" (or (no Moves Mover) (is Decided "End"))))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Woli is a two-row mancala-style game played in the Senegal Valley of West Africa. It was popular among young married women.")
        (rules "2x6 board. Six counters in each hole. A player picks up all of the counters in one of the holes in their row and sows them one-by-one in a clockwise direction in consecutive holes from the hole the pieces originated. The starting hole is always left empty, even if a player sows in a complete circuit of the board, the original house is skipped and sowing continues in the next hole after it. Players capture counters when the final counter is sown in the opponent's row and the hole containing it has two or three counters (counting the counter just dropped into it). If the hole before it also has two or three counters, these are also captured and so on until reaching a hole without two or three counters or one not belonging to the opponent. A move which would capture all of the opponent's counters is not allowed. If an opponent's holes are all empty, the other player must make a move placing counters in the opponent's row. If not possible, the player captures all the counters in their row. The player who has captured the most counters wins. If the game continues in a repeating loop, the players can agree to end the game and capture the counters remaining in their row.")
        (source "BÃ©art 1955: 512.")
        (id "423")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in West Africa, around 1955.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Woli_ai"
    )
)
