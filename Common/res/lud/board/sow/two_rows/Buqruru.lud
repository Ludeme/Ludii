(define "PlayableSites" (sites (values Remembered "Playable")))
(define "OneOrLessPieceOnBoard" (> 2 (count in:(union (sites Top) (sites Bottom)))))

//------------------------------------------------------------------------------

(game "Buqruru" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 4 store:None
            (track "Track" <Direction> loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
    }) 
    (rules 
        
        (start {
            (set Count 6 to:(sites Track)) 
            (set RememberValue "Playable" (sites Board))
        })
        
        phases:{
        (phase "SelectingHole"
            (play
                (move Select 
                    (from 
                        (sites Mover)
                        if:(is In (from) ("PlayableSites"))			
                    )
                    (then (set Value Mover (last To)))
                )
            )
            (nextPhase Mover "Sowing")
        )
        
        (phase "Sowing"
            (play
                (if (is Occupied (value Player Mover))
                    (or {
                        (move Select
                            (from 
                                (value Player Mover)
                                if:(is In (from) ("PlayableSites"))	
                            )
                            (then
                                (sow 
                                    numPerHole:1
                                    sowEffect:(if (and 
                                            (is In (to) (sites Next))
                                            (or { 
                                                (= 1 (count at:(to)))
                                                (= 3 (count at:(to)))
                                                (= 5 (count at:(to)))
                                                (= 7 (count at:(to)))
                                            })
                                        )
                                        (fromTo
                                            (from (to))
                                            (to (handSite Mover))
                                            count:(count at:(to))
                                        )
                                    )
                                    apply:(if (or { 
                                            (= 2 (count at:(to)))
                                            (= 4 (count at:(to)))
                                            (= 6 (count at:(to)))
                                            (= 8 (count at:(to)))
                                        })
                                        (fromTo
                                            (from (to))
                                            (to (handSite Mover))
                                            count:(count at:(to))
                                        )
                                    )
                                    skipIf:(not (is In (to) ("PlayableSites")))
                                    backtracking:True
                                )
                            )
                        )
                        (if (= 0 (% (count at:(value Player Mover)) 2))
                            (move Select
                                (from (value Player Mover))
                                (then
                                    (sow 
                                        numPerHole:2
                                        sowEffect:(if (and 
                                                (is In (to) (sites Next))
                                                (or { 
                                                    (= 0 (count at:(to)))
                                                    (= 2 (count at:(to)))
                                                    (= 4 (count at:(to)))
                                                    (= 6 (count at:(to)))
                                                })
                                            )
                                            (fromTo
                                                (from (to))
                                                (to (handSite Mover))
                                                count:(count at:(to))
                                            )
                                        )
                                        apply:(if (or { 
                                                (= 2 (count at:(to)))
                                                (= 4 (count at:(to)))
                                                (= 6 (count at:(to)))
                                                (= 8 (count at:(to)))
                                            })
                                            (fromTo
                                                (from (to))
                                                (to (handSite Mover))
                                                count:(count at:(to))
                                            )
                                        )
                                        skipIf:(not (is In (to) ("PlayableSites")))
                                        backtracking:True
                                    )
                                )
                            )
                        )
                        (if (= 0 (% (count at:(value Player Mover)) 3))
                            (move Select
                                (from (value Player Mover))
                                (then
                                    (sow 
                                        numPerHole:3
                                        sowEffect:(if (and 
                                                (is In (to) (sites Next))
                                                (or { 
                                                    (= 1 (count at:(to)))
                                                    (= 3 (count at:(to)))
                                                    (= 5 (count at:(to)))
                                                })
                                            )
                                            (fromTo
                                                (from (to))
                                                (to (handSite Mover))
                                                count:(count at:(to))
                                            )
                                        )
                                        apply:(if (or { 
                                                (= 2 (count at:(to)))
                                                (= 4 (count at:(to)))
                                                (= 6 (count at:(to)))
                                                (= 8 (count at:(to)))
                                            })
                                            (fromTo
                                                (from (to))
                                                (to (handSite Mover))
                                                count:(count at:(to))
                                            )
                                        )
                                        skipIf:(not (is In (to) ("PlayableSites")))
                                        backtracking:True
                                    )
                                )
                            )
                        )
                        (if (= 0 (% (count at:(value Player Mover)) 4))
                            (move Select
                                (from (value Player Mover))
                                (then
                                    (sow 
                                        numPerHole:4
                                        sowEffect:(if (and 
                                                (is In (to) (sites Next))
                                                (or { 
                                                    (= 0 (count at:(to)))
                                                    (= 2 (count at:(to)))
                                                    (= 4 (count at:(to)))
                                                })
                                            )
                                            (fromTo
                                                (from (to))
                                                (to (handSite Mover))
                                                count:(count at:(to))
                                            )
                                        )
                                        apply:(if (or { 
                                                (= 2 (count at:(to)))
                                                (= 4 (count at:(to)))
                                                (= 6 (count at:(to)))
                                                (= 8 (count at:(to)))
                                            })
                                            (fromTo
                                                (from (to))
                                                (to (handSite Mover))
                                                count:(count at:(to))
                                            )
                                        )
                                        skipIf:(not (is In (to) ("PlayableSites")))
                                        backtracking:True
                                    )
                                )
                            )
                        )
                    })
                    
                    (then
                        (if ("OneOrLessPieceOnBoard")
                            (and { 
                                (forEach Site (sites P1)
                                    (if (is Occupied (site))
                                        (fromTo
                                            (from (site))
                                            (to (handSite P1))
                                            count:(count at:(site))
                                        )
                                    )
                                )
                                (forEach Site (sites P2)
                                    (if (is Occupied (site))
                                        (fromTo
                                            (from (site))
                                            (to (handSite P2))
                                            count:(count at:(site))
                                        )
                                    )
                                )
                                (forget Value "Playable" All)
                            })
                        )	
                    )
                )
            )
            (end {
                (if (and (= 0 (count at:(value Player P1))) (= 0 (count at:(value Player P2)))) (result Mover Draw))
                (if ("OneOrLessPieceOnBoard")
                    {
                    (if (= 0 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (= 0 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            })
            
            (nextPhase ("OneOrLessPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (not (all Sites (sites Mover) if:(is Occupied (site))))
                    (if (<= 6 (count at:(handSite Mover)))
                        (move
                            (from (handSite Mover))
                            (to (sites Mover))
                            count:6
                            (then (remember Value "Playable" (last To)))
                        )
                        (move
                            (from (handSite Mover))
                            (to (sites Mover))
                            count:(count at:(handSite Mover))
                            (then (remember Value "Playable" (last To)))
                        )
                    )
                )
            )
            (nextPhase (all Passed) "SelectingHole")
        )
        }
        
    )
)

//------------------------------------------------------------------------------

(option "Direction" <Direction> args:{ <choice> }
    {
    (item "CCW" <"0,E,N,W"> "The game is played counter clockwise.")* 
    (item "CW" <"3,W,N,E"> "The game is played clockwise.") 
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Buqruru is a two-row mancala-style board game played in the Tidikelt and Haggar regions of Algeria. It is typically made out of holes in the sand, and camel droppings are used as pieces.")
        (aliases {"Azid"})
        (rules "2x4 board. Six counters in each hole. At the beginning of the game, each player chooses one hole to be their sowing hole, from which each move on their turn must begin. The players agree on a direction of play, which is maintained throughout the game. Players alternate turns sowing from their sowing hole. The players may choose to sow one-by-one, two-by-two, three-by-three, four-by-four, and so forth, into the following holes. When the final counter lands in a hole causing it to contain two, four, six, or eight counters, the contents of that hole are captured. If the preceding hole also contains two, four, six, or eight counters, these are also captured, proceeding until there is a no longer a hole with two, four, six, or eight counters. If at the end of a turn a player sowed into the opponent's row and any of the opponent's holes contain two, four, six, or eight counters, providing the player sowed into them, the opponent captures these counters. In a players own row, if either player sows into a hole with an odd number of counters, making it an even number of counters, the player who owns that row captures the counters. If a player's sowing hole is empty at the beginning of their turn, they pass. Play continues until all of the counters are captured, or there is only one left on the board. The single counter is captured by the player in whose row it is located. 
            
        A new round begins. Players fill up as many holes as they can with six counters. If a player cannot, they fill as many as they can with six counters, and place the remainder in a hole, either between two with six counters or after them, and erases any empty holes. Play continues as before. When one player has erased all of their holes, the opponent wins.")
        (source "Bellin 1964: 77-78.")
        (id "1580")
        (version "1.3.5")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Algeria, around 1964.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
