(define "NextHole" (trackSite Move from:#1 #2 steps:#3))
(define "NumBigPiecesCaptured" (count Cell in:(sites {(handSite P1 1) (handSite P2 1)})))

(define "LeftMostEmpty" 
    (trackSite FirstSite 
        "TrackCCW"
        from:(mapEntry "LeftMost" Mover)
        if:(is Empty (to))
    )
)

(define "NoPieceOnBoard" (no Pieces All in:(sites Board)))

(define "NoPieceOnOwnedHoles"  (no Pieces All in:(sites #1)))

//------------------------------------------------------------------------------

(game "Yucebao"
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 5 largeStack:True
            {
            (track "TrackCCW" "0,1,E,11,10,W" loop:True)
            (track "TrackCW" "11,5,W,0,6,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (piece "Ball" P1)
        (piece "Ball" P2)
        (hand Each size:2)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))         // P2 home
        (map {(pair P1 LastSite) (pair P2 FirstSite)})  // kalah holes
        (map "LeftMost" {(pair P1 1) (pair P2 10)})  // leftMost holes
        (regions "EndHoles" (sites {"A1" "G1"}))
    })  
    (rules 
        (start {
            (place Stack "Seed" (union (sites Bottom) (sites Top)) count:5)
            (place Stack "Ball1" (mapEntry P1))
            (place Stack "Ball2" (mapEntry P2))
        })
        
        phases:{
        (phase "Sowing"
            (play
                (or {
                    (if (!= (var "Direction") 2)
                        (move Select
                            (from 
                                (if ("SameTurn")
                                    (sites {(var "Replay")})
                                    (sites Mover) 
                                )
                                if:(!= 0 (size Stack at:(from)))
                            )
                            (then
                                (and {
                                    (forEach Value min:1 max:(size Stack at:(last From))
                                        (if (= (id "Seed" Shared) (what at:(last From) level:(- (size Stack at:(last From)) (value))))
                                            (fromTo
                                                (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                (to ("NextHole" (last From) "TrackCCW" (value)))
                                                stack:True
                                            )
                                            (and
                                                (if (and (is Occupied ("NextHole" (last From) "TrackCCW" (value))) (!= ("NextHole" (last From) "TrackCCW" (value)) (last From)))
                                                    (if (= (who at:(last From) level:(- (size Stack at:(last From)) (value))) P1)
                                                        (forEach Level ("NextHole" (last From) "TrackCCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P1))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P1 1))
                                                                )
                                                            )
                                                        )
                                                        (forEach Level ("NextHole" (last From) "TrackCCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P2))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCCW" (value)) level:(level))
                                                                    (to (handSite P2 1))
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                                (fromTo
                                                    (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                    (to ("NextHole" (last From) "TrackCCW" (value)))
                                                    stack:True
                                                )
                                            )
                                        )
                                    )
                                    (if (< (var "Direction") 1) (set Var "Direction" 1))
                                    (if (< (size Stack at:(last From)) 8)
                                        (if (!= 0 (size Stack at:("NextHole" (last From) "TrackCCW" (+ 1 (size Stack at:(last From))))))
                                            (and 
                                                (moveAgain)
                                                (set Var "Replay" ("NextHole" (last From) "TrackCCW" (+ 1 (size Stack at:(last From)))))
                                            )
                                            (if (is Occupied ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))))
                                                (forEach Level ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) FromTop
                                                    (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) level:(level)))
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover))
                                                        )
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover 1))
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                })
                            )
                        )
                    )
                    (if (!= (var "Direction") 1)
                        (move Select
                            (from 
                                (if ("SameTurn")
                                    (sites {(var "Replay")})
                                    (sites Mover) 
                                ) 
                                if:(!= 0 (size Stack at:(from)))
                            )
                            (then
                                (and {
                                    (forEach Value min:1 max:(size Stack at:(last From))
                                        (if (= (id "Seed" Shared) (what at:(last From) level:(- (size Stack at:(last From)) (value))))
                                            (fromTo
                                                (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                (to ("NextHole" (last From) "TrackCW" (value)))
                                                stack:True
                                            )
                                            (and
                                                (if (and (is Occupied ("NextHole" (last From) "TrackCW" (value))) (!= ("NextHole" (last From) "TrackCW" (value)) (last From)))
                                                    (if (= (who at:(last From) level:(- (size Stack at:(last From)) (value))) P1)
                                                        (forEach Level ("NextHole" (last From) "TrackCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P1))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P1 1))
                                                                )
                                                            )
                                                        )
                                                        (forEach Level ("NextHole" (last From) "TrackCW" (value)) FromTop
                                                            (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCW" (value)) level:(level)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P2))
                                                                )
                                                                (fromTo
                                                                    (from ("NextHole" (last From) "TrackCW" (value)) level:(level))
                                                                    (to (handSite P2 1))
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                                (fromTo
                                                    (from (last From) level:(- (size Stack at:(last From)) (value)))
                                                    (to ("NextHole" (last From) "TrackCW" (value)))
                                                    stack:True
                                                )
                                            )
                                        )
                                    )
                                    (if (< (var "Direction") 1) (set Var "Direction" 2))
                                    (if (< (size Stack at:(last From)) 8)
                                        (if (!= 0 (size Stack at: ("NextHole" (last From) "TrackCW" (+ 1 (size Stack at:(last From))))))
                                            (and 
                                                (moveAgain)
                                                (set Var "Replay" ("NextHole" (last From) "TrackCW" (+ 1 (size Stack at:(last From)))))
                                            )
                                            (if (is Occupied ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))))
                                                (forEach Level ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) FromTop
                                                    (if (= (id "Seed" Shared) (what at:("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) level:(level)))
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover))
                                                        )
                                                        (fromTo
                                                            (from ("NextHole" (last From) "TrackCW" (+ 2 (size Stack at:(last From)))) level:(level))
                                                            (to (handSite Mover 1))
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                })
                            )
                        )
                    )
                    }
                    (then
                        (if ("NoPieceOnOwnedHoles" P1)
                            (forEach Site
                                (sites Board)
                                (if (is Occupied (site))
                                    (forEach Level (site) FromTop
                                        (if (= (id "Seed" Shared) (what at:(site) level:(level)))
                                            (fromTo
                                                (from (site) level:(level))
                                                (to (handSite P1))
                                                stack:True
                                            )
                                            (fromTo
                                                (from (site) level:(level))
                                                (to (handSite P1 1))
                                                stack:True
                                            )
                                        )
                                    )
                                )
                            )
                            (if ("NoPieceOnOwnedHoles" P2)
                                (forEach Site
                                    (sites Board)
                                    (if (is Occupied (site))
                                        (forEach Level (site) FromTop
                                            (if (= (id "Seed" Shared) (what at:(site) level:(level)))
                                                (fromTo
                                                    (from (site) level:(level))
                                                    (to (handSite P2))
                                                    stack:True
                                                )
                                                (fromTo
                                                    (from (site) level:(level))
                                                    (to (handSite P2 1))
                                                    stack:True
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (end (if ("NoPieceOnBoard")
                    {
                    (if (> 5 (size Stack Cell at:(handSite P1)))
                        (result P2 Win)
                    )
                    (if (> 5 (size Stack Cell at:(handSite P2)))
                        (result P1 Win)
                    )
                    }
                )
            )
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play 
                (or
                    (if (<= 5 (size Stack Cell at:(handSite Mover)))
                        (move Select
                            (from Cell (handSite Mover))
                            (to ("LeftMostEmpty"))
                            (then
                                (forEach Value min:1 max:5
                                    (fromTo
                                        (from Cell (last From) level:(- (size Stack Cell at:(last From)) (value)))
                                        (to (last To))
                                        stack:True
                                    )
                                )
                            )
                        )
                    )
                    (if (!= 0 (size Stack Cell at:(handSite Mover 1)))
                        (move Select
                            (from Cell (handSite Mover 1))
                            (to (sites Mover))
                            (then
                                (fromTo
                                    (from Cell (last From) level:(topLevel Cell at:(last From)))
                                    (to (last To))
                                    stack:True
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Yucebao is a two-row mancala-style board game played by the Bai people of Yunnan, China. It is typically played by men and boys, and involves the use of special stones which effect captures in addition to the usual mancala-style capturing rules.")
        (rules "2x5 board, with two stores. Five counters in each hole, with a large stone normally in each store, but which may be placed in any of the player's holes to start. The player owns the store to their right. Sowing proceeds in either direction, the first player chooses which and all sowing for the rest of the game proceeds in that direction. Sowing includes the stores. Players may sow from any hole on the board. If the last counter falls into a hole with counters, the counters in the next hole are picked up and sowing continues, and if this hole is empty, the counters in the hole following the empty hole are captured. If there is an unbroken sequence of alternating empty and occupied holes, the counters in the occupied holes are captured until there are two empty or two occupied holes. When the larger stone is sown into a hole with counters, those counters are taken by the player to whom the hole belongs. When a player has no further counters, they may, if they wish, place one captured counter in each of their holes to continue playing, or may choose to end the round, and the opponent captures the remaining counters on the board. Another round begins: Both players fill as many of their holes as they can with five counters. The player with more counters may continue filling holes, if possible, in a clockwise direction from the end of their row. Any remaining counters are lent to the player with fewer counters to fill their holes. Play continues until one player can no longer fill any holes.
        ")
        (source "Eagle 1995: 55-56.")
        (id "206")
        (version "1.3.3")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Yunnan, China, around 1992.")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (piece Scale "Ball" 1.3)
        (player Colour P1 (colour White))
        (player Colour P2 (colour White))
        (stackType Count)
    })
)

