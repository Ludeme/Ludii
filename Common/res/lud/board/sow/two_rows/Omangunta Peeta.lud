(define "Columns" 7)
(define "PlayableSites" (sites (values Remembered "Playable")))
(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))
(define "OppositePit" (if (is In #1 (sites Bottom)) (+ #1 "Columns") (- #1 "Columns")))
(define "NoPiecesOnBoardExceptCentre" (all Sites (difference (sites Board) (sites Centre)) if:(= 0 (count at:(site)))))
(define "BottomCentre" 3)
(define "TopCentre" 10)
(define "SumCentreSites" (+ (count at:"BottomCentre") (count at:"TopCentre")))

//------------------------------------------------------------------------------

(game "Omangunta Peeta" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        
        (start {
            (set Count 6 to:(difference (sites Track) (sites Centre)))
            (set Count 1 to:(sites Centre))
            (set RememberValue "Playable" (union (sites Top) (sites Bottom)))
        })
        
        phases:{
        (phase "Sowing"
            (play 
                (or {
                    (move Select
                        (from
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (difference (sites Mover) (sites Centre))
                            )
                            if:(and (is Occupied (from)) (is In (from) ("PlayableSites")))
                        )
                        (then 
                            (sow
                                apply:(if (is Occupied ("NextHole" (to) 1))
                                    (and
                                        (moveAgain)
                                        (set Var "Replay" (to))
                                    )
                                    (if (is In ("NextHole" (to) 2) (sites Centre))
                                        (set Value Mover 1)
                                        (and 
                                            (if (is Occupied ("NextHole" (to) 2))
                                                (fromTo
                                                    (from ("NextHole" (to) 2))
                                                    (to (handSite Mover))
                                                    count:(count at:("NextHole" (to) 2))
                                                )
                                            )
                                            (if (is Occupied ("OppositePit" ("NextHole" (to) 2)))
                                                (fromTo
                                                    (from ("OppositePit" ("NextHole" (to) 2)))
                                                    (to (handSite Mover))
                                                    count:(count at:("OppositePit" ("NextHole" (to) 2)))
                                                )
                                            )
                                        )
                                    )
                                )
                                skipIf:(and (not (is In (to) (sites Centre))) (not (is In (to) ("PlayableSites"))))
                            )
                        )
                    )
                    }
                    (then (if ("NoPiecesOnBoardExceptCentre")
                            (and {
                                (forget Value "Playable" All)
                                (if (!= 0 ("SumCentreSites"))
                                    (if (and (= 1 (value Player P1)) (= 1 (value Player P2)))
                                        (if (is Even ("SumCentreSites"))
                                            (and {
                                                (fromTo 
                                                    (from ("BottomCentre"))
                                                    (to Cell (handSite P1))
                                                    count:(- (count at:("BottomCentre")) 1)
                                                )
                                                (fromTo 
                                                    (from ("TopCentre"))
                                                    (to Cell (handSite P1))
                                                    count:(- (count at:("TopCentre")) 1)
                                                )
                                                (fromTo 
                                                    (from Cell (handSite P1))
                                                    (to Cell (handSite P2))
                                                    count:(/ (- ("SumCentreSites") 2) 2)
                                                )
                                            })
                                            (and {
                                                (fromTo 
                                                    (from ("BottomCentre"))
                                                    (to Cell (handSite P1))
                                                    count:(- (count at:("BottomCentre")) 1)
                                                )
                                                (fromTo 
                                                    (from ("TopCentre"))
                                                    (to Cell (handSite P1))
                                                    count:(count at:("TopCentre"))
                                                )
                                                (fromTo 
                                                    (from Cell (handSite P1))
                                                    (to Cell (handSite P2))
                                                    count:(/ (- ("SumCentreSites") 1) 2)
                                                )
                                            })
                                        )
                                        (if (= 1 (value Player P1))
                                            (and
                                                (fromTo 
                                                    (from ("BottomCentre"))
                                                    (to Cell (handSite P1))
                                                    count:(- (count at:("BottomCentre")) 1)
                                                )
                                                (fromTo 
                                                    (from ("TopCentre"))
                                                    (to Cell (handSite P1))
                                                    count:(- (count at:("TopCentre")) 1)
                                                )
                                            )
                                            (if (= 1 (value Player P2))
                                                (and
                                                    (fromTo 
                                                        (from ("BottomCentre"))
                                                        (to Cell (handSite P2))
                                                        count:(- (count at:("BottomCentre")) 1)
                                                    )
                                                    (fromTo 
                                                        (from ("TopCentre"))
                                                        (to Cell (handSite P2))
                                                        count:(- (count at:("TopCentre")) 1)
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            })
                        )
                    )
                )
            )
            
            (end (if ("NoPiecesOnBoardExceptCentre")
                    {
                    (if (= 0 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (= 0 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
            
            (nextPhase ("NoPiecesOnBoardExceptCentre") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (not (all Sites (difference (sites Mover) (sites Centre)) if:(is Occupied (site))))
                    (if (<= 6 (count Cell at:(handSite Mover)))
                        (move
                            (from Cell (handSite Mover))
                            (to (difference (sites Mover) (sites Centre)) if:(is Empty (to)))
                            count:6
                            (then (remember Value "Playable" (last To)))
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Omangunta Peeta is a two-row mancala-style board game from southern India. It was played in the Madras Presidency during the early twentieth century.")
        (aliases {"Palanguli"})
        (rules "2x7 board. Six counters in each hole, except the central hole in each row, which contains one counter. Sowing occurs in an anti-clockwise direction. When the last counter is sown, the player picks up the counters in the next hole in the direction of the sowing and continues sowing with those counters. When the hole after the hole in which the final counter is sown is empty, the counters in the next hole are captured, as well as the counters in the hole opposite this hole, and the turn ends. Sowing cannot begin from the central hole of either row. When a capture would occur from one of these central holes, the counters are not captured until the end of the round. At the end of the round, if both players are eligible to capture from the hole, they split the counters, leaving one in the hole if it is an odd number, or two if it is an even number. If only one player captured from the holes, they leave one counter in each hole. If neither player made a capture from the hole, the counters remain in the hole. 
            
        When the players can no longer play, a new round begins. Each player then fills up as many holes, not counting the central holes, on their side of the board with six counters if possible. Any holes that do not contain counters are out of play. Play continues until one player captures all the counters, and is the winner.")
        (source "Das Gupta 1923: 73-74.")
        (id "920")
        (version "1.3.12")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1923.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Omangunta Peeta_ai"
    )
)
