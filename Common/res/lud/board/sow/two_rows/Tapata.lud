(define "Columns" 12)

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "LastHole" (last To afterConsequence:True))

(define "PlayFromLastHole" (sites {("LastHole")}))

(define "NoPiece" (= (count in:(sites Player "Home")) 0))

(define "NextHoleFrom" (trackSite Move from:#1 steps:#2))

//------------------------------------------------------------------------------

(game "Tapata"
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            (track "TrackCCW" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions "Home" P1 (sites Bottom))                                                     // P1 home
        (regions "Home" P2 (sites Top))                                                        // P2 home
        (regions "Right Half" P1 (intersection (sites Bottom) (expand (sites Right) steps:5))) // P1 right half
        (regions "Right Half" P2 (intersection (sites Top) (expand (sites Left) steps:5)))     // P2 right half
        (map {(pair P1 0) (pair P2 23)})                                                       // First holes
        }
    )  
    (rules 
        (start (set Count 3 to:(sites {1 2 4 5 7 8 10 11 22 21 19 18 16 15 13 12}) ) )
        
        phases:{
        
        (phase "FirstMove" 
            (play 
                (move
                    Select 
                    (from (difference (sites Mover "Right Half") (sites Empty)))
                    (then 
                        (sow
                            apply:(if (is In (to) (sites Mover "Home"))
                                (fromTo 
                                    (from (to))
                                    (to (mapEntry Next))
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase Mover "Play")
        )
        
        (phase "Play" 
            (play
                (move
                    Select 
                    (from 
                        (if 
                            (is Mover Prev) 
                            "PlayFromLastHole" 
                            (sites Mover "Home") 
                        ) 
                        if:(and
                            (> (count at:(from)) 0)
                            (or (is Mover Prev)
                                (and (not (is Mover Prev))
                                    (not 
                                        (and 
                                            (is In ("NextHoleFrom" (from) (count at:(from))) (sites Mover "Home")) 
                                            (!= 0 (count at:("NextHoleFrom" (from) (count at:(from)))))
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (then 
                        (sow
                            apply:(if (< 1 (count at:(to)))
                                (moveAgain)
                                (if (and (!= 0 (count at:("OppositePit"))) (is In (to) (sites Mover "Home")))
                                    (remove ("OppositePit"))
                                )
                            )
                        )
                    )
                )
            )
        )
        }
        
        (end
            (forEach NonMover
                if:("NoPiece")
                (result Player Loss)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Tapata is a two-rown mancala-style board game played by the Konso people of Ethiopia. It is typically played in spaces reserved for large assemblies, and is typically played by adult men and, less frequently, by boys and adolescents.")
        (aliases {"TagÃ©ga"})
        (rules "2x12 board. Beginning from the leftmost hole closest to a player and proceeding in an anti-clockwise direction, the opening arrangement of counters is an alternating pattern of one empty hole, followed by two holes with three counters each. The first move for each player must be from an occupied hole in the right half of their row. Players sow in an anti-clockwise direction, but if the final counter is about to fall in a hole in the player's own row, it is instead placed in the first hole in the opponent's row. After this, players may sow from any hole in their row, provided that the final counter does not fall into an occupied hole in their row. When the final counter of a sowing lands in an occupied hole in the opponent's row, the counters there are picked up and sowing continues. When this happens, the player is allowed to drop the final counter into an occupied hole in their row, which would then be picked up and sowing would continue as before. When the final counter falls into an empty hole in the player's row, any counters in the opponent's hole opposite are captured. When the final counter falls into an empty hole in the opponent's row, the turn ends. The player who captures all of the opponent's counters wins. Players typically play to achieve five consecutive wins. ")
        (source "Pankhurst 1971: 187.")
        (id "359")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Horn of Africa, around 1971.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Tapata_ai"
    )
)

