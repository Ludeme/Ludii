(define "PiecesOwnedBy" (count Cell at:(handSite #1)))

//------------------------------------------------------------------------------

(game "Mangala (Turkey)" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 <Column:size> store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        (start (set Count 5 to:(sites Track)))
        
        (play (move Select 
                (from 
                    (sites Mover) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        if:(or 
                            (= (count at:(to)) 1) 
                            (= (count at:(to)) 3)
                        )
                        apply:(fromTo 
                            (from (to)) 
                            (to (handSite Mover)) 
                            count:(count at:(to))
                        )
                        backtracking:True
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (= (count in:(sites Track)) 0)))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Column> args:{ <size> }
    {
    (item "6" <6> "Each player has 6 holes.") 
    (item "7" <7> "Each player has 7 holes.")**
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Seven Holes (Observed)" 
        {
        "Board Size/7"
        }
    )
    
    (ruleset "Ruleset/Six Holes (Observed)" 
        {
        "Board Size/6"
        }
    )
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Mangala is a two-row mancala-style board game played in Turkey. It is similar to other two-row mancala games found throughout Southwest Asia and North Africa. The game is played with holes dug into the ground.")
        (useFor "Ruleset/Six Holes (Observed)" (rules "Two rows of six holes."))
        (useFor "Ruleset/Six Holes (Observed)" (source "And 1979: 52."))
        (useFor "Ruleset/Six Holes (Observed)" (id "28"))
        (useFor "Ruleset/Seven Holes (Observed)" (rules "2x7 board. The game starts with five counters in each hole. Play begins from a player's hole, sowing in a counter clockwise direction. If the last counter is sown in a hole containing one or three counters, they are taken. if the previous hole also has one or three, these are also taken. Play continues until all the holes are empty. The taken stones are then placed five in each hole, and the player with more counters than that needed to fill the holes in their row wins."))
        (useFor "Ruleset/Seven Holes (Observed)" (source "And 1979: 52."))
        (useFor "Ruleset/Seven Holes (Observed)" (id "284"))
        (id "284")
        (version "1.3.8")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Turkey, around 1979.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Mangala (Turkey)_ai"
    )
)

