(define "WestHole" (ahead (to) W))
(define "WestHoleExist" (!= (to) ("WestHole")))
(define "EastHole" (ahead (to) E))
(define "EastHoleExist" (!= (to) ("EastHole")))
(define "LastHole" (last To afterConsequence:True))
(define "NoPieceOnBoard" (no Pieces All in:(union (sites Top) (sites Bottom))))
(define "OneSingleCounterPerPlayer"
    (and
        (>= 1 (count Pieces All in:(sites P1)))
        (>= 1 (count Pieces All in:(sites P2)))
    )
)

//------------------------------------------------------------------------------

(game "Gifia" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 4
            {
            (track "TrackCCW" "1,E,N,W" loop:True)
            (track "TrackCW" "4,W,N,E" loop:True)
            }
        )
        (regions P1 (difference (expand (sites Left) steps:2) (sites Left)))  // P1 home
        (regions P2 (difference (expand (sites Right) steps:2) (sites Right))) // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})             // kalahs (storage pits)
        (piece "Seed" Shared)
    }) 
    (rules 
        
        (start (set Count 6 to:(sites Track)))
        
        phases:{
        (phase "Sowing"
            (play 
                (if ("SameTurn") 
                    (or
                        (move 
                            (from (ahead ("LastHole") W))
                            (to (mapEntry Mover))
                            count:(count at:(ahead ("LastHole") W))
                        )
                        (move 
                            (from (ahead ("LastHole") E))
                            (to (mapEntry Mover))
                            count:(count at:(ahead ("LastHole") E))
                        )
                    )
                    (or
                        (move Select
                            (from
                                (sites Mover)
                                if:(is Occupied (from))
                            )
                            (then
                                (sow
                                    "TrackCCW"
                                    apply:(if 
                                        (or
                                            (= 2 (count at:(to)))
                                            (= 4 (count at:(to)))
                                        )
                                        (and {
                                            (fromTo
                                                (from (to))
                                                (to (mapEntry Mover))
                                                count:(count at:(to))
                                            )
                                            (if ("WestHoleExist")
                                                (if 
                                                    (or
                                                        (= 2 (count at:("WestHole")))
                                                        (= 4 (count at:("WestHole")))
                                                    )
                                                    (fromTo
                                                        (from ("WestHole"))
                                                        (to (mapEntry Mover))
                                                        count:(count at:("WestHole"))
                                                    )
                                                )
                                            )
                                            (if ("EastHoleExist")
                                                (if 
                                                    (or
                                                        (= 2 (count at:("EastHole")))
                                                        (= 4 (count at:("EastHole")))
                                                    )
                                                    (fromTo
                                                        (from ("EastHole"))
                                                        (to (mapEntry Mover))
                                                        count:(count at:("EastHole"))
                                                    )
                                                )
                                            )
                                        })
                                        (if 
                                            (and
                                                (if ("WestHoleExist")
                                                    (if 
                                                        (or
                                                            (= 2 (count at:("WestHole")))
                                                            (= 4 (count at:("WestHole")))
                                                        )
                                                        False
                                                    )
                                                )
                                                (if ("EastHoleExist")
                                                    (if 
                                                        (or
                                                            (= 2 (count at:("EastHole")))
                                                            (= 4 (count at:("EastHole")))
                                                        )
                                                        False
                                                    )
                                                )
                                            )
                                            (moveAgain)
                                        )
                                    )
                                )
                            )
                        )
                        (move Select
                            (from
                                (sites Mover)
                                if:(is Occupied (from))
                            )
                            (then
                                (sow
                                    "TrackCW"
                                    apply:(if 
                                        (or
                                            (= 2 (count at:(to)))
                                            (= 4 (count at:(to)))
                                        )
                                        (and {
                                            (fromTo
                                                (from (to))
                                                (to (mapEntry Mover))
                                                count:(count at:(to))
                                            )
                                            (if ("WestHoleExist")
                                                (if 
                                                    (or
                                                        (= 2 (count at:("WestHole")))
                                                        (= 4 (count at:("WestHole")))
                                                    )
                                                    (fromTo
                                                        (from ("WestHole"))
                                                        (to (mapEntry Mover))
                                                        count:(count at:("WestHole"))
                                                    )
                                                )
                                            )
                                            (if ("EastHoleExist")
                                                (if 
                                                    (or
                                                        (= 2 (count at:("EastHole")))
                                                        (= 4 (count at:("EastHole")))
                                                    )
                                                    (fromTo
                                                        (from ("EastHole"))
                                                        (to (mapEntry Mover))
                                                        count:(count at:("EastHole"))
                                                    )
                                                )
                                            )
                                        })
                                        (if 
                                            (and
                                                (if ("WestHoleExist")
                                                    (if 
                                                        (or
                                                            (= 2 (count at:("WestHole")))
                                                            (= 4 (count at:("WestHole")))
                                                        )
                                                        False
                                                    )
                                                )
                                                (if ("EastHoleExist")
                                                    (if 
                                                        (or
                                                            (= 2 (count at:("EastHole")))
                                                            (= 4 (count at:("EastHole")))
                                                        )
                                                        False
                                                    )
                                                )
                                            )
                                            (moveAgain)
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (then
                        (if ("OneSingleCounterPerPlayer")
                            (and 
                                (forEach Site (sites P1)
                                    (fromTo
                                        (from (site))
                                        (to (mapEntry P1))
                                        count:(count at:(site))
                                    )
                                )
                                (forEach Site (sites P2)
                                    (fromTo
                                        (from (site))
                                        (to (mapEntry P2))
                                        count:(count at:(site))
                                    )
                                )
                            )
                        )
                    )
                )
            )
            
            (end (if ("NoPieceOnBoard")
                    {
                    (if (> 6 (count at:(mapEntry P1))) (result P2 Win))
                    (if (> 6 (count at:(mapEntry P2))) (result P1 Win))
                    }
                )
            )
            
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (not (all Sites (sites Mover) if:(is Occupied (site))))
                    (if (<= 6 (count at:(mapEntry Mover)))
                        (move
                            (from (mapEntry Mover))
                            (to (sites Mover) if:(is Empty (to)))
                            count:6
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Gifia is a two-row mancala-style board game that was played in the early twentieth century in Nigeria. ")
        (rules "2x4 board with two stores on the ends. Six counters in each hole. Instead of owning the row of holes closest to them, players own the holes on the left half of the board. Play begins from any of the player's holes, sowing in either direction. When the last counter is sown, making the hole contain two or four counters, these are captured. If the adjacent holes also contain two or four counters, these are also captured. If the last counter falls into a hole, making it contain a number other than two or four, and adjacent holes contain two or four, the counters from only one of these adjacent holes may be captured. Play ends when each player is reduced to one counter. A second round begins with each player placing six counters in as many of their holes as they can, returning surplus to the store. Play continues as before. Rounds are played until one player loses all their counters.")
        (source "Murray 1951: 192-193.")
        (id "237")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
