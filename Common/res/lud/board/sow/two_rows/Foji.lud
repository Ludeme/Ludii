(define "Columns" 6)
(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns")) )
(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))
(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

//------------------------------------------------------------------------------

(game "Foji"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "5,W,N,E" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
    })  
    (rules 
        
        (start (set Count 4 to:(sites Track)) )
        
        (play 
            (if (is Proposed "End")
                (or (move Vote "End") (move Vote "No"))
                (or
                    (if (is Cycle) (move Propose "End")) 
                    (if (is Mover Prev) 
                        (move Select
                            (from "PlayFromLastHole")
                            (then
                                (sow
                                    apply:(if (< 1 (count at:(to)))
                                        (moveAgain)
                                        (if (is In (to) (sites Mover))
                                            (if (is Occupied ("OppositePit" (to)))
                                                (fromTo
                                                    (from ("OppositePit" (to)))
                                                    (to (handSite Mover))
                                                    count:(count at:("OppositePit" (to)))
                                                )
                                            )
                                        )
                                    )	
                                    includeSelf:False
                                )
                            )
                        )
                        (do
                            (move Select
                                (from 
                                    (sites Mover) 
                                    if:(is Occupied (from))
                                )
                                (then
                                    (sow
                                        apply:(if (< 1 (count at:(to)))
                                            (moveAgain)
                                            (if (is In (to) (sites Mover))
                                                (if (is Occupied ("OppositePit" (to)))
                                                    (fromTo
                                                        (from ("OppositePit" (to)))
                                                        (to (handSite Mover))
                                                        count:(count at:("OppositePit" (to)))
                                                    )
                                                )
                                            )
                                        )	
                                        includeSelf:False
                                    )
                                )
                            )
                            ifAfterwards:(if (is Mover P1) (< 0 (count in:(sites P2))) (< 0 (count in:(sites P1))))
                        )
                    )
                )
            )
        )
        
        (end
            (if (or (no Moves Mover) (is Decided "End"))
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Foji is a two-row mancala-style board game oberved in Dahomey during the early twentieth century. It is similar to other two-row mancala games from West Africa.")
        (rules "2x6 board. Four counters in each hole.  A player picks up all of the counters in one of the holes in their row and sows them one-by-one in a clockwise direction in consecutive holes from the hole the pieces originated. The starting hole is always left empty, even if a player sows in a complete circuit of the board, the original house is skipped and sowing continues in the next hole after it. When the final counter falls into an occupied hole, these are picked up and sowing continues. When the final counter of a sowing lands in an empty hole in the player's own row, the contents of the hole in the opponent's row opposite it are captured. If the opponent's holes are all empty, the other player must make a move placing counters in the opponent's row. If not possible, the player captures all the counters in their row. The player who has captured the most counters wins. If the game continues in a repeating loop, the players can agree to end the game and capture the counters remaining in their row.
        ")
        (source "BÃ©art 1955: 510.")
        (id "425")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Benin, around 1955.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)

