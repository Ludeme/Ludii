(game "Wore"
    (players 2)
    (equipment {
        (mancalaBoard 2 4 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (piece "Seed" Shared)
        (hand Each)
    })

    (rules
        (start (set Count 6 to:(sites Track)))
        (play
            (move
                Select
                (from
                    (sites Mover)
                    if:(< 0 (count at:(from)))
                )
                (then
                    (sow
                        if:(or {
                            (= (count at:(to)) 2)
                            (= (count at:(to)) 4)
                            (= (count at:(to)) 6)
                        })
                        apply:(fromTo
                            (from (to))
                            (to (handSite Mover))
                            count:(count at:(to))
                        )
                    )
                )
            )
        )
        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 (count Cell at:(handSite P1)))
                    (score P2 (count Cell at:(handSite P2)))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Wöré was observed in the early twentieth century in St. Louis, Senegal. It was considered a game for women, and in some families it is forbidden for young girls and uncircumcised boys to play it.")
        (aliases {"Wöré" "Weuré"})
        (rules "Counters are placed in piles: two rows of four piles. Six counters in each pile. A player sows from any of the piles in the player's row in an anti-clockwise direction. Any pile which contains 2, 4, or 6 counters is captured. The player with the most counters captured wins.")
        (source "Béart 1955: 512.")
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in West Africa., around 1955.")
        }
    )

    (graphics {
        (board Style Mancala)
    })
    (ai
        "Wore_ai"
    )
)
