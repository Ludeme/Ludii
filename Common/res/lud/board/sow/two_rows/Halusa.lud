(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Halusa" 
    (players 2) 
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (piece "Seed" Shared)
        (hand Each)
    }) 
    (rules 
        (start (set Count 6 to:(sites Track)))
        
        (play (do (move Select 
                    (from 
                        (sites Mover) 
                        if:(> (count at:(from)) 0)
                    )
                    (then 
                        (sow
                            if:(and 
                                (is In (to) (sites Mover)) 
                                (is Even (count at:(to)))
                            ) 
                            apply:(fromTo 
                                (from (to)) 
                                (to (handSite Mover)) 
                                count:(count at:(to))
                            )
                            backtracking:True
                        )
                    )
                )
                ifAfterwards:(> (count in:(sites Next)) 0)
            )
        )
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Halusa is two-row mancala-style game documented in early modern Mesopotamia. It was said to be played by Arabs and Turks living there, and it is one of the earliest descriptions of the rules of a mancala game that has survived. It is similar to other mancala games which are still played in Southwest Asia today. The board is drawn as a hinged wooden box, with two rows of six holes.")
        (aliases {"Mangala" "Halusi" "Mancala"})
        (rules "Play begins with six counters in each hole. Sowing is anti-clockwise. If the last counter of a sowing lands in the player's own hole making it even, the counters are captured. If the contents of the hole before it is also even, these are also taken, continuing until an odd or empty hole is reached. If the last counter makes a hole odd, the turn ends. If a player has no counters in their holes at the end of the turn, the opponent must play so that the player can play on the next turn. Play ends when neither player is able to move; the last player who was able to move takes the remaining counters and the player with the most counters captured wins.")
        (source "Hyde 1694: 226-230.")
        (id "211")
        (version "1.3.5")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Mesopotamia, around 1694.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Halusa_ai"
    )
)
