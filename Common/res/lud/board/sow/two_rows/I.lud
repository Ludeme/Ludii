(define "PiecesOwnedBy" (count Cell at:(handSite #1)))

//------------------------------------------------------------------------------

(game "I" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 <Board:size> store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom)) // P1 home
        (regions P2 (sites Top))  // P2 home
        (hand Each)
    }) 
    (rules 
        
        (start (set Count 4 to:(sites Track)))
        
        (play
            (move Select 
                (from 
                    (sites Mover) 
                    if:(> (count at:(from)) 1)
                )
                (then 
                    (sow 
                        apply:(if (or (= 2 (count at:(to))) (= 3 (count at:(to))))
                            (and {
                                (fromTo 
                                    (from (to)) 
                                    (to (handSite Mover)) 
                                    count:(count at:(to))
                                )
                                (if (!= (to) (ahead (to) E))
                                    (if (or (= 2 (count at:(ahead (to) E))) (= 3 (count at:(ahead (to) E))))
                                        (fromTo 
                                            (from (ahead (to) E)) 
                                            (to (handSite Mover)) 
                                            count:(count at:(ahead (to) E))
                                        )
                                    )
                                )
                                (if (!= (to) (ahead (to) W))
                                    (if (or (= 2 (count at:(ahead (to) W))) (= 3 (count at:(ahead (to) W))))
                                        (fromTo 
                                            (from (ahead (to) W)) 
                                            (to (handSite Mover)) 
                                            count:(count at:(ahead (to) W))
                                        )
                                    )
                                )
                            })
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (and (no Moves P1) (no Moves P2))))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size>}
    {
    (item "3" <3> "Each row has 3 holes.")* 
    (item "4" <4> "Each row has 4 holes.") 
    (item "5" <5> "Each row has 5 holes.") 
    (item "6" <6> "Each row has 6 holes.") 
    (item "7" <7> "Each row has 7 holes.") 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/I (Observed)" {
        "Board Size/3"
        }
        variations:{"Board Size/4" "Board Size/5" 
        "Board Size/6" "Board Size/7" 
        }
    )*
})

//------------------------------------------------------------------------------

(metadata 
(info
{
(description "I is a two-row mancala-style board game played by the Dogon people of Mali. It is generally played by boys in the sand, using pebbles as counters.")
(rules "2x3-7 board. Four counters in each hole. Players sow counters from their side of the board in an anti-clockwise direction. When the final counter of a sowing lands in a hole containing one or two counters, making it now contain two or three, these are captured. Any adjacent holes with two or three counters are also captured. Single counters cannot be sown. When a player only has single counters in holes in their row, they pass. The player who captures the most counters wins.")
(source "Griaule 1938: 166-168.")
(id "1392")
(version "1.3.7")
(classification "board/sow/two rows")
(credit "Eric Piette")
(origin "This game was played in Mali, around 1938.")
}
)
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "I_ai"
    )
)

