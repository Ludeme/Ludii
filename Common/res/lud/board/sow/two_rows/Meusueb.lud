(define "NextHoleFrom" (trackSite Move from:#1 steps:1))

(define "PlayFromNextHole" (sites {("NextHoleFrom" (last From afterConsequence:True))}))

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Meusueb"  
    (players 2)  
    (equipment { 
        (mancalaBoard 2 6
            (track "Track" "1,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
        (piece "Seed" Shared)
        }
    )  
    (rules 
        
        (start (set Count 4 to:(sites Track)))
        
        (play 
            (move
                Select 
                (from 
                    (if 
                        "SameTurn" 
                        (if (is Pending) "PlayFromNextHole" "LastHoleSowed") 
                        (sites Mover) 
                    ) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        apply:(if (= (count at:(to)) 4)
                            (and
                                (fromTo 
                                    (from (to)) 
                                    (to (mapEntry (mover))) 
                                    count:(count at:(to))
                                ) 
                                (if (> (count at:(trackSite Move from:(to) steps:1)) 0)
                                    (and
                                        (moveAgain)
                                        (set Pending)
                                    )
                                )
                            )
                            (if (> (count at:(to)) 1)
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        
        (end
            (if (no Moves Mover) 
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Meusueb is a two-row mancala-style board game played by the Acehnese people on the island of Sumatra in Indonesia and particularly popular with women and children. It was documented in the early twentieth century, but it is probably older. It shares several features with similar mancala-style games in Pacific and Indian Ocean islands.")
        (aliases {"Meusuëb" "Meusuëb"})
        (rules "2x6 board with two stores. Play begins with four counters in each hole. A move begins from any of a player's holes, sowing happens anti-clockwise. Move ends if the last counter falls into an empty hole. If it lands in a hole with counters, these are picked up and sowing continues, unless the sowing makes the last hole contain four counters, in which case these are captured and the sowing continues from the next hole. When both players have too few counters to play, each one makes one move with one of their counters and then adds all of the counters on their side of the board to the store. The player with the most counters in the store wins.")
        (source "Snouck Hurgronje 1906: 200-201.")
        (id "220")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Indonesia, around 1909.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai 
        "Meusueb_ai"
    )
    
)
