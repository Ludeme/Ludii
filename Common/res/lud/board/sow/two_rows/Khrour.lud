(define "PlayableSites" (sites (values Remembered "Playable")))
(define "NoPieceOnBoard" (all Sites (union (sites Top) (sites Bottom)) if:(= 0 (count at:(site)))))

//------------------------------------------------------------------------------

(game "Khrour" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 <Board:size> store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
    }) 
    (rules 
        
        (start {
            (set Count <Board:init> to:(sites Track)) 
            (set RememberValue "Playable" (union (sites Top) (sites Bottom)))
        })
        
        phases:{
        (phase "Sowing"
            (play
                (or {
                    (move Select
                        (from 
                            (sites Mover)
                            if:(and (is Occupied (from)) (is In (from) ("PlayableSites")))
                        )
                        (then
                            (sow
                                apply:(if
                                    (and
                                        (or {
                                            (= 2 (count at:(to)))
                                            (= 4 (count at:(to)))
                                            (= 6 (count at:(to)))
                                            (= 8 (count at:(to)))
                                        })
                                        (is In (to) ("PlayableSites"))
                                    )
                                    (fromTo
                                        (from (to))
                                        (to (handSite Mover))
                                        count:(count at:(to))
                                    )
                                )
                            )
                        )
                    )
                    }
                    (then
                        (if ("NoPieceOnBoard")
                            (forget Value "Playable" All)
                            (forEach Site (difference (sites Board) ("PlayableSites"))
                                (if (<= <Board:init> (count at:(site)))
                                    (remember Value "Playable" (site))
                                )
                            )
                        )
                    )
                )
            )
            (end (if ("NoPieceOnBoard")
                    {
                    (if (= 0 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (= 0 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
            
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (not (all Sites (sites Mover) if:(is Occupied (site))))
                    (if (<= <Board:init> (count Cell at:(handSite Mover)))
                        (move
                            (from (handSite Mover))
                            (to (sites Mover) if:(is Empty (to)))
                            count:<Board:init>
                            (then (remember Value "Playable" (last To)))
                        )
                        (move
                            (from (handSite Mover))
                            (to (sites Mover) if:(is Empty (to)))
                            count:(count Cell at:(handSite Mover))
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <init>}
    {
    (item "2" <2> <4> "Each player has 2 holes.") 
    (item "3" <3> <6> "Each player has 3 holes.") 
    (item "4" <4> <8> "Each player has 4 holes.") 
    (item "6" <6> <12> "Each player has 6 holes.") 
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Moudjeria (Described)" {
        "Board Size/2"
        }
        variations:{"Board Size/3" "Board Size/4" "Board Size/6"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Khrour is a two-row mancala-style board game played in West Africa. It is typically played in holes scooped out of the sand, rather than on an actual game board.")
        (aliases {"Hrur"})
        (rules "2x2, 3, 4, or 6 board. Each hole contains a number of counters equal to the total number of holes in the board. Sowing begins from any hole in the player's row, and proceeds anti-clockwise. Counters are captured when the final counter lands in an occupied hole, making it contain 2, 4, 6, or 8 counters. When all of the counters have been captured, a second game begins. Each player fills their holes with the same number of counters as the beginning of the previous game. The player who has extra counters holds these in reserve. The other player will have either empty holes or one without the requisite number to fill the hole. Sowing cannot begin and captures cannot be made from these holes until the contents of these holes reaches the required number of counters to fill them. The second player in the first game now becomes the first player. Play continues with subsequent games until one player captures all the counters.
            
        Evidence Map")
        (source "BÃ©art 1955: 512-514.")
        (id "287")
        (version "1.3.5")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
