(define "Columns" 8)

(define "NextHoleFrom" (trackSite Move from:#1 #2 #3))

(define "OppositePit" (if (is In #1 (sites Bottom)) (+ #1 "Columns") (- #1 "Columns") ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "NoPiecesInPlayerSide" 
    (no Pieces All in:(sites #1))
)

//------------------------------------------------------------------------------

(game "Iyogh"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            {
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "7,W,N,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom)) 
        (regions P2 (sites Top)) 
        (hand Each)
        (map "LeftMost" {(pair P1 0) (pair P2 15)})
        (map "RightMost" {(pair P1 7) (pair P2 8)})
        (regions "RightMost" P1 (sites {5 6})) 
        (regions "RightMost" P2 (sites {9 10})) 
    })  
    (rules 
        
        (start (set Count 4 to:(sites Track) ) )
        
        phases:{
        (phase "Opening"
            (play 
                (move
                    (from (sites Mover "RightMost") if:(is Occupied (from)))
                    (to (mapEntry "RightMost" Mover))
                    count:(count at:(from))
                )
            )
            (nextPhase Mover (all Sites (sites Mover "RightMost") if:(is Empty (site))) "Sowing")
        )
        (phase "Sowing"
            (play 
                (or {
                    (if (is Occupied Cell (handSite Mover))
                        (forEach Value min:1 max:(count Cell at:(handSite Mover))
                            (do (set Var "NumToSow" (value))
                                next:(move
                                    (from Cell (handSite Mover))
                                    (to (mapEntry "LeftMost" Mover))
                                    count:(value)
                                    (then
                                        (and
                                            (sow
                                                count:(var "NumToSow")
                                                "TrackCCW"
                                                if:(or
                                                    (= 1 (count at:("OppositePit" (to))))
                                                    (= 3 (count at:("OppositePit" (to))))
                                                )
                                                apply:(if (or
                                                        (= 1 (count at:("OppositePit" (to))))
                                                        (= 3 (count at:("OppositePit" (to))))
                                                    )
                                                    (fromTo
                                                        (from ("OppositePit" (to)))
                                                        (to (handSite Mover))
                                                        count:(count at:("OppositePit" (to)))
                                                    )
                                                )
                                                forward:True
                                            )	
                                            (set Var "NumToSow" 0)
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (if (is Occupied Cell (handSite Mover))
                        (forEach Value min:1 max:(count Cell at:(handSite Mover))
                            (do (set Var "NumToSow" (value))
                                next:(move
                                    (from Cell (handSite Mover)
                                        if:(or
                                            (= 1 (count at:("OppositePit" ("NextHoleFrom" (mapEntry "LeftMost" Mover) "TrackCW" steps:(value)))))
                                            (= 3 (count at:("OppositePit" ("NextHoleFrom" (mapEntry "LeftMost" Mover) "TrackCW" steps:(value)))))
                                        )
                                    )
                                    (to (mapEntry "LeftMost" Mover))
                                    count:(value)
                                    (then
                                        (and
                                            (sow
                                                count:(var "NumToSow")
                                                "TrackCW"
                                                if:(or
                                                    (= 1 (count at:("OppositePit" (to))))
                                                    (= 3 (count at:("OppositePit" (to))))
                                                )
                                                apply:(if (or
                                                        (= 1 (count at:("OppositePit" (to))))
                                                        (= 3 (count at:("OppositePit" (to))))
                                                    )
                                                    (fromTo
                                                        (from ("OppositePit" (to)))
                                                        (to (handSite Mover))
                                                        count:(count at:("OppositePit" (to)))
                                                    )
                                                )
                                                forward:True
                                            )	
                                            (set Var "NumToSow" 0)
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (move Select
                        (from 
                            (sites Mover) 
                            if:(< 0 (count at:(from)))
                        )
                        (then 
                            (sow
                                "TrackCCW"
                                if:(or
                                    (= 1 (count at:("OppositePit" (to))))
                                    (= 3 (count at:("OppositePit" (to))))
                                )
                                apply:(if (or
                                        (= 1 (count at:("OppositePit" (to))))
                                        (= 3 (count at:("OppositePit" (to))))
                                    )
                                    (fromTo
                                        (from ("OppositePit" (to)))
                                        (to (handSite Mover))
                                        count:(count at:("OppositePit" (to)))
                                    )
                                )
                                forward:True
                            )
                        )
                    )
                    (move Select
                        (from 
                            (sites Mover) 
                            if:(and 
                                (< 0 (count at:(from)))
                                (or
                                    (= 1 (count at:("OppositePit" ("NextHoleFrom" (from) "TrackCW" steps:(count at:(from))))))
                                    (= 3 (count at:("OppositePit" ("NextHoleFrom" (from) "TrackCW" steps:(count at:(from))))))
                                )
                            )
                        )
                        (then 
                            (sow
                                "TrackCW"
                                if:(or
                                    (= 1 (count at:("OppositePit" (to))))
                                    (= 3 (count at:("OppositePit" (to))))
                                )
                                apply:(if (or
                                        (= 1 (count at:("OppositePit" (to))))
                                        (= 3 (count at:("OppositePit" (to))))
                                    )
                                    (fromTo
                                        (from ("OppositePit" (to)))
                                        (to (handSite Mover))
                                        count:(count at:("OppositePit" (to)))
                                    )
                                )
                                forward:True
                            )
                            
                        )
                    )
                })
            )
        )
        }
        
        (end {
            (if  ("NoPiecesInPlayerSide" P1)
                (result P2 Win)
            )
            (if  ("NoPiecesInPlayerSide" P2)
                (result P1 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Iyogh is a two-row mancala-style board game plauyed by the Kukuma people in Nigeria. It was documented in the early twentieth century.")
        (rules "2x8 board. Four counters in each hole. Play begins with both players placing all of the counters in the rightmost three holes in their row into the rightmost hole. Counters are sown in an anti-clockwise fashion, but they can be sown clockwise only if it leads to a capture. Counters are captured when the final counter is sown into a hole opposite a hole containing one or three counters, and those are take. If the hole opposite the next hole also contains one or three, these are also taken, continuing until a hole with any other number of counters is reached. Instead of taking counters from one of their holes, a player may sow any or all of the counters they have captured, beginning from the leftmost hole. The same rules for sowing apply. The game ends when one player has no counters in their holes.")
        (source "Murray 1951: 189-190.")
        (id "233")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Nigeria, around 1951.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)

