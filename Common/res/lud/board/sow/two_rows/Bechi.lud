(define "Column" <Board:size>)
(define "NextHole" ("NextSiteOnTrack" #3 from:#1 #2))
(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1 "Home"))))

(define "CaptureMove" 
    (fromTo 
        (from (to)) 
        (to (handSite Mover)) 
        count:(count at:(to))
        (then
            (if (is Even (count at:("NextHole" (last From) #1 1)))
                (and
                    (fromTo 
                        (from ("NextHole" (last From) #1 1)) 
                        (to (handSite Mover)) 
                        count:(count at:("NextHole" (last From) #1 1))
                   )
                    (if (is Even (count at:("NextHole" (last From) #1 2)))
                        (and
                            (fromTo 
                                (from ("NextHole" (last From) #1 2)) 
                                (to (handSite Mover)) 
                                count:(count at:("NextHole" (last From) #1 2))
                           )
                            (if (is Even (count at:("NextHole" (last From) #1 3)))
                                (and
                                    (fromTo 
                                        (from ("NextHole" (last From) #1 3)) 
                                        (to (handSite Mover)) 
                                        count:(count at:("NextHole" (last From) #1 3))
                                   )
                                    (if (is Even (count at:("NextHole" (last From) #1 4)))
                                        (and
                                            (fromTo 
                                                (from ("NextHole" (last From) #1 4)) 
                                                (to (handSite Mover)) 
                                                count:(count at:("NextHole" (last From) #1 4))
                                           )
                                            (if (is Even (count at:("NextHole" (last From) #1 5)))
                                                (and
                                                    (fromTo 
                                                        (from ("NextHole" (last From) #1 5)) 
                                                        (to (handSite Mover)) 
                                                        count:(count at:("NextHole" (last From) #1 5))
                                                   )
                                                    (if (is Even (count at:("NextHole" (last From) #1 6)))
                                                        (fromTo 
                                                            (from ("NextHole" (last From) #1 6)) 
                                                            (to (handSite Mover)) 
                                                            count:(count at:("NextHole" (last From) #1 6))
                                                       )
                                                   )
                                               )
                                           )
                                       )
                                   )
                               )
                           )
                       )
                   )
               )
           )
       )
   )	
)

(define "Move"
    (or {
        (move Select 
            (from 
                (sites Mover "LeftSide") 
                if:(> (count at:(from)) 1)
           )
            (then 
                (and
                    (remember Value "HolesSowed" (last To))
                    (sow
                        "TrackCW"
                        apply:(if (and (is In (to) (values Remembered "HolesSowed")) (is Even (count at:(to))))
                            ("CaptureMove" "TrackCW")
                       )
                        origin:True
                   )
               )
           )
       )
        (move Select 
            (from 
                (sites Mover "RightSide") 
                if:(> (count at:(from)) 1)
           )
            (then 
                (and
                    (remember Value "HolesSowed" (last To))
                    (sow
                        "TrackCCW"
                        apply:(if (and (is In (to) (values Remembered "HolesSowed")) (is Even (count at:(to))))
                            ("CaptureMove" "TrackCCW")
                       )
                        origin:True
                   )
               )
           )
       )
        <Board:centreMove>
    })		
)

//------------------------------------------------------------------------------

(game "Bechi"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 ("Column") store:None
            <Board:tracks>
       )
        (regions "Home" P1 (sites Bottom))                     // P1 home
        (regions "Home" P2 (sites Top))                        // P2 home
        <Board:regions>
        (piece "Seed" Shared) 
        (hand Each)
    })  
    (rules 
        
        (start (set Count 6 to:(sites Track)))
        
        phases:{
        (phase "Sowing"
            (play 
                (if (can Move ("Move"))
                    ("Move")
                    (move Pass
                        (then 
                            (if (all Passed)
                                (and {
                                    (forEach Site (sites P1 "Home")
                                        (if (is Occupied (site))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P1))
                                                count:(count at:(site))
                                           )
                                       )
                                   )
                                    (forEach Site (sites P2 "Home")
                                        (if (is Occupied (site))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P2))
                                                count:(count at:(site))
                                           )
                                       )
                                   )
                                    (forget Value "HolesSowed" All)
                                })
                           )
                       )
                   )
               )
           )
            (end (if (all Passed)
                    {
                    (if (> 6 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (> 6 (count Cell at:(handSite P2))) (result P1 Win))
                    }
               )
           )
            (nextPhase (all Passed) "BetweenRounds")
       )
        (phase "BetweenRounds"
            (play
                (if (<= 6 (count Cell at:(handSite Mover)))
                    (move
                        (from (handSite Mover))
                        (to (intersection (sites Empty) (sites Mover "Home")))
                        count:6
                   )
               )
           )
            (nextPhase (all Passed) "Sowing")
       )
        }
        
   )
)

//------------------------------------------------------------------------------

(option "Variant" <Board> args:{ <size> <tracks> <regions> <centreMove>}
    {
    (item "Kunama"  
        <4>     
        <
        {
        (track "TrackCW" "3,W,N,E" loop:True)
        (track "TrackCCW" "0,E,N,W" loop:True)
        }
        >
        <
        (regions "LeftSide" P1 (intersection (expand (sites Left) steps:1) (sites Bottom)))
        (regions "LeftSide" P2 (intersection (expand (sites Right) steps:1) (sites Top)))
        (regions "RightSide" P1 (intersection (expand (sites Right) steps:1) (sites Bottom)))
        (regions "RightSide" P2 (intersection (expand (sites Left) steps:1) (sites Top)))
        >
        <>
        "The board has 4 columns."
   )
    (item "Three Holes"  
        <3> 
        <
        {
        (track "TrackCW" "2,W,N,E" loop:True)
        (track "TrackCCW" "0,E,N,W" loop:True)
        }
        >
        <
        (regions "LeftSide" P1 (intersection (sites Left) (sites Bottom)))
        (regions "LeftSide" P2 (intersection (sites Right) (sites Top)))
        (regions "RightSide" P1 (intersection (sites Right) (sites Bottom)))
        (regions "RightSide" P2 (intersection (sites Left) (sites Top)))
        (regions "Centre" P1 (intersection (sites Centre) (sites Bottom)))
        (regions "Centre" P2 (intersection (sites Centre) (sites Top)))
        >
        <
        (move
            Select 
            (from 
                (sites Mover "Centre") 
                if:(> (count at:(from)) 1)
           )
            (then 
                (and
                    (remember Value "HolesSowed" (last To))
                    (sow
                        "TrackCW"
                        apply:(if (and (is In (to) (values Remembered "HolesSowed")) (is Even (count at:(to))))
                            ("CaptureMove" "TrackCW")
                       )
                        origin:True
                   )
               )
           )
       )
        (move
            Select 
            (from 
                (sites Mover "Centre") 
                if:(> (count at:(from)) 1)
           )
            (then 
                (and
                    (remember Value "HolesSowed" (last To))
                    (sow
                        "TrackCCW"
                        apply:(if (and (is In (to) (values Remembered "HolesSowed")) (is Even (count at:(to))))
                            ("CaptureMove" "TrackCCW")
                       )
                        origin:True
                   )
               )
           )
       )
        >
        "The board has 3 columns."
   )
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Kunama (Observed)" {
        "Variant/Kunama"
    })*
    
    (ruleset "Ruleset/Three Holes (Observed)" {
        "Variant/Three Holes"
    })
    
})

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Béchi is a two-row mancala-style board game played by the Kunama people of Eritrea, particularly by boys and adolescent young men. It involves trickery, in which eliminated holes can be deceptively filled with counters to fool the other player into thinking they can play there.")
        (aliases {"Béchi"})
        (useFor "Ruleset/Kunama (Observed)" (rules "2x4 board. Six counters per hole. Sowing occurs in a clockwise direction from the left half of a player's row, and anti-clockwise when played from the player's right half of the row. The first seed of sowing is placed in the hole from which the sowing began. When the final counter of a sowing falls into an occupied hole, and it now contains an even number of counters, these are captured, provided the contents of that hole have already been moved. Should the following hole also contain an even number of counters, these are also captured, as well as the next one, until there is a hole with an odd number of counters. If a player cannot move, they must pass until the opponent plays in such a way that they may play. If neither player is able to move, they both capture the counters in their rows. Players then count their counters by placing them six per hole. The player who has more counters than necessary to fill their holes places them aside for use in successive rounds. The player who is unable to fill a hole with six counters leaves it empty, also placing the extra counters aside for later use. This hole is not to be sown into. The player chooses which hole to leave empty. Subsequent rounds continue until one player cannot fill a hole with six counters. "))
        (useFor "Ruleset/Kunama (Observed)" (source "Pankhurt 1971: 170."))
        (useFor "Ruleset/Kunama (Observed)" (id "345"))
        (useFor "Ruleset/Three Holes (Observed)" (rules "2x3 board. Sowing occurs in a clockwise direction from the left hole, anti-clockwise from the right hole, and the player may choose the direction from the central hole."))
        (useFor "Ruleset/Three Holes (Observed)" (source "Pankhurst 1971: 170."))
        (useFor "Ruleset/Three Holes (Observed)" (id "641"))
        (id "345")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
   )
    
    (graphics {
        (board Style Mancala)
    })
)
