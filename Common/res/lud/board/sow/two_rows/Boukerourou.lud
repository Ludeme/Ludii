(define "Columns" 4)

(define "PlayFromLastHole" (last To afterConsequence:True))

(define "NextHole" (trackSite Move from:#1 "Track" steps:1))

(define "NoCounters" (= 0 (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Boukerourou"
    (players 2)

    (equipment {
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom))                    // P1 home
        (regions P2 (sites Top))                       // P2 home
        }
    )
    (rules

        (start (set Count 6 to:(sites Track) ) )

        (play
            (move
                Select
                (from
                    (if (is Mover Prev)
                        (sites {("NextHole" ("PlayFromLastHole"))})
                        (sites Mover)
                    )
                    if:(> (count at:(from)) 0)
                )
                (then
                    (sow
                        apply:(if (and
                                (= 1 (count at:(to)))
                                (not (is Empty ("NextHole" (to))))
                            )
                            (moveAgain)
                            (if (and (is In (to) (sites Next)) (= 6 (count at:(to))))
                                (remove (to))
                            )
                        )
                    )
                )
            )
        )

        (end {
            (if  ("NoCounters" P1)
                (result P2 Win)
            )
            (if  ("NoCounters" P2)
                (result P1 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Boukerourou is a two-row mancala-style board game played in the Tidikelt region of Algeria at the turn of the twentieth century.")
        (rules "2x4 board, rendered as holes in the sand. Six counters per hole. A player picks up all of the counters in one of the holes in their row and sows them in an anti-clockwise direction. When the final counter lands in an empty hole, the contents of the following hole are picked up and the sowing continues. When the final counter lands in a hole in the opponent's row causing it to contain six counters, these counters are captured. Play continues until one player has no counters in their holes, and the opponent wins.")
        (source "Voinot 1909: 133.")
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Algeria, from around 1899 to 1909.")
        }
    )

    (graphics {
        (board Style Mancala)
    })
    (ai
        "Boukerourou_ai"
    )
)
