(define "Columns" 7)
(define "OppositePit" (if (is In (to) (sites Bottom)) (+ (to) "Columns") (- (to) "Columns") ) )
(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))
(define "GoalNumCapturedCounters"
    (/ (* 7 (- (count Sites in:(sites Board)) 2)) 2)
)

//------------------------------------------------------------------------------

(game "Chonka"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 "Columns"
            {
            (track "Track1" <Track:directionP1> loop:True P1)
            (track "Track2" <Track:directionP2> loop:True P2)
            }
        )
        (piece "Seed" Shared) 
        (regions P1 (sites Bottom))                    // P1 home
        (regions P2 (sites Top))                       // P2 home   
        <Track:stores>  // kalahs (storage pits)
    })  
    (rules 
        
        (start { 
            (set Count 7 to:(union (sites P1)  (sites P2)) ) 
        })
        
        (play 
            (move
                Select 
                (from 
                    (if 
                        (is Mover Prev) 
                        "PlayFromLastHole" 
                        (sites Mover) 
                    ) 
                    if:(> (count at:(from)) 0)
                )
                (then 
                    (sow
                        apply:(if (!= (to) (mapEntry Mover))
                            (if (> (count at:(to)) 1)
                                (moveAgain)
                                (and
                                    (fromTo 
                                        (from "OppositePit") 
                                        (to (mapEntry Mover)) 
                                        count:(count at:"OppositePit" )
                                    ) 
                                    (fromTo 
                                        (from (to)) 
                                        (to (mapEntry Mover)) 
                                        count:(count at:(to))
                                    )
                                )
                            )
                        )
                        
                    )
                )
            )
        )
        
        (end
            (if (<= ("GoalNumCapturedCounters") (count at:(mapEntry Mover)))
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Direction" <Track> args:{ <directionP1> <directionP2> <stores> }
    {
    (item "CCW" <"1,E,ENE,WNW,W"> <"1,E,N,W,WSW"> <(map {(pair P1 LastSite) (pair P2 FirstSite)})> "Sowing occurs in CCW.")*   
    (item "CW"  <"7,W,WNW,ENE,E"> <"7,W,N,E,ESE"> <(map {(pair P1 FirstSite) (pair P2 LastSite)})> "Sowing occurs in CW.")
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Chonka (Described)" {
        "Direction/CCW"
        }
        variations:{"Direction/CW"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Chonka is a two-row mancala-style board game played in Colombo in Sri Lanka, and known by Tamil-speaking people as Pallankuli, though it is different from the Pallankuli known from India. It is typically played on a board with cowrie shells or tamarind seeds as counters.")
        (aliases {"Pallankuli"})
        (rules "2x7 board, with two stores. Seven counters per hole. The first player may choose the direction of sowing, which must be maintained throughout the game. Sowing includes the player's store, which is the store following the last hole in the player's row, according to the direction of sowing. When the final counter of a sowing falls into an occupied hole, the counters in that hole are picked up and sowing continues. When the final counter of a sowing falls into an empty hole, that counter is captured along with any counters in the hole in the row opposite it. When the final counter falls into the player's store, the turn ends. The player who first captures half of the counters wins.
        ")
        (source "Parker 1909: 599-600.")
        (id "744")
        (version "1.3.3")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Sri Lanka., around 1909.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
