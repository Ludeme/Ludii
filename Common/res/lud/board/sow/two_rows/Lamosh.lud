(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))
(define "OwnedHoles" 
    (if (is Mover P1)
        (sites (values Remembered "OwnedP1"))
        (sites (values Remembered "OwnedP2"))
    )
)

(define "RightMostEmpty" 
    (trackSite FirstSite "TrackCW"
        from:(mapEntry "RightMost" Mover)
        if:(is Empty (to))
    )
)

(define "RemmemberOwnedHoles" 
    (if (is Mover P1)
        (remember Value "OwnedP1" #1)
        (remember Value "OwnedP2" #1)
    )
)

(define "APlayerCanNotMove"
    (and (not (is Next Mover))
        (or
            (all Sites (sites (values Remembered "OwnedP1")) if:(is Empty (site)))
            (all Sites (sites (values Remembered "OwnedP2")) if:(is Empty (site)))
        )
    )
)

//------------------------------------------------------------------------------

(game "Lamosh"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            {
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "5,W,N,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (map "RightMost" {(pair P1 5) (pair P2 6)})
    })  
    (rules 
        
        (start { 
            (set Count 4 to:(sites Track)) 
            (set RememberValue "OwnedP1" (sites Bottom))
            (set RememberValue "OwnedP2" (sites Top))
        })
        
        phases:{
        (phase "Opening1"
            (play
                (move
                    (from
                        (if ("SameTurn")
                            (var "Replay")
                            11
                        )
                    )	
                    (to
                        ("NextHole" (from) 1)	
                    )
                    (then
                        (if (!= ("NextHole" (last To) 1) 11)
                            (and
                                (moveAgain)
                                (set Var "Replay" ("NextHole" (last To) 1))
                            )
                            (
                                (fromTo
                                    (from 11)
                                    (to (handSite Mover))
                                    count:(count at:11)
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase (not (is Next Mover)) "Sowing")
        )
        
        (phase "Sowing"
            (play
                (or 
                    {
                    (move Select 
                        (from
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                ("OwnedHoles") 
                            )
                            if:(is Occupied (from))
                        )
                        (then
                            (do
                                (set Var "NumSowed" (count at:(last To)))
                                next:(sow
                                    "TrackCCW"
                                    apply:(if (= 4 (count at:(to)))
                                        (fromTo
                                            (from (to))
                                            (to (handSite Mover))
                                            count:4
                                        )
                                        (if (< 1 (count at:(to)))
                                            (and
                                                (moveAgain)
                                                (set Var "Replay" (to))
                                            )
                                        )
                                    )
                                )
                                (then
                                    (and
                                        (forEach Site (sites Track from:(last From) to:(trackSite Move from:(last From) "Track" steps:(- (var "NumSowed") 1)))
                                            (if (= 4 (count at:(site)))
                                                (fromTo
                                                    (from (site))
                                                    (to (if (is In (to) (sites (values Remembered "OwnedP1"))) (handSite P1) (handSite P2)))
                                                    count:4
                                                )
                                            )
                                        )
                                        (set Var "NumSowed" 0)
                                    )
                                )
                            )
                        )
                    )
                    }
                    (then
                        (if ("APlayerCanNotMove")
                            (and { 
                                (if (no Moves P1)
                                    (forEach Site (sites Board)
                                        (if (is Occupied (site))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P1))
                                                count:(count at:(site))
                                            )
                                        )
                                    )
                                    (forEach Site (sites Board)
                                        (if (is Occupied (site))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P2))
                                                count:(count at:(site))
                                            )
                                        )
                                    )
                                )
                                (forget Value "OwnedP1" All)
                                (forget Value "OwnedP2" All)
                            })
                        )
                    )
                )
            )
            
            (end (if ("NoPieceOnBoard")
                    {
                    (if (> 3 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (> 3 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
            
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (<= 3 (count Cell at:(handSite Mover)))
                    (move
                        (from (handSite Mover))
                        (to ("RightMostEmpty"))
                        count:(if (>= (count Cell at:(handSite Mover)) 4) 4 3)
                        (then
                            ("RemmemberOwnedHoles" (last To))
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Lamosh is a two-row mancala-style board game played in Ethiopia. It is similar to the many other games played in the area.")
        (aliases {"Gabata"})
        (rules "2x6 board. Four counters in each hole. Play begins with a stylized move. One player takes one counter from their rightmost hole, and holds it in their hand. They then take one counter from the next hole, moving in an anti-clockwise direction, and place it in the next hole. They then take a counter from the next hole after that, and placing it in the next hole, continuing until there is an alternating pattern of a hole with five counters followed by a hole with three counters. Upon reaching the original hole, one counter is dropping in it, and then one counter in the following hole, thus capturing the counters in this hole. Players then alternate turns, sowing in an anti-clockwise direction. When the final counter of a sowing lands in an occupied hole, these counters are picked up and the sowing continues. During sowing, the contents of any hole made to contain four counters are captured by the player in whose row the hole is located, except when this is the final counter of a sowing, in which case the player who is sowing captures these counters. When the sowing ends in an empty hole, the turn ends. Play continues until one player cannot move, and the other player captures the remaining counters. A second round is played, each player placing four counters into each hole starting from the rightmost hole in their row. The player with more counters gains a hole from their opponent for every four extra balls they've captured. If an opponent has three extra after counting in such a way, they also gain a hole, but not if there are one or two extra. Play continues in several rounds like this until one player captures all the holes.
        ")
        (source "Pankhurst 1971: 178.")
        (id "362")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Ethiopia, around 1971.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)

