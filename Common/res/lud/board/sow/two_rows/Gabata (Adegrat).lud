(define "NextHoleFrom" (trackSite Move from:#1 #2))

(define "PlayFromNextHole" (sites {("NextHoleFrom" (last To afterConsequence:True) steps:1)}))

(define "OwnedHoles" 
    (if (is Mover P1)
        (sites (values Remembered "OwnedP1"))
        (sites (values Remembered "OwnedP2"))
    )
)

(define "OneRowIsEmpty"
    (or 
        (no Pieces All in:(sites P1))
        (no Pieces All in:(sites P2))
    )
)

(define "NoPieceOnBoard" (no Pieces All in:(sites Board)))

(define "ForgetValue" 
    (if (is Mover P1)
        (forget Value "OwnedP1" #1)
        (forget Value "OwnedP2" #1)
    )
)

(define "NoOwnedHoles" 
    (if (= #1 P1)
        (= 0 (size Array (values Remembered "OwnedP1")))
        (= 0 (size Array (values Remembered "OwnedP2")))
    )
)

(define "NoPiecesInP1Side" 
    (no Pieces All in:(sites P1))
)

(define "LeftMostEmpty" 
    (trackSite FirstSite 
        from:(mapEntry "LeftMost" Mover)
        if:(is Empty (to))
    )
)

(define "RemmemberOwnedHoles" 
    (if (is Mover P1)
        (remember Value "OwnedP1" #1)
        (remember Value "OwnedP2" #1)
    )
)

//------------------------------------------------------------------------------

(game "Gabata (Adegrat)"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom)) 
        (regions P2 (sites Top)) 
        (hand Each)
        (map "LeftMost" {(pair P1 0) (pair P2 11)})
        }
    )  
    (rules 
        
        (start { 
            (set Count 3 to:(sites Track) ) 
            (set RememberValue "OwnedP1" (sites Bottom))
            (set RememberValue "OwnedP2" (sites Top))
        })
        
        phases:{
        (phase "Sowing"
            (play 
                (or {
                    (move 
                        Select 
                        (from 
                            (if 
                                (is Mover Prev) 
                                "PlayFromNextHole" 
                                ("OwnedHoles") 
                            ) 
                            if:(> (count at:(from)) 0)
                        )
                        (then 
                            (sow
                                apply:(if (> (count at:(to)) 1)
                                    (if (>= (count at:("NextHoleFrom" (to) steps:1)) 1)
                                        (moveAgain)
                                    )
                                    (if (>= (count at:("NextHoleFrom" (to) steps:1)) 1)
                                        (fromTo
                                            (from ("NextHoleFrom" (to) steps:1))
                                            (to (handSite Mover))
                                            count:(count at:("NextHoleFrom" (to) steps:1))
                                        )
                                    )
                                )
                            )
                        )
                    )	
                    }
                    (then 
                        (if ("OneRowIsEmpty")
                            (and {
                                (if ("NoPiecesInP1Side")
                                    (forEach Site (sites Board)
                                        (if (< 0 (count at:(site)))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P2))
                                                count:(count at:(site))
                                            )
                                        )
                                    )
                                    (forEach Site (sites Board)
                                        (if (< 0 (count at:(site)))
                                            (fromTo
                                                (from (site))
                                                (to (handSite P1))
                                                count:(count at:(site))
                                            )
                                        )
                                    )
                                )
                                (forget Value "OwnedP1" All)
                                (forget Value "OwnedP2" All)
                            })
                        )
                    )
                )
            )
            (end (if ("NoPieceOnBoard")
                    {
                    (if (> 3 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (> 3 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (<= 3 (count Cell at:(handSite Mover)))
                    (move
                        (from (handSite Mover))
                        (to ("LeftMostEmpty"))
                        count:3
                        (then
                            ("RemmemberOwnedHoles" (last To))
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
        
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Gabata is a word that applies to many mancala-style games throughout the Horn of Africa. This game is a two-row version played in the Adegrat area of Ethiopia.")
        (rules "2x6 board. Three counters in each hole. Sowing occurs in an anti-clockwise direction. When the final counter falls into an occupied hole, the player picks up the counters in the next hole and continues sowing from it. If that next hole is empty, the turn ends. If the final counter falls into an empty hole, the contents of the following hole are captured. Play continues until one player can no longer play because there are no counters left on their side. The opponent then takes the remainder of the counters on the board. For the next round, each player counts out the counters they captured, placing three each of the holes in their row. For every three in excess of the amount required to fill all of the holes in a row, the player claims one hole from the opponent's row. Play continues in this fashion until one player captures all of the holes on the board.")
        (source "Pankhurst 1971: 172.")
        (id "351")
        (version "1.3.3")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
