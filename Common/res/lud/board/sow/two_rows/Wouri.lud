(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1))))

//------------------------------------------------------------------------------

(game "Wouri" 
    (players 2)  
    (equipment {
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (piece "Seed" Shared)
        (hand Each)
    })  
    
    (rules 
        (start (set Count 4 to:(sites Track)))		  
        (play 
            (do 
                (move
                    Select 
                    (from 
                        (if 
                            ("SameTurn") 
                            "LastHoleSowed" 
                            (sites Mover)
                        ) 
                        if:(< 0 (count at:(from)))
                    )
                    (then 
                        (sow  
                            apply:(if (and 
                                    (is In (to) (sites Next)) 
                                    (= (count at:(to)) 4)
                                ) 
                                (fromTo 
                                    (from (to)) 
                                    (to (handSite Mover)) 
                                    count:(count at:(to))
                                    (then 
                                        (forEach Site
                                            (if (is Mover P1) (sites P2) (sites P1))
                                            (if (= (count at:(site)) 4)
                                                (fromTo 
                                                    (from (site)) 
                                                    (to (handSite Mover)) 
                                                    count:(count at:(site))
                                                )
                                            )
                                        )
                                    )
                                )
                                (if (< 1 (count at:(to))) 
                                    (moveAgain)
                                )
                            )
                            includeSelf:False
                        )
                    )
                )
                ifAfterwards:(< 0 (count in:(if (is Mover P1) (sites P2) (sites P1))))
            )
        )
        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Wouri is a two-row mancala-style board game observed in Mopti, Mali in the early twentieth century. ")
        (rules "2x6 board. Four counters in each hole. Sowing occurs in an anti-clockwise direction, beginning from any hole in the player's row. When the final counter lands in a hole in the opponent's row containing three (four counting the last counter dropped into it), these are taken. In addition, the contents of any hole in the opponent's row which a player causes to contain four counters are captured. When the final counter lands in an empty hole, the turn ends. When the final counter lands in an occupied hole containing a number of counters other than three, the counters are picked up and sowing continues. If sowing reaches the original hole from which the sowing began, this hole is skipped over. A player must play so that the opponent has counters with which to play, if possible.")
        (source "Béart 1955: 490–504.")
        (id "281")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Wouri_ai"
    )
)
