(define "Columns" 6)

(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))
(define "NextHole" (trackSite Move from:(last To) "Track" steps:1))

//------------------------------------------------------------------------------

(game "Ti"
    (players 2)  
    (equipment { 
        (mancalaBoard 2 "Columns"
            (track "Track" "1,E,N,W" loop:True)
        )
        (regions "Home" P1 (sites Bottom))                     // P1 home
        (regions "Home" P2 (sites Top))                        // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
        (piece "Seed" Shared)
    })  
    
    (rules 
        (start (set Count 4 to:(sites Track)))
        phases:{
        (phase "TwoFirstTurn" 
            (play 
                (if ("SameTurn")
                    (forEach Value min:1 max:(var "NumCounters")
                        (move Add 
                            (piece (id "Seed" Shared)) 
                            (to ("NextHole")
                                (apply
                                    (set Var "NumCounters" (- (var "NumCounters") (value)))
                                )
                            ) 
                            count:(value)
                            (then
                                (if (< 0 (var "NumCounters"))
                                    (moveAgain)
                                    (set Value Mover (+ 1 (value Player Mover)))
                                )
                            )
                        )
                    )
                    (move
                        Select
                        (from (sites Mover "Home") if:(> (count at:(from)) 0))
                        (then 
                            (and {
                                (moveAgain)
                                (remove (last To) count:(count at:(last To)))
                                (set Var "NumCounters" (count at:(last To)))
                            })
                        )
                    )
                )
            )
            (nextPhase Mover (= (value Player Mover) 1) "Sowing")
        )
        
        (phase "Sowing"
            (play 
                (do
                    (or
                        (move
                            Select
                            (from (sites Mover "Home") if:(> (count at:(from)) 1))
                            (then 
                                (sow
                                    if:(or 
                                        (= (count at:(to)) 2) 
                                        (= (count at:(to)) 3)
                                    ) 
                                    apply:(fromTo 
                                        (from (to)) 
                                        (to (mapEntry (mover))) 
                                        count:(count at:(to))
                                    )
                                    includeSelf:False
                                    backtracking:True
                                )
                            )
                        )
                        (move
                            Select
                            (from (sites Mover "Home") if:(= (count at:(from)) 1))
                            (then 
                                (sow)
                            )
                        )
                    )
                    ifAfterwards:(< 0 (count in:(sites Next "Home")))
                )
            )
        )
        }
        (end
            (if (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1)) 
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )	
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Ti is a two-row mancala-style baord game played by the Mende people of Sierra Leone. It can be played on pedestaled boards, or as holes in the ground. The holes of the board are called \"towns,\" and the pieces are people moving from town to town.")
        (aliases {"Warri"})
        (rules "2x6 board with two stores. Four counters per hole. Sowing occurs in an anti-clockwise direction. In the first and second moves, players may choose to drop as many counters as they wish into the holes they are sowing into. From the third turn on, the players must sow one counter at a time. The hole from which a sowing began is skipped if the sowing goes all the way around the board. When the final counter lands in a hole containing one or two people, thus causing it to contain two or three, these are captured. Any previous holes, in an unbroken sequence, also containing two or three counters, are captured. A move of a single counter cannot capture. When a player's row is empty, the opponent must sow into their row if it is possible to do so. Play ends when one player's row is empty, and the opponent claims all of the remaining counters. The player with the most counters wins.")
        (source "Migeod 1926: 281-283.")
        (id "1307")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Sierra Leone, from around 1924 to 1925.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)
