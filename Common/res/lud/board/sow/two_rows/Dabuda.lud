(define "Columns" 10)

(define "OppositePit" (if (is In (to) (sites P1 "Home")) (+ (to) "Columns") (- (to) "Columns") ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "Sow"
    (move
        Select
        (from
            (if
                (is Mover Prev)
                "PlayFromLastHole"
                (sites Mover #1)
            )
            if:(> (count at:(from)) 0)
        )
        (then
            (sow
                apply:(if
                    (and {
                        (is In (to) (sites Mover "Home"))
                        (= (count at:(to)) 1)
                        (> (count at:"OppositePit") 0)
                    } )
                    (and
                        (fromTo
                            (from "OppositePit")
                            (to (handSite Mover))
                            count:(count at:"OppositePit")
                        )
                        (fromTo
                            (from (to))
                            (to (handSite Mover))
                            count:1
                        )
                    )
                    (if (> (count at:(to)) 1) (moveAgain) )
                )
            )
        )
    )
)

(define "PiecesOwnedBy" (+ (count Cell at:(handSite #1)) (count in:(sites #1 "Home"))))

//------------------------------------------------------------------------------

(game "Dabuda"
    (players 2)

    (equipment {
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (regions "Home" P1 (sites Bottom)) 			// P1 home
        (regions "Home" P2 (sites Top)) 				// P2 home
        (regions "RightMost" P1 (sites <Rightmost:sitesP1>))   // The Rightmost holes of the P1 home
        (regions "RightMost" P2 (sites <Rightmost:sitesP2>))  // The Rightmost  holes of the P2 home
        (piece "Seed" Shared)
        (hand Each)
        }
    )
    (rules

        (start (set Count 4 to:(sites Track) ) )

        phases:{
        (phase "Rightmost"
            (play ("Sow" "RightMost") )
            (nextPhase (= (count Turns) 2) "HomePhase")
        )
        (phase "HomePhase"
            (play ("Sow" "Home") )
        )
        }

        (end
            (if  (no Moves Mover)
                (byScore {
                    (score P1 ("PiecesOwnedBy" P1))
                    (score P2 ("PiecesOwnedBy" P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Opening" <Rightmost> args:{<sitesP1> <sitesP2>}
    {
    (item "One Rightmost hole" <{9}> <{10}>            "The player has to open the game with the rightmost hole.")
    (item "Four Rightmost holes" <{6..9}> <{10..13}>   "The player has to open the game with one of the four rightmost holes.")**
    }
)

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Afar (Observed)"
        {
        "Opening/One Rightmost hole"
        }
    )**

    (ruleset "Ruleset/Damohoita (Observed)"
        {
        "Opening/Four Rightmost holes"
        }
    )

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Dabuda is a two-row mancala-style game that is particularly popular among the Afar who live in the Horn of Africa, and described by men from Asaita, Jibuti and Thio.")
        (useFor "Ruleset/Damohoita (Observed)" (rules "Two rows of ten holes Four counters in each hole First play must be from the rightmost hole."))
        (useFor "Ruleset/Damohoita (Observed)" (source "Pankhurst 1971: 180."))
        (useFor "Ruleset/Afar (Observed)" (rules "2x10 board. Play begins with four counters in each hole. Play begins from one of the rightmost four in the player's row, sowing in an anti-clockwise direction. When the final counter lands in an occupied hole, these counters are picked up and sowing continues. Captures are made if the player's final counter lands in an empty hole in their row; the contents of the opponent's hole opposite and the final counter are both captured. If the hole opposite is empty, nothing happens. If the empty hole is in the opponent's row, the turn ends. After each player performs this move, they may sow from any hole in their row, provided the sowing leads into the opponent's row or finishes in an empty hole in the player's own row. Play continues until one player cannot play because their holes are empty. When this happens, the opponent captures the remaining counters. Each player counts their captured counters, four by four, into their holes. The player who has the most counters wins."))
        (useFor "Ruleset/Afar (Observed)" (source "Pankhurst 1971: 180."))
        (version "1.3.0")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Horn of Africa, around 1971.")
        }
    )

    (graphics {
        (board Style Mancala)
    })

    (ai
        "Dabuda_ai"
    )
)

