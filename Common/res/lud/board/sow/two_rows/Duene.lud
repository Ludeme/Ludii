(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))

(define "LastHoleSowed" (sites {(var "To")}))

//------------------------------------------------------------------------------

(game "Duene"
    (players 2)  
    (equipment { 
        (mancalaBoard 2 6
            (track "Track" "1,E,N,W" loop:True)
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
        (piece "Seed" Shared)
    })  
    
    (rules 
        (start (set Count 4 to:(sites Track)))
        phases:{
        (phase "Playing"
            (play 
                (move Select 
                    (from 
                        (if ("SameTurn") 
                            ("LastHoleSowed") 
                            (sites Mover) 
                        )  
                        if:(< 0 (count at:(from)))
                    )
                    (then 
                        (do
                            (and
                                (set Var "From" (last From))
                                (sow  
                                    apply:(and
                                        (set Var "To" (to))
                                        (if (= (count at:(to)) 4)
                                            (fromTo 
                                                (from (to)) 
                                                (to (mapEntry (mover))) 
                                                count:(count at:(to))
                                            )
                                            (if (> (count at:(to)) 1)
                                                (moveAgain)
                                            )
                                        )
                                    )
                                    
                                    backtracking:(= (count at:(to)) 4)
                                )
                            )
                            next:(and {
                                (pass)
                                (forEach Site
                                    (forEach (sites Track from:(var "From") to:(var "To"))
                                        if:(and (is In (site) (sites P1)) (= 4 (count at:(site))))
                                    )
                                    (fromTo
                                        (from (site))
                                        (to (mapEntry P1)) 
                                        count:(count at:(site))
                                    )
                                )
                                (forEach Site
                                    (forEach (sites Track from:(var "From") to:(var "To"))
                                        if:(and (is In (site) (sites P2)) (= 4 (count at:(site))))
                                    )
                                    (fromTo
                                        (from (site))
                                        (to (mapEntry P2)) 
                                        count:(count at:(site))
                                    )
                                )
                            })
                        )
                    )
                )
            )
            (nextPhase (<= (count in:(sites Track)) 8) "EndGame")
        )
        (phase "EndGame"
            (play 
                (move Select 
                    (from 
                        (if ("SameTurn") 
                            ("LastHoleSowed") 
                            (sites Mover) 
                        )  
                        if:(< 0 (count at:(from)))
                    )
                    (then 
                        (sow  
                            apply:(if (= (count at:(to)) 4)
                                (forEach Site 
                                    (sites Track)
                                    (if (< 0 (count at:(site)))
                                        (fromTo 
                                            (from (site)) 
                                            (to (mapEntry (mover))) 
                                            count:(count at:(site))
                                        )
                                    )
                                )
                                (if (> (count at:(to)) 1)
                                    (moveAgain)
                                )
                            )
                            
                        )
                    )
                )
            )
        )
        }

    (end ("MancalaByScoreWhen" (all Sites (sites Track) if:(= 0 (count at:(site))))))
    )	
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Duene is  two-row mancala-style board game played by the Ewe people in what is now togo and Ghana.")
        (rules "2x6 board with two stores. Four counters in each hole. Sowing occurs in an anti-clockwise direction. When the final counter lands in an occupied hole the counters are picked up and sowing continues, unless the hole contained three counters, causing it to contain four, in which case the counters are captured. An unbroken sequence of holes with four counters in them behind the hole from which the first capture was made are also captured. In addition, any holes which were sown into during the course of the turn which now contain four counters are captured by the owner of the row. Play continues until there are only eight counters left. The first player to make a capture with these eight counters captures them all. The player with the most counters wins.")
        (source "Schönhärl 1909: 147.")
        (id "1305")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in West Africa, from around 1908 to 1909.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
    (ai
        "Duene_ai"
    )
)
