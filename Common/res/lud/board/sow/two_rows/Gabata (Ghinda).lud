(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))
(define "NoPieceOnOwnedHolesP1" (all Sites (sites (values Remembered "OwnedP1")) if:(= 0 (count at:(site)))))
(define "NoPieceOnOwnedHolesP2" (all Sites (sites (values Remembered "OwnedP2")) if:(= 0 (count at:(site)))))
(define "RemmemberOwnedHoles" 
    (if (is Mover P1)
        (remember Value "OwnedP1" #1)
        (remember Value "OwnedP2" #1)
    )
)

(define "OwnedHoles" 
    (if (is Mover P1)
        (sites (values Remembered "OwnedP1"))
        (sites (values Remembered "OwnedP2"))
    )
)

(define "RememberOwnedHolesRandom" 
    (if (< 50 (value Random (range 1 100)))
        (remember Value "OwnedP1" #1)
        (remember Value "OwnedP2" #1)
    )
)

(define "LeftMostEmpty" 
    (trackSite FirstSite "Track"
        from:(mapEntry "LeftMost" Mover)
        if:(is Empty (to))
    )
)

//------------------------------------------------------------------------------

(game "Gabata (Ghinda)" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (map "LeftMost" {(pair P1 0) (pair P2 11)})
    }) 
    (rules 
        
        (start {
            (set RememberValue "OwnedP1" (sites Bottom))
            (set RememberValue "OwnedP2" (sites Top))
            (set Count 4 to:(sites Track "Track")) 
        })
        
        phases:{
        (phase "Sowing"
            (play
                (or {
                    (move Select 
                        (from
                            (if ("SameTurn")
                                (sites {(var "Replay")})
                                (if (= 1 (value Player Mover))
                                    ("OwnedHoles")
                                    (sites {(mapEntry "LeftMost" Mover)})
                                )
                            )
                            if:(is Occupied (from))
                        )
                        (then
                            (and
                                (do
                                    (set Var "NumSowed" (count at:(last To)))
                                    next:(sow
                                        apply:(if (< 1 (count at:(to)))
                                            (if (= 4 (count at:(to)))
                                                (fromTo
                                                    (from (to))
                                                    (to (handSite Mover))
                                                    count:4
                                                )
                                                (and
                                                    (moveAgain)
                                                    (set Var "Replay" (to))
                                                )
                                            )
                                        )
                                    )
                                    (then // No need of the Jen rule, because the capture is automatic in Ludii, so no errors are possible.
                                        (and
                                            (forEach Site (sites Track from:(last From) to:(trackSite Move from:(last From) "Track" steps:(- (var "NumSowed") 1)))
                                                (if (= 4 (count at:(site)))
                                                    (fromTo
                                                        (from (site))
                                                        (to (if (is In (site) (sites (values Remembered "OwnedP1"))) (handSite P1) (handSite P2)))
                                                        count:4
                                                    )
                                                )
                                            )
                                            (set Var "NumSowed" 0)
                                        )
                                    )
                                )
                                (if (!= 1 (value Player Mover)) (set Value Mover 1))
                            )
                        )
                    )
                    }
                    (then
                        (if (or ("NoPieceOnOwnedHolesP1") ("NoPieceOnOwnedHolesP2"))
                            (and {
                                (if ("NoPieceOnOwnedHolesP2")
                                    (forEach Site (sites Board)
                                        (fromTo
                                            (from (site))
                                            (to (handSite P1))
                                            count:(count at:(site))
                                        )
                                    )
                                    (forEach Site (sites Board)
                                        (fromTo
                                            (from (site))
                                            (to (handSite P2))
                                            count:(count at:(site))
                                        )
                                    )
                                )
                                (forget Value "OwnedP1" All)
                                (forget Value "OwnedP2" All)
                            })
                        )
                    )
                )
            )
            
            (end (if ("NoPieceOnBoard")
                    {
                    (if (= 0 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (= 0 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
            
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                (if (<= 4 (count Cell at:(handSite Mover)))
                    (move
                        (from (handSite Mover))
                        (to ("LeftMostEmpty"))
                        count:4
                        (then
                            (and {
                                ("RemmemberOwnedHoles" (last To))
                                (if (<= 4 (count Cell at:(handSite Mover)))
                                    (moveAgain)
                                    (if (= 3 (count Cell at:(handSite Mover)))
                                        (and {
                                            (fromTo 
                                                (from (handSite Mover))
                                                (to ("LeftMostEmpty"))
                                                count:3
                                            )
                                            (fromTo 
                                                (from (handSite Next))
                                                (to ("LeftMostEmpty"))
                                                count:1
                                            )
                                            ("RemmemberOwnedHoles" ("LeftMostEmpty"))
                                        })
                                        (if (= 2 (count Cell at:(handSite Mover)))
                                            (and {
                                                (fromTo 
                                                    (from (handSite Mover))
                                                    (to ("LeftMostEmpty"))
                                                    count:2
                                                )
                                                (fromTo 
                                                    (from (handSite Next))
                                                    (to ("LeftMostEmpty"))
                                                    count:2
                                                )
                                                ("RememberOwnedHolesRandom" ("LeftMostEmpty"))
                                            })
                                        )
                                    )
                                )
                            })
                        )
                    )
                )
            )
            (nextPhase (and (is Empty (handSite P1)) (is Empty (handSite P2))) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Gabata is a term for many games played throughout the Horn of Africa. This version is played in Ghinda, and is similar to others played throughout Eritrea.")
        (rules "2x6 board. Four counters per hole. Each player takes their first turn from their leftmost hole. Sowing occurs in an anti-clockwise direction. If the final counter falls into an occupied hole, these are picked up and sowing continues. If the final counter falls into an empty hole, the turn ends. At any time during the sowing, if a hole is made to contain four counters, the owner of the hole in which the four counters are located captures them, unless it is the final hole of the sowing, in which case the player who is sowing captures them. The captures happen immediately. If a player does not capture when they should have, and a subsequent sowing causes this hole to contain five counters, it becomes a Jen. Players can no longer capture from a Jen in the same manner as before. Captures are made from it when the final counter of a sowing lands in it, in which case the final counter and one counter from the Jen are captured by the player who is sowing. Play continues until a player can no longer move because there are no counters in their row, and the opponent captures the remaining counters. Players then count their pieces by placing four in each hole, and the player who has more than their original number takes ownership of one of the opponent's holes for every four counters more than the original number that have been taken. If no player took four more than the original, the player with three extra gets the hole, if each player has two extra the weaker player is given the two extra counters. Play then begins again as before. The game ends when one player owns all of the counters, and thus all of the holes.")
        (source "Pankhurst 1971: 172.")
        (id "661")
        (version "1.3.5")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Horn of Africa, around 1971.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
