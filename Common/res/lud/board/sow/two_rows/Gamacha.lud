(define "Columns" 12)

(define "NextHoleFrom" (trackSite Move from:#1 #2))

(define "OppositePit" (if (is Mover P1) (+ (to) "Columns") (- (to) "Columns") ) )

(define "PlayFromLastHole" (sites {(last To afterConsequence:True)}))

(define "NoPiecesInPlayerSide" 
    (no Pieces All in:(sites #1))
)

//------------------------------------------------------------------------------

(game "Gamacha"  
    (players 2)  
    
    (equipment { 
        (mancalaBoard 2 "Columns" store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom)) 
        (regions P2 (sites Top)) 
        (hand Each)
    })  
    (rules 
        
        (start 
            (set Count 4 
                to:(sites {"C1" "E1" "G1" "I1" "K1" "L2" "J2" "H2" "F2" "D2" "B2"}) 
            ) 
        )
        
        phases:{
        (phase "Opening"
            (play 
                (move 
                    (from 6)
                    (to 18)
                    count:4
                )
            )
            (nextPhase "Sowing")
        )
        (phase "Sowing"
            (play 
                (move Select
                    (from 
                        (if 
                            (is Mover Prev) 
                            "PlayFromLastHole" 
                            (sites Mover) 
                        ) 
                        if:(if  (is Mover Prev) 
                            True
                            (and
                                (> (count at:(from)) 0)
                                (if (is In ("NextHoleFrom" (from) steps:(count at:(from))) (sites Next))
                                    True
                                    (is Empty ("NextHoleFrom" (from) steps:(count at:(from))))
                                )
                            )
                        )
                    )
                    (then 
                        (sow
                            apply:(if (and (is In (to) (sites Mover)) (<= (count at:(to)) 1))
                                (if (!= 0 (count at:("OppositePit")))
                                    (fromTo
                                        (from ("OppositePit"))
                                        (to (handSite Mover))
                                        count:(count at:("OppositePit"))
                                    )
                                )
                                (if (> (count at:(to)) 1)
                                    (moveAgain)
                                )
                            )
                        )
                        
                    )
                )
            )
        )
        }
        
        (end {
            (if  ("NoPiecesInPlayerSide" P1)
                (result P2 Win)
            )
            (if  ("NoPiecesInPlayerSide" P2)
                (result P1 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata  
    (info
        {
        (description "Gamacha is a two-row mancala-style board game played by the Hadiya people of Ethiopia. This version of the game was considered to be the older version of several types played.")
        (rules "2x12 board. The board begins with the following pattern of counters in the holes, beginning from the bottom left row and proceeding in an anti-clockwise direction: 0-0-4-0-4-0-4-0-4-0-4-0-4-0-4-0-4-0-4-0-4-0-4-0. One player starts with an additional four counters in their left hand hole. The player with fewer counters on their first move places the counters from their seventh hole into the opponent's opposite hole. Sowing occurs in an anti-clockwise direction, and may only happen when the final counter lands either in an empty hole in the player's own row or in any hole in the opponent's row. When the final counter lands in an empty hole in the player's own row, the contents of the opposite hole in the opponent's row are captured. When the final counter lands in an occupied hole in the opponent's row, these counters are picked up and sowing continues. The final counter of this sowing is allowed to fall into an occupied hole in the player's own row, and these are picked up and sowing continues. Sowing ends when the final counter lands in an empty hole. The game ends when one player has no counters on their side of the board, and the opponent wins.")
        (source "Pankhurst 1972: 184-185.")
        (id "1381")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Ethiopia, around 1971.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
