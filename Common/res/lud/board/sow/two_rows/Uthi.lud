(define "Columns" 8)
(define "PiecesOwnedBy" (count at:(mapEntry #1)))
(define "NextHole" ("NextSiteOnTrack" #3 from:#1 #2))
(define "Capture" 
    (if (and (= 1 (var "GoInOpponentRow")) (is In (to) (sites Mover)))
        (if (!= 0 (count at:("OppositePit" (to))))
            (fromTo
                (from ("OppositePit" (to)))
                (to (mapEntry Mover))
                count:(count at:("OppositePit" (to)))
                (then
                    (if (and (is Empty ("NextHole" (last From) #1 1)) (is In ("NextHole" (last From) #1 1) (sites Mover)))
                        (and
                            (if (!= 0 (count at:("NextHole" (last From) #1 1)))
                                (fromTo
                                    (from ("NextHole" (last From) #1 1))
                                    (to (mapEntry Mover))
                                    count:(count at:("NextHole" (last From) #1 1))
                                )
                            )
                            (if (and (is Empty ("NextHole" (last From) #1 2)) (is In ("NextHole" (last From) #1 2) (sites Mover)))
                                (and
                                    (if (!= 0 (count at:("NextHole" (last From) #1 2)))
                                        (fromTo
                                            (from ("NextHole" (last From) #1 2))
                                            (to (mapEntry Mover))
                                            count:(count at:("NextHole" (last From) #1 2))
                                        )
                                    )
                                    (if (and (is Empty ("NextHole" (last From) #1 3)) (is In ("NextHole" (last From) #1 3) (sites Mover)))
                                        (and
                                            (if (!= 0 (count at:("NextHole" (last From) #1 3)))
                                                (fromTo
                                                    (from ("NextHole" (last From) #1 3))
                                                    (to (mapEntry Mover))
                                                    count:(count at:("NextHole" (last From) #1 3))
                                                )
                                            )
                                            (if (and (is Empty ("NextHole" (last From) #1 4)) (is In ("NextHole" (last From) #1 4) (sites Mover)))
                                                (and
                                                    (if (!= 0 (count at:("NextHole" (last From) #1 4)))
                                                        (fromTo
                                                            (from ("NextHole" (last From) #1 4))
                                                            (to (mapEntry Mover))
                                                            count:(count at:("NextHole" (last From) #1 4))
                                                        )
                                                    )
                                                    (if (and (is Empty ("NextHole" (last From) #1 5)) (is In ("NextHole" (last From) #1 5) (sites Mover)))
                                                        (and
                                                            (if (!= 0 (count at:("NextHole" (last From) #1 5)))
                                                                (fromTo
                                                                    (from ("NextHole" (last From) #1 5))
                                                                    (to (mapEntry Mover))
                                                                    count:(count at:("NextHole" (last From) #1 5))
                                                                )
                                                            )
                                                            (if (and (is Empty ("NextHole" (last From) #1 6)) (is In ("NextHole" (last From) #1 6) (sites Mover)))
                                                                (and
                                                                    (if (!= 0 (count at:("NextHole" (last From) #1 6)))
                                                                        (fromTo
                                                                            (from ("NextHole" (last From) #1 6))
                                                                            (to (mapEntry Mover))
                                                                            count:(count at:("NextHole" (last From) #1 6))
                                                                        )
                                                                    )
                                                                    (if (and (is Empty ("NextHole" (last From) #1 7)) (is In ("NextHole" (last From) #1 7) (sites Mover)))
                                                                        (if (!= 0 (count at:("NextHole" (last From) #1 7)))
                                                                            (fromTo
                                                                                (from ("NextHole" (last From) #1 7))
                                                                                (to (mapEntry Mover))
                                                                                count:(count at:("NextHole" (last From) #1 7))
                                                                            )
                                                                        )
                                                                    )	
                                                                )
                                                            )	
                                                        )
                                                    )	
                                                )
                                            )	
                                        )
                                    )	
                                )
                            )	
                        )
                    )		
                )
            )
        )
    )		
)

//------------------------------------------------------------------------------

(game "Uthi"
    (players 2)  
    (equipment { 
        (mancalaBoard 2 "Columns"
            {
            (track "TrackCW"  "8,W,N,E" loop:True)
            (track "TrackCCW" "1,E,N,W" loop:True)
            }
        )
        (regions P1 (sites Bottom))                     // P1 home
        (regions P2 (sites Top))                        // P2 home
        (map {(pair P1 FirstSite) (pair P2 LastSite)})  // kalahs (storage pits)
        (piece "Seed" Shared)
    })  
    
    (rules 
        (start (set Count 5 to:(sites Track)))
        
        phases:{
        (phase "Opening"
            (play
                (or
                    (move Select
                        (from (sites Mover))
                        (then
                            (and
                                (fromTo 
                                    (from (last To))
                                    (to ("NextHole" (last To) "TrackCW" 1))
                                    count:3
                                )
                                (fromTo 
                                    (from (last To))
                                    (to ("NextHole" (last To) "TrackCW" 2))
                                    count:2
                                )
                            )
                        )
                    )
                    (move Select
                        (from (sites Mover))
                        (then
                            (and
                                (fromTo 
                                    (from (last To))
                                    (to ("NextHole" (last To) "TrackCCW" 1))
                                    count:3
                                )
                                (fromTo 
                                    (from (last To))
                                    (to ("NextHole" (last To) "TrackCCW" 2))
                                    count:2
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase Mover "Sowing")
        )
        
        (phase "Sowing"
            (play
                (or
                    (if (!= (value Player Mover) 1)
                        (move Select
                            (from 
                                (if 
                                    ("SameTurn") 
                                    "LastHoleSowed" 
                                    (sites Mover) 
                                ) 
                                if:(> (count at:(from)) 0)
                            )
                            (then 
                                (sow
                                    "TrackCW"
                                    apply:(if (> (count at:(to)) 1)
                                        (and {
                                            (moveAgain)
                                            (set Value Mover 1)
                                            (if (is In (to) (sites Next))
                                                (set Var "GoInOpponentRow" 1)
                                            )
                                        })
                                        (and {
                                            (set Value Mover 0)
                                            (set Var "GoInOpponentRow" 0)
                                            ("Capture" "TrackCW")
                                        })
                                    )
                                    
                                )
                            )
                        )
                    )
                    
                    (if (!= (value Player Mover) 2)
                        (move Select
                            (from 
                                (if 
                                    ("SameTurn") 
                                    "LastHoleSowed" 
                                    (sites Mover) 
                                ) 
                                if:(> (count at:(from)) 0)
                            )
                            (then 
                                (sow
                                    "TrackCCW"
                                    apply:(if (> (count at:(to)) 1)
                                        (and {
                                            (moveAgain)
                                            (set Value Mover 2)
                                            (if (is In (to) (sites Next))
                                                (set Var "GoInOpponentRow" 1)
                                            )
                                        })
                                        (and {
                                            (set Value Mover 0)
                                            (set Var "GoInOpponentRow" 0)
                                            ("Capture" "TrackCCW")
                                        })
                                    )
                                    
                                )
                            )
                        )
                    )
                )
            )
        )
        
        }
        
        (end ("MancalaByScoreWhen" (no Moves Mover)))
    )	
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Uthi is a two-row mancala-style board game played by the Embu people of Kenya.")
        (rules "2x8 and two stores. Five counters per hole. On each player's first move, they sow three counters into the first hole and two into the second hole. After this, sowing proceeds as usual, with one counter in each hole. Sowing may occur in either direction. When the final counter lands in an occupied hole, the contents of this hole are picked up and sowing continues in the opposite direction. When the final counter falls in an empty hole in the player's own row, the contents of the opposite hole are captured. If the final counter falls into the first of a series of empty holes, the contents of all of the holes opposite the empty holes are captured. It is necessary to enter the opponent's row first and return before making a capture.If the final counter lands in an empty hole in the opponent's row, the turn ends. The winner is the player who has captured the most counters.")
        (source "Orde Brown 1925: 125-128.")
        (id "1518")
        (version "1.3.4")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin  "This game was played in Kenya, around 1925.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    
)
