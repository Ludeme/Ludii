(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))

(define "RemmemberSowedHoles" 
    (if (is Mover P1)
        (remember Value "SowedHolesP1" #1 unique:True)
        (remember Value "SowedHolesP2" #1 unique:True)
    )
)

(define "SowedHoles" 
    (if (is Mover P1)
        (values Remembered "SowedHolesP1")
        (values Remembered "SowedHolesP2")
    )
)

//------------------------------------------------------------------------------

(game "Adjiboto" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 5 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
    }) 
    (rules 
        
        (start (set Count 10 to:(sites Track)))
        
        phases:{
        (phase "Opening"
            (play
                (move Select
                    (from (difference (sites Mover) (sites ("SowedHoles"))) if:(< 1 (count at:(to))))
                    (then
                        (and
                            (sow origin:True)
                            ("RemmemberSowedHoles" (last From))
                        )
                    )
                )
            )
            (nextPhase Mover (< 8 (count Turns)))
        )
        (phase "Main"
            (play
                (move Select
                    (from (sites Mover) if:(< 1 (count at:(to))))
                    (then
                        (sow
                            if:(or {
                                (= 1 (count at:("NextHole" (to) 1)))
                                (= 3 (count at:("NextHole" (to) 1)))
                                (= 5 (count at:("NextHole" (to) 1)))
                            })
                            apply:(fromTo
                                (from ("NextHole" (to) 1))
                                (to (handSite Mover))
                                count:(count at:("NextHole" (to) 1))
                            )
                            origin:True		
                            forward:True
                        )
                    )
                )
            )
            (end 
                (if (no Moves Next) 
                    {
                    (if (< (count Cell at:(handSite Next)) 50) (result Mover Win))
                    (if (>= (count Cell at:(handSite Next)) 50) (result Mover Draw))
                    }
                )
                
            )
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Adjiboto is a two-row mancala-style board game that was observed in the early twentieth century in Beidotti village, Suriname, where Saamaka people lived. The board was kept in an open structure which served as the meeting place for the village council, house of mourning, and to accommodate travelers.")
        (rules "2x5 board. Ten counters in each hole. Sowing occurs in an anti-clockwise direction, sowing first into the hole from which the counters originally came. Opening phase: In the first ten turns, each player must sow from each of their five holes. Main phase: Captures are made when the last counter of a sowing falls into a hole preceding a hole containing one, three, or five counters. These counters are taken. Any subsequent holes also containing one, three, or five counters are captured, until a hole is reached that does not have one of these numbers of counters. The exception to this is in the first move when no captures are made. Sowing cannot begin from a hole with a single counter. A player wins when his opponent can no longer sow. However, if a player can no longer move, they can redistribute their captured beans into their holes, and if all five can be filled with ten, the game is a draw.")
        (source "Herskovits 1929: 123-124.")
        (id "252")
        (version "1.3.13")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Adjiboto_ai"
    )
)
