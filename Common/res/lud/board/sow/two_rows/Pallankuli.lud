(define "PiecesOwnedBy" (+ (count at:(mapEntry #1)) (count in:(sites #1))))
(define "PlayableSites" (sites (values Remembered "Playable")))
(define "OneRowIsEmpty"
    (or
        (all Sites (sites Bottom) if:(= 0 (count at:(site))))
        (all Sites (sites Top) if:(= 0 (count at:(site))))
    )
)
(define "LeftMostEmpty" 
    (trackSite FirstSite 
        from:(mapEntry "LeftMost" Mover)
        if:(is Empty (to))
    )
)
(define "NextHoleFrom" ("NextSiteOnTrack" #2 from:#1))

//------------------------------------------------------------------------------

(game "Pallankuli" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 7 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
        (map "LeftMost" {(pair P1 0) (pair P2 13)})
        (hand Each)
    }) 
    (rules 
        (start { 
            (set Count 6 to:(sites Track)) 
            (set RememberValue "Playable" (union (sites Top) (sites Bottom)))
        })
        
        phases:{
        (phase "Sowing"
            (play
                (or {
                    (move Select 
                        (from 
                            (if ("SameTurn") 
                                (sites {(var "Replay")})
                                (sites Mover) 
                            ) 
                            if:(and (is Occupied (from)) (is In (from) ("PlayableSites")))
                        )
                        (then
                            (do
                                (set Var "NumSowed" (count at:(last To)))
                                next:(sow
                                    apply:(if 
                                        (and 
                                            (!= 4 (count at:(to))) 
                                            (< 1 (count at:(to)))
                                        ) 
                                        (and
                                            (moveAgain)
                                            (set Var "Replay" (to))
                                        )
                                        (if (= 1 (count at:(to)))
                                            (if (is Occupied ("NextHoleFrom" (to) 1))
                                                (fromTo
                                                    (from ("NextHoleFrom" (to) 1))
                                                    (to (handSite Mover))
                                                    count:(count at:("NextHoleFrom" (to) 1))
                                                )
                                            )
                                        )
                                    )
                                    skipIf:(not (is In (to) ("PlayableSites")))
                                )
                                (then
                                    (and
                                        (forEach Site (sites Track from:(last From) to:(trackSite Move from:(last From) "Track" steps:(var "NumSowed")))
                                            (if (= 4 (count at:(site)))
                                                (fromTo
                                                    (from (site))
                                                    (to (if (is In (to) (sites P1)) (handSite P1) (handSite P2)))
                                                    count:4
                                                )
                                            )
                                        )
                                        (set Var "NumSowed" 0)
                                    )
                                )
                            )
                        )
                    )
                    }
                    (then
                        (if ("OneRowIsEmpty")
                            (and { 
                                (forEach Site (sites P1)
                                    (fromTo
                                        (from (site))
                                        (to (handSite P1))
                                        count:(count at:(site))
                                    )
                                )
                                (forEach Site (sites P2)
                                    (fromTo
                                        (from (site))
                                        (to (handSite P2))
                                        count:(count at:(site))
                                    )
                                )
                                (forget Value "Playable" All)
                            })
                        )
                    )
                )
            )
            
            (end (if ("NoPieceOnBoard")
                    {
                    (if (> 6 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (> 6 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
            
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
            
        )
        
        (phase "BetweenRounds"
            (play
                (if (<= 6 (count Cell at:(handSite Mover)))
                    (move
                        (from (handSite Mover))
                        (to ("LeftMostEmpty") if:(is In (to) (sites Mover)))
                        count:6
                        (then
                            (remember Value "Playable" (last To))
                        )
                    )
                )
            )
            (nextPhase (all Passed) "Sowing")
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Pallankuli is a two-row mancala-style board game played primarily by women in South India. Men do play the game, but typically gambled when playing.")
        (aliases {"Pallanguli"})
        (rules "2x7 board. Six counters in each hole. Play begins from any one of a player's holes, even if there is just one. Sowing occurs in an anti-clockwise direction. If they end in a hole with counters, these are picked up and sowing continues. If sowing ends in an empty hole, the contents of the next hole are captured and the turn ends. When sowing, if the contents of a hole are brought to four, they are immediately captured by the player in whose row the counters are located. Play ends when a player has no counters in their holes, remaining player takes all the remaining counters. In the next round, the player with the smaller number of counters captured from the previous round fills as many of their holes as they can, starting from the left and filling each hole with six counters. Leftover counters are placed in the player's store. The opponent then does the same. Any holes remaining empty are out of play for this round, otherwise play continues as before. The right to begin alternates from round to round. Further rounds are played until one player has fewer than six counters.")
        (source "Durai 1928: 135.")
        (id "215")
        (version "1.3.7")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
    (ai
        "Pallankuli_ai"
    )
)

