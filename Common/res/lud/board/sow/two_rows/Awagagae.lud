(define "NextHole" ("NextSiteOnTrack" #2 from:#1 "Track"))

(define "OwnedHoles" 
    (if (is Mover P1)
        (sites (values Remembered "OwnedP1"))
        (sites (values Remembered "OwnedP2"))
    )
)

(define "PossibleSowFrom" 
    (if (is Mover P1)
        (not (is In (from) (values Remembered "CapturedP2")))
        (not (is In (from) (values Remembered "CapturedP1")))
    )
)

(define "CapturedByOpponent" 
    (if (= #1 P1) 
        (is In #2 (values Remembered "CapturedP2"))
        (is In #2 (values Remembered "CapturedP1"))
    )
)

(define "CapturedHole"
    (if (= #1 P1) 
        (is In #2 (values Remembered "CapturedP1"))
        (is In #2 (values Remembered "CapturedP2"))
    )		
)

(define "NoFreePiece"
    (no Pieces in:(forEach (sites Board) 
            if:(and 
                (not (is In (site) (values Remembered "CapturedP1")))
                (not (is In (site) (values Remembered "CapturedP2")))
            )
        )
    )
)

(define "RemmemberOwnedHoles" 
    (if (is Mover P1)
        (remember Value "OwnedP1" #1)
        (remember Value "OwnedP2" #1)
    )
)

//------------------------------------------------------------------------------

(game "Awagagae" 
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            (track "Track" "0,E,N,W" loop:True)
        )
        (piece "Seed" Shared)
        (hand Each)
        (regions P1 (sites Bottom))      // P1 home
        (regions P2 (sites Top))      // P2 home
    }) 
    (rules 
        (start {
            (set RememberValue "OwnedP1" (sites Bottom))
            (set RememberValue "OwnedP2" (sites Top))
            (set Count 4 to:(sites Track))
        })
        
        phases:
        {
        (phase "Sowing"
            (play 
                (or {
                    (move Select
                        (from 
                            (if 
                                ("SameTurn") 
                                "LastHoleSowed" 
                                ("OwnedHoles") 
                            )
                            if:(and ("PossibleSowFrom") (< 0 (count at:(from))))
                        )
                        (then 
                            (sow 
                                "Track"
                                apply:(if (not ("CapturedHole" (mover) (to)))
                                    (if (and 
                                            (is In (to) (sites Next)) 
                                            (= (count at:(to)) 4)
                                        ) 
                                        (if (is Mover P1)
                                            (remember Value "CapturedP1" (to))
                                            (remember Value "CapturedP2" (to))
                                        )
                                        (if ("CapturedByOpponent" (mover) (to))
                                            (fromTo
                                                (from (to))
                                                (to (handSite Next))
                                                count:1
                                            )
                                            (if (> (count at:(to)) 1)
                                                (moveAgain)
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                    }
                    (then 
                        (if ("NoFreePiece")
                            (and { 
                                (forEach Site (sites Board)
                                    (if 
                                        (and
                                            (is In (site) (values Remembered "CapturedP1"))
                                            (is Occupied (site))
                                        )
                                        (fromTo
                                            (from (site))
                                            (to (handSite P1))
                                            count:(count at:(site))
                                        )
                                    )
                                )
                                (forEach Site (sites Board)
                                    (if 
                                        (and
                                            (is In (site) (values Remembered "CapturedP2"))
                                            (is Occupied (site))
                                        )
                                        (fromTo
                                            (from (site))
                                            (to (handSite P2))
                                            count:(count at:(site))
                                        )
                                    )
                                )
                                (forget Value "OwnedP1" All)
                                (forget Value "OwnedP2" All)
                                (forget Value "CapturedP1" All)
                                (forget Value "CapturedP2" All)
                            })
                        )
                    )
                )
            )
            (end (if ("NoPieceOnBoard")
                    {
                    (if (= 0 (count Cell at:(handSite P1))) (result P2 Win))
                    (if (= 0 (count Cell at:(handSite P2))) (result P1 Win))
                    }
                )
            )
            (nextPhase ("NoPieceOnBoard") "BetweenRounds")
        )
        
        (phase "BetweenRounds"
            (play
                
                (if (<= 4 (count Cell at:(handSite Mover)))
                    (move
                        (from (handSite Mover))
                        (to (if (all Sites (sites Mover) if:(is Occupied (site))) (sites Next) (sites Mover))
                            if:(is Empty (to))
                        )
                        count:4
                        (then
                            (and 
                                ("RemmemberOwnedHoles" (last To))
                                (if (and (> 4 (count Cell at:(handSite P1))) (> 4 (count Cell at:(handSite P2))))
                                    (and {
                                        (fromTo 
                                            (from Cell (handSite P1))
                                            (to (sites Empty))
                                            count:(count Cell at:(handSite P1))
                                        )
                                        (fromTo 
                                            (from Cell (handSite P2))
                                            (to (sites Empty))
                                            count:(count Cell at:(handSite P2))
                                        )
                                        (if (= 3 (count Cell at:(handSite P1))) 
                                            (remember Value "OwnedP1" (regionSite (sites Empty) index:0))
                                            (remember Value "OwnedP2" (regionSite (sites Empty) index:0))
                                        )
                                    })
                                )
                            )
                        )
                    )
                )
            )
            (nextPhase (and (is Empty (handSite P1)) (is Empty (handSite P2))) "Sowing")
        )
        
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Awagagae is a two-row mancala-style board game from Eritrea. It is closely related to other Gabata games from the region.")
        (aliases {"Gabata"})
        (rules "2x6 board. Play begins with four counters in each hole. A player picks up the contents of any of their holes and sowing them in an anti-clockwise direction, picking up the contents of the last hole in which his counters fall, and continuing sowing. This continues until the last counter falls in an empty hole. Then it is the other player's turn. A hole is captured when the last ball is dropped into an opponent's hole containing three counters, making it four. A player cannot then take from one of these holes that they have captured. Therefore, the player cannot begin a turn from their own captured hole nor can they continue sowing from it. If the last counter of a sowing falls into an opponent's captured hole, nothing happens on the first instance, but every time after that, one of those counters are removed from the board and placed in the store, and the sowing ends. Ownership of a hole continues even if it becomes empty. When a player cannot move (i.e., there are no counters in their holes except any that are in captured holes), the opponent continues to move until the player is able to move. Play continues until all counters are either placed in the store or in captured holes and thus cannot be moved. Players then count their pieces by placing four in each hole, and the player who has more than their original number takes ownership of one of the opponent's holes for every four counters more than the original number that have been taken. If no player took four more than the original, the player with three extra gets the hole, if each player has two extra the weaker player is given the two extra counters. Play then begins again as before. The game ends when one player owns all of the counters, and thus all of the holes.")
        (source "Pankhurst 1971: 169")
        (id "659")
        (version "1.3.6")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Horn of Africa, around 1971.")
        }
    )
    
    (graphics {
        (board Style Mancala)
    })
)
