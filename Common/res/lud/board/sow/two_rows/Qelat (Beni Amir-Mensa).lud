(define "PiecesOwnedBy" (count in:(forEach (sites Board) if:(= (id #1) (state at:(site))))))

//------------------------------------------------------------------------------

(game "Qelat (Beni Amir-Mensa)"
    (players 2) 
    
    (equipment { 
        (mancalaBoard 2 6 store:None
            {
            (track "TrackCCW" "0,E,N,W" loop:True)
            (track "TrackCW" "5,W,N,E" loop:True)
            }
        )
        (piece "Seed" Shared)
        (regions "Left" P1 (intersection (expand (sites Left) steps:2) (sites Bottom))) // P1 home left
        (regions "Left" P2 (intersection (expand (sites Right) steps:2) (sites Top))) // P2 home left
        (regions "Right" P1 (intersection (expand (sites Right) steps:2) (sites Bottom))) // P1 home right
        (regions "Right" P2 (intersection (expand (sites Left) steps:2) (sites Top)))  // P2 home right
    }) 
    (rules 
        (start (set Count 4 to:(sites Track)))
        
        (play 
            (or 
                (move Select
                    (from (sites Mover "Left") if:(and (= 0 (state at:(from))) (is Occupied (from))))
                    (then
                        (sow
                            "TrackCW"
                            apply:(if (and {
                                    (is In (to) (union (expand (sites Right)) (expand (sites Left))))
                                    (= 4 (count at:(to)))
                                    (= 0 (state at:(to)))
                                })
                                (set State at:(to) (mover))
                            )
                        )
                    )
                )
                (move Select
                    (from (sites Mover "Right") if:(and (= 0 (state at:(from))) (is Occupied (from))))
                    (then
                        (sow
                            "TrackCCW"
                            apply:(if (and {
                                    (is In (to) (union (expand (sites Right)) (expand (sites Left))))
                                    (= 4 (count at:(to)))
                                    (= 0 (state at:(to)))
                                })
                                (set State at:(to) (mover))
                            )
                        )
                    )
                )
            )
        )
        
        (end ("MancalaByScoreWhen" (and (no Moves P1) (no Moves P2))))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Qelat is a two-row mancala-style board game played by the Beni Amir and Mensa people of Eritrea. This game is typically played by older men, and is played for large stakes. Instead of capturing counters, players create \"walda,\" captured holes in which counters accumulate throughout the game.")
        (rules "2x6 board. Four counters in each hole. Sowing occurs in a clockwise direction when starting from the three holes on the left of the board, and in an anti-clockwise direction when starting from the three holes on the right. Sowing ends when the final counter is dropped, regardless of whether the hole is occupied or not. When the final counter is dropped into a hole containing three counters, thus making it contain four, the hole is captured. This can only be done if the hole is one of the end holes on either side of either row, or the penultimate holes on either side of the opponent's row. Sowing is not allowed from a captured hole. When no player is able to move, the player with the most counters in their captured holes wins.")
        (source "Pankhurst 1971: 171.")
        (id "347")
        (version "1.3.11")
        (classification "board/sow/two rows")
        (credit "Eric Piette")
        (origin "This game was played in Horn of Africa, around 1971.")
        }
    )
    
    (graphics {
        (board Style Mancala)
        (show LocalStateHoles)
    })
    (ai
        "Qelat (Beni Amir-Mensa)_ai"
    )
)

