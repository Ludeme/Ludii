(define "Custodial"
    (custodial
        (from (last To))
        Orthogonal
        (between
            (max 1)
            if:(= (what at:(between)) (id #1))
            (apply (remove (between)))
        )
        (to
            if:(or {
                (is Friend (who at:(to)))
                (is In (to) (sites "Fortresses"))
                (and
                    (is In (to) (sites Centre))
                    (is Empty (to))
                )
            })
        )
    )
)

(define "CaptureJarl" (apply (trigger "Surrounded" P2) ))

(define "JarlWasCaptured" (is Triggered "Surrounded" P2))

(define "EmptyButNotFortress"
    (and
        (not (is In (between) (sites "Fortresses")))
        (is Empty (between))
    )
)

(define "AFriendOrTheCentre"
    (or
        (is Friend (who at:(to)))
        (is In (to) (sites Centre))
    )
)

(define "JarlAndExpandedCentre" (and (= (what at:(between)) (id "Jarl2")) (is In (between) (expand origin:(centrePoint)  Orthogonal))))

(define "JarlButNotTheExpandedCentre" (and (= (what at:(between)) (id "Jarl2")) (not (is In (between) (expand origin:(centrePoint)  Orthogonal)))))

//------------------------------------------------------------------------------

(game "Alea Evangelii"
    (players 2)
    (equipment {
        (board (square 19))
        (regions "Fortresses" (expand (sites Corners)))

        (piece "Thrall" P1
            (move
                Slide
                Orthogonal
                (between if:"EmptyButNotFortress")
                (to (apply if:(!= (to) (centrePoint))))
                (then
                    (or {
                        ("Custodial" "Thrall2")
                        (surround
                            (from (last To))
                            Orthogonal
                            (between
                                if:"JarlAndExpandedCentre"
                                "CaptureJarl"
                            )
                            (to if:"AFriendOrTheCentre")
                        )
                        (custodial
                            (from (last To))
                            Orthogonal
                            (between
                                if:"JarlButNotTheExpandedCentre"
                                "CaptureJarl"
                            )
                            (to if:(is Friend (who at:(to))))
                        )
                    })
                )
            )
        )
        (piece "Thrall" P2
            (move
                Slide
                Orthogonal
                (between if:"EmptyButNotFortress")
                (to (apply if:(!= (to) (centrePoint))))
                (then ("Custodial" "Thrall1") )
            )
        )
        (piece "Jarl" P2
            (move
                Slide
                Orthogonal
                (to (apply if:(!= (to) (centrePoint)) ))
            )
        )
        }
    )
    (rules
        (start
            {
            (place "Thrall1" {"C1" "F1" "N1" "Q1" "A3" "F3" "N3" "S3" "H4" "L4" "G5" "I5" "K5" "M5"
                "A6" "C6" "Q6" "S6" "E7" "O7" "D8" "P8" "E9" "O9" "E11" "O11" "D12"
                "P12" "E13" "O13" "A14" "C14" "Q14" "S14" "G15" "I15" "K15" "M15" "H16" "L16"
                "A17" "F17" "N17" "S17" "C19" "F19" "N19" "Q19"
                }
            )
            (place "Thrall2" {"J4" "F6" "N6" "J7" "I8" "K8" "H9" "J9" "L9" "D10" "G10" "I10" "K10" "M10" "P10"
                "H11" "J11" "L11" "I12" "K12" "J13" "F14" "N14" "J16"
                }
            )
            (place "Jarl2" (centrePoint))
            }
        )

        (play (forEach Piece))

        (end
            {
            (if (is Within (id "Jarl2") in:(sites "Fortresses") ) (result P2 Win))
            (if "JarlWasCaptured"  (result P1 Win))
            }
        )
    )
)

//--------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <type> }
    {
    (item "Cyningstan"   <>       "The rules are describing with the Cyningstan ruleset.")
})

//--------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Cyningstan (Suggested)" {
        "Variant/Cyningstan"
    })*

    (ruleset "Ruleset/Historical Information (Incomplete)" {

    })

})

//--------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Alea Evangelii is a game that was discussed in Corpus Christi College Manuscript 122, dated to the twelfth century CE and probably written at Bangor Abbey, Ireland. The document illustrates the board with pieces on it and provides an ecclesiastical explanation for the board with reference to the Christian Gospels. It seems to be a Christian justification for an earlier game, which appears to have been similar to other known tafl games. Modern scholars have suggested that the author of the manuscript may have made several errors in reconstructing the board and the placement of the pieces.")
        (aliases {"Alea"})
        (useFor "Ruleset/Cyningstan (Suggested)" (rules "The game begins with the pieces set out as in the diagram. 2. The king's side takes the first move, play then alternating between players. 3. In his turn a player moves one of his pieces along a straight line, horizontally or vertically. 4. No piece may land on another, nor is there any jumping. 5. Only the king can land on the central space, though other pieces can pass through it. 6. Only the king may move to the spaces occupied by the fixed men: see rule 11. 7. The king is captured by surrounding him on all four sides by counts. If he is next to the central square or the edge of the board, he may be captured by surrounding him on the other three sides. 8. Dukes and counts are captured by surrounding them with enemies on two opposite sides, horizontally or vertically. Two or three men may be captured simultaneously if each falls between the moving piece and another enemy. 9. The fixed men in the corners may be used to capture pieces by either player. 10. A piece may come to rest voluntarily between two others, without being captured. 11. If the king moves to one of the squares occupied by the fixed men, he has escaped the board and wins the game. 12. If the king is captured by his opponents, then he has lost the game."))
        (useFor "Ruleset/Cyningstan (Suggested)" (source "<a href=\"http://www.cyningstan.com/game/219/alea-evangelii\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />cyningstan</a>"))
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "18x18 board. Markings in the corner squares. 69 or 72 pieces. One king piece. King piece begins in the center."))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP evidence."))
        (source "Murray 1951: 61.")
        (version "1.3.0")
        (classification "board/war/custodial")
        (credit "Eric Piette")
        (origin  "This game was played in British Isles, from around 1100 to 1199.")
        }
    )

    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Style Tafl)
        (show Symbol "knotSquare" "Fortresses")
    })

    (ai
        "Alea Evangelii_ai"
    )

)
