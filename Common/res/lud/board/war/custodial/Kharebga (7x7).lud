(game "Kharebga (7x7)"
    (players 2)
    (equipment {
        (board (square 7))
        (piece "Marker" Each 
            (move Slide Orthogonal
                (then ("CustodialCapture" Orthogonal (max 1)))
            )
        )
        (hand Each)
    })
    (rules
        (start (place "Marker" "Hand" count:24))
        phases:{
        (phase "Placement"
            (play
                (move 
                    (from (handSite Mover))
                    (to (difference (sites Board) (centrePoint)) if:(is Empty (to)))
                    (then
                        (if ("NewTurn") (moveAgain))
                    )
                )
            )
            (nextPhase Mover (is Empty (handSite Mover)) "Capture")
        )
        ("PhaseMovePiece" "Capture")
        }
        (end ("CaptureAll" Next))
    )	
)

//--------------------------------------

(metadata 
    (info
        {
        (description "Kharebga is a game with custodial captures played by children in El Oued, Algeria.")
        (rules "7x7 board. 24 pieces per player. Players alternate turns placing two pieces on an empty space on the board, except in the central space. Captures cannot be made during the placement phase. When all of the pieces have been placed, players alternate turns moving a piece orthogonally any distance. When an opponent's piece is between two of a player's pieces, it is captured. If a player is unable to make a move, they pass their turn and the opponent plays again. The player who captures all of the opponent's pieces wins.
        ")
        (source "Bellin 1964: 53-54.")
        (id "1574")
        (version "1.3.7")
        (classification "board/war/custodial")
        (credit "Eric Piette")
        (origin "This game was played in Algeria, around 1964.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    (ai
        "Kharebga (7x7)_ai"
    )
)
