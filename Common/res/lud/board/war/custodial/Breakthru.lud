(game "Breakthru"
    (players 2)
    (equipment {
        (board (square 11))
        (piece "Disc" Each 
            (or 
                (if ("NewTurn") ("StepToEnemy" Diagonal))
                (move Slide Orthogonal (then (if ("NewTurn") (moveAgain))))
            )
        )
        (piece "Commander" P1 
            (if ("NewTurn") 
                (or 
                    ("StepToEnemy" Diagonal)
                    (move Slide Orthogonal)
                )
            )
        )
        (hand Each)
        (regions "CentreSites" (expand (sites Centre) steps:2))
    })
    (rules
        (start {
            (place "Commander1" (sites Centre))
            (place "Disc1" (handSite P1) count:12)
            (place "Disc2" (handSite P2) count:20)
        })
        phases:
        {
        (phase "PlacementP1"
            (play 
                (move 
                    (from (handSite P1))
                    (to (intersection (sites "CentreSites") (sites Empty)))
                    (then (if (is Occupied (handSite P1)) (moveAgain)))
                )
            )
            (nextPhase (is Empty (handSite P1)) "PlacementP2")
        )
        
        (phase "PlacementP2"
            (play 
                (move 
                    (from (handSite P2))
                    (to (difference (sites Empty) (sites "CentreSites")))
                    (then (if (is Occupied (handSite P2)) (moveAgain)))
                )
            )
            (nextPhase (is Empty (handSite P2)) "Movement")
        )
        
        ("PhaseMovePiece" "Movement"
            (end {
                ("PieceTypeReachWin" "Commander1" (sites Outer) P1)
                (if ("IsOffBoard" (where "Commander" P1)) (result P2 Win))
            })
        )
        }
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Breakthru is a modern member of the Hnefatafl family of games, where the players have unequal forces and different objectives. ")
        (rules "Objectives
            One player has a gold fleet consisting of one large flagship and twelve escorts, with the objective of evading capture while breaking through his or her opponent's blockade to transport the flagship to the perimeter of the board. The other player has a silver fleet of twenty ships, and forms a blockade to trap the gold flagship and destroy the gold fleet's escorts with the objective of capturing the flagship.
            
            Setup
            Gold player places the flagship on the center square of the game board, and positions the rest of the ships anywhere within the boldly ruled central area of the board.
            
            Silver player then positions the silver ships on twenty squares in the lightly ruled peripheral area of the board.
            
            Play
            Gold player chooses who goes first. Players move alternately by making two moves or one capture anywhere on the board. When the flagship is moved, only one move or capture is made (thus only the flagship).
            
            Moves
            A player may move two of the smaller playing pieces any number of vacant squares either horizontally or vertically on the board (as a rook in Chess, except that no captures can be made with this move), although if the flagship is moved, the gold player may not move another playing piece.
            
            Captures
            A player may move any playing piece (including the flagship) one square diagonally to capture one of his opponent's playing pieces. (This move is similar to the capture-move of the pawn in Chess, except that captures can be made on any of the four diagonals.)
            
            This game uses displacement capture (like Chess), rather than custodial capture (like Hnefatafl), thus when a capture is made, the captured piece is removed from the board and the vacated square is occupied by the captor.
            
        Play continues until one player achieves his or her objective. If the flagship of the gold fleet reaches one of the outermost squares on the board, gold player wins. If the flagship is captured before it reaches the outer edge of the board, silver player wins.")
        (id "1537")
        (source "<a href=\"https://www.boardgamegeek.com/boardgame/335/breakthru\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.11")
        (classification "board/war/custodial")
        (author "Alex Randolph")
        (credit "Eric Piette")
        (date "1965")
        }
    )
    
    (graphics {
        (piece Scale "Disc" 0.6)
        (piece Rotate "Commander" degrees:45)
        (player Colour P1 (colour Yellow))
        (player Colour P2 (colour Grey))
        
        (board Colour Phase0 (colour Black))
        (board Colour InnerEdges (colour Grey))
        (board Colour OuterEdges (colour Grey))
        (region Colour Edge (expand (sites Centre) steps:2) regionSiteType:Cell (colour White) scale:2)
    })
    
    (ai
        "Breakthru_ai"
    )
)
