(define "Blocked" 
    (custodial 
        (from (last To)) 
        Orthogonal 
        (between 
            (max 1) 
            if:(= (what at:(between)) (next)) 
            (apply 
                (and 
                    (remove (between)) 
                    (add 
                        (piece (id "CounterStar" Next)) 
                        (to (between)) 
                        (then "Rescue")
                    ) 
                )
            ) 
        )
        (to if:(is Friend (who at:(to)))) 
    )
)

(define "Released"
    (custodial 
        (from (last From)) 
        Orthogonal 
        (between 
            (max 1) 
            if:(= (what at:(between)) (id "CounterStar" Next) ) 
            (apply 
                (and 
                    (remove (between)) 
                    (add 
                        (piece (id "Counter" Next)) 
                        (to (between))
                    )
                )
            ) 
        )
        (to if:(is Friend (who at:(to)))) 
    )
)

(define "Rescue" 
    (custodial 
        (from (to)) 
        Orthogonal 
        (between 
            (max 1) 
            if:(= (what at:(between)) (id "CounterStar" Mover) ) 
            (apply 
                (and 
                    (remove (between)) 
                    (add 
                        (piece (id "Counter" Mover)) 
                        (to (between))
                    )
                )
            ) 
        )
        (to if:(is Enemy (who at:(to)))) 
    )
)

//------------------------------------------------------------------------------

(game "Ludus Latrunculorum"  
    (players 2)  
    
    <Version:equipment> 
    <Version:rules>
)

//------------------------------------------------------------------------------

(option "Suggested Variants" <Version> args:{ <equipment> <rules> }
    {
    (item "Ulrich Schaedler"      
        <
        (equipment { 
            (board (square 8)) 
            (hand Each)
            
            (piece "Counter" Each (or 
                    (move
                        Step
                        Orthogonal 
                        (to if:(is Empty (to))) 
                        (then (and ("Released") ("Blocked"))) 
                    ) 
                    (move
                        Hop
                        Orthogonal 
                        (between 
                            if:(not (is Empty (between)))
                        )
                        (to 
                            if:(and 
                                (is Empty (to)) 
                                (not (is Visited (to))) 
                            )
                        )
                        (then 
                            (and
                                {
                                (if (can Move 
                                        (hop 
                                            (from (last To)) 
                                            Orthogonal 
                                            (between 
                                                if:(not (is Empty (between)))
                                            ) 
                                            (to 
                                                if:(and 
                                                    (is Empty (to)) 
                                                    (not (is Visited (to)))
                                                )
                                            ) 
                                        ) 
                                    )
                                    (moveAgain)
                                )
                                ("Released")
                                ("Blocked")
                                }
                            )
                        )
                    ) 
                ) 
            )
            
            (piece "CounterStar" Each) 
            }
        )
        >
        <
        (rules 
            (meta
                (noRepeat)
            )
            (start { (place "Counter" "Hand" count:16) })
            
            phases:{
            (phase "Placement" 
                (play 
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty))
                    )
                ) 
                (nextPhase ("HandEmpty" P2) "Movement")
            )
            
            (phase "Movement" 
                (play 
                    (if "SameTurn"
                        (or
                            (move
                                Hop
                                (from (last To)) 
                                Orthogonal
                                (between 
                                    if:(not (is Empty (between)))
                                )
                                (to 
                                    if:(and 
                                        (is Empty (to)) 
                                        (not (is Visited (to))) 
                                    )
                                )
                                (then 
                                    (and
                                        {
                                        (if (can Move 
                                                (hop 
                                                    (from (last To)) 
                                                    Orthogonal 
                                                    (between 
                                                        if:(not (is Empty (between)))
                                                    ) 
                                                    (to 
                                                        if:(and
                                                            (is Empty (to)) 
                                                            (not (is Visited (to))) 
                                                        )
                                                    )
                                                ) 
                                            )
                                            (moveAgain)
                                        )
                                        "Released"
                                        "Blocked"
                                        }
                                    )
                                )
                            )
                            (move Pass)
                        )
                        (or
                            (forEach Piece "Counter")
                            (move
                                Select 
                                (from (sites Occupied by:Next component:"CounterStar")) 
                                (then (remove (last To)))
                            )
                        )
                    )
                )
                (end (if (= (count Pieces Next) 1) (result Mover Win))) 
            )
            }
        )
        >  
        "The version of the game described by Ulrich Schaedler."
    )**
    
    (item "Museum Quintana"      
        <
        (equipment { 
            (board (square 8)) 
            (piece "Counter" Each 
                (move
                    Slide
                    Orthogonal
                    (then 
                        (custodial 
                            (from (last To)) 
                            Orthogonal
                            (between 
                                (max 1) 
                                if:(is Enemy (who at:(between))) 
                                (apply (remove (between)))
                            )
                            (to if:(is Friend (who at:(to)))) 
                        ) 
                    ) 
                ) 
            )
            }
        )
        >
        <
        (rules 
            (meta (noRepeat))
            (start { (place "Counter1" (expand (sites Bottom) )) (place "Counter2" (expand (sites Top) )) })
            (play (forEach Piece))
            (end (if ("NoPiece" Next) (result Mover Win))) 
        )
        >
        "The version of the game described by in the museum Quintana."
    )
    
    (item "Edward Falkener"      
        <
        (equipment { 
            (board (square 12)) 
            (piece "Counter" Each 
                (move
                    Step
                    (to if:(is Empty (to))) 
                    (then 
                        (custodial 
                            (from (last To)) 
                            Orthogonal
                            (between 
                                (max 1) 
                                if:(is Enemy (who at:(between))) 
                                (apply (remove (between)))
                            )
                            (to if:(is Friend (who at:(to)))) 
                        ) 
                    ) 
                ) 
            )
        })
        >
        <
        (rules 
            (meta (noRepeat))
            (start { 
                (place "Counter1" (difference (expand (sites Bottom) steps:4) (sites Phase 1))) 
                (place "Counter2" (difference (expand (sites Top) steps:4) (sites Phase 1))) 
            })
            (play (forEach Piece))
            (end (if (no Moves Next) (result Mover Win))) 
        )
        >
        "The version of the game described by Edward Falkener."
    )
    
    (item "Kowalski 8x12"      
        <
        (equipment { 
            (board (rectangle 8 12)) 
            (piece "Counter" Each 
                (move
                    Slide
                    Orthogonal
                    (then 
                        (and
                            (custodial 
                                (from (last To)) 
                                Orthogonal 
                                (between 
                                    (max 1) 
                                    if:(= (what at:(between)) (id "Counter" Next)) 
                                    (apply (remove (between)))
                                )
                                (to if:(is Friend (who at:(to)))) 
                            )
                            (surround 
                                (from (last To)) 
                                Orthogonal 
                                (between 
                                    if:(and 
                                        (is In (between) (sites Corners)) 
                                        (= (what at:(between)) (id "Counter" Next))
                                    ) 
                                    (apply (remove (between)))
                                ) 
                                (to if:(is Friend (who at:(to)))) 
                            )
                        )
                    )
                )
            )
            (piece "DoubleCounter" Each     	
                (move
                    Slide
                    Orthogonal
                    (then 
                        (and
                            (custodial 
                                (from (last To)) 
                                Orthogonal 
                                (between 
                                    (max 1) 
                                    if:(= (what at:(between)) (id "Counter" Next)) 
                                    (apply (remove (between)))
                                )
                                (to if:(is Friend (who at:(to)))) 
                            )
                            (surround 
                                (from (last To)) 
                                Orthogonal 
                                (between 
                                    if:(and 
                                        (is In (between) (sites Corners)) 
                                        (= (what at:(between)) (id "Counter" Next))
                                    ) 
                                    (apply (remove (between)))
                                ) 
                                (to if:(is Friend (who at:(to)))) 
                            )
                        )
                    )
                )
            )
            }
        )
        >
        <
        (rules 
            (meta (noRepeat))
            (start { 
                (place "Counter1" (sites Bottom)) (place "Counter2" (sites Top)) 
                (place "DoubleCounter1" coord:"G2") (place "DoubleCounter2" coord:"F7")
            })
            (play (forEach Piece))
            (end {
                (if (no Moves Next) (result Mover Win))
                (if (and (< (count Pieces P1) 4) (not (< (count Pieces P2) 4) ) ) (result P2 Win))
                (if (and (not (< (count Pieces P1) 4)) (< (count Pieces P2) 4) ) (result P1 Win))
            }) 
        )
        >
        "The version of the game described by Kowalski on a 8x12 board."
    )
    
    (item "Kowalski 10x11"      
        <
        (equipment { 
            (board (rectangle 10 11)) 
            (piece "Counter" Each 
                (move
                    Slide
                    Orthogonal
                    (then 
                        (and
                            (custodial 
                                (from (last To))
                                Orthogonal 
                                (between 
                                    (max 1) 
                                    if:(= (what at:(between)) (id "Counter" Next)) 
                                    (apply (remove (between)))
                                )
                                (to if:(is Friend (who at:(to)))) 
                            )
                            (surround 
                                (from (last To)) 
                                Orthogonal 
                                (between 
                                    if:(and 
                                        (is In (between) (sites Corners)) 
                                        (= (what at:(between)) (id "Counter" Next))
                                    ) 
                                    (apply (remove (between)))
                                ) 
                                (to if:(is Friend (who at:(to)))) 
                            )
                        )
                    )
                )
            )
            (piece "DoubleCounter" Each     	
                (move
                    Slide
                    Orthogonal
                    (then 
                        (and
                            (custodial 
                                (from (last To))
                                Orthogonal 
                                (between 
                                    (max 1) 
                                    if:(= (what at:(between)) (id "Counter" Next)) 
                                    (apply (remove (between)))
                                )
                                (to if:(is Friend (who at:(to)))) 
                            )
                            (surround 
                                (from (last To)) 
                                Orthogonal 
                                (between 
                                    if:(and 
                                        (is In (between) (sites Corners)) 
                                        (= (what at:(between)) (id "Counter" Next))
                                    ) 
                                    (apply (remove (between)))
                                ) 
                                (to if:(is Friend (who at:(to)))) 
                            )
                        )
                    )
                )
            )
            }
        )
        >
        <
        (rules 
            (meta (noRepeat))
            (start { 
                (place "Counter1" (difference (sites Bottom) (sites {5}))) 
                (place "Counter2" (difference (sites Top) (sites {104}))) 
                (place "DoubleCounter1" coord:"F1") (place "DoubleCounter2" coord:"F10")
            })
            (play (forEach Piece))
            (end {
                (if (no Moves Next) (result Mover Win))
                (if (and (< (count Pieces P1) 4) (not (< (count Pieces P2) 4)) ) (result P2 Win))
                (if (and (not (< (count Pieces P1) 4)) (< (count Pieces P2) 4) ) (result P1 Win))
            }) 
        )
        >
        "The version of the game described by Kowalski on a 10x11 board."
    )
    
    (item "R.C. Bell 8x7"      
        <
        (equipment { 
            (board (rectangle 8 7)) 
            (hand Each size:2)
            
            (piece "Counter" Each 
                (move
                    Step
                    Orthogonal 
                    (to if:(is Empty (to))) 
                    (then 
                        (custodial 
                            (from (last To)) 
                            Orthogonal 
                            (between (max 1) if:(is Enemy (who at:(between))) (apply (remove (between) (then (and (moveAgain) (set Counter))))))
                            (to if:(or (is Friend (who at:(to))) (is In (to) (sites Corners))))
                        ) 
                    ) 
                ) 
            ) 
            
            (piece "DoubleCounter" Each 
                (or
                    (move Step Orthogonal (to if:(is Empty (to))))
                    (move Hop Orthogonal (between if:(is Enemy (who at:(between)))) (to if:(is Empty (to))))
                    (then 
                        (custodial 
                            (from (last To)) 
                            Orthogonal 
                            (between (max 1) if:(is Enemy (who at:(between))) (apply (remove (between) (then (and (moveAgain) (set Counter))))))
                            (to if:(or (is Friend (who at:(to))) (is In (to) (sites Corners))))
                        ) 
                    ) 
                )
            )  
            }
        )
        >
        <
        (rules 
            (meta (noRepeat))
            (start { 
                (place "Counter1" (handSite P1) count:16) 
                (place "Counter2" (handSite P2) count:16) 
                (place "DoubleCounter1" (handSite P1 1) ) 
                (place "DoubleCounter2" (handSite P2 1)) 
            })
            
            phases:{
            (phase "CounterPlacement" 
                (play 
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty))
                        (then (if (not (is Mover Prev)) (moveAgain)))
                    )
                ) 
                (nextPhase (> (count Moves) 31) "DoubleCounterPlacement")
            )
            (phase "DoubleCounterPlacement" 
                (play 
                    (move 
                        (from (handSite Mover 1)) 
                        (to (sites Empty)) 
                        (then (set Counter))
                    )
                ) 
                (nextPhase (> (count Moves) 33) "Movement")
            )
            
            (phase "Movement" 
                (play (if "SameTurn"
                        (if (= (what at:(last To)) (id "Counter" Mover))
                            (move
                                Step
                                (from (last To)) 
                                Orthogonal 
                                (to if:(is Empty (to))) 
                                (then 
                                    (custodial 
                                        (from (last To))
                                        Orthogonal
                                        (between 
                                            (max 1) 
                                            if:(is Enemy (who at:(between))) 
                                            (apply 
                                                (remove 
                                                    (between) 
                                                    (then 
                                                        (and 
                                                            (moveAgain) 
                                                            (set Counter)
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                        (to 
                                            if:(or 
                                                (is Friend (who at:(to))) 
                                                (is In (to) (sites Corners))
                                            )
                                        ) 
                                    ) 
                                ) 
                            ) 
                            (or
                                (move
                                    Step
                                    (from (last To)) 
                                    Orthogonal 
                                    (to if:(is Empty (to)))
                                )
                                (move
                                    Hop
                                    (from (last To)) 
                                    Orthogonal 
                                    (between if:(is Enemy (who at:(between))))
                                    (to if:(is Empty (to))) 
                                )
                                (then 
                                    (custodial 
                                        (from (last To))
                                        Orthogonal
                                        (between 
                                            (max 1) 
                                            if:(is Enemy (who at:(between))) 
                                            (apply 
                                                (remove (between) 
                                                    (then 
                                                        (and 
                                                            (moveAgain) 
                                                            (set Counter)
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                        (to 
                                            if:(or 
                                                (is Friend (who at:(to))) 
                                                (is In (to) (sites Corners))
                                            )
                                        ) 
                                    ) 
                                )
                            )
                        )
                        (forEach Piece)
                    )
                )
                (end {
                    (if ("NoPiece" Next) (result Mover Win))
                    (if (= (counter) 30) 
                        (if (> (count Pieces P1) (count Pieces P2)) (result P1 Win))
                        (result P2 Win)
                    ) 
                }) 
            )
            }
        )
        >
        "The version of the game described by R.C. Bell on a 8x7 board."
    )
    
    (item "R.C. Bell 8x8"      
        <
        (equipment { 
            (board (square 8)) 
            (hand Each size:2)
            
            (piece "Counter" Each 
                (move
                    Step
                    Orthogonal 
                    (to if:(is Empty (to))) 
                    (then 
                        (custodial 
                            (from (last To)) 
                            Orthogonal 
                            (between 
                                (max 1) 
                                if:(is Enemy (who at:(between)))
                                (apply 
                                    (remove 
                                        (between) 
                                        (then 
                                            (and 
                                                (moveAgain) 
                                                (set Counter)
                                            )
                                        )
                                    )
                                )
                            )
                            (to 
                                if:(or 
                                    (is Friend (who at:(to))) 
                                    (is In (to) (sites Corners))
                                )
                            )
                        ) 
                    )
                )
            ) 
            
            (piece "DoubleCounter" Each (or
                    (move
                        Step
                        Orthogonal 
                        (to if:(is Empty (to)))
                    )
                    (move
                        Hop
                        Orthogonal 
                        (between if:(is Enemy (who at:(between)))) 
                        (to if:(is Empty (to))) 
                    )
                    (then 
                        (custodial 
                            (from (last To)) 
                            Orthogonal 
                            (between 
                                (max 1) 
                                if:(is Enemy (who at:(between))) 
                                (apply 
                                    (remove 
                                        (between) 
                                        (then 
                                            (and 
                                                (moveAgain) 
                                                (set Counter)
                                            )
                                        )
                                    )
                                )
                            )
                            (to 
                                if:(or 
                                    (is Friend (who at:(to))) 
                                    (is In (to) (sites Corners))
                                )
                            )
                        ) 
                    )
                )
            )  
            }
        )
        >
        <
        (rules 
            (meta (noRepeat))
            (start { 
                (place "Counter1" (handSite P1) count:16) 
                (place "Counter2" (handSite P2) count:16) 
                (place "DoubleCounter1" (handSite P1 1) ) 
                (place "DoubleCounter2" (handSite P2 1)) 
            })
            
            phases:{
            (phase "CounterPlacement" 
                (play 
                    (move 
                        (from (handSite Mover)) 
                        (to (sites Empty)) 
                        (then 
                            (if 
                                (not "SameTurn") 
                                (moveAgain)
                            )
                        )
                    ) 
                ) 
                (nextPhase (> (count Moves) 31) "DoubleCounterPlacement")
            )
            (phase "DoubleCounterPlacement" 
                (play
                    (move 
                        (from (handSite Mover 1)) 
                        (to (sites Empty)) 
                        (then (set Counter))
                    )
                ) 
                (nextPhase (> (count Moves) 33) "Movement")
            )
            
            (phase "Movement" 
                (play (if "SameTurn"
                        (if (= (what at:(last To)) (id "Counter" Mover))
                            (move
                                Step
                                (from (last To)) 
                                Orthogonal 
                                (to if:(is Empty (to))) 
                                (then 
                                    (custodial 
                                        (from (last To)) 
                                        Orthogonal 
                                        (between 
                                            (max 1) 
                                            if:(is Enemy (who at:(between))) 
                                            (apply 
                                                (remove 
                                                    (between) 
                                                    (then 
                                                        (and 
                                                            (moveAgain) 
                                                            (set Counter)
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                        (to if:(or 
                                                (is Friend (who at:(to))) 
                                                (is In (to) (sites Corners))
                                            )
                                        )
                                    ) 
                                )
                            ) 
                            (or
                                (move
                                    Step
                                    (from (last To)) 
                                    Orthogonal 
                                    (to if:(is Empty (to)))
                                )
                                (move
                                    Hop
                                    (from (last To)) 
                                    Orthogonal 
                                    (between if:(is Enemy (who at:(between)))) 
                                    (to if:(is Empty (to))) 
                                )
                                (then 
                                    (custodial 
                                        (from (last To)) 
                                        Orthogonal 
                                        (between 
                                            (max 1) 
                                            if:(is Enemy (who at:(between))) 
                                            (apply 
                                                (remove 
                                                    (between) 
                                                    (then 
                                                        (and 
                                                            (moveAgain) 
                                                            (set Counter)
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                        (to 
                                            if:(or 
                                                (is Friend (who at:(to))) 
                                                (is In (to) (sites Corners))
                                            )
                                        )
                                    ) 
                                )
                            )
                        )
                        (forEach Piece)
                    )
                )
                (end {
                    (if ("NoPiece" Next) (result Mover Win))
                    (if (= (counter) 30) 
                        (if (> (count Pieces P1) (count Pieces P2)) (result P1 Win))
                        (result P2 Win)
                    ) 
                }) 
            )
            }
        )
        >
        "The version of the game described by R.C. Bell on a 8x8 board."
    )
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Schadler (Scholarly)" 
        {
        "Suggested Variants/Ulrich Schaedler"
        }
    )*
    
    (ruleset "Ruleset/Museum Quintana (Suggested)" 
        {
        "Suggested Variants/Museum Quintana"
        }
    )
    
    (ruleset "Ruleset/Falkener (Suggested)" 
        {
        "Suggested Variants/Edward Falkener"
        }
    )
    
    (ruleset "Ruleset/Kowalski 8x12 (Suggested)" 
        {
        "Suggested Variants/Kowalski 8x12"
        }
    )
    
    (ruleset "Ruleset/Kowalski 10x11 (Suggested)" 
        {
        "Suggested Variants/Kowalski 10x11"
        }
    )
    
    (ruleset "Ruleset/Bell 8x7 (Suggested)" 
        {
        "Suggested Variants/R.C. Bell 8x7"
        }
    )
    
    (ruleset "Ruleset/Bell 8x8 (Suggested)" 
        {
        "Suggested Variants/R.C. Bell 8x8"
        }
    )
    
    (ruleset "Ruleset/Historical Information (Incomplete)" {
        
    })
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Ludus Latrunculorum (\"Game of Robbers\") is a game from the Roman Empire, beginning in the final centuries BCE, that appears to have been particularly popular in the Roman military. It is mentioned in various authors such as Ovid, Seneca, and Isidore of Seville. Boards have ben found throughout the areas occupied by the Romans, especially in forts. Boards range in size from 6x7 to 14x14 and are not always exactly square. Glass counters were frequently used as pieces. Some speculation exists that this game developed into later tafl games.")
        (aliases {"Latrunculi"})
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "Played on a rectangular grid. 6x7 to 14x14. Two players. Pieces move orthogonally. Pieces can be blocked. Custodial captures. "))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP evidence. "))
        (useFor "Ruleset/Schadler (Scholarly)" (rules "The players take turns placing one piece on any vacant square. In this phase no captures are made.
                When all the pieces have been placed, the players take turns moving pieces on the board. The pieces can be moved orthogonally to any adjacent square. A piece can leap over any single piece of either color, if the square behind is unoccupied. Several leaps in one turn are possible (as in Draughts).
                If a player can trap an enemy piece between two friendly pieces, the enemy piece is blocked and cannot be moved. 
                On the next turn, instead of moving a piece, the player can capture the trapped piece by removing it from the board, provided his own two surrounding pieces are still free. The trapped piece is immediately free if one of its two enemies is itself surrounded.
                A player reduced to only one piece left on the board has lost the game.
        "))
        (useFor "Ruleset/Schadler (Scholarly)" (source "Sch√§dler 1994."))
        (useFor "Ruleset/Museum Quintana (Suggested)" (rules "Two players have sixteen pieces each, which are arranged in two rows facing each other. The goal of the game is to capture all of the opponent's pieces. The pieces move orthogonally any unobstructed distance. A piece is captured when it is caught between two opposing pieces on adjacent squares in a rank or file. The captured piece is removed from the board."))
        (useFor "Ruleset/Museum Quintana (Suggested)" (source "https://upload.wikimedia.org/wikipedia/commons/2/2b/Museum_Quintana_-_R%C3%A4uberspiel.jpg"))
        (useFor "Ruleset/Falkener (Suggested)" (rules "The game is played on a 12x12 board.
                Each player has five rows of pieces, beginning at the left hand corner they are placed alternately. The pieces move and take in all directions, perpendicularly, horizontally, diagonally, forwards and backwards.
        Pieces attack each other when in contiguous cells, and when another piece comes up on the opposite side the intermediate piece is taken off. A piece can go between two adverse pieces without being taken. When one side is hopelessly beaten or locks himself in the game is lost."))
        (useFor "Ruleset/Falkener (Suggested)" (source "Falkener 1892"))
        (useFor "Ruleset/Kowalski 10x11 (Suggested)" (rules "10x11 board. The board has eight ranks and twelve files. Each player has ten pieces and a dux. The dux starts in the center of the back row, flanked by five men on each side. Pieces move any number of spaces orthogonally. Custodial capture. If a piece is moved voluntarily between two enemy pieces, it is not captured. A piece in a corner is captured if the opponent places his pieces on the two squares adjacent to the corner.
                The dux cannot be captured. It is immobilized if blocked on all four sides. Play continues until one player cannot move, and so loses.
        "))
        (useFor "Ruleset/Kowalski 10x11 (Suggested)" (source "https://web.archive.org/web/20081003114509/http://www.aerobiologicalengineering.com/wxk116/Roman/BoardGames/latruncu.html"))
        (useFor "Ruleset/Kowalski 8x12 (Suggested)" (rules "8x12 board. Each player has twelve pieces and a dux. In the starting array the pieces fill the first rank and the dux stands on the second, on the square just to the right of the center line (from each player's point of view). 
                Pieces move orthogonally at any distance.
                A piece is captured if the enemy places a piece adjacent to it on each side in an orthogonal line.
                If a piece is moved voluntarily between two enemy pieces, it is not captured.
                A piece in a corner is captured if the opponent places pieces on the two squares adjacent to the corner.
                The dux cannot be captured. It is immobilized if blocked on all four sides. Play continues until one player cannot move, and so loses.
        "))
        (useFor "Ruleset/Kowalski 8x12 (Suggested)" (source "https://web.archive.org/web/20081003114509/http://www.aerobiologicalengineering.com/wxk116/Roman/BoardGames/latruncu.html"))
        (useFor "Ruleset/Bell 8x7 (Suggested)" (rules "8x7 board. Each player has 16 pieces and one \"dux.\"  Pieces are placed two at a time by alternate turns of play anywhere on this board. During this first phase no captures are made.
                The dux piece is added last. Pieces move orthogonally one square at a time.
                A piece is captured when the opponent brackets it orthogonally between two of the opponent's pieces, or between an opponent piece and a corner (but not side) square. The dux is captured like any other piece. A piece that makes a capture gains an immediate second move.
        The dux can move like the rest of the pieces, or can jump over an enemy piece that is in an adjacent square. The jumped piece is not captured by the move. If a piece is moved voluntarily between two enemy pieces, it is not captured. A player who loses all their pieces loses the game. If no captures are made in thirty moves, the game is ended, and the player with more pieces on the board wins."))
        (useFor "Ruleset/Bell 8x7 (Suggested)" (source "Bell 1979: 86."))
        (useFor "Ruleset/Bell 8x8 (Suggested)" (rules "8x8 board. Each player has 16 pieces and one \"dux.\"  Pieces are placed two at a time by alternate turns of play anywhere on this board. During this first phase no captures are made.
                The dux piece is added last. Pieces move orthogonally one square at a time.
                A piece is captured when the opponent brackets it orthogonally between two of the opponent's pieces, or between an opponent piece and a corner (but not side) square. The dux is captured like any other piece. A piece that makes a capture gains an immediate second move.
        The dux can move like the rest of the pieces, or can jump over an enemy piece that is in an adjacent square. The jumped piece is not captured by the move. If a piece is moved voluntarily between two enemy pieces, it is not captured. A player who loses all their pieces loses the game. If no captures are made in thirty moves, the game is ended, and the player with more pieces on the board wins."))
        (useFor "Ruleset/Bell 8x8 (Suggested)" (source "Bell 1979: 86."))
        (version "1.2.1")
        (classification "board/war")
        (credit "Eric Piette")
        (origin  "This game was played in Roman Empire, from around 116BCE to 636.")
        }
    )
    
    (graphics {
        (piece Families {"Defined" "Isometric"})
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai 
        "Ludus Latrunculorum_ai"
    )
    
)

