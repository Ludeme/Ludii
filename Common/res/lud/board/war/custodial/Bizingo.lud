(define "Surrounded"
    (surround
        (from (last To))
        Orthogonal
        (between
            #1
            (apply (remove (between)))
        )
        (to if:(is Friend (who at:(to))))
        #2
    )
)

(define "SurroundedThrall"
    ("Surrounded"
        if:(and
            (or
                (not (is In (between) (sites Outer)))
                (and
                    (is In (between) (sites Outer))
                    (= 1 (phase of:(between)))
                )
            )
            (= (what at:(between)) (id "Thrall" Next))
        )
    )
)

(define "SurroundedJarl"
    ("Surrounded"
        if:(and
            (or
                (not (is In (between) (sites Outer)))
                (and
                    (is In (between) (sites Outer))
                    (= 1 (phase of:(between)))
                )
            )
            (= (what at:(between)) (id "Jarl" Next))
        )
        with:(piece (id "Jarl" Mover))
    )
)

(define "SurroundedEnemyOnOuterEdge"
    ("Surrounded"
        if:(and
            {
            (is In (between) (sites Outer))
            (= 0 (phase of:(between)))
            (is Enemy (what at:(between)))
            }
        )
        with:(piece (id "Jarl" Mover))
    )
)

(define "SiteSurrounded"
    (= 3
        (count Sites
            in:(intersection
                (sites Around (last To) Orthogonal)
                (sites Occupied by:Next)
            )
        )
    )
)

(define "SuicideSurrounded"
    (if (not
            (can Move
                (or
                    {
                    ("SurroundedEnemyOnOuterEdge")
                    ("SurroundedThrall")
                    ("SurroundedJarl")
                    }
                )
            )
        )
        (if
            ("SiteSurrounded")
            (remove (last To))
        )
    )
)

(define "PieceMove"
    (move Step
        (to
            if:(and
                (is Empty (to))
                (= (phase of:(from)) (phase of:(to)))
            )
        )
        (then
            (and
                {
                ("SuicideSurrounded")
                ("SurroundedEnemyOnOuterEdge")
                ("SurroundedThrall")
                ("SurroundedJarl")
                }
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Bizingo"
    (players 2)
    (equipment {
        (board
            (remove
                (tri 13)
                cells:{ 0 1 13 25 11 12 24 36 165..168}
            )
        )
        (piece "Thrall" Each ("PieceMove"))
        (piece "Jarl" Each ("PieceMove"))
    })
    (rules
        (start
            {
            (place "Thrall1" (sites {32..36 53..58 72 74..76 78}))
            (place "Jarl1" (sites {73 77}))
            (place "Thrall2" (sites {137..139 126..129 113..117 98 100 101 103}))
            (place "Jarl2" (sites {99 102}))
            }
        )
        (play (forEach Piece))
        (end
            (forEach Player
                if:(<= (count Pieces Player) 2)
                (result Player Loss)
            )
        )
    )
)

(metadata
    (info
        {
        (description "Bizingo is a two-player strategy board game created sometime in the 1850s in the United States. Two opposing armies on a triangular grid face off against one another. ")
        (rules "A player can move a piece to any empty adjacent cell of the same color; so, on an open board, six moves are possible. Throughout the game, a player's pieces are restricted to cells of the same color.

            A captured piece is immediately removed from the game.

            A player captures a regular enemy piece by surrounding it on three sides.
            A player captures an enemy captain by surrounding it on three sides, with the caveat that one of the surrounding pieces must be a captain. (Three regular pieces are insufficient and the enemy captain would be unaffected.)
            An enemy piece on the edge of the board can be captured by surrounding it on two sides, with the caveat that one of the surrounding pieces must be a captain. (Two regular pieces are insufficient.)
            If a piece (regular or captain) is moved to a cell already surrounded by three enemy pieces, it is instantly captured, unless the move itself performed a capture.

        A player wins the game by reducing the opponent to two pieces.")
        (source "<a href=\"https://en.wikipedia.org/wiki/Bizingo\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.0")
        (classification "board/war/custodial")
        (credit "Eric Piette")
        (date "1850")
        }
    )

    (graphics {
        (board Style Chess)
    })
    (ai
        "Bizingo_ai"
    )
)
