(define "Custodial"
    (custodial
        (from (last To))
        Orthogonal
        (between
            (max 1)
            if:(= (what at:(between)) (id #1))
            (apply (remove (between)))
        )
        (to if:"FriendOrCentreOrFortress")
    )
)

(define "CaptureJarl" (apply (trigger "Surrounded" P2) ))

(define "JarlWasCaptured" (is Triggered "Surrounded" P2))

(define "EmptyButNotCentreOrFortress"
    (and (not (is In (between) (union (sites Centre) (sites "Fortresses")))) (is Empty (between)))
)

(define "FriendOrCentreOrFortress" (or (is Friend (who at:(to))) (is In (to) (union (sites Centre) (sites "Fortresses")))))

//------------------------------------------------------------------------------

(game "Tawlbwrdd"
    (players 2)
    (equipment {
        (board (square 11))
        (regions "Fortresses" (sites Corners))

        (piece "Thrall" P1
            (move
                Slide
                Orthogonal
                (between if:"EmptyButNotCentreOrFortress")
                (then
                    (or {
                        ("Custodial" "Thrall2")
                        (custodial
                            (from (last To))
                            Orthogonal
                            (between (max 1) if:(= (what at:(between)) (id "Jarl2")) "CaptureJarl")
                            (to if:"FriendOrCentreOrFortress")
                        )
                        }
                    )
                )
            )
        )
        (piece "Thrall" P2
            (move
                Slide
                Orthogonal
                (between if:"EmptyButNotCentreOrFortress")
                (then ("Custodial" "Thrall1") )
            )
        )
        (piece "Jarl" P2
            (move
                Slide
                Orthogonal
                (then ("Custodial" "Thrall1") )
            )
        )
        }
    )
    (rules
        (start
            {
            (place "Thrall1" {"D1" "E1" "F1" "G1" "H1" "F2" "A4" "A5" "A6" "A7" "A8" "B6" "K4" "K5" "K6" "K7" "K8" "J6" "F10" "D11" "E11" "F11" "G11" "H11" "F10"})
            (place "Thrall2" {"F3" "F4" "F5" "C6" "D6" "E6" "G6" "H6" "I6" "F7" "F8" "F9"})
            (place "Jarl2" (centrePoint))
            }
        )

        (play (forEach Piece))

        (end
            {
            (if (is Within (id "Jarl2") in:(sites Outer) ) (result P2 Win))
            (if "JarlWasCaptured"  (result P1 Win))
            }
        )
    )
)

//--------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <type> }
    {
    (item "Cyningstan"   <>       "The rules are describing with the Cyningstan ruleset.")
})

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Cyningstan (Suggested)" {
        "Variant/Cyningstan"
    })*

    (ruleset "Ruleset/Historical Information (Incomplete)" {

    })

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Tawlbwrdd is a game in the tafl family mentioned in Peniarth Manuscript 158, describing a portion of the game as played in Wales during the sixteenth century.")
        (useFor "Ruleset/Cyningstan (Suggested)" (rules "11x11 board. The king is placed in the centre of the board, with his defenders around him and the attackers at the edge of the board. The attackers move first. Pieces move orthogonally any number of spaces. A pieces is captured by surrounding it on two opposite sides, by an opponent's piece. It is possible to capture two or three pieces at once by so surrounding them. It is not possible to capture a row of pieces. The defending player wins the game by moving the king to any square on the edge of the board. The attacking player wins by capturing the king."))
        (useFor "Ruleset/Cyningstan (Suggested)" (source "<a href=\"http://www.cyningstan.com/game/175/tawlbwrdd\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />cyningstan</a>"))
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "11x11 board. One player plays with a king piece and twelve defenders, the other with 24 attackers. The king piece begins on the central space with the defenders surrounding it, and the attackers in the central five spaces of each outer edge, and one in the central spot in front of each of these lines. Captures are made when a piece is caught between two opposing pieces. "))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP evidence."))
        (source "Murray 1951: 62-63")
        (version "1.3.0")
        (classification "board/war/custodial")
        (credit "Eric Piette")
        (origin  "This game was played in Wales, from around 1587 to 1857.")
        }
    )

    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Style Tafl)
        (show Symbol "knotSquare" "Fortresses")
    })

    (ai
        "Tawlbwrdd_ai"
    )

)
