(game "Seega"
    (players 2)
    (equipment {
        (board (square <Board:size>))
        (hand Each)

        (piece "Marker" Each
            (move Step Orthogonal (to if:(is Empty (to)))
                (then
                    (custodial
                        (from (last To))
                        Orthogonal
                        (between
                            (max 1)
                            if:(is Enemy (who at:(between)))
                            (apply (remove (between)))
                        )
                        (to if:(is Friend (who at:(to))))
                    )
                )
            )
        )
    })

    (rules
        (start (place "Marker" "Hand" count:<Board:numPieces>))
        phases:{
        (phase "Placement"
            (play
                (move
                    (from (handSite Mover))
                    (to (difference (sites Empty) (centrePoint)))
                    (then
                        (if
                            (not "SameTurn")
                            (moveAgain)
                        )
                    )
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        (phase "Movement"
            (play
                (forEach Piece)
            )
        )
        }

        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> <numPieces>}
    {
    (item "5x5"    <5>  <12>  "The game is played on a 5x5 board.")**
    (item "7x7"    <7>  <24>  "The game is played on a 7x7 board.")
    (item "9x9"    <9>  <40>  "The game is played on a 9x9 board.")
})

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Khamsawee (Observed)"
        {
        "Board Size/5x5"
        }
    )*

    (ruleset "Ruleset/Sebawee (Observed)"
        {
        "Board Size/7x7"
        }
    )

    (ruleset "Ruleset/Tisawee (Observed)"
        {
        "Board Size/9x9"
        }
    )

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Seega is a game played in North Africa, and is particularly well-known in Egypt and Sudan. It is a game that is similar to Draughts or Alquerque, but has a custodial capture mechanism instead of leaping. It has been documented since the nineteenth century, but is likely to be older. The boards are typically scooped out of the sand or etched into stone surfaces.")
        (aliases {"Siga" "Sija" "Seeja"})
        (useFor "Ruleset/Khamsawee (Observed)" (rules "5x5 board. Players begin by placing their pieces in prescribed spaces, and then alternating two-by-two in places as they see fit, except for the central space. Once all the spaces except the central one are filled, the first player moves a piece one space orthogonally to the empty space. Pieces are captured by surrounding them on either side by a player's own pieces. The player to capture all of the opponent's pieces wins."))
        (useFor "Ruleset/Khamsawee (Observed)" (source "Lane 1836: 356-357; Davies 1925: 138-139; Bolton 190."))
        (useFor "Ruleset/Sebawee (Observed)" (rules "Same rules as Khamsáwee Seega with a 7x7 board. The number of pieces per player is unspecified."))
        (useFor "Ruleset/Sebawee (Observed)" (source "Lane 1836: 356-357."))
        (useFor "Ruleset/Tisawee (Observed)" (rules "Played with the same rules as Khamsáwee Seega, except played on a 9x9 board. The number of piece per player is not specified."))
        (useFor "Ruleset/Tisawee (Observed)" (source "Lane 1836: 356-357."))
        (source "Murray 1951: 54-55; Parker 1909: 603-604.")
        (version "1.3.0")
        (classification "board/war/custodial")
        (credit "Eric Piette")
        (origin  "This game was played in North Africa, from around 1825 to 1925.")
        }
    )

    (graphics {
        (board Colour Phase0 (colour 255 230 150))
        (board Colour InnerEdges (colour 160 140 100))
        (board Colour OuterEdges (colour Black))
        (board StyleThickness InnerEdges 0.5)
        (board StyleThickness OuterEdges 0.5)

    })

    (ai
        "Seega_ai"
    )

)
