(game "Caturvimsatikosthakatmiki Krida"
    (players 2) 
    (equipment { 
        (board (rectangle 3 8))
        (piece "Marker" Each
            (or {
                (move Hop 
                    Orthogonal
                    (between 
                        if:(and 
                            ("IsEnemyAt" (between)) 
                            (not (is In (between) (union (sites Bottom) (sites Top))))
                        )
                        (apply (remove (between)))
                    )
                    (to if:(is Empty (to)))
                )
                ("StepOrthogonalToEmpty")
                (if (= 1 (state at:(from)))
                    (move 
                        Slide
                        Orthogonal
                        (between
                            (exact 2)
                            if:(is Empty (between))
                        )
                        (then (set State at:(last To) 0))
                    )
                )
            })			
        )
    }) 
    
    (rules 
        (start {
            (place "Marker1" (sites Bottom) state:1)
            (place "Marker2" (sites Top) state:1)
        })
        (play (forEach Piece))
        (end ("HavingLessPiecesLoss" Next 3))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Caturvimsatikosthakatmiki Krida is a capturing game played during the nineteenth century in India. It is documented in the Kridakausalya by Harikrsna. ")
        (rules "3x8 board. The second space from the left and from the right in the center row are marked with an X. Eight pieces per player, which begin in the row closest to the player. Players alternate turns moving a piece to an empty adjacent space in an orthogonal direction. On the first move of a piece, that piece may move two spaces in one direction. A player may capture an opponent's piece in the central row by hopping over it to an empty space on the opposite side of it. The player who reduces their opponent to fewer than four pieces wins.")
        (source "Bock-Raming 1995: 122-123.")
        (id "1460")
        (version "1.3.11")
        (classification "board/war/leaping/orthogonal")
        (origin "This game was played in India, around 1871.")
        }
    )
    
    (graphics {
        (show Symbol "thinCross" {9 14} scale:0.9)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Caturvimsatikosthakatmiki Krida_ai"
    )
)
