(game "Challis Ghutia"  
    (players 2)  
    (equipment { 
        (board
            (square 9)
            use:Vertex
        )
        (piece "Marker" Each 
            (or 
                ("HopSequenceCapture")
                ("StepToEmpty")
            )
        )
        }
    )  
    (rules 
        (start {
            (place "Marker1" (union (expand (sites Bottom) steps:3) (sites {"F5" "G5" "H5" "I5"})))
            (place "Marker2" (union (expand (sites Top) steps:3) (sites {"A5" "B5" "C5" "D5"})))
        })
        (play 
            (if "SameTurn"
                (or
                    ("HopSequenceCaptureAgain")	
                    (move Pass)
                )
                (forEach Piece)
            )
        )
        
        (end 
            (if (no Pieces Next) (result Mover Win))
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Challis Gutia (the game of forty pieces) is a game of leaping captures played in India.")
        (rules "9x9 intersecting lines forming a square. Forty pieces per player, lined up on the intersections on the rows closest to them, and the right half of the central line. Players alternate turns moving pieces to an empty adjacent spot along the lines. A piece may capture an opponent's piece by hopping over it to the empty spot immediately on the opposite side of it, following the lines of the board. Multiple captures are allowed. The player who captures all of the opponent's pieces wins.")
        (source "Datta 1939: 257-258.")
        (id "907")
        (version "1.3.4")
        (classification "board/war/leaping/orthogonal")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1939.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Challis Ghutia_ai"
    )
)
