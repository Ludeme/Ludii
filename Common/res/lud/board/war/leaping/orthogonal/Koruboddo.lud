(game "Koruboddo" 
    (players 2) 
    (equipment { 
        (board (square 5))
        (piece "Marker" Each 
            (or 
                ("HopOrthogonalSequenceCapture")
                ("StepOrthogonalToEmpty")
            )
        )
    }) 
    (rules 
        (start { 
            (place "Marker1" (union {(sites Row 0) (sites Row 1) (sites {"A3" "B3"})}))
            (place "Marker2" (union {(sites Row 3) (sites Row 4) (sites {"D3" "E3"})}))
        })
        
        (play 
            (if "SameTurn"
                (or
                    ("HopOrthogonalSequenceCaptureAgain")
                    (move Pass)
                )
                (forEach Piece)
            )
        )
        
        (end ("CaptureAll" Next))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Koruböddo is a capturing game played in Somalia in the early twentieth century. ")
        (aliases {"Koruböddo" "Korkaböd"})
        (rules "5x5 board. Twelve pieces per player, arranged in the two rows in front of each player, and in the right two squares in the central row (the central space is empty). Players alternate turns moving a piece to an adjacent orthogonal space. The player may capture an opponent's piece by hopping over it in an orthogonal direction. Multiple hops are allowed in the same turn, but captures are not compulsory, The player who captures all of their opponent's pieces wins.")
        (source "Marin 1931: 506.")
        (id "791")
        (version "1.3.5")
        (classification "board/war/leaping/orthogonal")
        (credit "Eric Piette")
        (origin "This game was played in Horn of Africa, around 1931.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Koruboddo_ai"
    )
)
