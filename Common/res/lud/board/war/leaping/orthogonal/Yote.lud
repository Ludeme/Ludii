(define "RemoveEnemyPiece" 
    (move
        Remove
        (sites Occupied by:Next container:"Board") 
    )
)

//------------------------------------------------------------------------------

(game "Yote"
    (players 2)
    (equipment {
        (board (square 5))
        (piece "Marker" Each 
            (or
                ("StepOrthogonalToEmpty")
                (move Hop 
                    (between 
                        if:(is Enemy (who at:(between))) 
                        (apply (remove (between)))
                    )
                    (to if:(is Empty (to)))
                    (then (moveAgain))
                )
            )
        )
        (hand Each)
    })
    (rules 
        (start (place "Marker" "Hand" count:12))
        (play 
            (if "SameTurn"
                "RemoveEnemyPiece"
                (or
                    (if (not ("HandEmpty" Mover))
                        (move 
                            (from (handSite Mover)) 
                            (to (sites Empty)) 
                        )  
                    )
                    (forEach Piece)
                )
            )
        )
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Yoté is a game with leaping captures played in Senegal. It is typically played in the sand with one side playing with stones or shells and the other with sticks. In it, a player making a capture is allowed to choose another of the opponent's pieces to remove from the board.")
        (aliases {"Yoté"})
        (rules "5x5 board. Twelve pieces per player. Players alternate turns placing a piece on the board or moving a piece already on the board. Pieces move one space in any orthogonal direction. A player may capture an opponent's piece by hopping over it to an empty space on the other side of it. When a capture is made in this fashion, the player chooses another piece belonging to the opponent to capture in addition. The player who captures all of the opponent's pieces wins.")
        (source "Béart 1955: 462-464.")
        (id "779")
        (version "1.3.4")
        (classification "board/war/leaping/orthogonal")
        (credit "Eric Piette")
        (origin  "This game was played in Senegal, around 1955.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Yote_ai"
    )
)
