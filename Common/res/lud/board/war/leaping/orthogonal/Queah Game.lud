(define "HaveRemainingSticks" (not ("HandEmpty" Mover)) )

(define "LessThanFourSticksOnBoard" (< (count Sites in:(sites Occupied by:Mover container:"Board")) 4) )

(define "ReplaceStick" 
    (move 
        (from (handSite Mover)) 
        (to (sites Empty)) 
        (then (moveAgain))
    )
)

//------------------------------------------------------------------------------

(game "Queah Game"  
    (players 2)
    (equipment {
        (board 
            (rotate 45
                (merge
                    (shift 1 -1 (rectangle 5 1))
                    (merge
                        (shift -1 1 (rectangle 1 5))
                        (square 3)
                    )
                )
            )
        )
        (piece "Stick" Each
            (or
                ("StepOrthogonalToEmpty")
                ("HopOrthogonalCapture")
            )
        )
        (hand Each)
    })
    (rules 
        (start {
            (place "Stick1" {"A1" "B2" "A3" "B4"})
            (place "Stick2" {"E5" "D4" "E3" "D2"})
            (place "Stick" "Hand" count:6)
        })
        (play
            (if (and 
                    ("HaveRemainingSticks") 
                    ("LessThanFourSticksOnBoard") 
                )
                ("ReplaceStick")
                (forEach Piece)
            )
        )
        (end
            (if (no Pieces Next)
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "The Queah Game is a game from nineteenth century Liberia, the name of which has not been reported. It is typically played in a \"board\" of interwoven sticks, in which \"male\" and \"female\" sticks are placed as pieces.")
        (rules "Played on a board with two rows of five spaces forming an \"X,\" with an extra space diagonally adjacent to the central space. Ten sticks per player. Each player begins with one stick in each of the rightmost four spaces on the edge of the board closest to them. Players alternate turns moving a stick to an adjacent space. A player may capture one of the opponent's sticks by jumping over it to an empty space beyond it. The opponent replaces the captured stick with one of those remaining in their reserve, until there are no sticks remaining. The player who captures all of the opponent's sticks wins. ")
        (source "BÃ¼ttikofer 1888: 91.")
        (id "164")
        (version "1.3.4")
        (classification "board/war/leaping/orthogonal")
        (credit "Eric Piette")
        (origin  "This game was played in Liberia, from around 1879 to 1882.")
        }
    )
    
    (graphics {
        (piece Scale "Stick" 0.7)
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    (ai
        "Queah Game_ai"
    )
)
