(game "Tre Guti"  
    (players 2)
    (equipment
        {
        (board (square 3) use:Vertex)
        (hand Each)
        (piece "Marker" Each
            (or
                (move Step
                    (to if:(is Empty (to))) 
                )
                (move Hop
                    (between 
                        if:(is Enemy (who at:(between))) 
                        (apply (remove (between)))
                    )
                    (to if:(is Empty (to)))
                )
            )	 
        )
        }
    )
    (rules 
        (start (place "Marker" "Hand" count:3))
        
        phases:{
        (phase "Placement"
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase ("HandEmpty" P2) "Movement")
        )
        
        (phase "Movement"
            (play (forEach Piece))
        )
        }
        
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tre Guti is a capturing game played on a small board. It was played by all ages and genders in the Mianwali district of Pakistan.")
        (rules "3x3 crossed lines, pieces move on the intersections and along the lines. Three pieces per player. In the placement phase, players alternate turns placing one of their pieces on an empty spot. When all of the pieces are placed, Players attempt to capture their opponent's pieces by hopping over them. The player who captures all of the opponent's pieces wins.")
        (source "Das-Gupta 1926a: 143-144")
        (id "711")
        (version "1.3.4")
        (classification "board/war/leaping/orthogonal")
        (credit "Matthew Stephenson")
        (origin  "This game was played in South Asia, around 1922.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Tre Guti_ai"
    )
)
