(game "T'oki"
    (players 2)
    (equipment {
        (board (square 5)) 
        (piece "Marker" Each
            (or
                ("StepOrthogonalToEmpty")
                (move Hop 
                    Orthogonal
                    (between 
                        (range 1 (count Rows))
                        if:("IsEnemyAt" (between)) 
                        (apply (remove (between)))
                    )
                    (to 
                        if:(or 
                            (and 
                                (is In (to) (sites Outer)) 
                                ("IsEnemyAt" (to))
                            ) 
                            (is Empty (to))
                        )
                    )
                )
            )
        )
        (hand Each)
    })
    (rules 
        (start (place "Marker" "Hand" count:20))
        (play
            (or
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty)) 
                    count:2
                ) 
                (forEach Piece)
            )
        )
        (end (if (and ("HandEmpty" Next) (no Pieces Next)) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "T'oki is a capturing game played by the Fula and Tukulor people of Mali. It combines the typical placement and movement phases into a single phase, with captures made by hopping over opponent's pieces. Typically played by men, it is a complex  game unique among games with this type of capture because multiple pieces may occupy one spot. It was primarily a game played by chiefs.")
(aliases {"Tioki"})
(rules "5x5 board. Twenty pieces per player. Players alternate turns placing pieces on the board, two at a time into the same square. At any time, a player may move one piece from a square one spot in an orthogonal direction. Pieces may capture opponent's pieces when either a single square or an unbroken line of squares with opponent's pieces has an empty space beyond it; the player's piece hops over the opponent's pieces and captures them all. When the unbroken line ends at the edge of the board, it is treated as though there were an empty square beyond the edge of the board, and the capturing piece is placed on the last space on the board in that line. Players may enter any reserved pieces on any turn as they see fit. The player to capture all of the opponent's pieces wins.")
(source "BÃ©art 1955: 460-461.")
(id "777")
(version "1.3.9")
(classification "board/war/leaping/orthogonal")
(credit "Eric Piette")
(origin "This game was played in Mali, around 1955.")
}
)
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "T'oki_ai"
    )
)

