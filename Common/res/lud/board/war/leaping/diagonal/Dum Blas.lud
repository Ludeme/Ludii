(define "HopCounter" 
    (move
        Hop 
        (from #1) 
        Diagonal 
        (between 
            if:(and (not (is In (between) (sites ToClear))) ("IsEnemyAt" (between)))
            (apply (remove (between) at:EndOfTurn))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)))

//------------------------------------------------------------------------------

(game "Dum Blas" 
    (players 2)  
    (equipment { 
        (board (square 10)) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })  
    
    (rules 
        (start { 
            (place "Counter1" (difference (expand (sites Bottom) steps:3) (sites Phase 1)))
            (place "Counter2" (difference (expand (sites Top) steps:3) (sites Phase 1)))
        })
        
        (play 
            (if "SameTurn"
                (if "IsUnpromoted"
                    (max Moves
                        ("HopCounter" (last To)
                            (then 
                                (if (can Move ("HopCounter" (last To))) 
                                    (moveAgain)
                                    (if "JumpToPromotionZone" 
                                        (move Promote (last To) (piece "DoubleCounter") Mover)
                                    )
                                ) 
                            ) 
                        )
                    )
                    (max Moves ("HopDiagonalSequenceCaptureAgain" before:(count Rows) after:(count Rows) at:EndOfTurn))
                )
                (priority {
                    (max Moves
                        (or 
                            (forEach Piece "Counter"
                                ("HopCounter" (from) 
                                    (then 
                                        (if (can Move ("HopCounter" (last To))) 
                                            (moveAgain) 
                                            (if "JumpToPromotionZone" 
                                                (move Promote (last To) (piece "DoubleCounter") Mover)
                                            )
                                        ) 
                                    ) 
                                ) 
                            )
                            (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture" before:(count Rows) after:(count Rows) at:EndOfTurn))
                        )
                    )
                    (or  
                        (forEach Piece "Counter" 
                            (move
                                Step 
                                (directions {FR FL}) 
                                (to if:(is Empty (to)))
                            )
                            (then 
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            )
                        )
                        (forEach Piece "DoubleCounter" (move Slide Diagonal))
                    )
                })
            )
        ) 
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Dum Blas is a game with leaping captures played in Malaysia during the early twentieth century.")
        (aliases {"Main Dum" "Dum Papan" "Dam Blas"})
        (rules "10x10 board, with red and white checkeres squares. Twenty pieces per player, which are arranged on the first four ranks closest to the player. Pieces move diagonally one space forward. Pieces may capture an opponent's piece by hopping over it diagonally to an empty adjacent square immediately on the other side of it. Pieces cannot capture in a backwards direction. The maximum number of pieces must be captured. If a player does not capture the maximum number of pieces, the other player may huff this piece, removing it from play. When a piece reaches the opposite edge of the board from where it started, it is promoted to king. Kings may move any distance diagonally, in a forward or backward direction. The player who captures all of their opponent's pieces wins.")
        (source "Robinson 1904.")
        (id "1203")
        (version "1.3.4")
        (classification "board/war/leaping/diagonal")
        (origin  "This game was played in Malaysia, around 1904.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
        (board Colour Phase0 (colour Red))
        (board Colour Phase1 (colour LightGrey))
    })
    
)
