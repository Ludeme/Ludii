(define "HopMan" 
    (move
        Hop 
        (from #1) 
        (directions {FR FL}) 
        (between 
            if:(is Enemy (who at:(between))) 
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)) )

//------------------------------------------------------------------------------

(game "English Draughts" 
    (players 2)  
    (equipment { 
        (board (square 8)) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })  
    (rules 
        (start
            { 
            (place "Counter1" (difference (expand (sites Bottom) steps:2) (sites Phase 1)  ) )
            (place "Counter2" (difference (expand (sites Top) steps:2) (sites Phase 1) ) )
            }
        )
        
        (play 
            (if "SameTurn"
                (if "IsUnpromoted"
                    ("HopMan" (last To) 
                        (then 
                            (if "JumpToPromotionZone" 
                                (move Promote (last To) (piece "DoubleCounter") Mover)
                                (if (can Move ("HopMan" (last To))) (moveAgain) ) 
                            ) 
                        ) 
                    )
                    ("HopDiagonalSequenceCaptureAgain")
                )
                (priority {
                    (or  
                        (forEach Piece "Counter" 
                            ("HopMan" (from) 
                                (then 
                                    (if "JumpToPromotionZone"
                                        (move Promote (last To) (piece "DoubleCounter") Mover) 
                                        (if (can Move ("HopMan" (last To)) ) (moveAgain) ) 
                                    ) 
                                ) 
                            ) 
                        )
                        (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture") )
                    )
                    
                    (or  
                        (forEach Piece "Counter" 
                            (move
                                Step 
                                (directions {FR FL}) 
                                (to if:(is Empty (to)))
                            ) 
                            (then 
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            )
                        )
                        (forEach Piece "DoubleCounter" "StepDiagonalToEmpty")
                    )
                })
            )
        ) 
        
        (end (if (no Moves Next) (result Mover Win))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "One of the most popular versions of Draughts, derived from the medieval game of Draughts but with the difference of the forced capture, hence its early name \"Forçat.\" It probably originated in France, but it gained greater popularity in Britain, and spread from there to the American colonies and to some Native American populations. It remains a popular game today.")
        (aliases {"Dams" "Dames" "Forçat" "Forcé" "Dammes" "Checkers"})
        (rules "Played on an 8x8 board with each player having twelve pieces. Pieces move diagonally one space forward, and can capture opponent's pieces by jumping them in a forward direction if they are adjacent. Capturing must happen if it is possible. Multiple captures are allowed. Once pieces reach the opposite side of the board from their starting position, they become kings and can move diagonally either forwards or backwards. The goal is to capture all of the opponent's pieces.")
        (source "Murray 1951: 76-77.")
        (id "56")
        (version "1.3.3")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin  "This game was played in France, around 1534.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
    (ai 
        "English Draughts_ai"
    )
    
)
