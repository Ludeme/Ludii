(define "Step" 
    (move 
        Step 
        #1 
        (to 
            if:(and 
                (is In (from) (sites Occupied by:Mover)) 
                (is Empty (to))
            )
        ) 
        stack:True
    )
)

(define "Hop" 
    (move
        Hop 
        #1 
        (between 
            #2
            #3
            if:("IsEnemyAt" (between)) 
            (apply (fromTo (from (between)) (to)))
        )
        (to if:(and 
                (is In (from) (sites Occupied by:Mover)) 
                (is Empty (to))
            )
        )
        stack:True 
        (then 
            (if 
                (can Move 
                    (hop 
                        (from (last To)) 
                        #1 
                        (between 
                            #2
                            #3
                            if:(and (not (is In (between) (last Between))) ("IsEnemyAt" (between))) 
                            (apply (fromTo (from (between)) (to)))
                        )     	
                        (to if:(and 
                                (is In (from) (sites Occupied by:Mover)) 
                                (is Empty (to))
                            )
                        )
                        stack:True
                    )
                ) 
                (moveAgain) 
            ) 
        )
    ) 
)

(define "IsUnpromoted" (= (what at:(last To) level:(topLevel at:(last To))) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)))

//------------------------------------------------------------------------------

(game "Bashni"
    (players {(player N) (player S)}) 
    (equipment { 
        (board (square 8))
        (piece "Counter" Each)
        (piece "CounterStar" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })  
    
    (rules 
        (start { 
            (place "Counter1" (difference (expand (sites Bottom) steps:2) (sites Phase 1)  ))
            (place "Counter2" (difference (expand (sites Top) steps:2) (sites Phase 1)))
        })
        
        (play
            (if "SameTurn"
                (if "IsUnpromoted" 
                    (move
                        Hop 
                        (from (last To)) 
                        (directions {FR FL}) 
                        (between if:("IsEnemyAt" (between)) (apply (fromTo (from (between)) (to)))) 
                        (to if:(and 
                                (is In (from) (sites Occupied by:Mover)) 
                                (is Empty (to))
                            )
                        )
                        stack:True 
                        (then 
                            (if "JumpToPromotionZone" (promote (last To) (piece "CounterStar") Mover)
                                (if 
                                    (can Move 
                                        (hop 
                                            (from (last To)) 
                                            (directions {FR FL}) 
                                            (between 
                                                if:("IsEnemyAt" (between)) 
                                                (apply (fromTo (from (between)) (to)))
                                            ) 		
                                            (to if:(and 
                                                    (is In (from) (sites Occupied by:Mover)) 
                                                    (is Empty (to))
                                                )
                                            )
                                            stack:True
                                        )
                                    ) 
                                    (moveAgain) 
                                )
                            ) 
                        )
                    ) 
                    (move
                        Hop
                        (from (last To)) 
                        Diagonal 
                        (between 
                            before:(count Rows)
                            after:(count Rows)
                            if:(and (not (is In (between) (last Between))) ("IsEnemyAt" (between)))
                            (apply (fromTo (from (between)) (to)))
                        ) 
                        (to if:(and 
                                (is In (from) (sites Occupied by:Mover)) 
                                (is Empty (to))
                            )
                        ) 
                        stack:True 
                        (then 
                            (if 
                                (can Move 
                                    (hop 
                                        (from (last To))  
                                        Diagonal 
                                        (between 
                                            before:(count Rows)
                                            after:(count Rows)
                                            if:(and (not (is In (between) (last Between))) ("IsEnemyAt" (between)))
                                            (apply (fromTo (from (between)) (to)))
                                        ) 
                                        (to if:(and 
                                                (is In (from) (sites Occupied by:Mover)) 
                                                (is Empty (to))
                                            )
                                        )  
                                        stack:True
                                    )
                                ) 
                                (moveAgain) 
                            ) 
                        )
                    ) 
                )
                (priority {
                    (or { 
                        (forEach Piece "Counter" ("Hop" (directions {FR FL})) top:True (then (if "JumpToPromotionZone" (promote (last To) (piece "CounterStar") Mover))))
                        (forEach Piece "CounterStar" ("Hop" Diagonal before:(count Rows) after:(count Rows)) top:True)
                    })
                    
                    (or { 
                        (forEach Piece "Counter" ("Step" (directions {FR FL})) top:True (then (if "JumpToPromotionZone" (promote (last To) (piece "CounterStar") Mover))))
                        (forEach Piece "CounterStar" (move Slide Diagonal stack:True) top:True)
                    })
                })
            )
        ) 

        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Bashni is a game with leaping captures played in Russia during the nineteenth century. It is the inspiration behind the game Lasca. Instead of removing pieces from the board when they are captured, they are stacked underneath the capturing piece.")
        (aliases {"Bashne" "Bashny"})
        (rules "8x8 board. Twelve pieces per player. Pieces begin on the dark spaces. White plays first. Players alternate turns moving their pieces diagonally one space forward to an empty space. A piece may capture an opponent's piece by hopping over it to an empty space on the opposite side of the opponent's piece, in a forward direction only. When pieces are captured, they are placed underneath the piece making the capture, forming a stack, controlled by the player whose piece is on top, the \"commander.\" When a stack is captured, the commander is captured and the capturing piece becomes the commander, thus reducing the stack by one. The piece which is now on top of the stack now becomes the commander of that stack. Stacks always move together and cannot be broken apart. Capturing is compulsory when it is possible. There is no obligation to capture the maximum number of pieces. A player who captures the commmander of a stack can only capture from the same stack in the same turn again if there is another intervening capture between the two captures from the stack. When a piece reaches the opposite end of the board from where it started, it becomes a king immediately. If it its promoted in the midst of a sequence of captures, it may begin to capture as a king immediately when it is promoted. Kings move any number of spaces in either direction. Kings may capture by hopping over any distance, as long as there are no intervening pieces, and may land on any space beyond the captured piece, as long as the move is in a straight line and the captured piece is the only piece in the line. When a king is capturing and there is a choice for where it ends its capturing leap, an option which allows further captures must be taken if possible. Kings retain their rank when in a stack. Stacks move according to the rank of its commander. Play continues until one player cannot make a legal move; the opponent wins.")
        (source "Michaelsen and Pakhomov 2000: 6-7.")
        (id "1530")
        (version "1.3.4")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin  "This game was played in Russia, around 1875.")
        }
    )
    
    (graphics {
        (board Style Chess)
    })
    
)
