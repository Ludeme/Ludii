(define "OrthogonalHopCaptureCounter"
    (do
        (move
            Hop 
            #1
            Orthogonal
            (between 
                before:2
                after:2
                if:(and (= 2 (count Steps (from) (between))) ("IsEnemyAt" (between)))
                (apply (remove (between)))
            ) 
            (to if:(is Empty (to))) 
        )
        ifAfterwards:(is In (last To) (sites Phase 1))
        #2
    )
)

(define "DiagonalHopCaptureCounter"
    (move
        Hop 
        #1
        Diagonal
        (between 
            if:("IsEnemyAt" (between))
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to)))
        #2
    )
)

(define "MoveAgainIfCanCaptureAgain" 
    (then 
        (if 
            (can Move 
                (or 
                    ("DiagonalHopCaptureCounter" (from (last To))) 
                    ("OrthogonalHopCaptureCounter" (from (last To)))
                )
            )
            (moveAgain)
        )
    )
)

(define "CounterMove"
    (or {
        (move
            Step 
            Diagonal
            (to if:(is Empty (to)))
        )
        (move
            Hop 
            Orthogonal
            (between if:(is Empty (between)))
            (to if:(is Empty (to)))
        )
        ("DiagonalHopCaptureCounter" (from) ("MoveAgainIfCanCaptureAgain"))
        ("OrthogonalHopCaptureCounter" (from) ("MoveAgainIfCanCaptureAgain"))
        }
    )
)

//------------------------------------------------------------------------------

(game "The Babylonian" 
    (players 2) 
    (equipment { 
        (board (square 10)) 
        (piece "Counter" Each "CounterMove")
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
        }
    ) 
    (rules 
        (start
            { 
            (place "Counter1" (difference (expand (sites Bottom) steps:3) (sites Phase 0) ))
            (place "Counter2" (difference (expand (sites Top) steps:3) (sites Phase 0)))
            }
        )
        
        (play 
            (if ("SameTurn")
                (or {
                    ("DiagonalHopCaptureCounter" (from (last To)) ("MoveAgainIfCanCaptureAgain"))
                    ("OrthogonalHopCaptureCounter" (from (last To)) ("MoveAgainIfCanCaptureAgain"))
                    (move Pass)
                })
                (forEach Piece)
            )
        ) 
        
        (end (if (no Pieces Next) (result Mover Win))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The Babylonian was a game that became popular in Paris around 1736, and continued to be played in the Netherlands into the nineteenth century. It was played on a Polish Draughts Board, but the pieces move as Chess queens.")
        (rules "10x10 checkered board. Twenty pieces per player, arranged on the four rows closest to the player. Pieces move orthogonally or diagonally in any direction, only on the white spaces. Pieces may capture by jumping over an enemy piece to an empty space on the other side of it. Multiple captures in one turn are allowed. The player to capture all of the opponent's pieces wins.")
        (source "Twiss 1805: 172; Murray 1951: 81.")
        (id "884")
        (version "1.3.5")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin "This game was played in France, from around 1736 to 1805.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
    (ai
        "The Babylonian_ai"
    )
)
