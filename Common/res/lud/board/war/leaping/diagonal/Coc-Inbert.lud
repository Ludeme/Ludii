(define "HopMan"
    (move
        Hop
        (from #1)
        (directions {FR FL})
        (between
            if:(and (is Enemy (who at:(between))) (!= (what at:(last To)) (id "DoubleCounter" Next)))
            (apply (remove (between)))
        )
        (to if:(is Empty (to)))
        #2
    )
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)) )

//------------------------------------------------------------------------------

(game "Coc-Inbert"
    (players 2)
    (equipment {
        (board (square 8))
        (piece "Counter" P1 N)
        (piece "Counter" P2 S)
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
        }
    )
    (rules
        (start
            {
            (place "Counter1" (difference (expand (sites Bottom) steps:2) (sites Phase 1)  ) )
            (place "Counter2" (difference (expand (sites Top) steps:2) (sites Phase 1) ) )
            }
        )

        (play
            (if "SameTurn"
                (if "IsUnpromoted"
                    (max Moves
                        ("HopMan" (last To)
                            (then
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                    (if (can Move ("HopMan" (last To))) (moveAgain) )
                                )
                            )
                        )
                    )
                    (max Moves ("HopDiagonalSequenceCaptureAgain"))
                )
                (priority {
                    (max Moves
                        (priority {
                            (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture") )
                            (forEach Piece "Counter"
                                ("HopMan" (from)
                                    (then
                                        (if "JumpToPromotionZone"
                                            (move Promote (last To) (piece "DoubleCounter") Mover)
                                            (if (can Move ("HopMan" (last To)) ) (moveAgain) )
                                        )
                                    )
                                )
                            )
                        })
                    )
                    (or
                        (forEach Piece "Counter"
                            (move
                                Step
                                (directions {FR FL})
                                (to if:(is Empty (to)))
                            )
                            (then
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            )
                        )
                        (forEach Piece "DoubleCounter" "StepDiagonalToEmpty")
                    )
                })
            )
        )

        (end {
            (if (no Pieces P1) (result P1 Win))
            (if (no Pieces P2) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Coc-Inbert is a Draughts game from early modern France. In it, the goal is reversed: The player who wins is the one who successfully forces the opponent to take all of the player's pieces.")
        (rules "8x8 Draughts board. Twelve pieces per player, arranged on the three rows closest to the players. Players alternate turns moving a piece forward diagonally to an adjacent empty space. Pieces capture an opponent's piece by hopping over it to an empty space on the opposite side of it, in a forward direction only. Captures are mandatory, and the maximum number of captures is required. Kings are made when a piece reaches the opposite edge of the board from where it started. Kings may move one space diagonally in either direction, and capture rules are the same as for pieces, except when different capturing routes are available with the same number of captures, the one which takes the most kings must be taken. In addition, if a sequence of captures can be made by a king or a regular piece, it must be made with the king. Kings cannot be captured by regular pieces. The first player to force the opponent to capture all of their pieces wins.")
        (source "Mallet 1668: 438-442.")
        (version "1.3.0")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin  "This game was played in France, around 1668.")
        }
    )

    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })

    (ai
        "Coc-Inbert_ai"
    )
)
