(define "HopMan" 
    (move
        Hop 
        (from #1) 
        (directions {FR FL}) 
        (between 
            if:(is Enemy (who at:(between))) 
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)) )

//------------------------------------------------------------------------------

(game "Main Dam" 
    (players 2)  
    (equipment { 
        (board (square 8)) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
        }
    )  
    (rules 
        (start
            { 
            (place "Counter1" (difference (expand (sites Bottom) steps:2) (sites Phase 1)  ) )
            (place "Counter2" (difference (expand (sites Top) steps:2) (sites Phase 1) ) )
            }
        )
        
        (play 
            (if "SameTurn"
                (if "IsUnpromoted"
                    ("HopMan" (last To) 
                        (then 
                            (if "JumpToPromotionZone" 
                                (move Promote (last To) (piece "DoubleCounter") Mover)
                                (if (can Move ("HopMan" (last To))) (moveAgain) ) 
                            ) 
                        ) 
                    )
                    ("HopDiagonalSequenceCaptureAgain" before:(count Rows) after:(count Rows))
                )
                (priority {
                    (or  
                        (forEach Piece "Counter" 
                            ("HopMan" (from) 
                                (then 
                                    (if "JumpToPromotionZone"
                                        (move Promote (last To) (piece "DoubleCounter") Mover) 
                                        (if (can Move ("HopMan" (last To)) ) (moveAgain) ) 
                                    ) 
                                ) 
                            ) 
                        )
                        (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture" before:(count Rows) after:(count Rows)) )
                    )
                    
                    (or  
                        (forEach Piece "Counter" 
                            (move
                                Step 
                                (directions {FR FL}) 
                                (to if:(is Empty (to)))
                            ) 
                            (then 
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            )
                        )
                        (forEach Piece "DoubleCounter" (move Slide Diagonal))
                    )
                })
            )
        ) 
        
        (end (if (no Moves Next) (result Mover Win))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Main Dam is a game with leaping captures played in Malaya in the early twentieth century.")
        (rules "8x8 board. Twelve pieces per player. Pieces move diagonally one space forward, and can capture opponent's pieces by jumping them in a forward direction if they are adjacent. Capturing must happen if it is possible. Multiple captures are allowed. Once pieces reach the opposite side of the board from their starting position, they become kings and can move diagonally either forwards or backwards, and can capture by jumping over any number of pieces, any distance. The goal is to capture all of the opponent's pieces.")
        (source "Wilkinson 1915:59.")
        (id "1050")
        (version "1.3.4")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin  "This game was played in Malaysia, around 1915.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
    (ai
        "Main Dam_ai"
    )
)
