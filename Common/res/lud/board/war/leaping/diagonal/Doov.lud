(define "HopMan" 
    (move
        Hop 
        (from #1) 
        (directions {FR FL}) 
        (between 
            if:("IsEnemyAt" (between)) 
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)))

//------------------------------------------------------------------------------

(game "Doov" 
    (players 2) 
    (equipment { 
        (board (square 8)) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    }) 
    (rules 
        (start { 
            (place "Counter1" (difference (expand (sites Bottom)) (sites Phase 1) ))
            (place "Counter2" (difference (expand (sites Top)) (sites Phase 1)))
        })
        
        (play 
            (or 
                (or 
                    (forEach Piece "Counter" 
                        ("HopMan" (from) 
                            (then 
                                (if "JumpToPromotionZone" (promote (last To) (piece "DoubleCounter") Mover)) 
                            ) 
                        ) 
                    )
                    (forEach Piece "DoubleCounter" ("HopDiagonalCapture"))
                )
                
                (or 
                    (forEach Piece "Counter" 
                        (move
                            Step 
                            (directions {FR FL}) 
                            (to if:(is Empty (to)))
                        ) 
                        (then 
                            (if "JumpToPromotionZone"
                                (promote (last To) (piece "DoubleCounter") Mover)
                            )
                        )
                    )
                    (forEach Piece "DoubleCounter" "StepDiagonalToEmpty")
                )
            )
        ) 
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Dööv is a capturing game played in Mongolia. Each player plays as a pack of dogs (one pack spotted, the other black), who jump over opposing dogs to capture them.")
        (aliases {"Dööv"})
        (rules "8x8 board. Eight pieces per player, which begin on alternating squares in the first two rows on opposing sides of the board. Players alternate turns moving one of their pieces to an adjacent space. Pieces move diagonally forward only. A piece may capture one of the opponent's adjacent pieces by hopping over it to an empty adjacent space immediately on the opposite side of the opponent's piece. Multiple captures are not allowed. When a piece arrives at the opposite edge of the board from where it began, it is promoted and may move diagonally forward or backward. The player who captures all of the opponent's pieces wins.")
        (source "Popova 1974: 34, 36-38.")
        (id "1043")
        (version "1.3.5")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
    (ai
        "Doov_ai"
    )
)
