(define "HopMan" 
    (move
        Hop 
        (from #1) 
        (directions {FR FL}) 
        (between 
            if:(is Enemy (who at:(between))) 
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)) )

//------------------------------------------------------------------------------

(game "Pleasant Draughts" 
    (players 2)  
    (equipment { 
        (board (square 8)) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
        }
    )  
    (rules 
        (start
            { 
            (place "Counter1" (difference (expand (sites Bottom) steps:2) (sites Phase 1)  ) )
            (place "Counter2" (difference (expand (sites Top) steps:2) (sites Phase 1) ) )
            }
        )
        
        (play 
            (if "SameTurn"
                (or
                    (if "IsUnpromoted"
                        ("HopMan" (last To) 
                            (then 
                                (if "JumpToPromotionZone" 
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                    (if (can Move ("HopMan" (last To))) (moveAgain) ) 
                                ) 
                            ) 
                        )
                        ("HopDiagonalSequenceCaptureAgain")
                    )
                    (move Pass)
                )
                (or  
                    (forEach Piece "Counter" 
                        (or
                            ("HopMan" (from) 
                                (then 
                                    (if "JumpToPromotionZone"
                                        (move Promote (last To) (piece "DoubleCounter") Mover) 
                                        (if (can Move ("HopMan" (last To)) ) (moveAgain) ) 
                                    ) 
                                ) 
                            ) 
                            (move
                                Step 
                                (directions {FR FL}) 
                                (to if:(is Empty (to)))
                            ) 
                            (then 
                                (if "JumpToPromotionZone"
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            )
                        )
                    )
                    (forEach Piece "DoubleCounter" 
                        (or
                            ("StepDiagonalToEmpty")
                            ("HopDiagonalSequenceCapture") 
                        )
                    )
                )
            )
        ) 
        
        (end (if (no Pieces Next) (result Mover Win))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Pleasant Draughts was a game played in France and England in the seventeenth century, particularly in rural communities. It lacked the forced capture of regular English Draughts.")
        (aliases {"Le Jeu Plaisant de Dames; Plaisant" "Pleasant"})
        (rules "Played on an 8x8 board with each player having twelve pieces. Pieces move diagonally one space forward, and can capture opponent's pieces by jumping them if they are adjacent. Capturing is not mandatory. Multiple captures are allowed. Once pieces reach the opposite side of the board from their starting position, they become kings and can move diagonally either forwards or backwards. The goal is to capture all of the opponent's pieces.
        ")
        (source "van der Stoep 1984: 146-147.")
        (id "882")
        (version "1.3.3")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin  "This game was played in England, around 1605.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
    (ai
        "Pleasant Draughts_ai"
    )
)
