(define "OrthogonalHopCaptureDoubleCounter"
    (do
        (move
            Hop
            #1
            Orthogonal
            (between
                before:3
                after:1
                if:(and (= 4 (count Steps (from) (between))) (is Enemy (who at:(between))))
                (apply (remove (between)))
            )
            (to if:(is Empty (to)))
        )
        ifAfterwards:(is In (last To) (sites Phase 0))
        #2
    )
)

(define "DiagonalHopCaptureDoubleCounter"
    (move
        Hop
        #1
        Diagonal
        (between
            before:1
            if:(and (= 2 (count Steps (from) (between))) (is Enemy (who at:(between))))
            (apply (remove (between)))
        )
        (to if:(is Empty (to)))
        #2
    )
)

(define "OrthogonalHopCaptureCounter"
    (do
        (move
            Hop
            #1
            Orthogonal
            (between
                before:3
                after:1
                if:(and (= 4 (count Steps (from) (between))) (is Enemy (who at:(between))))
                (apply (remove (between)))
            )
            (to if:(is Empty (to)))
        )
        ifAfterwards:(is In (last To) (sites Phase 0))
        #2
    )
)

(define "DiagonalHopCaptureCounter"
    (move
        Hop
        #1
        Diagonal
        (between
            before:1
            if:(and (= 2 (count Steps (from) (between))) (is Enemy (who at:(between))))
            (apply (remove (between)))
        )
        (to if:(is Empty (to)))
        #2
    )
)

(define "DoubleCounterMoveAgainIfCanCaptureAgain"
    (then
        (if
            (can Move
                (or
                    ("DiagonalHopCaptureDoubleCounter" (from (last To)) )
                    ("OrthogonalHopCaptureDoubleCounter" (from (last To)))
                )
            )
            (moveAgain)
        )
    )
)

(define "DoubleCounterMove"
    (or {
        (do
            (move Slide)
            ifAfterwards:(is In (last To) (sites Phase 0))
        )
        ("DiagonalHopCaptureDoubleCounter" (from) ("DoubleCounterMoveAgainIfCanCaptureAgain"))
        ("OrthogonalHopCaptureDoubleCounter" (from) ("DoubleCounterMoveAgainIfCanCaptureAgain"))
        }
    )
)

(define "CounterMoveAgainIfCanCaptureAgain"
    (then
        (if ("JumpToPromotionZone")
            (and
                (promote (last To) (piece "DoubleCounter") Mover)
                (if
                    (can Move
                        (or
                            ("DiagonalHopCaptureDoubleCounter" (from (last To)) )
                            ("OrthogonalHopCaptureDoubleCounter" (from (last To)))
                        )
                    )
                    (moveAgain)
                )
            )
            (if
                (can Move
                    (or
                        ("DiagonalHopCaptureCounter" (from (last To)) )
                        ("OrthogonalHopCaptureCounter" (from (last To)))
                    )
                )
                (moveAgain)
            )
        )
    )
)

(define "CounterMove"
    (or {
        (move
            Step
            (directions {FR FL})
            (to if:(is Empty (to)))
            (then
                (if ("JumpToPromotionZone")
                    (move Promote (last To) (piece "DoubleCounter") Mover)
                )
            )
        )
        ("DiagonalHopCaptureCounter" (from) ("CounterMoveAgainIfCanCaptureAgain"))
        ("OrthogonalHopCaptureCounter" (from) ("CounterMoveAgainIfCanCaptureAgain"))
        }
    )
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))
(define "JumpToPromotionZone" (is In (last To) (sites Next)) )

//------------------------------------------------------------------------------

(game "Maleys"
    (players 2)
    (equipment {
        (board (square 10))
        (piece "Counter" P1 N "CounterMove")
        (piece "Counter" P2 S "CounterMove")
        (piece "DoubleCounter" Each "DoubleCounterMove")
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
        }
    )
    (rules
        (start
            {
            (place "Counter1" (difference (expand (sites Bottom) steps:3) (sites Phase 1)  ) )
            (place "Counter2" (difference (expand (sites Top) steps:3) (sites Phase 1) ) )
            }
        )

        (play
            (if ("SameTurn")
                (or
                    (if ("IsUnpromoted")
                        (or
                            ("DiagonalHopCaptureCounter" (from (last To)) ("CounterMoveAgainIfCanCaptureAgain"))
                            ("OrthogonalHopCaptureCounter" (from (last To)) ("CounterMoveAgainIfCanCaptureAgain"))
                        )
                        (or
                            ("DiagonalHopCaptureDoubleCounter" (from (last To)) ("DoubleCounterMoveAgainIfCanCaptureAgain"))
                            ("OrthogonalHopCaptureDoubleCounter" (from (last To)) ("DoubleCounterMoveAgainIfCanCaptureAgain"))
                        )
                    )
                    (move Pass)
                )
                (forEach Piece)
            )
        )

        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Maleys is a Draughts game played in the Netherlands during the nineteenth century. It was speculated at the time that the rules were derived from another game from Dutch-colonised Indonesia, but there is no evidence of this.")
        (aliases {"Malayan"})
        (rules "10x10  board. Twenty pieces per player, which begin in the four rows closest to the players. Pieces move diagonally forward one space. Pieces capture orthogonally or diagonally, in any direction, when there is one empty space between the piece and the opponent's piece, and an empty space immediately on the opposite side of the opponent's piece. Pieces are promoted to Queen when they reach the edge of the board opposite from where they start. Queens may capture orthogonally or diagonally in any direction over any distance, as long as there are no other pieces intervening and there is an empty spot immediately after the piece to be captured, which is where the Queen lands to make the capture. Multiple captures are allowed  for Queens and regular pieces. Queens move orthogonally or diagonally any distance. The player who captures all of the opponent's pieces wins.")
        (source "Twiss 1805: 173-174.")
        (version "1.3.0")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin  "This game was played in Holland, around 1805.")
        }
    )

    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })

    (ai
        "Maleys_ai"
    )
)
