(define "HopCounter" 
    (move
        Hop 
        (from #1) 
        Diagonal 
        (between 
            if:(and 
                (not (is In (between) (sites ToClear))) 
                ("IsEnemyAt" (between))
            )
            (apply (remove (between) at:EndOfTurn))
        ) 
        (to if:(is Empty (to))) 
        #2
    ) 
)

(define "IsUnpromoted" (= (what at:(last To)) (id "Counter" Mover)))

(define "JumpToPromotionZone" (is In (last To) (sites Next)))

(define "PlayRules"
    (if "SameTurn"
        (if "IsUnpromoted"
            (max Moves
                ("HopCounter" (last To)
                    (then 
                        (if (can Move ("HopCounter" (last To))) 
                            (moveAgain)
                            (if "JumpToPromotionZone" 
                                (move Promote (last To) (piece "DoubleCounter") Mover)
                            )
                        ) 
                    ) 
                )
            )
            (max Moves ("HopDiagonalSequenceCaptureAgain" before:(count Rows) after:(count Rows) at:EndOfTurn))
        )
        (priority {
            (max Moves
                (or 
                    (forEach Piece "Counter"
                        ("HopCounter" (from) 
                            (then 
                                (if (can Move ("HopCounter" (last To))) 
                                    (moveAgain) 
                                    (if "JumpToPromotionZone" 
                                        (move Promote (last To) (piece "DoubleCounter") Mover)
                                    )
                                ) 
                            ) 
                        ) 
                    )
                    (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture" before:(count Rows) after:(count Rows) at:EndOfTurn))
                )
            )
            (or 
                (forEach Piece "Counter" 
                    (move 
                        Step 
                        (directions {FR FL}) 
                        (to if:(is Empty (to)))
                    )
                    (then 
                        (if "JumpToPromotionZone"
                            (move Promote (last To) (piece "DoubleCounter") Mover)
                        )
                    )
                )
                (forEach Piece "DoubleCounter" (move Slide Diagonal))
            )
        })
    )
)

(define "MovesWithCapture"
    (if "SameTurn"
        (if "IsUnpromoted"
            (max Moves
                ("HopCounter" (last To)
                    (then 
                        (if (can Move ("HopCounter" (last To))) 
                            (moveAgain)
                            (if "JumpToPromotionZone" 
                                (move Promote (last To) (piece "DoubleCounter") Mover)
                            )
                        ) 
                    ) 
                )
            )
            (max Moves ("HopDiagonalSequenceCaptureAgain" before:(count Rows) after:(count Rows) at:EndOfTurn))
        )
        (max Moves
            (or 
                (forEach Piece "Counter"
                    ("HopCounter" (from) 
                        (then 
                            (if (can Move ("HopCounter" (last To))) 
                                (moveAgain) 
                                (if "JumpToPromotionZone" 
                                    (move Promote (last To) (piece "DoubleCounter") Mover)
                                )
                            ) 
                        ) 
                    ) 
                )
                (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture" before:(count Rows) after:(count Rows) at:EndOfTurn))
            )
        )
    )
)

(define "PlayRulesHuff"
    (if (and ("SameTurn") (is Occupied (last To)))
        (if "IsUnpromoted"
            ("HopCounter" (last To)
                (then 
                    (if (can Move ("HopCounter" (last To))) 
                        (moveAgain)
                        (if "JumpToPromotionZone" 
                            (move Promote (last To) (piece "DoubleCounter") Mover)
                        )
                    ) 
                ) 
            )
            ("HopDiagonalSequenceCaptureAgain" before:(count Rows) after:(count Rows) at:EndOfTurn)
            (then ("HasCaptured"))
        )
        (or {
            (or 
                (forEach Piece "Counter"
                    ("HopCounter" (from) 
                        (then
                            (and
                                (if (can Move ("HopCounter" (last To))) 
                                    (moveAgain) 
                                    (if "JumpToPromotionZone" 
                                        (move Promote (last To) (piece "DoubleCounter") Mover)
                                    )
                                )
                                ("HasCaptured")
                            )
                        ) 
                    ) 
                )
                (forEach Piece "DoubleCounter" ("HopDiagonalSequenceCapture" before:(count Rows) after:(count Rows) at:EndOfTurn))
                (then ("HasCaptured"))
            )
            (forEach Piece "Counter" 
                (move 
                    Step 
                    (directions {FR FL}) 
                    (to if:(is Empty (to)))
                )
                (then 
                    (and
                        (if "JumpToPromotionZone"
                            (move Promote (last To) (piece "DoubleCounter") Mover)
                        )
                        ("HasNotCaptured")
                    )
                )
            )
            (forEach Piece "DoubleCounter" (move Slide Diagonal (then ("HasNotCaptured"))))
        })
    )
)

(define "HasCaptured" (set Value Mover 0))
(define "HasNotCaptured" (set Value Mover 1))
(define "DidNotCaptured" (= (value Player Prev) 1))
(define "RememberSiteWithPossibleCapture" 
    (set Pending 
        (sites From
            ("MovesWithCapture")
        )
    )
)

(define "SitesWithPossibleCaptureInPreviousTurn" (sites Pending))

(define "HuffOnePieceOf" 
    (move
        Select 
        (from #1
            if:(!= (what at:(from)) 0)
        )
        (then 
            (and {
                (remove (last To)) 
                (moveAgain) 
                (set Value Prev 0)
            })
        )
    )
)

(define "ShouldCapturedButMoved" 
    (and 
        (is In (last From) ("SitesWithPossibleCaptureInPreviousTurn")) 
        (is In (last From) (sites Empty))
    )
)

//------------------------------------------------------------------------------

(game "Canadian Draughts" 
    (players 2) 
    (equipment { 
        (board (square 12)) 
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    }) 
    
    (rules 
        (start { 
            (place "Counter1" (difference (expand (sites Bottom) steps:4) (sites Phase 1) ))
            (place "Counter2" (difference (expand (sites Top) steps:4) (sites Phase 1)))
        })
        
        (play 
            <Rules:rule>
        )
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(option "Variant" <Rules> args:{ <rule> }
    {  
    (item "Standard" <("PlayRules")> "The rules are the standard ones.")** 
    (item "Huff" 
        <
        (or
            (if ("DidNotCaptured")
                (or
                    (if ("ShouldCapturedButMoved") 
                        ("HuffOnePieceOf" (last To))
                    )
                    ("HuffOnePieceOf" ("SitesWithPossibleCaptureInPreviousTurn"))
                )
            )
            (do 
                ("RememberSiteWithPossibleCapture") 
                next:("PlayRulesHuff")
            )
        )
    > "The rules are the standard ones.")** 
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Standard (Described)" {
        "Variant/Standard"
    })*
    
    (ruleset "Ruleset/Huff Rule (Described)" {
        "Variant/Huff"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Canadian Draughts is played on a 12x12 checker board and is popular in Canada (particularly Quebec) and was also known to be played in New England. Anecdotally, it is said to have been invented by French settlers to Quebec, but 12x12 boards are known from Europe before this. Played on a 12x12 board with 30 pieces per player. Otherwise, follows the rules of International Draughts.")
        (aliases {"Le Jeu Franc" "Le Jeu de Dames Canadien"})
        (useFor "Ruleset/Standard (Described)" (rules "Played on a 12x12 board with 30 pieces per player, arranged on the dark squares. Pieces move diagonally one or can jump an opponent's adjacent player to capture it. Pieces can move forward or backwards. When a piece reaches the opposite edge of the board from its starting position, it becomes a king and can may move and jump over any number of spaces diagonally. Captures are compulsory and the maximum number of jumps must be made. Winning is achieved by capturing all the opponent's pieces or by blocking them from moving."))
        (useFor "Ruleset/Standard (Described)" (source "Murray 1951: 81."))
        (useFor "Ruleset/Standard (Described)" (id "54"))
        (useFor "Ruleset/Huff Rule (Described)" (rules "When a player fails to capture when it is possible, or when the player fails to capture the maximum number of pieces possible, the opponent has the option to remove the piece that should have captured but did not (or did not capture enough)."))
        (useFor "Ruleset/Huff Rule (Described)" (source "Anonymous 1875."))
        (useFor "Ruleset/Huff Rule (Described)" (id "1776"))
        (source "Murray 1951: 81.")
        (id "54")
        (version "1.3.5")
        (classification "board/war/leaping/diagonal")
        (credit "Eric Piette")
        (origin "This game was played in Canada, around 1875.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (piece Families {"Defined" "Isometric"})
    })
    
    (ai 
        "Canadian Draughts_ai"
    )
    
)
