(game "Padaivettu" 
    (players 2) 
    (equipment { 
        (board 
            (remove
                ("AlquerqueGraphWithFourTriangles")
                edges:{{20 16} {16 12} {12 8} {8 4} {0 6} {6 12} {12 18} {18 24}}
            )
            use:Vertex
        ) 
        (piece "Marker" Each
            (or
                ("StepToEmpty")
                ("HopCapture")
            )
        )
    }) 
    (rules 
        (start {
            (place "Marker1"
                (difference
                    (union 
                        (expand (sites Right)) 
                        (expand (sites Bottom) steps:4)
                    )
                    (expand (sites Centre) steps:2 W)
                )
            )
            (place "Marker2"
                (difference
                    (union 
                        (expand (sites Left)) 
                        (expand (sites Top) steps:4)
                    )
                    (expand (sites Centre) steps:2 E)
                )
            )
        })
        
        (play (forEach Piece))
        
        (end ("CaptureAll" Next))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Padaivettu is a game of captures played by the Tamil people of southern India.")
        (rules "5x5 intersecting lines. Triangles on each side, with the apex intersecting with the midpoint of the side of the square. A line is drawn from the apex to the base of each triangle, and a line bisecting it and the two opposite sides. A diamond is drawn within the square, the corners of which intersect with the apices of the triangles and the midpoint of each side is the center point of each quadrant of the square. 24 pieces per player, which begin on the points of the board closest to the player and in the triangle to their right and the right half of the central row. The central point is vacant. Players alternate turns moving a piece to an empty adjacent spot on the board. A piece may capture an opponent's piece by hopping over it to an empty adjacent spot immediately on the opposite side of the opponent's pieces along the lines on the board. The player who captures all of the opponent's pieces wins.")
        (source "Balambal 2019: 6-7.")
        (id "1130")
        (version "1.3.13")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin "This game was played in India, around 2019.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Padaivettu_ai"
    )
)
