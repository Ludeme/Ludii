(game "Dash Guti" 
    (players 2) 
    (equipment { 
        (board 
            (add
                (merge {
                    (wedge 4)
                    (shift 0 3 (rotate 180 (wedge 4)))
                })
                vertices:{{0 3} {6 3}}
                edges:{{7 19} {19 18} {0 19} {0 20} {20 16} {20 9}}
            )
            use:Vertex
        )
        (piece "Counter" Each 
            (or 
                ("HopCapture")
                ("StepToEmpty")
            )
        )
    }) 
    
    (rules 
        (start {
            (place "Counter1" (union {(sites Row 0) (sites Row 1) (sites Row 2) (intersection (sites Row 3) (sites Left))}))
            (place "Counter2" (union {(sites Row 6) (sites Row 5) (sites Row 4) (intersection (sites Row 3) (sites Right))}))
        })
        
        (play (forEach Piece))
        
        (end ("NoMoves" Loss))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Dash Guti is a game with leaping captures played in India, and similar to many other games with similar movement and capturing rules.")
(aliases {"Kowwu Dunki" "Kowwa Dunki"})
(rules "Each player lays out the pieces on the board on the intersections of the lines, each filling all the intersections of the triangle closest to the player, plus the extra intersection to their right. Play moves to the next adjacent intersection connected by a line. Opponent's pieces can be captured by hopping over them. The player who captures all of the opponent's pieces wins.")
(source "Gupta 1924: 166.")
(id "160")
(version "1.3.9")
(classification "board/war/leaping/lines")
(credit "Eric Piette")
(origin "This game was played in India, from around 1904 to 1923.")
}
)
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai 
        "Dash Guti_ai"
    )
    
)
