(define "PromoteIfBorderReached"
    (if (is In (last To) (sites Next))
        (move Promote (last To) (piece "DiscDouble") Mover)
    )
)

(define "HopDisc"
    (move
        Hop
        (between
            if:(is Enemy (who at:(between)))
            (apply (remove (between)))
        )
        (to if:(is Empty (to)))
        (then
            (and
                ("PromoteIfBorderReached")
                ("HasCaptured")
            )
        )
    )
)

(define "HopDiscDouble"
    (move
        Hop
        (between
            before:9
            after:9
            if:(is Enemy (who at:(between)))
            (apply (remove (between)))
        )
        (to if:(is Empty (to)))
        (then ("HasCaptured"))
    )
)

(define "HasCaptured"  (set Value Mover 0))
(define "HasNotCaptured" (set Value Mover 1))
(define "DidNotCaptured"  (= (value Player Prev) 1) )
(define "RememberSiteWithPossibleCapture"
    (set Pending
        (sites From
            (or
                (forEach Piece "Disc"
                    ("HopDisc")
                )
                (forEach Piece "DiscDouble"
                    (move
                        Hop
                        (between
                            before:9
                            after:9
                            if:(is Enemy (who at:(between)))
                            (apply (remove (between)))
                        )
                        (to if:(is Empty (to)))
                    )
                )
            )
        )
    )
)

(define "SitesWithPossibleCaptureInPreviousTurn" (sites Pending))

(define "HuffOnePieceOf"
    (move
        Select
        (from #1
            if:(!= (what at:(from)) 0)
        )
        (then
            (and  {
                (remove (last To))
                (moveAgain)
                (set Value Prev 0)
            })
        )
    )
)

(define "ShouldCapturedButMoved"
    (and
        (is In (last From) ("SitesWithPossibleCaptureInPreviousTurn"))
        (is In (last From) (sites Empty))
    )
)

//------------------------------------------------------------------------------

(game "Zamma"
    (players {(player N) (player S)})
    (equipment {
        ("AlquerqueBoard" 9 9)
        (piece "Disc" Each
            (or
                ("HopDisc")
                (move
                    Step
                    Forwards
                    (to if:(is Empty (to)))
                    (then ("HasNotCaptured"))
                )
                (then ("PromoteIfBorderReached"))
            )
        )
        (piece "DiscDouble" Each
            (or
                ("HopDiscDouble")
                (move Slide (then ("HasNotCaptured")))
            )
        )
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })

    (rules
        (start {
            (place "Disc1" (forEach (sites Board) if:(< (site) (centrePoint)) ))
            (place "Disc2" (forEach (sites Board) if:(> (site) (centrePoint)) ))
        })

        (play
            (or
                (if ("DidNotCaptured")
                    (or
                        (if ("ShouldCapturedButMoved")
                            ("HuffOnePieceOf" (last To))
                        )
                        ("HuffOnePieceOf" ("SitesWithPossibleCaptureInPreviousTurn"))
                    )
                )
                (do
                    ("RememberSiteWithPossibleCapture")
                    next:(forEach Piece)
                )
            )
        )

        (end (if (no Moves Mover) (result Next Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Zamma is a leaping capture game played in North Africa. It has been documented since at least the twentieth century, but is probably much older. It is related to other similar games from the area such as Draughts and Alquerque.")
        (aliases {"DÃ¢mma" "Zamet"})
        (rules "Played on a square Alquerque board with 9x9 intersecting lines with diagonals. Each player has 40 pieces. Pieces are placed on the intersections of the lines, and move forward along the lines to an adjacent unoccupied intersection. Once pieces reach the opposite side of the board from their starting position at the end of their turn, they are promoted and can move in any direction and over any distance. Opponent's pieces are captured by jumping them. Captures are obligatory if possible. If a player does not capture when they are supposed to, the opponent may remove that piece immediately and then play as normal. The player who captures all of their opponent's pieces or blocks them from being able to move wins.")
        (source "Monod 1951: 11-12.")
        (version "1.3.0")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin  "This game was played in North Africa, around 1950.")
        }
    )

    (graphics {
        (player Colour P1 (colour VeryDarkGrey))
        (player Colour P2 (colour White))
        (piece ExtendName P2 "Stick")

    })

    (ai
        "Zamma_ai"
    )

)
