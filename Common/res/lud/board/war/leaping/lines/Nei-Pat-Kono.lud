(game "Nei-Pat-Kono"
    (players 2) 
    (equipment { 
        (board <Board> use:Vertex) 
        (piece "Marker" Each 
            (or 
                ("StepOrthogonalToEmpty")
                (move Hop
                    Orthogonal 
                    (between if:("IsFriendAt" (between)))
                    (to if:("IsEnemyAt" (to))) 
                ) 
            ) 
        )
    }) 
    (rules 
        (start { <Start> })
        (play (forEach Piece))
        (end 
            (if 
                (or 
                    (no Moves Next) 
                    (<= (count Pieces Next) 1)
                ) 
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "4x4" <(square 4)> "The game is played on a 4x4 board.")* 
    (item "6x6" <(square 6)> "The game is played on a 6x6 board.") 
    (item "8x8" <(square 8)> "The game is played on a 8x8 board.") 
    (item "10x10" <(square 10)> "The game is played on a 10x10 board.") 
    (item "12x12" <(square 12)> "The game is played on a 12x12 board.") 
    (item "14x14" <(square 14)> "The game is played on a 14x14 board.") 
    (item "16x16" <(square 16)> "The game is played on a 16x16 board.") 
})

(option "Start Rules" <Start> args:{ <rules> }
    {
    (item "Half"  <(place "Marker1" (expand (sites Bottom) steps:(- (/ (count Rows) 2) 1))) (place "Marker2" (expand (sites Top) steps:(- (/ (count Rows) 2) 1)))>     "The game starts with half the board owned by each player.")* 
    (item "Empty Line" <(place "Marker1" (expand (sites Bottom) steps:(- (/ (count Rows) 2) 2))) (place "Marker2" (expand (sites Top) steps:(- (/ (count Rows) 2) 2)))> "The game starts with half the board owned by each player but with the two middle lines empty.")  
})

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Nei-Pat-Kono is a game played in Korea at least since the end of the nineteenth century, and likely earlier. It is a game of leaping captures similar to others common around the world.")
(aliases {"Four Field Kono"})
(rules "4x4 board, eight pieces each player. Pieces move orthogonally by either jumping a player's own piece to capture an opponent's piece or by moving one space into an empty hole. Captures are not compulsory. The goal is to reduce the opponents pieces to 1 or blocking them so they can no longer move.")
(source "Culin 1895: 101.")
(id "57")
(version "1.3.9")
(classification "board/war/leaping/lines")
(credit "Eric Piette")
(origin "This game was played in Korea, around 1895.")
}
)
    
    (graphics {
        (board Style Graph)
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Nei-Pat-Kono_ai"
    )
)
