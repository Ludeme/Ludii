(game "Kharbaga"
    (players {(player S) (player N)})
    (equipment {
        (board (square 5 diagonals:Solid) use:Vertex)
        (piece "Counter" Each 
            (or
                ("StepForwardsToEmpty")
                ("HopCapture")
                (then ("PromoteIfReach" (sites Next) "DoubleCounter"))
            )
        )
        (piece "DoubleCounter" Each
            (or
                ("HopCaptureDistance")
                (move Slide)
            )
        )
        (regions P1 (sites Top))
        (regions P2 (sites Bottom))
    })
    
    (rules 
        ("BeforeAfterCentreSetup" "Counter2" "Counter1")
        (play (forEach Piece)) 
        (end (if (no Moves Mover) (result Next Win))) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Kharbaga is a game played in North Africa, and is related to similar games played there that are similar to Alquerque. The board is a five by five grid.")
(rules "Players decide which colors to play, and who starts first. The board is filled in the beginning with all the players pieces. The only point vacant on the board is the middle point. Each player's pieces are placed on their half of the board, and lined up on the right-hand side of the fifth rank (the middle horizontal line of the grid board). Players alternate turns. Pieces move forward only (straight forward or diagonally forward) one space per turn following the pattern on the board until they are crowned Mullah by reaching the last rank. A non-Mullah piece may capture an enemy piece by hopping over it. The capture can be in any direction. When a piece reaches the other player's rank, it is promoted to Mullah. The Mullah can move and capture in any direction. It can also move any number of spaces. The Mullah can also capture an enemy piece from any distance, and land anywhere behind the captured piece provided there are no other pieces (friendly or not) within the spaces of the leap. If a piece piece reaches the other player's rank as an intermediate step of a capturing sequence, the piece does not get promoted to Mullah. The player to capture all of the opponent's pieces wins.")
(source "Hamidoun 1952: 68.")
(id "415")
(version "1.3.7")
(classification "board/war/leaping/lines")
(credit "Eric Piette")
(origin "This game was played in North Africa, from around 1952 to 2012.")
}
)
    
    (graphics {
        (piece Families {"Defined" "Isometric"})
    })
    
    (ai 
        "Kharbaga_ai"
    )
    
)
