(game "Pretwa"  
    (players 2)  
    (equipment { 
        (board (concentric {1 6 6 6}) use:Vertex)
        (piece "Counter" Each 
            (or 
                ("HopRotationalSequenceCapture") 
                ("StepRotationalToEmpty") 
            )
        )
    })  
    
    (rules 
        (start {
            (place "Counter1" (sites {2..4 8..10 14..16})  )
            (place "Counter2" (sites {1 6 5 7 12 11 13 18 17}))
        })
        
        (play 
            (if "SameTurn"
                (or
                    ("HopRotationalSequenceCaptureAgain")	
                    (move Pass)
                )
                (forEach Piece)
            )
        )
        
        (end <End>)  
    )
) 

//------------------------------------------------------------------------------

(option "End Rules" <End> args:{ <rules> }
    {
    (item "No Pieces" <(if (no Pieces Next) (result Next Loss))>    "The player loses when he has no piece.")*   
    (item "No Moves" <("NoMoves" Loss)>                             "A player loses when he can not move.")   
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Pretwa is a game played in India by the same rules of Alquerque, but with a circular board.")
        (rules "Each player begins with nine pieces. Pieces moves along the lines to the next intersection point. Players may hop an opponent's piece to capture it. Captures can be made in sequence if another capture is available after the previous one is made. The player who captures all of their opponent's pieces wins.")
        (source "Murray 1951: 71.")
        (id "162")
        (version "1.3.4")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, around 1951.")
        }
    )
    
    (graphics {
        
    })
    
    (ai 
        "Pretwa_ai"
    )
    
)
