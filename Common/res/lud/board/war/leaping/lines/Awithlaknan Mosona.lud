(game "Awithlaknan Mosona" 
    (players 2) 
    (equipment { 
        (board
            (merge {
                (repeat 1 1 step:{{0.5 0} {1 0}} (poly {{0.5 0} {0 0} {0 0.5}}))
                (repeat 1 1 step:{{0.5 0} {1 0}} (poly {{0.5 0} {0 0} {0 -0.5}}))
                (repeat 7 1 step:{{0.5 0} {1 0}} (poly {{0.5 0} {0 0.5} {1 0.5}}))
                (repeat 7 1 step:{{0.5 0} {1 0}} (poly {{0.5 0} {0 -0.5} {1 -0.5}}))
                (shift (- 7 1) 0 (repeat 1 1 step:{{0.5 0} {1 0}} (poly {{0.5 0} {1 0.5} {1 0}})))
                (shift (- 7 1) 0 (repeat 1 1 step:{{0.5 0} {1 0}} (poly {{0.5 0} {1 0} {1 -0.5}})))
                (shift 0.5 0 (rectangle 1 7))
            })
            use:Vertex
        )
        (piece "Counter" Each 
            (or 
                ("HopCapture")
                ("StepToEmpty")
            )
        )
    }) 
    
    (rules 
        (start {
            (place "Counter1" (union (sites Bottom) (sites Direction from:(centrePoint) W)))
            (place "Counter2" (union (sites Direction from:(centrePoint) E) (sites Top)))
        })
        (play (forEach Piece))
        (end ("NoMoves" Loss))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Awithlaknan Mosona was played by the Zuni in New Mexico. Older Zuni men claimed it came there from Mexico. Zuni men described the game and were observed playing it by Matilda Coxe Stevenson. Nick Graham, a Zuni man, drew the board for Stuart Culin. Culin suggests there are two different versions based on the two boards he documented, but Stevenson is clear that the size of the board is not of a specific size.")
        (aliases {"Awithlaknannai" "Awe Thlacnawe"})
        (rules "A series of three parallel lines are drawn, with diagonals connecting the outer lines at intervals, crossing each other at the central line. Eight in the outer rows, nine in the central. Pieces are moved along the intersections, and they are placed on the board on opposing sides, leaving the central spot empty. The first player moves to this spot along one of the lines, and the opponent jumps this pieces, thereby capturing it.")
        (source "Culin 1907: 801; Stevenson 1903: 496-497.")
        (id "158")
        (version "1.3.14")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin "This game was played in Zuni, from around 1902 to 1903.")
        }
    )
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Awithlaknan Mosona_ai"
    )
)
