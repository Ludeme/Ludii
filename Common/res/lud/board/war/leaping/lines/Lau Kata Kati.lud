(game "Lau Kata Kati" 
    (players 2) 
    (equipment { 
        (board 
            (merge
                (wedge 4)
                (shift 0 3 (rotate 180 (wedge 4)))
            ) 
            use:Vertex
        )
        (piece "Counter" Each 
            (or 
                ("HopCapture")
                ("StepToEmpty")
            )
        )
    }) 
    
    (rules 
        (start {
            (place "Counter1" (expand (sites Bottom) steps:2))
            (place "Counter2" (expand (sites Top) steps:2))
        })
        
        (play (forEach Piece))
        
        (end <End>)
    )
)

//------------------------------------------------------------------------------

(option "End Rules" <End> args:{ <rules> }
    {
    (item "No Pieces" <("CaptureAll" Next)> "The player loses when he has no piece.")* 
    (item "No Moves"  <("NoMoves" Loss)>    "A player loses when he can not move.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Lau Kata Kati is a leaping capture game similar to Alquerque observed during the nineteenth century in India, but which is probably much older.")
        (aliases {"Kowwu Dunki"})
        (rules "Play begins with each player's pieces on the pieces occupying the points of one of the triangles, leaving the shared apex empty. Pieces move to the next adjacent point connected by a line. Captures are made by hopping over an opponent's piece. The player who captures all of the opponent's pieces wins.")
        (source "Datta 1933: 168, Humphries 1906: 123.")
        (id "159")
        (version "1.3.9")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin "This game was played in India, from around 1863 to 1933.")
        }
    )
    (graphics {
        (show Edges Diagonal Hidden)
    })
    (ai 
        "Lau Kata Kati_ai"
    )
    
)
