(define "IsUnpromoted" ("IsPieceAt" "Counter" Mover (last To)))

(define "HopStyle"
    (move Hop 
        #1 
        #2 
        (between 
            if:(and 
                (not (is In (between) (sites ToClear))) 
                ("IsEnemyAt" (between))
            )
            (apply (remove (between)))
        ) 
        (to if:(is Empty (to))) 
        #3
    )
)

(define "HopSequenceCaptureAgain" 
    (move Hop
        (from (last To)) 
        Adjacent
        (between 
            #1
            #2
            if:(and (not (is In (between) (sites ToClear))) ("IsEnemyAt" (between))) 
            (apply (remove (between) #3))
        )
        (to if:(is Empty (to)))
        (then 
            (if (can Move 
                    (hop 
                        (from (last To))
                        Adjacent 
                        (between
                            #1
                            #2
                            if:(and 
                                (not (is In (between) (sites ToClear))) 
                                ("IsEnemyAt" (between))
                            )
                        )
                        (to if:(is Empty (to)))
                    )
                ) 
                (moveAgain)
            )
        )
    )
)

(define "HopSequenceCapture" 
    (move Hop
        Adjacent
        (between 
            #1
            #2
            if:("IsEnemyAt" (between)) 
            (apply (remove (between) #3))
        )
        (to if:(is Empty (to)))
        (then 
            (if (can Move 
                    (hop 
                        (from (last To)) 
                        Adjacent
                        (between 
                            #1
                            #2
                            if:(and 
                                (not (is In (between) (sites ToClear))) 
                                ("IsEnemyAt" (between))
                            )
                        )
                        (to if:(is Empty (to)))
                    )
                ) 
                (moveAgain)
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Permainan-Tabal" 
    ("TwoPlayersNorthSouth")
    (equipment { 
        ("AlquerqueBoardWithBottomAndTopTriangles")
        (piece "Counter" P1 N)
        (piece "Counter" P2 S) 
        (piece "DoubleCounter" Each)
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    }) 
    
    (rules 
        (start {
            (place "Counter1" (difference (expand (sites Bottom) steps:4) (sites Row 4)))
            (place "Counter2" (difference (expand (sites Top) steps:4) (sites Row 4)))
        })
        (play 
            (if "SameTurn"
                (if "IsUnpromoted"
                    ("HopStyle" (from (last To)) Adjacent
                        (then 
                            ("ReplayIfCanMove" ("HopStyle" (from (last To)) Adjacent)
                                ("PromoteIfReach" (sites Next) "DoubleCounter")
                            )
                        ) 
                    )
                    ("HopSequenceCaptureAgain" before:(count Rows) after:(count Rows))
                )
                (priority {
                    (or 
                        (forEach Piece "Counter"
                            ("HopStyle" (from) Adjacent
                                (then 
                                    ("ReplayIfCanMove" ("HopStyle" (from (last To)) Adjacent) 
                                        ("PromoteIfReach" (sites Next) "DoubleCounter")
                                    )
                                ) 
                            ) 
                        )
                        (forEach Piece "DoubleCounter" ("HopSequenceCapture" before:(count Rows) after:(count Rows)))
                    )
                    (or 
                        (forEach Piece "Counter" 
                            ("StepToEmpty" (directions {Forwards Rightward Leftward}))
                            (then ("PromoteIfReach" (sites Next) "DoubleCounter"))
                        )
                        (forEach Piece "DoubleCounter" (move Slide Adjacent))
                    )
                })
            )
        )
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        }
    )
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
)
