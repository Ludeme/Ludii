(define "BoardSize" 9)

//------------------------------------------------------------------------------

(game "Satoel"
    (players 2)
    (equipment {
        (board
            (merge {
                (square "BoardSize" diagonals:Alternating)
                (shift 2 8 (rotate 180 (wedge 3)))
                (shift 2 -2 (wedge 3))
                }
            )
            use:Vertex
        )
        (piece "Marker" Each
            (or
                ("HopSequenceCapture")
                ("StepToEmpty")
            )
        )
    })
    (rules
        (start {
            (place "Marker1" (forEach (sites Board) if:(< (site) (centrePoint)) ))
            (place "Marker2" (forEach (sites Board) if:(and (> (site) (centrePoint)) (< (site) (* "BoardSize" "BoardSize")) )))
        })
        (play
            (if "SameTurn"
                (or
                    ("HopSequenceCaptureAgain")
                    (move Pass)
                )
                (forEach Piece)
            )
        )
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Satoel is a capturing game played on the island of Simeulue. It is similar to other capturing games from South and Southeast Asia, but it is played on a larger board than the others. It is said to be of Malaysian origin.")
        (rules "9x9 intersecting lines, with diagonals in each 3x3 square. Two triangles on opposite sides, the apices of which intersect the central point on that side of the square. A line from the apex bisects the base, and this line is bisected with another line which intersects with the other two sides of the triangle. Forty pieces per player, which start on the four rows closest to the player, and on half of the central row. The central space remains empty. Players alternate turns moving a piece to an empty adjacent spot along the lines. Pieces may capture the adjacent piece of an opponent by hopping over it to an empty space directly behind it in a straight line. Multiple hops can be made in one turn if possible, with direction changes allowed. The player who captures all of their opponent's pieces wins.")
        (source "Jacobson 1919: 10-11.")
        (version "1.3.0")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin  "This game was played in Simeulue, around 1919.")
        }
    )

    (graphics {
        (show Edges Diagonal Hidden)

    })

    (ai
        "Satoel_ai"
    )
)
