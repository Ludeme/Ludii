(game "Felli"
    (players 2)
    (equipment {
        (board
            (graph
                vertices:{ {0 0} {2 0} {4 0} {1 1} {2 1} {3 1} {2 2} {1 3} {2 3} {3 3} {0 4} {2 4} {4 4}}
                edges:{ {0 1} {0 3} {1 2} {1 4} {2 5} {3 4} {3 6} {4 5} {4 6} {5 6} {6 7} {6 8} {6 9} {7 8} {7 10} {8 9} {8 11} {9 12}
                {10 11} {11 12}}
            )
            use:Vertex
        )
        (piece "Counter" Each)
        }
    )

    (rules
        (start
            {
            (place "Counter1" (expand (sites Bottom))  )
            (place "Counter2" (expand (sites Top)) )
            }
        )

        (play <Play>)

        (end ("NoMoves" Loss))
    )
)

//------------------------------------------------------------------------------

(option "Play Rules" <Play> args:{ <rules>> }
    {
    (item "Simplified"
        <
        (or {
            (forEach Piece "Counter" ("HopCapture"))
            (forEach Piece "Counter" ("StepToEmpty"))
        })
        >
        "Capturing is optional."
    )*
    (item "BoardGameGeek"
        <
        (if "SameTurn"
            ("HopSequenceCaptureAgain")
            (priority {
                (forEach Piece "Counter" ("HopSequenceCapture"))
                (forEach Piece "Counter" ("StepToEmpty"))
            })
        )
        >
        "Capturing is forced."
    )
})

//------------------------------------------------------------------------------

(rulesets {

    (ruleset "Ruleset/Board Game Geek (Suggested)"
        {
        "Play Rules/BoardGameGeek"
        }
    )

    (ruleset "Ruleset/Simplified (Suggested)"
        {
        "Play Rules/Simplified"
        }
    )

})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Felli is an Alquerque-like game from Morocco. Its history is uncertain, but is has been documented there at least since the 1980s. Its similarity to Alquerque and other games in the region suggests that it is older.")
        (aliases {"Fich"})
        (useFor "Ruleset/Simplified (Suggested)" (rules "Each player's six pieces are set up on their respective triangle.
                A piece is moved one space in any direction per turn onto an empty point along the lines of the board.
                A piece captures an enemy piece by hopping over it as in draughts.
        Only one piece may be used to move or capture per turn. Players alternate their turns throughout the game."))
        (useFor "Ruleset/Simplified (Suggested)" (source "<a href=\"https://en.wikipedia.org/wiki/Felli\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>"))
        (useFor "Ruleset/Board Game Geek (Suggested)" (rules "The same as the Wikipedia version, with the following additions:

        Pieces are promoted to Mullah upon reaching the other player's two corners on the first rank. The Mullahs may move any number of unoccupied spaces on the board, and capture an enemy piece from any distance and land any distance behind it provided no other pieces are in between. Multiple captures are allowed. If a non-Mullah piece captures enemy pieces in a sequence of multiple captures, and happens to land on the other player's first rank corners in the process, the player's piece does not become promoted to Mullah. Captures are compulsory."))
        (useFor "Ruleset/Board Game Geek (Suggested)" (source "<a href=\"https://boardgamegeek.com/boardgame/137830/felli\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>"))
        (source "Lh√¥te 1994: 481.")
        (version "1.3.0")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        }
    )

    (graphics {
        (show Edges Diagonal Hidden)

    })

    (ai
        "Felli_ai"
    )

)
