(game "Kotu Ellima" 
    (players 2)  
    (equipment { 
        (board 
            (merge 
                { 
                (shift  2 2 (square 5 diagonals:Alternating) ) 
                (shift  2 0 (wedge 3)) 
                (shift  5 3 (rotate  90 (wedge 3))) 
                (shift  2 6 (rotate 180 (wedge 3))) 
                (shift -1 3 (rotate 270 (wedge 3))) 
                } 
            ) 
            use:Vertex
        ) 
        (piece "Marker" Each 
            (or 
                ("HopCapture")
                ("StepToEmpty")
            )
        )
    })  
    (rules 
        (start { 
            (place "Marker1" (union { (expand (sites Bottom) steps:2) (expand (sites Right) steps:3) (sites {"F3" "G3" "E4" "F4" "F6" "F7" "G7"}) }))
            (place "Marker2" (union { (expand (sites Top) steps:2) (expand (sites Left) steps:3) (sites {"C3" "D3" "D4" "D6" "E6" "D7" "C7"}) }))
        })
        
        (play (forEach Piece))
        
        (end ("NoMoves" Loss))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Kotu Ellima is a leaping capture game played in Sri Lanka since at least the nineteenth century, but which may potentially be older.")
        (aliases {"Kotu Ellime" "Taking of the Castles"})
        (rules "From the starting position, each player takes turns moving one piece to an adjacent point connected to its current position by a line. Captures are made by hopping over an adjacent piece. The player who captures all the opponent's pieces wins.")
        (source "Ludovisi 1873: 33-34.")
        (id "155")
        (version "1.3.4")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, around 1873.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    
    (ai 
        "Kotu Ellima_ai"
    )
    
)
