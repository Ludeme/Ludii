(define "HopSequenceCapture" 
    (move Hop
        #1
        (between 
            if:(and ("IsEnemyAt" (between)) (>= (value Piece at:(from)) (value Piece at:(between))))
            (apply (remove (between)))
        )
        (to if:(is Empty (to)))
        (then 
            ("ReplayIfCanMove" 
                (hop 
                    (from (last To)) 
                    (between 
                        if:(and ("IsEnemyAt" (between)) (>= (value Piece at:(from)) (value Piece at:(between))))
                        (apply (remove (between)))
                    )
                    (to if:(is Empty (to)))
                )
            )
        )
    )
)

(define "OnlyKings" 
    (and {
        (= 1 (count Pieces P1))
        ("IsNotOffBoard" (where "King" P1))
        (= 1 (count Pieces P2))
        ("IsNotOffBoard" (where "King" P2))
    })
)

(define "HigherValue" 
    (max 
        (results
            from:(sites Occupied by:#1)
            to:(sites Occupied by:#1)
            (value Piece at:(to))
        )
    )
)

(define "OneSinglePiece" (= 1 (count Pieces #1)))

(define "SingleEnemyHigherPieceBlocked" 
    (and {
        (< ("HigherValue" Mover) ("HigherValue" Next))
        ("OneSinglePiece" Next)	
        (no Moves Next)
    })	
)

(define "OneSinglePieceSameRank"
    (and {
        ("OneSinglePiece" P1)
        ("OneSinglePiece" P2)
        (= ("HigherValue" P1) ("HigherValue" P2))
    })
)

(define "StepToEmptyToward"
    (move Step 
        (to 
            if:(and 
                (is Empty (to))
                (<
                    (count Steps (to) (regionSite (sites Occupied by:Next) index:0))
                    (count Steps (from) (regionSite (sites Occupied by:Next) index:0))
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Dablot Prejjesne"
    (players {(player S) (player N)})
    (equipment {
        (board (rectangle 7 6 diagonals:Solid) use:Vertex)
        (piece "Cone" Each 
            (or
                ("StepToEmpty")
                ("HopSequenceCapture")
            )
        )
        (piece "Prince" Each
            (or
                ("StepToEmpty")
                ("HopSequenceCapture")
            )		
        )
        (piece "King" Each
            (or
                ("StepToEmpty")
                ("HopSequenceCapture")
            )		
        )
        (regions P1 (sites Top))
        (regions P2 (sites Bottom))
    })
    
    (rules 
        (start { 
            (place "Cone1" (expand (sites Bottom) steps:2))
            (place "Cone2" (expand (sites Top) steps:2))
            (place "Prince1" coord:"H4" value:1)
            (place "Prince2" coord:"E9" value:1)
            (place "King1" coord:"I4" value:2)
            (place "King2" coord:"D9" value:2)
        })
        (play
            (if	("SameTurn")
                (or
                    ("HopSequenceCapture" 
                        (from (last To)) 
                    )
                    (move Pass)
                )
                (if ("OneSinglePieceSameRank")
                    (forEach Piece
                        (priority
                            ("HopCapture")
                            ("StepToEmptyToward")
                        )
                    )
                    (forEach Piece)
                )
            )
        ) 
        (end {
            ("CaptureAll" Next)	
            (if ("SingleEnemyHigherPieceBlocked") (result Mover Win))
            (if ("OnlyKings" Next) (result Mover Draw))	
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Dablot Prejjesne is a capturing game played by the Sámi people of Sweden. The game pits Sámi soldiers, a prince, and a king against peasants and their prince and king.")
        (rules "6x7 lines forming a grid. Diagonals in each small square, with a point where the diagonals cross. 28 pieces, which begin on opposite sides of the board. In addition, each player has a Prince piece, which begins on the rightmost point immediately in front of the other pieces, and a King piece, which begins on the rightmost point in the line in front of the Prince. All pieces move one space to an empty adjacent spot along the lines. A piece may capture an adjacent opponent's piece by hopping over it to an empty adjacent spot on the opposite side of it. Multiple captures are allowed. Captures are not compulsory and neither are the maximum number of captures. Pieces may only capture other pieces of the same rank or lower. A player wins when they capture all of the opponent's pieces, or by blocking a higher ranking piece from being able to move if it is the only remaining piece belonging to the opponent. If players are left with only one piece each of the same rank, one player may declare single combat, and both pieces must move toward one another until one captures the other. If only the Kings are left on the board the game is a draw. ")
        (source "Keyland 1921: 35-47.")
        (id "1228")
        (version "1.3.14")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin "This game was played in Sweden, around 1921.")
        }
    )
    
    (graphics {
        (piece Scale "King" 1.4)
        (piece Scale "Prince" 1.15)
        (piece Rename piece:"King" "Cone")
        (piece Rename piece:"Prince" "Cone")
    })
    
    (ai
        "Dablot Prejjesne_ai"
    )
)
