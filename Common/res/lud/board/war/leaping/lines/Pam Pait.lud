(game "Pam Pait" 
    (players 2) 
    (equipment { 
        (board
            (add
                (remove 
                    ("AlquerqueGraphWithBottomAndTopTriangles")
                    vertices:{1 11 21 23 13 3}
                )
                edges:{ {0 1} {1 2} {8 9} {9 10} {16 17} {17 18} }
            )
            use:Vertex
        )
        (piece "Marker" Each (or "HopCapture" "StepToEmpty"))
    }) 
    (rules 
        (start {
            (place "Marker1" (union (sites {"A4" "E4"}) (expand (sites Bottom) steps:3)))
            (place "Marker2" (union (sites {"A6" "E6"}) (expand (sites Top) steps:3)))
        })
        (play (forEach Piece))
        
        (end ("CaptureAll" Next))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Pam Pait (\"to cut a crowd\") is a game with leaping captures from the Assam region of India. ")
        (rules "3x5 lines, intersecting one another and forming a square. Diagonals are drawn in the four quadrants of the board. Two triangles, their apices intersecting the main board at opposite midpoints. The base of the triangle is bisected by a line drawn from the apex, and this line is bisected and intersects with the other two sides of the triangle. Fourteen pieces per player, which begin on the points in the triangle and the first two rows of the square closest to each player. The central line remains empty. Players alternate turns moving to an empty adjacent point along the lines. A player may capture an opponent's piece by hopping over it to an empty space on the opposite side of the opponent's piece along the lines of the board. The player who captures all of the opponent's pieces wins.")
        (source "Das Gupta 135: 152-154.")
        (id "900")
        (version "1.3.7")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1935.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Pam Pait_ai"
    )
)
