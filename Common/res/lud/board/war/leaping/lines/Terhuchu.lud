(define "SitesNotInTriangle" (difference (expand (sites Centre) steps:2) (sites {"F6" "I6" "L6" "F9" "L9" "F12" "I12" "L12"})))

(define "WithinTrianglePieceCanMoveDistanceTwo" (move Slide (between (exact 2) if:(and (is Empty (between)) (not (is In (between) ("SitesNotInTriangle")))))))

//------------------------------------------------------------------------------

(game "Terhuchu"
    (players 2)
    (equipment {
        (board
            (merge
                {
                (shift  2 2 (square 5 diagonals:Alternating) )
                (shift  2 0 (wedge 3))
                (shift  5 3 (rotate  90 (wedge 3)))
                (shift  2 6 (rotate 180 (wedge 3)))
                (shift -1 3 (rotate 270 (wedge 3)))
                (shift  0.65 1.15 (scale 0.5 (rotate -45 (wedge 3))))
                (shift  5.35 1.15 (scale 0.5 (rotate 45 (wedge 3))))
                (shift  5.35 5.85 (scale 0.5 (rotate 135 (wedge 3))))
                (shift  0.65 5.85 (scale 0.5 (rotate -135 (wedge 3))))
                }
            )
            use:Vertex
        )
        (piece "Marker" Each
            (or {
                ("HopCapture")
                ("StepToEmpty")
                (if (not (is In (from) "SitesNotInTriangle"))
                    ("WithinTrianglePieceCanMoveDistanceTwo")
                )
            })
        )
    })
    (rules
        (start {
            (place "Marker1" (sites {"H3" "F6" "G6" "H6" "I6" "J6" "L6" "H8" "I8" "J8"}))
            (place "Marker2" (sites {"H10" "I10" "J10" "F12" "H12" "I12" "J12" "L12" "J15"}) )
        })

        (play (forEach Piece))

        (end ("NoMoves" Loss))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Terh端ch端 is a leaping capture game played on an expanded Alquerque board. It was played by the Naga people in the early twentieth century, but is likely to be much older.")
        (aliases {"Terh端ch端"})
        (rules "Played on a board similar to Perali Kotuma, with the addition of triangular extensions on the four corners. Play begins with nine pieces for each player. Pieces move along the lines to the next open space. Opponent's pieces can be captured by hopping over them. Within the triangular extensions, pieces may move two places at a time, in a straight line. The player to capture all of the opponent's pieces wins.")
        (source "Hutton 1921: 101-102.")
        (version "1.3.0")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, around 1921.")
        }
    )

    (graphics {
        (show Edges Diagonal Hidden)

    })

    (ai
        "Terhuchu_ai"
    )

)
