(game "Peralikatuma" 
    (players 2)  
    (equipment { 
        (board 
            (merge { 
                (shift  2 2 (square 5 diagonals:Alternating) ) 
                (shift  2 0 (wedge 3)) 
                (shift  5 3 (rotate  90 (wedge 3))) 
                (shift  2 6 (rotate 180 (wedge 3))) 
                (shift -1 3 (rotate 270 (wedge 3))) 
            }) 
            use:Vertex
        )
        (piece "Marker" Each)
    })  
    (rules 
        (start { 
            (place "Marker1" (union { (expand (sites Bottom) steps:2) (expand (sites Right) steps:3) (sites {"F3" "G3" "F4" "F6" "F7" "G7"}) }))
            (place "Marker2" (union { (expand (sites Top) steps:2) (expand (sites Left) steps:3) (sites {"C3" "D3" "D4" "D6" "D7" "C7"}) }))
        })
        
        (play 
            (if "SameTurn"
                (or
                    ("HopSequenceCaptureAgain")
                    (move Pass)
                )
                (forEach Piece "Marker" 
                    (or
                        ("HopSequenceCapture")
                        ("StepToEmpty")
                    )
                )
            )
        )
        
        (end ("NoMoves" Loss))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Peralikatuma is a board game played in Sri Lanka and other places in South Asia, documented since the early twentieth century but likely to be older. The game is played on an Alquerque board, 5x5 intersecting lines, with triangular appendages on each of the four sides.")
        (aliases {"Perali Kotuwa"})
        (rules "Each player has 24 pieces, which are placed on the intersections of the lines and move along the lines to an adjacent intersection. Players may capture opponents' pieces by jumping them. Captures are not obligatory. Multiple captures can be made. A player wins by capturing all of the opponent's pieces.")
        (source "Parker 1909: 583.")
        (id "62")
        (version "1.3.3")
        (classification "board/war/leaping/lines")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, around 1909.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai 
        "Peralikatuma_ai"
    )
    
)
