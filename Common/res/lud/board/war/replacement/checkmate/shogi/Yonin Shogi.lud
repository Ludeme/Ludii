(define "CapturePiece"
    (apply
        (if (is Enemy (who at:(to)))
            (add
                (piece ("Captured" (what at:(to))))
                (to (mapEntry "Where" ("Captured" (what at:(to)))))
            )
        )
    )
)

(define "NextCannotMove"
    (not
        (can Move
            (do
                (or {
                    (move
                        (from (sites Occupied by:Next container:"Hand" components:{"Kakugyo" "Ginsho" "Hisha" "Kinsho"}))
                        (to (sites Empty))
                    )
                    (do
                        (move
                            (from (sites Occupied by:Next container:"Hand" component:"Fuhyo"))
                            (to (difference (sites Empty) (sites Next "LastRank"))
                                if:("OnePawnPerColumn" Next)
                            )
                        )
                        ifAfterwards:("IsInCheck" "Osho" Mover)
                    )
                    ("PlaceLance" Next)
                    ("PlaceKnight" Next)
                    (forEach Piece Next)
                } )
                ifAfterwards:(not ("IsInCheck" "Osho" Next))
            )
        )
    )
)

(define "CanNotMove"
    (not (can Move (do (and (forEach Piece #1) (set NextPlayer (player #2))) ifAfterwards:(not ("IsInCheck" "Osho" #1)))) )
)

(define "OnePawnPerColumn"
    (=
        (count Sites in:(sites Occupied by:#1 container:"Board" component:"Fuhyo"))
        (count Sites in:(difference (sites Occupied by:#1 container:"Board" component:"Fuhyo") (sites Column (column of:(to)))))
    )
)

(define "PlaceKnight"
    (move
        (from (sites Occupied by:#1 container:"Hand" component:"Keima"))
        (to (difference (sites Empty) (sites #1 "TwoFurthestRank")))
    )
)

(define "PlaceLance"
    (move
        (from (sites Occupied by:#1 container:"Hand" component:"Kyosha"))
        (to (difference (sites Empty) (sites #1 "LastRank")))
    )
)

(define "PlacePawn"
    (do
        (move
            (from (sites Occupied by:#1 container:"Hand" component:"Fuhyo"))
            (to (difference (sites Empty) (sites #1 "LastRank"))
                if:("OnePawnPerColumn" #1)
            )
        )
        ifAfterwards:(not (and ("IsInCheck" "Osho" #2) ("NextCannotMove")))
    )
)

(define "InPromotionZone"
    (is In #1 (sites Mover "Promotion"))
)

(define "InLastRank"
    (is In #1 (sites Mover "LastRank"))
)

(define "InTwoFurthestRank"
    (is In #1 (sites Mover "TwoFurthestRank"))
)

(define "Promote"
    (move Promote (last To) (piece (mapEntry "Promoted" (what at:(last To)))))
)

(define "CanPromote"
    (then
        (if
            (or ("InPromotionZone" (last To)) ("InPromotionZone" (last From)))
            (moveAgain)
        )
    )
)

(define "SlideMove"
    (move
        Slide
        #1
        (to if:(is Enemy (who at:(to))) "CapturePiece")
        #2
    )
)

(define "StepMove"
    (move
        Step
        #1
        (to if:(not (is Friend (who at:(to)))) "CapturePiece")
        #2
    )
)

(define "GoldMove" ("StepMove" (directions {Forward Backward Rightward Leftward FL FR})))

(define "Captured"
    (if (is Mover P1)
        (if (or {(= #1 (id "Fuhyo" P2)) (= #1 (id "Fuhyo" P3)) (= #1 (id "Fuhyo" P4))})
            (id "Fuhyo" P1)
            (if (or {(= #1 (id "Keima" P2)) (= #1 (id "Keima" P3)) (= #1 (id "Keima" P4))})
                (id "Keima" P1)
                (if (or {(= #1 (id "Hisha" P2)) (= #1 (id "Hisha" P3)) (= #1 (id "Hisha" P4))})
                    (id "Hisha" P1)
                    (if (or {(= #1 (id "Ginsho" P2)) (= #1 (id "Ginsho" P3)) (= #1 (id "Ginsho" P4))})
                        (id "Ginsho" P1)
                        (if (or {(= #1 (id "Kakugyo" P2)) (= #1 (id "Kakugyo" P3)) (= #1 (id "Kakugyo" P4))})
                            (id "Kakugyo" P1)
                            (if (or {(= #1 (id "Kyosha" P2)) (= #1 (id "Kyosha" P3)) (= #1 (id "Kyosha" P4))})
                                (id "Kyosha" P1)
                                (if (or {(= #1 (id "Kinsho" P2)) (= #1 (id "Kinsho" P3)) (= #1 (id "Kinsho" P4))})
                                    (id "Kinsho" P1)
                                    (if (or {(= #1 (id "Tokin" P2)) (= #1 (id "Tokin" P3)) (= #1 (id "Tokin" P4))})
                                        (id "Fuhyo" P1)
                                        (if (or {(= #1 (id "Narigin" P2)) (= #1 (id "Narigin" P3)) (= #1 (id "Narigin" P4))})
                                            (id "Ginsho" P1)
                                            (if (or {(= #1 (id "Ryuo" P2)) (= #1 (id "Ryuo" P3)) (= #1 (id "Ryuo" P4))})
                                                (id "Hisha" P1)
                                                (if (or {(= #1 (id "Ryuma" P2)) (= #1 (id "Ryuma" P3)) (= #1 (id "Ryuma" P4))})
                                                    (id "Kakugyo" P1)
                                                    (if (or {(= #1 (id "Narikei" P2)) (= #1 (id "Narikei" P3)) (= #1 (id "Narikei" P4))})
                                                        (id "Keima" P1)
                                                        (if (or {(= #1 (id "Narikyo" P2)) (= #1 (id "Narikyo" P3)) (= #1 (id "Narikyo" P4))})
                                                            (id "Kyosha" P1)
                                                            (id "Osho" P1)
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
        (if (is Mover P2)
            (if (or {(= #1 (id "Fuhyo" P1)) (= #1 (id "Fuhyo" P3)) (= #1 (id "Fuhyo" P4))})
                (id "Fuhyo" P2)
                (if (or {(= #1 (id "Keima" P1)) (= #1 (id "Keima" P3)) (= #1 (id "Keima" P4))})
                    (id "Keima" P2)
                    (if (or {(= #1 (id "Hisha" P1)) (= #1 (id "Hisha" P3)) (= #1 (id "Hisha" P4))})
                        (id "Hisha" P2)
                        (if (or {(= #1 (id "Ginsho" P1)) (= #1 (id "Ginsho" P3)) (= #1 (id "Ginsho" P4))})
                            (id "Ginsho" P2)
                            (if (or {(= #1 (id "Kakugyo" P1)) (= #1 (id "Kakugyo" P3)) (= #1 (id "Kakugyo" P4))})
                                (id "Kakugyo" P2)
                                (if (or {(= #1 (id "Kyosha" P1)) (= #1 (id "Kyosha" P3)) (= #1 (id "Kyosha" P4))})
                                    (id "Kyosha" P2)
                                    (if (or {(= #1 (id "Kinsho" P1)) (= #1 (id "Kinsho" P3)) (= #1 (id "Kinsho" P4))})
                                        (id "Kinsho" P2)
                                        (if (or {(= #1 (id "Tokin" P1)) (= #1 (id "Tokin" P3)) (= #1 (id "Tokin" P4))})
                                            (id "Fuhyo" P2)
                                            (if (or {(= #1 (id "Narigin" P1)) (= #1 (id "Narigin" P3)) (= #1 (id "Narigin" P4))})
                                                (id "Ginsho" P2)
                                                (if (or {(= #1 (id "Ryuo" P1)) (= #1 (id "Ryuo" P3)) (= #1 (id "Ryuo" P4))})
                                                    (id "Hisha" P2)
                                                    (if (or {(= #1 (id "Ryuma" P1)) (= #1 (id "Ryuma" P3)) (= #1 (id "Ryuma" P4))})
                                                        (id "Kakugyo" P2)
                                                        (if (or {(= #1 (id "Narikei" P1)) (= #1 (id "Narikei" P3)) (= #1 (id "Narikei" P4))})
                                                            (id "Keima" P2)
                                                            (if (or {(= #1 (id "Narikyo" P1)) (= #1 (id "Narikyo" P3)) (= #1 (id "Narikyo" P4))})
                                                                (id "Kyosha" P2)
                                                                (id "Osho" P2)
                                                            )
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (if (is Mover P3)
                (if (or {(= #1 (id "Fuhyo" P1)) (= #1 (id "Fuhyo" P2)) (= #1 (id "Fuhyo" P4))})
                    (id "Fuhyo" P3)
                    (if (or {(= #1 (id "Keima" P1)) (= #1 (id "Keima" P2)) (= #1 (id "Keima" P4))})
                        (id "Keima" P3)
                        (if (or {(= #1 (id "Hisha" P1)) (= #1 (id "Hisha" P2)) (= #1 (id "Hisha" P4))})
                            (id "Hisha" P3)
                            (if (or {(= #1 (id "Ginsho" P1)) (= #1 (id "Ginsho" P2)) (= #1 (id "Ginsho" P4))})
                                (id "Ginsho" P3)
                                (if (or {(= #1 (id "Kakugyo" P1)) (= #1 (id "Kakugyo" P2)) (= #1 (id "Kakugyo" P4))})
                                    (id "Kakugyo" P3)
                                    (if (or {(= #1 (id "Kyosha" P1)) (= #1 (id "Kyosha" P2)) (= #1 (id "Kyosha" P4))})
                                        (id "Kyosha" P3)
                                        (if (or {(= #1 (id "Kinsho" P1)) (= #1 (id "Kinsho" P2)) (= #1 (id "Kinsho" P4))})
                                            (id "Kinsho" P3)
                                            (if (or {(= #1 (id "Tokin" P1)) (= #1 (id "Tokin" P2)) (= #1 (id "Tokin" P4))})
                                                (id "Fuhyo" P3)
                                                (if (or {(= #1 (id "Narigin" P1)) (= #1 (id "Narigin" P2)) (= #1 (id "Narigin" P4))})
                                                    (id "Ginsho" P3)
                                                    (if (or {(= #1 (id "Ryuo" P1)) (= #1 (id "Ryuo" P2)) (= #1 (id "Ryuo" P4))})
                                                        (id "Hisha" P3)
                                                        (if (or {(= #1 (id "Ryuma" P1)) (= #1 (id "Ryuma" P2)) (= #1 (id "Ryuma" P4))})
                                                            (id "Kakugyo" P3)
                                                            (if (or {(= #1 (id "Narikei" P1)) (= #1 (id "Narikei" P2)) (= #1 (id "Narikei" P4))})
                                                                (id "Keima" P3)
                                                                (if (or {(= #1 (id "Narikyo" P1)) (= #1 (id "Narikyo" P2)) (= #1 (id "Narikyo" P4))})
                                                                    (id "Kyosha" P3)
                                                                    (id "Osho" P3)
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
                (if (or {(= #1 (id "Fuhyo" P1)) (= #1 (id "Fuhyo" P2)) (= #1 (id "Fuhyo" P3))})
                    (id "Fuhyo" P4)
                    (if (or {(= #1 (id "Keima" P1)) (= #1 (id "Keima" P2)) (= #1 (id "Keima" P3))})
                        (id "Keima" P4)
                        (if (or {(= #1 (id "Hisha" P1)) (= #1 (id "Hisha" P2)) (= #1 (id "Hisha" P3))})
                            (id "Hisha" P4)
                            (if (or {(= #1 (id "Ginsho" P1)) (= #1 (id "Ginsho" P2)) (= #1 (id "Ginsho" P3))})
                                (id "Ginsho" P4)
                                (if (or {(= #1 (id "Kakugyo" P1)) (= #1 (id "Kakugyo" P2)) (= #1 (id "Kakugyo" P3))})
                                    (id "Kakugyo" P4)
                                    (if (or {(= #1 (id "Kyosha" P1)) (= #1 (id "Kyosha" P2)) (= #1 (id "Kyosha" P3))})
                                        (id "Kyosha" P4)
                                        (if (or {(= #1 (id "Kinsho" P1)) (= #1 (id "Kinsho" P2)) (= #1 (id "Kinsho" P3))})
                                            (id "Kinsho" P4)
                                            (if (or {(= #1 (id "Tokin" P1)) (= #1 (id "Tokin" P2)) (= #1 (id "Tokin" P3))})
                                                (id "Fuhyo" P4)
                                                (if (or {(= #1 (id "Narigin" P1)) (= #1 (id "Narigin" P2)) (= #1 (id "Narigin" P3))})
                                                    (id "Ginsho" P4)
                                                    (if (or {(= #1 (id "Ryuo" P1)) (= #1 (id "Ryuo" P2)) (= #1 (id "Ryuo" P3))})
                                                        (id "Hisha" P4)
                                                        (if (or {(= #1 (id "Ryuma" P1)) (= #1 (id "Ryuma" P2)) (= #1 (id "Ryuma" P3))})
                                                            (id "Kakugyo" P4)
                                                            (if (or {(= #1 (id "Narikei" P1)) (= #1 (id "Narikei" P2)) (= #1 (id "Narikei" P3))})
                                                                (id "Keima" P4)
                                                                (if (or {(= #1 (id "Narikyo" P1)) (= #1 (id "Narikyo" P2)) (= #1 (id "Narikyo" P3))})
                                                                    (id "Kyosha" P4)
                                                                    (id "Osho" P4)
                                                                )
                                                            )
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )
    )
)

(define "RemoveKingIfCheckmate"
    (if (and (is Active #1) (not (is Mover #1)))
        (if
            (and
                ("IsInCheck" "Osho" #1)
                ("CanNotMove" #1 #2)
            )
            (remove (where (id "Osho" #1)))
        )
    )
)

//------------------------------------------------------------------------------

(game "Yonin Shogi"
    (players {(player N) (player W) (player S) (player E)})

    (equipment {
        (board (square 9))

        // King
        (piece "Osho" Each ("StepMove"))

        //Pawn
        (piece "Fuhyo" Each
            ("StepMove"
                Forward
                (then
                    (if ("InPromotionZone" (last To))
                        (if ("InLastRank" (last To))
                            ("Promote")
                            (moveAgain)
                        )
                    )
                )
            )
        )

        // Silver General
        (piece "Ginsho" Each ("StepMove" (directions {Forward BL BR FL FR}) "CanPromote"))

        // Rook
        (piece "Hisha" Each ("SlideMove" Orthogonal "CanPromote"))

        // Bishop
        (piece "Kakugyo" Each ("SlideMove" Diagonal "CanPromote"))

        // Knight
        (piece "Keima" Each
            (forEach Direction
                Forward
                (to
                    if:True
                    (apply
                        (forEach Direction
                            (from (to))
                            (directions {FR FL})
                            (if (not (is Friend (who at:(to))))
                                (move
                                    (from)
                                    (to
                                        (apply
                                            if:(is Enemy (who at:(to)))
                                            (add
                                                (piece ("Captured" (what at:(to))))
                                                (to (mapEntry "Where" ("Captured" (what at:(to)))))
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
                (then
                    (if ("InPromotionZone" (last To))
                        (if ("InTwoFurthestRank" (last To))
                            "Promote"
                            (moveAgain)
                        )
                    )
                )
            )
        )

        // Lance
        (piece "Kyosha" Each
            ("SlideMove"
                Forward
                (then
                    (if ("InPromotionZone" (last To))
                        (if ("InLastRank" (last To))
                            "Promote"
                            (moveAgain)
                        )
                    )
                )
            )
        )

        // Gold General
        (piece "Kinsho" Each "GoldMove")

        // Promoted Pawn
        (piece "Tokin" Each "GoldMove")

        // Promoted Silver
        (piece "Narigin" Each "GoldMove")

        // Promoted Rook
        (piece "Ryuo" Each (or ("SlideMove" Orthogonal) ("StepMove" Diagonal)))

        // Promoted Bishop
        (piece "Ryuma" Each (or ("SlideMove" Diagonal) ("StepMove" Orthogonal)))

        // Promoted Knight
        (piece "Narikei" Each "GoldMove")

        // Promoted Lance
        (piece "Narikyo" Each "GoldMove")

        (regions "LastRankP1" P1 (sites Top))
        (regions "LastRankP2" P2 (sites Left))
        (regions "LastRankP3" P3 (sites Bottom))
        (regions "LastRankP4" P4 (sites Right))
        (regions "TwoFurthestRankP1" P1 (expand (sites Top)))
        (regions "TwoFurthestRankP2" P2 (expand (sites Left)))
        (regions "TwoFurthestRankP3" P3 (expand (sites Bottom)))
        (regions "TwoFurthestRankP4" P4 (expand (sites Right)))
        (regions "Promotion" P1 (expand (sites Top) steps:2))
        (regions "Promotion" P2 (expand (sites Left) steps:2))
        (regions "Promotion" P3 (expand (sites Bottom) steps:2))
        (regions "Promotion" P4 (expand (sites Right) steps:2))
        (map "Where" {
            (pair (id "Fuhyo" P1) (handSite P1)) (pair (id "Fuhyo" P2) (handSite P2)) (pair (id "Fuhyo" P3) (handSite P3)) (pair (id "Fuhyo" P4) (handSite P4))
            (pair (id "Ginsho" P1) (handSite P1 1)) (pair (id "Ginsho" P2) (handSite P2 1)) (pair (id "Ginsho" P3) (handSite P3 1)) (pair (id "Ginsho" P4) (handSite P4 1))
            (pair (id "Hisha" P1) (handSite P1 2)) (pair (id "Hisha" P2) (handSite P2 2)) (pair (id "Hisha" P3) (handSite P3 2)) (pair (id "Hisha" P4) (handSite P4 2))
            (pair (id "Kakugyo" P1) (handSite P1 3)) (pair (id "Kakugyo" P2) (handSite P2 3)) (pair (id "Kakugyo" P3) (handSite P3 3)) (pair (id "Kakugyo" P4) (handSite P4 3))
            (pair (id "Keima" P1) (handSite P1 4)) (pair (id "Keima" P2) (handSite P2 4)) (pair (id "Keima" P3) (handSite P3 4)) (pair (id "Keima" P4) (handSite P4 4))
            (pair (id "Kyosha" P1) (handSite P1 5)) (pair (id "Kyosha" P2) (handSite P2 5)) (pair (id "Kyosha" P3) (handSite P3 5)) (pair (id "Kyosha" P4) (handSite P4 5))
            (pair (id "Kinsho" P1) (handSite P1 6)) (pair (id "Kinsho" P2) (handSite P2 6)) (pair (id "Kinsho" P3) (handSite P3 6)) (pair (id "Kinsho" P4) (handSite P4 6))
            (pair (id "Tokin" P1) (handSite P1)) (pair (id "Tokin" P2) (handSite P2)) (pair (id "Tokin" P3) (handSite P3)) (pair (id "Tokin" P4) (handSite P4))
            (pair (id "Narigin" P1) (handSite P1 1)) (pair (id "Narigin" P2) (handSite P2 1)) (pair (id "Narigin" P3) (handSite P3 1)) (pair (id "Narigin" P4) (handSite P4 1))
            (pair (id "Ryuo" P1) (handSite P1 2)) (pair (id "Ryuo" P2) (handSite P2 2)) (pair (id "Ryuo" P3) (handSite P3 2)) (pair (id "Ryuo" P4) (handSite P4 2))
            (pair (id "Ryuma" P1) (handSite P1 3)) (pair (id "Ryuma" P2) (handSite P2 3)) (pair (id "Ryuma" P3) (handSite P3 3)) (pair (id "Ryuma" P4) (handSite P4 3))
            (pair (id "Narikei" P1) (handSite P1 4)) (pair (id "Narikei" P2) (handSite P2 4)) (pair (id "Narikei" P3) (handSite P3 4)) (pair (id "Narikei" P4) (handSite P4 4))
            (pair (id "Narikyo" P1) (handSite P1 5)) (pair (id "Narikyo" P2) (handSite P2 5)) (pair (id "Narikyo" P3) (handSite P3 5)) (pair (id "Narikyo" P4) (handSite P4 5))
        })
        (map "Promoted" {
            (pair (id "Fuhyo" P1) (id "Tokin" P1)) (pair (id "Fuhyo" P2) (id "Tokin" P2)) (pair (id "Fuhyo" P3) (id "Tokin" P3)) (pair (id "Fuhyo" P4) (id "Tokin" P4))
            (pair (id "Ginsho" P1) (id "Narigin" P1)) (pair (id "Ginsho" P2) (id "Narigin" P2)) (pair (id "Ginsho" P3) (id "Narigin" P3)) (pair (id "Ginsho" P4) (id "Narigin" P4))
            (pair (id "Hisha" P1) (id "Ryuo" P1)) (pair (id "Hisha" P2) (id "Ryuo" P2)) (pair (id "Hisha" P3) (id "Ryuo" P3)) (pair (id "Hisha" P4) (id "Ryuo" P4))
            (pair (id "Kakugyo" P1) (id "Ryuma" P1)) (pair (id "Kakugyo" P2) (id "Ryuma" P2)) (pair (id "Kakugyo" P3) (id "Ryuma" P3)) (pair (id "Kakugyo" P4) (id "Ryuma" P4))
            (pair (id "Keima" P1) (id "Narikei" P1)) (pair (id "Keima" P2) (id "Narikei" P2)) (pair (id "Keima" P3) (id "Narikei" P3)) (pair (id "Keima" P4) (id "Narikei" P4))
            (pair (id "Kyosha" P1) (id "Narikyo" P1)) (pair (id "Kyosha" P2) (id "Narikyo" P2)) (pair (id "Kyosha" P3) (id "Narikyo" P3)) (pair (id "Kyosha" P4) (id "Narikyo" P4))
        })

        (hand Each size:7)
    })

    (rules
        (start {
            <PiecePlacement:PiecePlacement>
        })

        (play
            (if ("SameTurn")
                (or
                    ("Promote")
                    (move Pass)
                )
                (do
                    (or {
                        (move
                            (from (sites Occupied by:Mover container:"Hand" components:{"Kakugyo" "Ginsho" "Hisha" "Kinsho"}))
                            (to (sites Empty))
                        )
                        ("PlacePawn" Mover Next)
                        ("PlaceLance" Mover)
                        ("PlaceKnight" Mover)
                        (forEach Piece)
                    } )
                    ifAfterwards:(not ("IsInCheck" "Osho" Mover))
                )
                (then
                    (and {
                        ("RemoveKingIfCheckmate" P1 1)
                        ("RemoveKingIfCheckmate" P2 2)
                        ("RemoveKingIfCheckmate" P3 3)
                        ("RemoveKingIfCheckmate" P4 4)
                    })
                )
            )
        )

        (end
            {
            (forEach NonMover
                if:(= (where "Osho" Player) Off)
                (result Player Loss)
            )
            (if (no Moves Next) (result Next Loss))
            }
        )
    )
)

(option "Setup" <PiecePlacement> args:{ <PiecePlacement> }
    {
    (item "1993"
        <
        (place "Osho1" coord:"E1") (place "Kinsho1" {"D1" "F1"}) (place "Ginsho1" {"C1" "G1"}) (place "Fuhyo1" {"D2" "F2" "E3"}) (place "Hisha1" coord:"E2")
        (place "Osho2" coord:"I5") (place "Kinsho2" {"I4" "I6"}) (place "Ginsho2" {"I3" "I7"}) (place "Fuhyo2" {"H4" "H6" "G5"}) (place "Hisha2" coord:"H5")
        (place "Osho3" coord:"E9") (place "Kinsho3" {"D9" "F9"}) (place "Ginsho3" {"C9" "G9"}) (place "Fuhyo3" {"D8" "F8" "E7"}) (place "Hisha3" coord:"E8")
        (place "Osho4" coord:"A5") (place "Kinsho4" {"A4" "A6"}) (place "Ginsho4" {"A3" "A7"}) (place "Fuhyo4" {"B4" "B6" "C5"}) (place "Hisha4" coord:"B5")
        >
    "Setup by Ota Mitsuyasu in 1993")**

    (item "1991"
        <
        (place "Osho1" coord:"E1") (place "Kinsho1" {"D1" "F1"}) (place "Ginsho1" {"C1" "G1"}) (place "Keima1" {"B1" "H1"}) (place "Kyosha1" coord:"A1") (place "Fuhyo1" {"D3" "F3" "E3" "G3" "H3"}) (place "Hisha1" coord:"G2") (place "Kakugyo1" coord:"B2")
        (place "Osho2" coord:"I5") (place "Kinsho2" {"I4" "I6"}) (place "Ginsho2" {"I3" "I7"}) (place "Keima2" {"I2" "I8"}) (place "Kyosha2" coord:"I1") (place "Fuhyo2" {"G4" "G5" "G6" "G7" "G8"}) (place "Hisha2" coord:"H7") (place "Kakugyo2" coord:"H2")
        (place "Osho3" coord:"E9") (place "Kinsho3" {"D9" "F9"}) (place "Ginsho3" {"C9" "G9"}) (place "Keima3" {"B9" "H9"}) (place "Kyosha3" coord:"I9") (place "Fuhyo3" {"B7" "C7" "D7" "E7" "F7"}) (place "Hisha3" coord:"C8") (place "Kakugyo3" coord:"H8")
        (place "Osho4" coord:"A5") (place "Kinsho4" {"A4" "A6"}) (place "Ginsho4" {"A3" "A7"}) (place "Keima4" {"A2" "A8"}) (place "Kyosha4" coord:"A9") (place "Fuhyo4" {"C2" "C3" "C4" "C5" "C6"}) (place "Hisha4" coord:"B8") (place "Kakugyo4" coord:"B3")
        >
    "Setup in weekly children’s magazine in February 1991")
})

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "A version of 4-player Shogi.")
        (rules "Pieces move the same as in normal Shogi. Once a player is checkmated their Osho is removed, but all their other pieces remain on the board as neutral obstacles. Neutral pieces cannot help checkmate other players.")
        (source "<a href=\\"http://wikipedia.org/wiki/Yonin_shogi/\\" target=\\"_blank\\" class=\\"style1\\" style=\\"color: #0000EE\\" />Yonin shogi</a>")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/shogi")
        (author "Ota Mitsuyasu")
        (credit "Matthew Stephenson")
        (date "1995")
        }
    )

    (graphics {
        (player Colour P1 (colour White))
        (player Colour P2 (colour White))
        (player Colour P3 (colour White))
        (player Colour P4 (colour White))
        (piece Families {"Defined" "Study"})
        (board Style Shogi)
        (piece Rotate P2 degrees:270)
        (piece Rotate P3 degrees:180)
        (piece Rotate P4 degrees:90)
        (piece Colour "Tokin" strokeColour:(colour Red))
        (piece Colour "Narigin" strokeColour:(colour Red))
        (piece Colour "Ryuo" strokeColour:(colour Red))
        (piece Colour "Ryuma" strokeColour:(colour Red))
        (piece Colour "Narikei" strokeColour:(colour Red))
        (piece Colour "Narikyo" strokeColour:(colour Red))
    })

    (ai
        "Yonin Shogi_ai"
    )

)
