(define "PieceMove" 
    (or
        (move Step 
            (directions {Forward Rightward Leftward})
            (to if:(is Empty (to))) 
        )
        (move Hop 
            (directions {Forward Rightward Leftward})
            (between 
                if:("IsEnemyAt" (between)) 
                (apply (remove (between)))
            )
            (to if:(is Empty (to)))
        )
    )	 
)

//------------------------------------------------------------------------------

(game "Tobi Shogi" 
    (players {(player N) (player S)}) 
    (equipment
        {
        (board (square 9))
        (piece "Osho" Each "PieceMove")
        (piece "Kinsho" Each "PieceMove")
        (piece "Ginsho" Each "PieceMove")
        (piece "Keima" Each "PieceMove")
        (piece "Kyosha" Each "PieceMove")
        (piece "Fuhyo" Each "PieceMove")
        }
    )
    (rules 
        (start { 
            (place "Osho1" coord:"E1") (place "Kinsho1" {"D1" "F1"}) (place "Ginsho1" {"C1" "G1"}) (place "Keima1" {"B1" "H1"}) (place "Kyosha1" {"A1" "I1"}) (place "Fuhyo1" (sites Row 1))
            (place "Osho2" coord:"E9") (place "Kinsho2" {"D9" "F9"}) (place "Ginsho2" {"C9" "G9"}) (place "Keima2" {"B9" "H9"}) (place "Kyosha2" {"A9" "I9"}) (place "Fuhyo2" (sites Row 7))
        })
        
        (play (forEach Piece))
        
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tobi Shogi is a game with leaping captures that was played in Japan during the early twentieth century. It uses the Shogi board and pieces, but adapts them to a different game entirely.")
        (rules "9x9 board. Played with the full complement of Shogi pieces, placed in the first and second rows. All pieces move only one space orthogonally forward or laterally. Pieces capture by hopping over an opponent's piece. The player to capture all of the opponent's pieces wins.")
        (source "Murray 1951: 147.")
        (id "305")
        (version "1.3.5")
        (classification "board/war/replacement/checkmate/shogi")
        (credit "Matthew Stephenson")
        (origin "This game was played in Japan, around 1913.")
        }
    )
    
    (graphics {
        (player Colour P1 (colour White))
        (player Colour P2 (colour White))
        (piece Families {"Defined" "Study"})
        (board Style Shogi)
        (piece Rotate P2 degrees:180)
        (piece ExtendName P2 piece:"Osho" "1")
    })
    
    (ai
        "Tobi Shogi_ai"
    )
)
