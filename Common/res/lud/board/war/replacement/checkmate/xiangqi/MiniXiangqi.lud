(define "JiangSameColumnWithNoPieceBetween"
    (if (= (column of:(where "Jiang" P1) ) (column of:(where "Jiang" P2) ))
        (not ("NoSites" (forEach (sites Direction from:(where "Jiang" P1) N stop:(= (to) (where "Jiang" P2))) if:(not (is Empty (site))))))
        True
    )
)

(define "EnemyCannotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "Jiang" Next))) ))
)

(define "StepMoveOnThePalace"
    (move
        Step
        #1
        (to
            if:(and
                (is In (to) (sites Mover "Palace") )
                (not (is Friend (who at:(to))))
            )
            (apply (remove (to) ))
        )
    )
)

//------------------------------------------------------------------------------

(game "MiniXiangqi"
    (players {(player N) (player S)})
    (equipment {
        (board (rectangle 7 7) use:Vertex)

        // General
        (piece "Jiang" Each ("StepMoveOnThePalace" Orthogonal))

        // Horse
        (piece "Ma" Each
            (forEach Direction
                Orthogonal
                (to
                    if:(is Empty (to))
                    (apply
                        (forEach Direction
                            (from (to))
                            (directions {FR FL} of:All)
                            (if (not (is Friend (who at:(to))))
                                (move
                                    (from)
                                    (to
                                        (apply
                                            if:(is Enemy (who at:(to)))
                                            (remove (to))
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
        )

        // Chariot
        (piece "Ju" Each
            (move
                Slide
                Orthogonal
                (to
                    if:(is Enemy (who at:(to)))
                    (apply (remove (to) ) )
                )
            )
        )

        // Canon
        (piece "Pao" Each
            (or
                (move Slide Orthogonal)
                (move
                    Hop
                    Orthogonal
                    (between
                        before:(count Rows)
                        after:(count Rows)
                        if:(not (is Empty (between)))
                    )
                    (to
                        if:(is Empty (to))
                        (apply
                            if:(is Enemy (who at:(to)))
                            (remove (to) )
                        )
                    )
                )
            )
        )

        // Soldier
        (piece "Zu" Each
            (move
                Step
                (directions {Forward Rightward Leftward})
                (to
                    if:(not (is Friend (who at:(to))))
                    (apply (remove (to)))
                )
            )
        )

        (regions "PalaceP1" P1 { 2..4 9..11 16..18 } )
        (regions "PalaceP2" P2 { 30..32 37..39 44..46 } )
    })

    (rules
        (start {
            (place "Jiang1" coord:"D1") (place "Ma1" {"C1" "E1"}) (place "Ju1" {"A1" "G1"}) (place "Pao1" {"B1" "F1"}) (place "Zu1" {"A2" "C2" "D2" "E2" "G2"})
            (place "Jiang2" coord:"D7") (place "Ma2" {"C7" "E7"}) (place "Ju2" {"A7" "G7"}) (place "Pao2" {"B7" "F7"}) (place "Zu2" {"A6" "C6" "D6" "E6" "G6"})
        })

        (play (do
                (forEach Piece)
                ifAfterwards:(and
                    ("JiangSameColumnWithNoPieceBetween")
                    (not ("IsInCheck" "Jiang" Mover))
                )
            )
        )

        (end {
            (if (and
                    ("IsInCheck" "Jiang" Next)
                    ("EnemyCannotMove")
                )
                (result Mover Win)
            )
            (if (no Moves Next) (result Mover Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "A smaller version of Xiangqi.")
        (rules "The rules for pieces are the same as regular Xiangqi, except pawns start with the ability to move sideways.")
        (source "<a href=\"http://mlwi.magix.net/bg/minixiangqi.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Website</a>")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/xiangqi")
        (author "Shigenobu Kusumoto")
        (credit "Matthew Stephenson")
        (date "1974")
        }
    )

    (graphics {
        (player Colour P1 (colour White))
        (player Colour P2 (colour Red))
        (region Colour Cell {2 3 8 9 26 27 32 33} regionSiteType:Cell (colour 223 178 110))
        (board Colour Phase0 (colour 255 230 130))
        (board Colour InnerEdges (colour 100 75 50))
        (board Colour OuterEdges (colour 100 75 50))
        (piece Families {"Defined" "Western"})
        (piece ExtendName P2 "_black")
        (board Style Board)
    })

)
