(define "JiangSameColumnWithNoPieceBetween"
    (if (= (column of:(where "Jiang" P1)) (column of:(where "Jiang" P2)))
        (not ("NoSites" (forEach (sites Direction from:(where "Jiang" P1) N stop:(= (to) (where "Jiang" P2))) if:(not (is Empty (site))))))
        True
    )
)

(define "EnemyCannotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "Jiang" Next)))))
)

(define "StepMoveOnThePalace" 
    (move
        Step
        #1 
        (to 
            if:(and 
                (is In (to) (sites Mover "Palace")) 
                (not ("IsFriendAt" (to)))
            ) 
            (apply (remove (to)))
        )
    ) 
)

//------------------------------------------------------------------------------

(game "Xiangqi" 
    (players {(player N) (player S)})  
    (equipment { 
        (board (rectangle 10 9) use:Vertex) 
        
        // General
        (piece "Jiang" Each ("StepMoveOnThePalace" Orthogonal)) 
        
        // Guard
        (piece "Shi" Each ("StepMoveOnThePalace" Diagonal)) 
        
        // Elephant
        (piece "Xiang" Each 
            (move
                Slide
                Diagonal 
                (between 
                    (exact 2) 
                    if:(and { 
                        (is In (between) (sites Mover "Home")) 
                        (is Empty (between)) 
                    })
                )
                (to 
                    if:("IsEnemyAt" (to)) 
                    (apply (remove (to)))
                ) 
            )
        ) 
        
        // Horse
        (piece "Ma" Each 
            (forEach Direction 
                Orthogonal 
                (to 
                    if:(is Empty (to)) 
                    (apply 
                        (forEach Direction 
                            (from (to)) 
                            (directions {FR FL} of:All) 
                            (if (not ("IsFriendAt" (to))) 
                                (move 
                                    (from) 
                                    (to 
                                        (apply 
                                            if:("IsEnemyAt" (to))
                                            (remove (to))
                                        )
                                    )
                                )
                            )
                        ) 
                    )
                )
            ) 
        )
        
        // Chariot
        (piece "Ju" Each 
            (move
                Slide
                Orthogonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    (apply (remove (to)))
                )
            )
        )
        
        // Canon
        (piece "Pao" Each 
            (or 
                (move Slide Orthogonal) 
                (move
                    Hop
                    Orthogonal 
                    (between 
                        before:(count Rows) 
                        after:(count Rows)  
                        if:(not (is Empty (between)))
                    )
                    (to 
                        if:(is Empty (to)) 
                        (apply 
                            if:("IsEnemyAt" (to)) 
                            (remove (to))
                        )
                    )  
                ) 
            )
        )
        
        // Soldier
        (piece "Zu" Each
            (move
                Step
                (if 
                    (is In (from) (sites Mover "Home")) 
                    Forward 
                    (directions {Forward Rightward Leftward})
                ) 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    (apply (remove (to)))
                )
            )
        )
        
        (regions "PalaceP1" P1 { 3..5 12..14 21..23 })
        (regions "PalaceP2" P2 { 66..68 75..77 84..86 })
        (regions "Home" P1 (expand (sites Bottom) steps:4))
        (regions "Home" P2 (expand (sites Top) steps:4))
    })  
    
    (rules 
        (start { 
            (place "Jiang1" coord:"E1") (place "Shi1" {"D1" "F1"}) (place "Xiang1" {"C1" "G1"}) (place "Ma1" {"B1" "H1"}) (place "Ju1" {"A1" "I1"}) (place "Pao1" {"B3" "H3"}) (place "Zu1" {"A4" "C4" "E4" "G4" "I4"})
            (place "Jiang2" coord:"E10") (place "Shi2" {"D10" "F10"}) (place "Xiang2" {"C10" "G10"}) (place "Ma2" {"B10" "H10"}) (place "Ju2" {"A10" "I10"}) (place "Pao2" {"B8" "H8"}) (place "Zu2" {"A7" "C7" "E7" "G7" "I7"})
        })
        
        (play (do 
                (forEach Piece) 
                ifAfterwards:(and 
                    ("JiangSameColumnWithNoPieceBetween")
                    (not ("IsInCheck" "Jiang" Mover))
                )
            )
        )
        
        (end {
            (if (and 
                    ("IsInCheck" "Jiang" Next) 
                    ("EnemyCannotMove")
                ) 
                (result Mover Win)
            ) 

            ("BlockWin")
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Xiangqi, or \"Elephant Game,\" is a Chinese game ultimately derived from Indian Chaturanga. Its exact date of origin is disputed, but it has been popular in China for over a thousand years.")
        (aliases {"Siang K'i"})
        (rules "Played on a board with 9x10 intersecting lines. Pieces are placed on the intersections of the lines. Centered on opposite sides of the boards are two areas, known as the gong (\"castle\"). Bisecting the board is a blank space known as the he (\"river\"). Pieces are double-sided, with different colors and names of the pieces indicating to which player they belong. Jiang(\"general\")/shuai(\"marshal\")(1): may move one space orthogonally and cannot leave the gong except when opposite the opponent's jiang/shuai, in which case the piece can capture the opponent's jiang/shuai. Shi(\"guard\")(2): moves one space diagonally and cannot leave the gong. Xiang(\"elephant\")/xiang(\"minister\")(2): move two places diagonally and cannot cross the river. Ma(\"horse\")(2): move orthogonally one and then diagonally one space. Can be blocked by a piece next to it orthogonally. Ju(\"chariot\"(2): moves any number of spaces orthogonally. Pao(\"catapult\")/pao(\"cannon\") (2): can move orthogonally any number of spaces, captures by jumping one piece (of either player). Zu(\"private\"/zu(\"soldier\")(5): can move one space forward. Once they cross the river, can also move one space horizontally. The goal is to checkmate the opponent's jiang/shuai.")
                (source "Murray 1913: 121-134.")
                (id "49")
                (version "1.3.4")
                (classification "board/war/replacement/checkmate/xiangqi")
                (credit "Eric Piette")
                (origin  "This game was played in China, from around 779 to 1127.")
                }
            )
            
            (graphics {
                (player Colour P1 (colour White))
                (player Colour P2 (colour Red))
                (board Style Xiangqi)
                (piece Families {"Defined" "Western"})
                (piece ExtendName P2 "_black")
            })
            
            (ai 
                "Xiangqi_ai"
            )
            
        )
