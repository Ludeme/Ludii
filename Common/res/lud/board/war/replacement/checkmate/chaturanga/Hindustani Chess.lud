(define "CaptureToPiece"
    (apply
        (remove
            (to)
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))) )
)

(define "HasNeverMoved"
    (= (state at:(from)) 1)
)

(define "PieceHasMoved"
    (set State at:(last To) 0)
)

(define "RememberPieceHasMoved"
    (then
        (if
            (= (state at:(last To)) 1)
            "PieceHasMoved"
        )
    )
)

(define "PowerfulPieceOnColumnHasBeenRemoved"
    (if (and (is In (to) (sites Corners)) (< (count Sites in:(sites Occupied by:Mover component:"Boat")) 2))
        True
        (if (and (or (= (column of:(to)) 1) (= (column of:(to)) 6)) (< (count Sites in:(sites Occupied by:Mover component:"Horse")) 2))
            True
            (if (and (or (= (column of:(to)) 2) (= (column of:(to)) 5)) (< (count Sites in:(sites Occupied by:Mover component:"Elephant")) 2))
                True
                (if (and (or (= (column of:(to)) 3) (= (column of:(to)) 4)) (< (count Sites in:(sites Occupied by:Mover component:"Ferz_noCross")) 1))
                    True
                    False
                )
            )
        )
    )
)

(define "PromotePiece"
    (if (is In (last To) (sites Mover "Promotion"))
        (if (is In (last To) (sites Corners))
            (promote (last To) (piece (id "Boat" Mover)))
            (if (or (= (column of:(last To)) 1) (= (column of:(last To)) 6))
                (promote (last To) (piece (id "Horse" Mover)))
                (if (or (= (column of:(last To)) 2) (= (column of:(last To)) 5))
                    (promote (last To) (piece (id "Elephant" Mover)))
                    (if (or (= (column of:(last To)) 3) (= (column of:(last To)) 4))
                        (promote (last To) (piece (id "Queen" Mover)))
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Hindustani Chess"
    (players {(player N) (player S)})
    (equipment {
        (board (square 8))

        (piece "Pawn" Each
            (move
                Step
                Forward
                (to
                    if:(and
                        (or
                            (not (is In (to) (sites Mover "Promotion")))
                            (and
                                (is In (to) (sites Mover "Promotion"))
                                ("PowerfulPieceOnColumnHasBeenRemoved")
                            )
                        )
                        (not (is Friend (who at:(to))))
                    )
                    (apply (remove (to)))
                )
                (then ("PromotePiece"))
            )
        )
        (piece "Boat" Each
            (move
                Slide
                Orthogonal
                (to
                    if:(is Enemy (who at:(to)))
                    "CaptureToPiece"
                )
            )
        )
        (piece "King_noCross" Each
            (or
                (move
                    Step
                    (to
                        if:(not (is Friend (who at:(to))))
                        "CaptureToPiece"
                    )
                )
                (if (and (not ("IsInCheck" "King_noCross" Mover)) "HasNeverMoved")
                    (move
                        Leap
                        "KnightWalk"
                        (to
                            if:(not (is Friend (who at:(to))))
                            "CaptureToPiece"
                        )
                    )
                )
                "RememberPieceHasMoved"
            )
        )
        (piece "Elephant" Each
            (move
                Slide
                Diagonal
                (to
                    if:(is Enemy (who at:(to)))
                    "CaptureToPiece"
                )
            )
        )
        (piece "Horse" Each
            (move
                Leap
                "KnightWalk"
                (to
                    if:(not (is Friend (who at:(to))))
                    "CaptureToPiece"
                )
            )
        )
        (piece "Ferz_noCross" Each
            (move
                Slide
                (to
                    if:(is Enemy (who at:(to)))
                    "CaptureToPiece"
                )
            )
        )
        (regions "Promotion" P1 (sites Top) )
        (regions "Promotion" P2 (sites Bottom) )
        }
    )
    (rules
        (start
            {
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Boat1" {"A1" "H1"}) (place "Horse1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1" state:1)
            (place "Boat2" {"A8" "H8"}) (place "Horse2" {"B8" "G8"}) (place "Elephant2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"E8") (place "King_noCross2" coord:"D8" state:1)
            }
        )

        (play (do (forEach Piece) ifAfterwards:(and (not (no Moves Next)) (not ("IsInCheck" "King_noCross" Mover)))))

        (end
            {
            (if (and
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                )
                (result Mover Win)
            )
            (if (= (count Pieces Next) 1) (result Mover Draw) )
            (if (= (count Pieces Next) (+ 1 (count Sites in:(sites Occupied by:Next component:"Pawn")))) (result Mover Draw) )
            }
        )
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Hindustani Chess is the form of Chess played in northern India. It allows a special opening move for the king, and the opening position is slightly different from European Chess.")
        (rules "8x8 board, marked as in Chaturanga. Pieces move according to specialized moves, as follows: Pawns (x8): can move one space forward and capture one space forward diagonally; Elephant (or Chariot or Boat) (x2): can move any number of spaces orthogonally; Elephant (or Camel) (x2): can move any number of spaces diagonally; Horse (2): moves in any direction, one space orthogonally with one space forward diagonally; Vizier (x1): can move any number of spaces orthogonally or diagonally; Raja (x1): can move one space orthogonally or diagonally, but can also move like the horse on its first move, if it has not yet been checked. The pieces are arranged as in Chess, except the Vizier is place to the left of the Raja on both sides. Players capture pieces by moving onto a space occupied by an opponent's piece. When a pawn reaches the opposite edge of the board from where it started, it may be promoted to the more powerful piece that begins the game in that position, but only if one of these belonging to the player has already been captured. If this has not happened, the pawn cannot move to the final row. When a player can capture the opponent's Raja on the next turn, the Raja is in Check, the opponent's next move must free the Raja from check. If the opponent cannot, it is checkmate and the player wins. If a player captures all of the opponent's pieces aside from the Raja and pawns, it is declared a half-win, or Burd. When both players are left with only a Raja, it is a draw. Players are not allowed to stalemate the opponent. If check is given 70 times in succession, the game is a draw.")
        (source "Murray 1913: 78-82")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1885.")
        }
    )

    (graphics {
        (piece Scale "Pawn" 0.825)
        (show Check "King")
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (board Colour Symbols (colour Black))
        (board Colour Phase0 (colour 222 173 123))
        (show Symbol "thinCross" {0 3 4 7 24 27 28 31 32 35 36 39 56 59 60 63} scale:0.9)
    })

    (ai
        "Hindustani Chess_ai"
    )
)

