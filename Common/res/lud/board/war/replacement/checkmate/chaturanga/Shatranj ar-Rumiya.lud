(game "Shatranj ar-Rumiya" 
    (players 2) 
    (equipment { 
        (board 
            <Variant:board>
        )
        (piece "Pawn" Each
            (or {
                ("StepToEmpty" (if (= (state at:(from)) 0) CW CCW))
                (move 
                    (from)
                    (to 
                        (sites Around 
                            (sites To 
                                (move Step
                                    (if (= (state at:(from)) 0) CW CCW)
                                    (to if:True)
                                )
                            )
                            Orthogonal
                        )
                        if:(and 
                            (is Related Diagonal (from) (to))
                            ("IsEnemyAt" (to))
                        )
                    )
                )
                }
                (then 
                    (if (= (state at:(last To)) 0) 
                        (if ("IsPieceAt" "Pawn" Next (ahead (last To) CW))
                            (and 
                                (remove (last To))
                                (remove (ahead (last To) CW))
                            )
                        )
                        (if ("IsPieceAt" "Pawn" Next (ahead (last To) CCW))
                            (and 
                                (remove (last To))
                                (remove (ahead (last To) CCW))
                            )
                        )
                    )	
                )
            )
        )
        (piece "Rook" Each ("SlideCapture" Rotational))
        (piece "Knight" Each
            (forEach Direction 
                Orthogonal
                (to 
                    if:True 
                    (apply 
                        (forEach Direction 
                            (from (to)) 
                            Diagonal
                            (if (and 
                                    (not ("IsFriendAt" (to))) 
                                    (= 3 (count Steps Orthogonal (from) (to)))
                                )
                                (move 
                                    (from) 
                                    (to 
                                        (apply 
                                            if:("IsEnemyAt" (to))
                                            (remove (to))
                                        )
                                    )
                                )
                            )
                        )
                    )
                ) 
            )
        )
        (piece "Elephant" Each
            (move 
                (from)
                (to
                    (sites Around (sites Around (from) Diagonal) Diagonal)
                    if:(and 
                        (not ("IsFriendAt" (to))) 
                        (< 2 (count Steps Orthogonal (from) (to)))
                    )
                    (apply 
                        if:("IsEnemyAt" (to))
                        (remove (to))
                    )
                )
            )	
        )
        (piece "Queen" Each ("StepToNotFriend" Diagonal))
        ("ChessKing" "King_noCross")
    }) 
    (rules 
        <Variant:start>
        (play 
            (do
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        (end { 
            ("Checkmate" "King_noCross")
            <Variant:end>
        })
    )
)

//------------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <start> <board> <end>}
    {
    (item "Murray" 
        <
        (start {
            (place "Rook1" (sites {52 53}))
            (place "Rook2" (sites {61 60}))
            (place "Knight1" (sites {36 37}))
            (place "Knight2" (sites {44 45}))
            (place "Elephant1" (sites {20 21}))
            (place "Elephant2" (sites {28 29}))
            (place "Queen1" 4) (place "Queen2" 12)
            (place "King_noCross1" 5) (place "King_noCross2" 13)
            (place "Pawn1" (sites {3 19 35 51}) state:0) 
            (place "Pawn1" (sites {54 38 22 6}) state:1) 
            (place "Pawn2" (sites {11 27 43 59}) state:0) 
            (place "Pawn2" (sites {14 30 46 62}) state:1) 
        })
        >
        <
        (remove
            (concentric {0 0 0 16 16 16 16})
            cells:{1}
        )
        >
        <>
    "Rules Murray.")* 
    (item "Al-Amuli" 
        <
        (start {
            (place "Rook1" (sites {4 5}))
            (place "Rook2" (sites {13 12}))
            (place "Knight1" (sites {20 21}))
            (place "Knight2" (sites {29 28}))
            (place "Elephant1" (sites {36 37}))
            (place "Elephant2" (sites {45 44}))
            (place "Queen1" 52) (place "Queen2" 60)
            (place "King_noCross1" 53) (place "King_noCross2" 61)
            (place "Pawn1" (sites {3 19 35 51}) state:0) 
            (place "Pawn1" (sites {54 38 22 6}) state:1) 
            (place "Pawn2" (sites {11 27 43 59}) state:0) 
            (place "Pawn2" (sites {14 30 46 62}) state:1) 
        })
        > 
        <
        (add
            (add
                (remove
                    (concentric {0 0 0 16 16 16 16})
                    cells:{1}
                )
                edges:{{0 5} {0 9} {0 13} {1 0}}
            )
            cells:{
            {0 5 6 7 8 9}
            {0 9 10 11 12 13}
            {0 13 14 15 16 1}
            {0 1 2 3 4 5}
            }
        )
        >
        <
        (if (is In (where "King_noCross" Mover) (sites {64..67})) (result Mover Draw))
        >
    "Rules Al-Amuli.")* 
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Murray (Described)" {
        "Variant/Murray"
    })*
    
    (ruleset "Ruleset/Al-Amuli (Described)" {
        "Variant/Al-Amuli"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Shatranj ar-Rumiya is a game known from Islamic manuscripts from the medieval period. It is similar to Shatranj but played on a circular board. The Name literally means \"Byzantine Chess,\" but there is no evidence it was played in the Byzantine Empire.")
(aliases {"Byzantine Chess" "Circular Chess" "Shatranj al-Mudawara"})
(useFor "Ruleset/Murray (Described)" (rules "Circular board. Empty central circle, with four concentric rows of spaces, sixteen spaces per circle. Pieces are as follows: Rukh (x2), placed on two adjacent squares in the outer circle, move any number of spaces orthogonally; Asb (x2), placed in the two spaces adjacent to the Rukh in the next circle, move as Chess knights; Pil (x2): Placed in the two spaces adjacent to the Asb in the next circle in, move two spaces diagonally, jumping over the first space; Fres (x1): placed on the inner circle adjacent to the left Pil, moves one space diagonally; Shah (x1), placed to the right of the Fers, moves one space in any direction, Baidaq (x8), placed in each space flanking the other pieces, those on the left move clockwise, those on the right anti-clockwise, one space forward or one space diagonally forward to capture. No en passant, no promotion. When two Baidaq meet and neither can move, the opponent captures them. The opponent who can checkmate the opponent's Shah wins."))
(useFor "Ruleset/Murray (Described)" (source "Murray 1913: 342-343."))
(useFor "Ruleset/Murray (Described)" (id "293"))
(useFor "Ruleset/Al-Amuli (Described)" (rules "Four citadels in the center of the circle. Shah and Fers begin in the outer circle, Pil on the next squares toward the center, the Asb on the next, and the Rukh in the inner spaces of the circle. If a player moves their Shah into one of the citadels, the game is a draw."))
(useFor "Ruleset/Al-Amuli (Described)" (source "Murray 1913: 342."))
(useFor "Ruleset/Al-Amuli (Described)" (id "650"))
(source "Murray 1913: 342-343.")
(id "293")
(version "1.3.7")
(classification "board/war/replacement/checkmate/chaturanga")
(credit "Eric Piette")
}
)
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Scale "Rook" 0.6)
        (piece Scale "King_noCross" 0.6)
        (piece Scale "Queen" 0.6)
        (piece Scale "Elephant" 0.6)
        (piece Scale "Pawn" 0.5)
        (piece Scale "Knight" 0.6)
        (board Background image:"disc.svg" fillColour:(colour 223 178 110) edgeColour:(colour 223 178 110) scale:0.5)
    })
    
    (ai
        "Shatranj ar-Rumiya_ai"
    )
)
