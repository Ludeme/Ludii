(game "Chaturanga (Kridakausalya 14x14)"
    ("TwoPlayersNorthSouth")
    (equipment { 
        (board (square 14)) 
        ("ChessPawn" "Pawn" ~
            (then
                (if (is In (last To) (sites Mover "Promotion")) 
                    (and
                        (promote (last To) (piece "Ferz_noCross") Mover)	
                        (fromTo (from (last To)) (to (last From)))
                    )
                )
            )
        )
        ("ChessRook" "Rook")
        ("ChessBishop" "Camel")
        ("ChessKing" "King_noCross")
        ("ChessBishop" "Bishop_noCross")
        (piece "Queen" Each ("StepToNotFriend" Orthogonal)) 
        ("ChessQueen" "Commoner")
        ("ChessRook" "Elephant")
        ("ChessKnight" "Knight")
        ("ChessQueen" "Ferz_noCross")
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Elephant1" {"A1" "N1"}) 
            (place "Knight1" {"B1" "M1"}) 
            (place "Bishop_noCross1" {"C1" "L1"}) 
            (place "Rook1" {"D1" "K1"}) 
            (place "Camel1" {"E1" "J1"}) 
            (place "Commoner1" coord:"F1") 
            (place "Ferz_noCross1" coord:"G1") 
            (place "King_noCross1" coord:"H1") 
            (place "Queen1" coord:"I1") 
            
            (place "Pawn2" (sites Row 12))
            (place "Elephant2" {"A14" "N14"}) 
            (place "Knight2" {"B14" "M14"}) 
            (place "Bishop_noCross2" {"C14" "L14"}) 
            (place "Rook2" {"D14" "K14"}) 
            (place "Camel2" {"E14" "J14"})
            (place "Commoner2" coord:"I14") 
            (place "Ferz_noCross2" coord:"H14") 
            (place "King_noCross2" coord:"G14") 
            (place "Queen2" coord:"F14") 
        })
        (play
            (if (no Moves Mover)
                (move Remove
                    (difference (sites Occupied by:Next) (where "King_noCross" Next))
                )
                (do (forEach Piece) ifAfterwards:(not ("IsInCheck" "King_noCross" Mover)))
            )
        )
        
        (end {
            ("Checkmate" "King_noCross")
            (if (and {
                    (no Pieces Next "Ferz_noCross")
                    (no Pieces Next "Knight")
                    (no Pieces Next "Bishop_noCross")
                    (no Pieces Next "Elephant")
                    (no Pieces Next "Rook")
                    (no Pieces Next "Camel")
                    (no Pieces Next "Commoner")
                    (no Pieces Next "Queen")
                })
                (result Mover Win) 
            ) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Chaturanga is a game with replacement captures played in India since the early centuries CE. This version was played in the nineteenth century on a 14x14 board.")
(rules "14x14 board. Pieces move as follows: Raja (King, x1): moves one space in any direction; Mantri (Minister, placed to the left of the Raja, x1): moves any distance orthogonally or diagonally; Queen (x1, placed to the right of the Raja); moves orthogonally one space; Prince (placed to the left of the Minister, x1), moves orthogonally or diagonally any distance; Ushtra (Camel, x2): moves diagonally any distance; Chariot (x2), moves orthogonally any distance; Flagcar (x2), moves diagonally any distance; Vaha (Horse, x2): move orthogonally one space and then diagonally another, jumping over any intervening pieces; Danti (Elephant, x2): moves orthogonally any distance. Padati (Pawn, x14): move forward orthogonally one space or one space diagonally forward to capture. When a Padati reaches the opposite edge of the board, it is promoted to a Mantri and is moved immediately to the space it last moved from. An opponent's piece is captured by moving one of the player's own pieces onto the space occupied by the opponent's piece. If the Raja can be captured on the opponent's next turn, it is in check. The Raja cannot be in check at the end of the player's turn. If this is impossible, the opponent wins. When a player is reduced to only their Raja and Padati, the opponent wins. In the case of a stalemate, the player in stalemate may remove any of the opponent's pieces (except their Raja).
")
(source "Iyer 1982: 27-28; Bock-Raming 1995: 116.")
(id "1736")
(version "1.3.9")
(classification "board/war/replacement/checkmate/chaturanga")
(credit "Eric Piette")
(origin "This game was played in India, around 1871.")
}
)
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (show Symbol "thinCross" {84 98 97 111 90 91 104 105 6 7 188 189} scale:0.9)
    })
    
    (ai
        "Chaturanga (Kridakausalya 14x14)_ai"
    )
)
