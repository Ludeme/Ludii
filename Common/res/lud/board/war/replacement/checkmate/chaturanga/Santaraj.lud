// Version of the game without the simultaneous phase.

(define "CaptureEnemyToPiece"
    (apply 
        (if ("IsEnemyAt" (to))
            (remove (to))
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))))
)

//------------------------------------------------------------------------------

(game "Santaraj"
    (players {(player N) (player S)}) 
    (equipment {
        (board (square 8))
        (hand Each size:5)
        (piece "King_noCross" Each
            (move Step
                (to if:(not ("IsFriendAt" (to)))
                    ("CaptureEnemyToPiece")
                )
            )		
        )
        (piece "Castle" Each
            (move
                Slide 
                Orthogonal
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureEnemyToPiece"
                ) 
            )		
        )
        (piece "Ferz_noCross" Each
            (move Hop Diagonal 
                (between (exact 2) if:True) 
                (to
                    if:(not ("IsFriendAt" (to))) 
                    ("CaptureEnemyToPiece")
                ) 
            )		
        )
        (piece "Knight" Each
            (move
                Leap 
                "KnightWalk" 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureEnemyToPiece"
                ) 
            )		
        )
        (piece "Pawn" Each
            (or 
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Mover "Promotion"))
                        (promote (last To) (piece "Queen") Mover)
                    )
                )
            )		
        ) 
        (piece "Queen" Each
            (move Step
                Diagonal
                (to if:(not ("IsFriendAt" (to)))
                    ("CaptureEnemyToPiece")
                )
            )		
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start {  
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Castle1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Ferz_noCross1" {"C1" "F1"}) (place "Queen1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Castle2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Ferz_noCross2" {"C8" "F8"}) (place "Queen2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        (play 
            (do
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
                (then 
                    (if
                        (and
                            (> (count Pieces P1) 1) 
                            (> (count Pieces P2) 1) 
                        )	
                        (set Counter)
                    )
                )
            )		
        )
        (end {
            (if (and 
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
            (if 
                (and 
                    (or
                        (= (count Pieces P1) 1) 
                        (= (count Pieces P2) 1) 
                    )	
                    (= (counter) 20)
                ) 
                (result Mover Draw)
            ) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Säntäräj is a capturing game played in Ethiopia since at least the seventeenth century, among the ruling classes. It is very similar to Shatranj, though it begins with an opening phase known as Wärära (\"marshalling of troops\"), where players make their moves simultaneously without regard to alternating turns, until the first piece is captured.")
        (aliases {"Säntäräj"})
        (rules "8x8 board. The pieces move as follows, with the number per player: 1 x Negus (king): moves one space orthogonally or diagonally. 1 x Ferz (counselor): One square diagonally. 2 x Der (castle): Any number of spaces orthogonally. 2 x Säbä (man): Three squares diagonally, jumping over the first two. 2 x Färäs (horse): Moves as a chess knight. 8 x Médéq: Moves one space forward orthogonally; one space forward diagonally to capture. No en passant. Promoted to Ferz when reaching the eighth rank. No castling, except in the opening phase. In the opening phase, players play simultaneously at will, with no turn structure, until the first piece is captured, at which point turns alternate for the main phase of the game. Castling is allowed during the opening phase, and pawns may move any distance forward. If only a player's Negus remains, the opponent must checkmate it in ten moves or less, or the game is a stalemate. The player who checkmates the king wins.")
        (source "Pankhurst 1971a: 158-164.")
        (id "136")
        (version "1.3.4")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin "This game was played in Ethiopia., from around 1892 to 1971.")
        }
    )
    
    (graphics {
        (piece Scale "Pawn" 0.825)
        (show Check "King_noCross")
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)

