(define "CaptureEnemyToPiece"
    (apply 
        (if ("IsEnemyAt" (to))
            (remove (to))
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove" (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next (forEach Piece)))))))

(define "KingMove"
    (move Step
        (to if:(not ("IsFriendAt" (to)))
            ("CaptureEnemyToPiece")
        )
    )
)

(define "KnightMove" 
    (move
        Leap 
        "KnightWalk" 
        (to 
            if:(not ("IsFriendAt" (to))) 
            "CaptureEnemyToPiece"
        ) 
    )		
)

(define "QueenMove" 
    (move Step
        Diagonal
        (to if:(not ("IsFriendAt" (to)))
            ("CaptureEnemyToPiece")
        )
    ) 		
)

(define "ElephantMove"
    (move Hop Orthogonal 
        (between if:True) 
        (to if:(not ("IsFriendAt" (to))) ("CaptureEnemyToPiece")) 
    )		
)

(define "RookMove" 
    (move
        Slide 
        Orthogonal
        (to 
            if:("IsEnemyAt" (to)) 
            "CaptureEnemyToPiece"
        ) 
    )	
)

//------------------------------------------------------------------------------

(game "Sarvatobhadra"
    (players {(player N) (player S)}) 
    (equipment { 
        (board (square 8)) 		
        (dice d:6 from:1 num:1)
        (piece "King_noCross" P1 ("KingMove"))
        (piece "Ferz_noCross" P1 ("QueenMove"))
        (piece "Knight" P1 ("KnightMove"))
        (piece "Elephant" P1 ("ElephantMove"))
        (piece "Rook" P1 ("RookMove"))
        (piece "Pawn" P1
            (or 
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Top)) 
                        (promote (last To) (piece "Ferz_noCross") Mover)
                    )
                )
            )		
        )
        (piece "King_noCross" P2
            (or {
                ("RookMove")
                ("KnightMove")
                ("ElephantMove")
                ("QueenMove")
                ("QueenMove")
                ("KingMove")
            })
        )
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "King_noCross2" coord:"E8") 
        })
        (play 
            (do
                (if (is Mover P1)
                    (do (roll) 
                        next:(if (= (count Pips) 6)
                            (forEach Piece "King_noCross")
                            (if (= (count Pips) 5)
                                (forEach Piece "Ferz_noCross")
                                (if (= (count Pips) 4)
                                    (forEach Piece "Elephant")
                                    (if (= (count Pips) 3)
                                        (forEach Piece "Knight")
                                        (if (= (count Pips) 2)
                                            (forEach Piece "Rook")
                                            (forEach Piece "Pawn")
                                        )
                                    )
                            ))
                        )
                    )
                    (forEach Piece)
                )
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover (forEach Piece Next)))
            )
        )
        
        (end {
            (if (and {
                    (is Mover P2)
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                }) 
                (result P2 Win)
            )
            (if
                (and
                    (is Mover P1)
                    (no Moves P2) 
                )
                (result P1 Win)
            )
            (if
                (and {
                    (is Mover P1)
                    ("IsInCheck" "King_noCross" P1)
                    (no Moves P1)
                })
                (result P2 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sarvatobhadra is a variation on Chaturanga played in medieval India, documented in the Manasollasa. It is played with dice, and one player plays as the king against the opponent's full complement of pieces.")
        (rules "Played on an 8x8 board. One player plays as one Raja, the other with the following pieces: 8 Bhata (or Padati; move like Chess pawns but without being able to move two on the first turn); 2 Ashva (horses; move like Chess knights); 2 Gaja (elephants; two spaces in any orthogonal direction, jumping over the first square); 2 Ratha (chariots; moves like a rook in chess); 1 Mantri (counselor; moves one square diagonally in any direction); 1 Raja (king; moves one square in any direction). These are set up along one edge of the board: Ratha-Ashva-Gaja-Mantri-Raja-Gaja-Ashva-Ratha, with the eight Bhata lined up in the row in front of these, as in chess. The player with the full complement of pieces rolls a six-sided die to determine which piece to move: 6=Raja, 5= Mantri, 4=Gaja, 3 = Ashva, 2=Ratha, 1= Bhata. The player with the single Raja can move it according to the moves of any piece. Players take turns moving. When one piece lands on the space occupied by another piece, it is captured. The goal of the player with a full complement of pieces is to block the single Raja so that it cannot move. The Goal of the single Raja is to checkmate the opponent's Raja.")
        (source "Bock-Raming 1996: 25-26.")
        (id "1448")
        (version "1.3.5")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin  "This game was played in India, from around 1126 to 1138.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
