(define "CaptureToPiece"
    (apply 
        (remove 
            (to) 
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))))
)

//------------------------------------------------------------------------------

(game "Chaturanga (12x12)"
    (players {(player N) (player S)})
    (equipment { 
        (board (square 12)) 
        (piece "Pawn" Each 
            (or 
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Mover "Promotion")) 
                        (and
                            (promote (last To) (piece "Ferz_noCross") Mover)	
                            (fromTo (from (last To)) (to (last From)))
                        )
                    )
                )
            )
        )
        (piece "Rook" Each 
            (move
                Slide 
                Orthogonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPiece"
                ) 
            )
        )
        (piece "Camel" Each 
            (move
                Slide 
                Diagonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPiece"
                ) 
            )
        )
        (piece "King_noCross" Each 
            (move
                Step 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece" 
                )
            )
        )
        (piece "Bishop_noCross" Each 
            (move
                Slide 
                Diagonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPiece"
                ) 
            )
        ) 
        (piece "Elephant" Each 
            (move
                Slide 
                Orthogonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPiece"
                ) 
            )
        ) 
        (piece "Knight" Each 
            (move
                Leap 
                "KnightWalk" 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece"
                ) 
            )
        )
        (piece "Ferz_noCross" Each 
            (move
                Slide 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPiece"
                ) 
            )
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 10))
            (place "Elephant1" {"A1" "L1"}) 
            (place "Knight1" {"B1" "K1"}) 
            (place "Bishop_noCross1" {"C1" "J1"}) 
            (place "Rook1" {"D1" "I1"}) 
            (place "Camel1" {"E1" "H1"}) 
            (place "Ferz_noCross1" coord:"F1") 
            (place "King_noCross1" coord:"G1") 
            
            (place "Elephant2" {"A12" "L12"}) 
            (place "Knight2" {"B12" "K12"}) 
            (place "Bishop_noCross2" {"C12" "J12"}) 
            (place "Rook2" {"D12" "I12"}) 
            (place "Camel2" {"E12" "H12"})
            (place "Ferz_noCross2" coord:"G12") 
            (place "King_noCross2" coord:"F12") 
        })
        (play
            (if (no Moves Mover)
                (move Remove
                    (difference (sites Occupied by:Next) (where "King_noCross" Next))
                )
                (do (forEach Piece) ifAfterwards:(not ("IsInCheck" "King_noCross" Mover)))
            )
        )
        
        (end {
            (if (and 
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
            (if (and {
                    (no Pieces Next "Ferz_noCross")
                    (no Pieces Next "Knight")
                    (no Pieces Next "Bishop_noCross")
                    (no Pieces Next "Elephant")
                    (no Pieces Next "Rook")
                    (no Pieces Next "Camel")
                })
                (result Mover Win) 
            ) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Chaturanga is a replacement capturing game that is the ancestor of Chess and which has been played in India for centuries. This version was documented in the nineteenth century, on a 12x12 board.")
        (rules "12x12 board. Pieces move as follows: Raja (King, x1): moves one space in any direction; Mantri (Minister, placed to the left of the Raja, x1): moves any distance orthogonally or diagonally; Ushtra (Camel, x2): moves diagonally any distance; Chariot (x2), moves orthogonally any distance; Flagcar (x2), moves diagonally any distance; Vaha (Horse, x2): move orthogonally one space and then diagonally another, jumping over any intervening pieces; Danti (Elephant, x2): moves orthogonally any distance. Padati (Pawn, x12): move forward orthogonally one space or one space diagonally forward to capture. When a Padati reaches the opposite edge of the board, it is promoted to a Mantri and is moved immediately to the space it last moved from. An opponent's piece is captured by moving one of the player's own pieces onto the space occupied by the opponent's piece. If the Raja can be captured on the opponent's next turn, it is in check. The Raja cannot be in check at the end of the player's turn. If this is impossible, the opponent wins. When a player is reduced to only their Raja and Padati, the opponent wins. In the case of a stalemate, the player in stalemate may remove any of the opponent's pieces (except their Raja).")
        (source "Iyer 1982: 25-26; Bock-Raming 1995: 116.")
        (id "1464")
        (version "1.3.5")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1871.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
