(define "CaptureEnemyToPiece"
    (apply 
        (if ("IsEnemyAt" (to))
            (remove (to))
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))))
)

(define "MovedBefore" (= 1 (state at:#1)))
(define "RememberMoved" (set State at:#1 1))

//------------------------------------------------------------------------------

(game "Shatranj (Turkey)" 
    (players {(player N) (player S)}) 
    (equipment { 
        (board (square 8)) 
        (piece "King_noCross"
            (move Step
                (to if:(not ("IsFriendAt" (to)))
                    ("CaptureEnemyToPiece")
                )
            )		
        )
        (piece "Ferz_noCross"
            (or
                (move Step Diagonal
                    (to if:(not ("IsFriendAt" (to)))
                        ("CaptureEnemyToPiece")
                    )
                )
                
                (if (not ("MovedBefore" (from)))
                    (move
                        Hop 
                        (between if:True)
                        (to 
                            if:(is Empty (to))
                        )
                    )
                )
                (then 
                    (if (not ("MovedBefore" (last To)))
                        ("RememberMoved" (last To))
                    )
                )
            )
        )
        (piece "Elephant"
            (move
                Hop 
                Diagonal
                (between if:True)
                (to if:(not ("IsFriendAt" (to)))
                    ("CaptureEnemyToPiece")
                )
            )	
        )
        (piece "Knight"
            (move
                Leap 
                "KnightWalk" 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureEnemyToPiece"
                ) 
            )		
        )
        (piece "Rook"
            (move
                Slide 
                Orthogonal
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureEnemyToPiece"
                ) 
            )		
        )
        (piece "Pawn"
            (or 
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Mover "Promotion"))
                        (promote (last To) (piece "Ferz_noCross") Mover)
                    )
                )
            )		
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    
    (rules 
        (start
            { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Elephant2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"D8") (place "King_noCross2" coord:"E8") 
            }
        )
        (play 
            (do
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        
        (end {
            (if (and 
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
            (if (no Moves Mover) (result Mover Loss))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This version of Shatranj is described in the seventeenth century Italian manuscript Libro che insegna giocar a scachi, as a version of Shatranj played by Turkish people. ")
        (aliases {"Shatranj"})
        (rules "8x8 board. The pieces move as follows, with the number per player: 1 x Shah (king): moves one space orthogonally or diagonally. 1 x Fers (counselor): One square diagonally or, one the first turn, may jump two squares diagonally or orthogonally, over any pieces on the first square. There can be no capture with this move. 2 x Rukh (rook): Any number of spaces orthogonally. 2 x Pil (elephant): Two squares diagonally, jumping over the first. 2 x Asb (horse): Moves as a chess knight. 8 x Sarbaz (soldier): Moves one space forward orthogonally; one space forward diagonally to capture. No en passant. Promoted to Fers when reaching the eighth rank. On its first move, this promoted piece may also use the jumping move of the Fers. No castling. An opponent's piece is captured by moving a player's own piece onto a space occupied by the opponent's piece. When a Shah can be captured on the next turn by an opponent's piece, it is in check. The Shah must not be in check at the end of the player's turn. If this is not possible, it is checkmate and the opponent wins. Stalemate results in a win for that player causing it.
        ")
        (source "Murray 1913: 353.")
        (id "1436")
        (version "1.3.5")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin "This game was played in Turkey, from around 1620 to 1640.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
