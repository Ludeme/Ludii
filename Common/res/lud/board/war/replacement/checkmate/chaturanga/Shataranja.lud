(game "Shataranja"
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 10)) 
        ("ChessPawn" "Pawn" ~
            (then
                (if (is In (last To) (sites Mover "Promotion")) 
                    (and
                        (promote (last To) (piece "Bishop_noCross") Mover)	
                        (fromTo (from (last To)) (to (last From)))
                    )
                )
            )
        )
        ("ChessRook" "Rook")
        ("ChessKing" "King_noCross")
        ("ChessRook" "Elephant")
        ("ChessKnight" "Knight")
        ("ChessKing" "Queen")
        (piece "Ferz_noCross" Each 
            (or
                ("SlideCapture")
                ("LeapCapture" "KnightWalk")
            )
        )
        ("ChessQueen" "Bishop_noCross")
        (piece "Commoner" Each 
            (or
                ("SlideCapture" Diagonal)
                ("LeapCapture" "KnightWalk")
            )		
        ) 
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start { 
            (place "Pawn1" (difference (sites Row 1) (sites {"E2" "F2"})))
            (place "Pawn2" (difference (sites Row 8) (sites {"E2" "F2"})))
            (place "Queen1" {"E2" "F2"})
            (place "Queen2" {"E9" "F9"})
            (place "Rook1" {"A1" "J1"}) (place "Knight1" {"B1" "I1" "E3" "F3"}) (place "Elephant1" {"C1" "H1"}) (place "Ferz_noCross1" coord:"E1") (place "Commoner1" coord:"G1") (place "Bishop_noCross1" coord:"D1") (place "King_noCross1" coord:"F1") 
            (place "Rook2" {"A10" "J10"}) (place "Knight2" {"B10" "I10" "E8" "F8"}) (place "Elephant2" {"C10" "H10"}) (place "Ferz_noCross2" coord:"F10") (place "Commoner2" coord:"D10") (place "Bishop_noCross2" coord:"G10") (place "King_noCross2" coord:"E10") 
        })
        
        (play 
            (if (no Moves Mover)
                (move Remove
                    (difference (sites Occupied by:Next) (where "King_noCross" Next))
                )
                (do (forEach Piece) ifAfterwards:(not ("IsInCheck" "King_noCross" Mover)))
            )
        )
        
        (end {
            ("Checkmate" "King_noCross")
            (if (and {
                    (no Pieces Next "Ferz_noCross")
                    (no Pieces Next "Queen")
                    (no Pieces Next "Knight")
                    (no Pieces Next "Bishop_noCross")
                    (no Pieces Next "Elephant")
                    (no Pieces Next "Rook")
                    (no Pieces Next "Commoner")
                })
                (result Mover Win) 
            ) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Shataranja is a replacement capture game played in India, described in the nineteenth century. It was played on a 10x10 board.

")
(rules "10x10 board. Pieces move as follows: Raja (x1): moves one space in any direction; Crown Prince (placed to the left of the Raja, x1): moves like any of the other pieces; Kotwal (Police Chief, placed to the right of the Raja, x1): Moves like the Elephant and the Vaha; Mantri (x1): moves any distance orthogonally or diagonally; Elephant (x2): moves diagonally any distance; Vaha (two placed in front of the Queens, x4): move orthogonally one space and then diagonally another, jumping over any intervening pieces; Chariot (x2): moves orthogonally any distance; Queen (placed in front of the Raja and Crown Prince, x2): move one square orthogonally or diagonally; Padati (placed in the second row, except for the central two squares, x8): move forward orthogonally one space or one space diagonally forward to capture. When a Padati reaches the opposite edge of the board, it is promoted to a Mantri and is moved immediately to the space it last moved from. An opponent's piece is captured by moving one of the player's own pieces onto the space occupied by the opponent's piece. If the Raja can be captured on the opponent's next turn, it is in check. The Raja cannot be in check at the end of the player's turn. If this is impossible, the opponent wins. When a player is reduced to only their Raja and Padati, the opponent wins. In the case of a stalemate, the player in stalemate may remove any of the opponent's pieces (except their Raja).
")
(source "Iyer 1982: 48-50,  Bock-Raming 1995: 116.")
(id "1463")
(version "1.3.7")
(classification "board/war/replacement/checkmate/chaturanga")
(credit "Eric Piette")
(origin "This game was played in India, around 1871.")
}
)
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
