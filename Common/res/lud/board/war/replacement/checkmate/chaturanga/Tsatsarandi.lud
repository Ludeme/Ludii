(define "CaptureToPiece"
    (apply 
        (remove 
            (to) 
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))))
)

//------------------------------------------------------------------------------

(game "Tsatsarandi"
    (players 2)
    (equipment {
        (board (square 8))
        (piece "Pawn"
            (or 
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
            )		
        )
        (piece "Rook"
            (move
                Slide 
                Orthogonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPiece"
                ) 
            )		
        )
        (piece "King_noCross"
            (move
                Step 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece" 
                )
            )		
        )
        (piece "Bishop_noCross"
            (move 
                Hop 
                Diagonal 
                (between if:True) 
                (to 
                    if:(or 
                        (is Empty (to)) 
                        ("IsEnemyAt" (to))
                    ) 
                    "CaptureToPiece"
                )
            )		
        )
        (piece "Knight"
            (move 
                Leap 
                "KnightWalk" 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece"
                ) 
            )		
        )
        (piece "Ferz_noCross"
            (move
                Step 
                Diagonal
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece" 
                )
            )		
        )
    })
    (rules 
        (start {
            (place "King_noCross1" coord:"E1") (place "King_noCross2" coord:"E8")
            (place "Ferz_noCross1" coord:"D1") (place "Ferz_noCross2" coord:"D8")
            (place "Bishop_noCross1" {"C1" "F1"}) (place "Bishop_noCross2" {"C8" "F8"})
            (place "Knight1" {"B1" "G1"}) (place "Knight2" {"B8" "G8"})
            (place "Rook1" {"A1" "H1"}) (place "Rook2" {"A8" "H8"})
            (place "Pawn1" (sites Row 1)) (place "Pawn2" (sites Row 6))
        })
        (play 
            (do (forEach Piece) ifAfterwards:(not ("IsInCheck" "King_noCross" Mover)))
        )
        
        (end
            {
            (if 
                (and 
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
            }
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Tsatsarandi is a capturing game that was played by the Kanuri people of Nigeria. Very few people still played it in the early twentieth century, and it was said to have been played during the time of the Kanem-Bornu Empire.")
        (rules "8x8 board. Pieces have specific moves, as follows: Mai (x1): moves one space in any direction; Chiroma (x1): Moves one space diagonally; Bintu (x2): moves exactly two spaces diagonally, jumping over the first square; Fer (x2): moves orthogonally one space and then diagonally another space, jumping over any intervening pieces; Kaigamma (x2): moves orthogonally any distance; Gollo: (x8) move one space forward orthogonally, or one space diagonally forward to capture. Pieces capture the opponent's pieces by moving onto the spot they occupy. No en passant; no castling. When the Mai can be captured at the beginning of the opponent's next turn, it is in check and must be removed from this state on the player's turn. If this is not possible, it is checkmate and the player who threatened the Mai wins. ")
        (source "Harris 1939: 31-32.")
        (id "1417")
        (version "1.3.4")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin "This game was played in Nigeria, from around 1926 to 1936.")
        }
    )
    
    (graphics {
        (show Check "King")
        (piece Scale "Pawn" 0.825)
        (piece Families {"Defined" "Microsoft" "Pragmata" "Symbola"})
        (board Colour Phase0 (colour 222 20 40))
        (board Colour Phase1 (colour 223 178 110))
        (board Colour InnerEdges (colour 0 0 0 0))
        (board Colour OuterEdges (colour 0 0 0))
        (board Checkered)
    })
    
    (ai
        "Tsatsarandi_ai"
    )
)
