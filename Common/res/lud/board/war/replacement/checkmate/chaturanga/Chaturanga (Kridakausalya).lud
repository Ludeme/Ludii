(define "CaptureEnemyToPiece"
    (apply 
        (if ("IsEnemyAt" (to))
            (remove (to))
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))))
)

(define "OnlyRajaAndPadati"
    (and {
        (no Pieces #1 "Ferz_noCross")
        (no Pieces #1 "Knight")
        (no Pieces #1 "Elephant")
        (no Pieces #1 "Rook")
    })
)

(define "Move" 
    (do
        (forEach Piece)
        ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
    )
)

//------------------------------------------------------------------------------

(game "Chaturanga (Kridakausalya)"
    (players {(player N) (player S)}) 
    (equipment { 
        (board (square 8)) 	
        (piece "King_noCross"
            (move Step
                (to if:(not ("IsFriendAt" (to)))
                    ("CaptureEnemyToPiece")
                )
            )
        )
        (piece "Ferz_noCross"
            (move
                Slide 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureEnemyToPiece"
                ) 
            ) 			
        )
        (piece "Knight"
            (move
                Leap 
                "KnightWalk" 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureEnemyToPiece"
                ) 
            )		
        )
        (piece "Elephant"
            (move
                Slide 
                Diagonal
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureEnemyToPiece"
                ) 
            )		
        )
        (piece "Rook"
            (move
                Slide 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureEnemyToPiece"
                ) 
            ) 		
        )
        (piece "Pawn"
            (or 
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Mover "Promotion"))
                        (and
                            (promote (last To) (piece "Ferz_noCross") Mover)
                            (fromTo (from (last To)) (to (last From)))	
                        )
                    )
                )
            )		
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Elephant2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        
        (play 
            (if (can Move ("Move"))
                ("Move")
                (move Remove (difference (sites Occupied by:Next) (sites Occupied by:Next component:"King_noCross")))
            )
        )
        
        (end {
            (if (and 
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
            (if ("OnlyRajaAndPadati" Next) (result Mover Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Chaturanga is a replacement capture game played in India. This version was described in the Kridakausalya of Harikrna during the nineteenth century.")
        (rules "8x8 board. Pieces move as follows: Raja (x1): moves one space in any direction; Mantri (x1): moves any distance orthogonally or diagonally; Ushtra (x2): moves diagonally any distance; Vaha (x2): move orthogonally one space and then diagonally another, jumping over any intervening pieces; Danti (x2): moves orthogonally any distance. Padati (x8): move forward orthogonally one space or one space diagonally forward to capture. When a Padati reaches the opposite edge of the board, it is promoted to a Mantri and is moved immediately to the space it last moved from. An opponent's piece is captured by moving one of the player's own pieces onto the space occupied by the opponent's piece. If the Raja can be captured on the opponent's next turn, it is in check. The Raja cannot be in check at the end of the player's turn. If this is impossible, the opponent wins. When a player is reduced to only their Raja and Padati, the opponent wins. In the case of a stalemate, the player in stalemate may remove any of the opponent's pieces (except their Raja). ")
        (source "Bock-Raming 1995: 319-320.")
        (id "1450")
        (version "1.3.5")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1871.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)
