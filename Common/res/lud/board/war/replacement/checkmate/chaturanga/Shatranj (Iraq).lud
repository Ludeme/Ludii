(game "Shatranj (Iraq)" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8)) 
        ("ChessKing" "King_noCross")
        (piece "Ferz_noCross"
            (or
                ("SlideCapture")
                ("LeapCapture" "KnightWalk")
            )
        )
        (piece "Elephant"
            (move Slide Diagonal 
                (between if:(or (is In (between) (sites Around (from))) (is Empty (between)))) 
                (to 
                    if:(and (not ("IsPieceAt" "Elephant" Next (to))) ("IsEnemyAt" (to)))
                    (apply 
                        if:(not (is In (to) (sites Around (from))))
                        (if ("IsEnemyAt" (to))
                            (remove (to))
                        )
                    ) 
                ) 
            )		
        )
        ("ChessKnight" "Knight")
        ("ChessRook" "Rook")
        ("ChessPawn" "Pawn" ~ (then ("PromoteIfReach" (sites Mover "Promotion") "Ferz_noCross")))
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1") 
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Elephant2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"D8") (place "King_noCross2" coord:"E8") 
        })
        (play 
            (do (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        
        (end {
            ("Checkmate" "King_noCross")
            (if (no Moves Mover) (result Mover Loss))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "These rules for Shatranj were reported from early modern Iraq. They are given in a note on an earlier manuscript on Shatranj in the British Library, noting the differences between the contemporary game and that played in the manuscript.")
        (rules "8x8 board. The pieces move as follows, with the number per player: 1 x Shah (king): moves one space orthogonally or diagonally. 1 x Fers (counselor): Any distance orthogonally or diagonally, or like the Asb. 2 x Rukh (rook): Any number of spaces orthogonally. No castling. 2 x Pil (elephant): Any distance diagonally, jumping over the first. Cannot capture another Pil. 2 x Asb (horse): Moves as a chess knight. 8 x Sarbaz (soldier): Moves one space forward orthogonally; one space forward diagonally to capture. No en passant. Promoted to Fers when reaching the eighth rank. An opponent's piece is captured by moving a player's own piece onto a space occupied by the opponent's piece. When a Shah can be captured on the next turn by an opponent's piece, it is in check. The Shah must not be in check at the end of the player's turn. If this is not possible, it is checkmate and the opponent wins. Stalemate results in a win for that player causing it.
        ")
        (source "Murray 1913: 354-355.")
        (id "1438")
        (version "1.3.11")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin "This game was played in Iraq, from around 1700 to 1799.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Shatranj (Iraq)_ai"
    )
)
