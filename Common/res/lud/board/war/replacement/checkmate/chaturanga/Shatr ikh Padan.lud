(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))))
)

(define "CaptureToPiece"
    (apply 
        (if ("IsEnemyAt" (to))
            (remove 
                (to) 
            )
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

//------------------------------------------------------------------------------

(game "Shatr ikh Padan" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8)) 
        
        (piece "Pawn" Each 
            (or {
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                }
            )
        )
        
        (piece "King_noCross" Each 
            (move
                Step 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece"
                ) 
            )
        )
    }) 
    (rules 
        (start { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "King_noCross1" coord:"E1") 
            (place "King_noCross2" coord:"D8") 
        })
        
        (play 
            (do 
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        
        (end {
            (if (and 
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Shatr ikh Padan is a game from nineteenth century India that was described by Lala Raja Babu Sahib, Superintendent of the Palace Games Department of Patiala.. It is played with only the Shah and eight Pawns.")
        (rules "8x8 board. Two players. Pieces and movement are as follows: King x1, placed on the fifth space from the left on one edge of the board: Moves one space orthogonally or diagonally. Pawns x8, arranged along the row in front of the king: moves one space forward orthogonally or one space forward diagonally to capture. The opponent's pieces are placed in the same position on the opposite side of the board. If the King can be taken on the next turn it is in check, it must not remain in check on the next turn. If the king cannot move out of check, checkmate is declared and the opponent wins.")
        (source "Murray 1913: 347.")
        (id "316")
        (version "1.3.5")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1901.")
        }
    )
    
    (graphics {
        (show Check "King_noCross")
        (piece Scale "Pawn" 0.825)
        (board Style Chess)
    })
    
)
