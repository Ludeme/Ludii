(define "CaptureEnemyToPiece"
    (apply
        (if (is Enemy (who at:(to)))
            (remove (to))
        )
    )
)

(define "CaptureForwardDiagonal"
    (move
        Step
        (directions {FR FL})
        (to
            if:(is Enemy (who at:(to)))
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove" (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next (forEach Piece))))) ))

(define "KingMove"
    (move Step
        (to if:(not (is Friend (who at:(to))))
            ("CaptureEnemyToPiece")
        )
    )
)

(define "KnightMove"
    (move
        Leap
        "KnightWalk"
        (to
            if:(not (is Friend (who at:(to))))
            "CaptureEnemyToPiece"
        )
    )
)

(define "QueenMove"
    (move Step
        Diagonal
        (to if:(not (is Friend (who at:(to))))
            ("CaptureEnemyToPiece")
        )
    )
)

(define "ElephantMove"
    (move Hop Diagonal
        (between if:True)
        (to if:(not (is Friend (who at:(to)))) ("CaptureEnemyToPiece"))
    )
)

(define "RookMove"
    (move
        Slide
        Orthogonal
        (to
            if:(is Enemy (who at:(to)))
            "CaptureEnemyToPiece"
        )
    )
)

(define "Move"
    (do
        (forEach Piece)
        ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
        (then
            (if ("IsInCheck" "King_noCross" Next)
                (set Value Mover (+ 1 (value Player Mover)))
                (set Value Mover 0)
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Krida Buddhibalasrita"
    (players {(player N) (player S)})
    (equipment {
        (board (square 8))
        (piece "King_noCross" Each ("KingMove"))
        (piece "Ferz_noCross" Each ("QueenMove"))
        (piece "Knight" Each ("KnightMove"))
        (piece "Elephant" Each ("ElephantMove"))
        (piece "Rook" Each ("RookMove"))
        (piece "Pawn" Each
            (or
                "StepForwardToEmpty"
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Mover "Promotion"))
                        (and
                            (promote (last To) (piece "Ferz_noCross") Mover)
                            (if (not (is In (last To) (sites "PromotionMarked")))
                                (fromTo (from (last To)) (to (last From)))
                            )
                        )
                    )
                )
            )
        )
        (regions "Promotion" P1 (sites Top) )
        (regions "Promotion" P2 (sites Bottom) )
        (regions "PromotionMarked" (sites {"A1" "D1" "E1" "H1" "A8" "D8" "E8" "H8"}) )
    })

    (rules
        (start {
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"}) (place "Knight1" {"B1" "G1"}) (place "Elephant1" {"C1" "F1"}) (place "Ferz_noCross1" coord:"D1") (place "King_noCross1" coord:"E1")
            (place "Rook2" {"A8" "H8"}) (place "Knight2" {"B8" "G8"}) (place "Elephant2" {"C8" "F8"}) (place "Ferz_noCross2" coord:"D8") (place "King_noCross2" coord:"E8")
        })

        (play
            (if (can Move ("Move"))
                ("Move")
                (move Remove (difference (sites Occupied by:Next) (sites Occupied by:Next component:"King_noCross")))
            )
        )

        (end {
            (if (or
                    (and
                        ("IsInCheck" "King_noCross" Next)
                        ("NextCanNotMove")
                    )
                    (= 1 (count Pieces Next))
                )
                (result Mover Win)
            )
            (if (= (value Player P1) 64) (result P1 Win) )
            (if (= (value Player P2) 64) (result P2 Win) )
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Krida Buddhibalasrita (\"Intellectual Game\") is a game with replacement captures played in southern India in the seventeenth of eighteenth century. It is a variation on Chaturanga, which is played throughout India in different ways.")
        (rules "8x8 board, with markings in the four central squares, the four corner squares, and the two middle squares of each edge. The pieces have the following movement values: Raja (x1): moves one space in any direction; Mantri (x1): moves diagonally one space; Karabha (x2): jumps exactly two squares diagonally; Raji (x2): moves orthogonally one space and then diagonally another space, jumping over any intervening pieces; Kunjara (x2): moves orthogonally any distance; Patti (x8): move forward orthogonally one space, diagonally forward one space to capture. When a Patti reaches a marked space on the opposite edge of the board, it is promoted to Mantri if it is on a marked square. If it is on an unmarked square, the Patti must move back to the space from which it moved to the last row and is then promoted to Mantri. If the Raja can be taken on the opponent's next turn, it is in check. The Raja must not be in check at the end of the player's turn. If this is not possible, it is checkmate and the opponent wins.If a Raja is in stalemate, and no other pieces can move the player may remove one of the opponent's pieces causing the stalemate. If the opponent is reduced to only their Raja, it is also a victory, though considered a lesser one. Checking the opponent's Raja 64 consecutive times is a win.
        ")
        (source "Murray 1913: 63-65.")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin  "This game was played in India, from around 1600 to 1699.")
        }
    )

    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (show Symbol "thinCross" {0 3 4 7 24 27 28 31 32 35 36 39 56 59 60 63} scale:0.9)
    })

)
