(define "CaptureToPiece"
    (apply
        (if (is Enemy (who at:(to)))
            (remove
                (to)
            )
        )
    )
)

(define "CaptureForwardDiagonal"
    (move
        Step
        (directions {FR FL})
        (to
            if:(is Enemy (who at:(to)))
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))) )
)

//------------------------------------------------------------------------------

(game "Qatranj"
    (players {(player N) (player S)})
    (equipment {
        (board (square 10))
        // Rukh
        (piece "Rook" Each
            (move
                Slide
                Orthogonal
                (to
                    if:(is Enemy (who at:(to)))
                    "CaptureToPiece"
                )
            )
        )
        // Ghora
        (piece "Knight" Each
            (move
                Leap
                "KnightWalk"
                (to
                    if:(not (is Friend (who at:(to))))
                    "CaptureToPiece"
                )
            )
        )
        // Fil
        (piece "Bishop_noCross" Each
            (move
                Slide
                Diagonal
                (to
                    if:(is Enemy (who at:(to)))
                    "CaptureToPiece"
                )
            )
        )
        // Wazir
        (piece "Queen" Each
            (move
                Slide
                (to
                    if:(is Enemy (who at:(to)))
                    "CaptureToPiece"
                )
            )
        )
        // Shahzada
        (piece "Commoner" Each
            (or
                (move
                    Slide
                    (to
                        if:(is Enemy (who at:(to)))
                        "CaptureToPiece"
                    )
                )
                (move
                    Leap
                    "KnightWalk"
                    (to
                        if:(not (is Friend (who at:(to))))
                        "CaptureToPiece"
                    )
                )
            )
        )
        // Padshah
        (piece "King_noCross" Each
            (move
                Step
                (to
                    if:(not (is Friend (who at:(to))))
                    "CaptureToPiece"
                )
            )
        )
        // Kotwal
        (piece "Mann" Each
            (or
                (move
                    Slide
                    (to
                        if:(is Enemy (who at:(to)))
                        "CaptureToPiece"
                    )
                )
                (move
                    Leap
                    "KnightWalk"
                    (to
                        if:(not (is Friend (who at:(to))))
                        "CaptureToPiece"
                    )
                )
            )
        )
        // Urdabegini
        (piece "Wazir" Each
            (do
                (move
                    Step
                    (to
                        if:(not (is Friend (who at:(to))))
                        "CaptureToPiece"
                    )
                )
                ifAfterwards:(> (count Steps
                        (last From)
                        (where "King_noCross" Next)
                    )
                    (count Steps
                        (last To)
                        (where "King_noCross" Next)
                    )
                )
            )
        )

        // Paidal
        (piece "Pawn" Each
            (or
                "StepForwardToEmpty"
                "CaptureForwardDiagonal"
            )
        )
    })
    (rules
        (start {
            (place "Rook1" (sites {"A1" "J1"})) (place "Rook2" (sites {"A10" "J10"}))
            (place "Knight1" (sites {"B1" "I1" "E3" "F3"})) (place "Knight2" (sites {"B10" "I10" "E8" "F8"}))
            (place "Bishop_noCross1" (sites {"C1" "H1"})) (place "Bishop_noCross2" (sites {"C10" "H10"}))
            (place "Queen1" coord:"D1") (place "Queen2" coord:"G10")
            (place "Commoner1" coord:"E1") (place "Commoner2" coord:"F10")
            (place "King_noCross1" coord:"F1") (place "King_noCross2" coord:"E10")
            (place "Mann1" coord:"G1") (place "Mann2" coord:"D10")
            (place "Wazir1" (sites {"E2" "F2"})) (place "Wazir2" (sites {"E9" "F9"}))
            (place "Pawn1" (difference (sites Row 1) (sites {"E2" "F2"})))
            (place "Pawn2" (difference (sites Row 8) (sites {"E9" "F9"})))
        })
        (play (do (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        (end {
            (if (and
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                )
                (result Mover Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Qatranj is a game with replacement captures played on a 10x10 board with 22 pieces. It was played in nineteenth century India.")
        (aliases {"Atranj"})
        (rules "10x10 board. 22 pieces per player. Pieces begin on the board in the following  arrangement, and with the following movements: Outer row, from  left corner: Rukh: moves orthogonally any distance; Ghora: moves orthogonally one space and then diagonally another  space, jumping over the first space;  Fil: moves diagonally any distance; Wazir: moves diagonally or orthogonally any distance; Shahzada: can move like the Wazir and like the Ghora; Padshah: moves one space in any direction; Kotwal: moves like the Shahzada; Fil, Ghora, Rukh. In the next row, the central two squares are occupied by the Urdabegini: move one space in the direction of the opponent's Padshah; the other spaces are occupied by Paidal: moves one square forward orthogonally or one diagonally to capture. The central two squares of the third row contain two more Ghora. Players alternate turns moving a piece to a space on the board. If one of the opponent's pieces is on the space to which a player moves their piece, the opponent's piece is captured. If  the Padshah can be captured on the opponent's next turn, it is in check. The Padshah cannot remain in check at the end of the player's turn. If this is not possible, it is checkmate and the opponent wins.  ")
        (source "Murray 1913: 347; Sahib 1901: 320.")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1901.")
        }
    )

    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })

)
