(define "CaptureToPiece"
    (apply
        (remove
            (to)
        )
    )
)

(define "CaptureForwardDiagonal"
    (move
        Step
        (directions {FR FL})
        (to
            if:(is Enemy (who at:(to)))
            (apply (remove (to)))
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King_noCross" Next)))) )
)

//------------------------------------------------------------------------------

(game "Shatranj at-Tamma"
    (players {(player N) (player S)})
    (equipment {
        (piece "Pawn" Each
            (or
                "StepForwardToEmpty"
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Mover "Promotion"))
                        (if (= Off (where "Queen" Mover))
                            (promote (last To) (piece "Queen") Mover)
                            (remove (last To))
                        )
                    )
                )
            )
        )
        (piece "King_noCross" Each
            (move
                Step
                (to
                    if:(not (is Friend (who at:(to))))
                    "CaptureToPiece"
                )
            )
        )
        (piece "Queen" Each
            (move
                Step
                Diagonal
                (to
                    if:(not (is Friend (who at:(to))))
                    "CaptureToPiece"
                )
            )
        )
        (piece "Knight" Each
            (move
                Leap
                "KnightWalk"
                (to
                    if:(not (is Friend (who at:(to))))
                    "CaptureToPiece"
                )
            )
        )
        (piece "Wazir" Each
            (move
                Step
                (to
                    if:(not (is Friend (who at:(to))))
                    "CaptureToPiece"
                )
            )
        )
        (piece "Rook" Each
            (move
                Slide
                Orthogonal
                (to
                    if:(is Enemy (who at:(to)))
                    "CaptureToPiece"
                )
            )
        )
        (piece "Elephant" Each
            (move
                Hop
                Diagonal
                (between if:True)
                (to
                    if:(or
                        (is Empty (to))
                        (and (is Enemy (who at:(to))) (!= (what at:(to)) (id "Elephant" Next)))
                    )
                    "CaptureToPiece"
                )
            )
        )
        (regions "Promotion" P1 (sites Top) )
        (regions "Promotion" P2 (sites Bottom) )
        (board (square 10))
    })
    (rules
        (start {
            (place "Pawn1" (sites Row 2))
            (place "Pawn2" (sites Row 7))
            (place "Rook1" (sites {"A1" "J1"}))
            (place "Rook2" (sites {"A10" "J10"}))
            (place "Knight1" (sites {"B1" "I1"}))
            (place "Knight2" (sites {"B10" "I10"}))
            (place "Elephant1" (sites {"C1" "H1"}))
            (place "Elephant2" (sites {"C10" "H10"}))
            (place "Wazir1" (sites {"D1" "G1"}))
            (place "Wazir2" (sites {"D10" "G10"}))
            (place "Queen1" coord:"E1")
            (place "Queen2" coord:"F10")
            (place "King_noCross1" coord:"F1")
            (place "King_noCross2" coord:"E10")
        })

        (play
            (do
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King_noCross" Mover))
            )
        )
        (end {
            (if (and
                    ("IsInCheck" "King_noCross" Next)
                    ("NextCanNotMove")
                )
                (result Mover Win)
            )
            (if (no Moves Next) (result Mover Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Shatranj at-Tamma is a game described in several manuscripts from the Medieval Islamic world. It is played similarly to Shatranj, with a 10x10 board and an extra piece, the Dabbaba. ")
        (aliases {"Decimal Chess" "Complete Chess"})
        (rules "10x10 board. The pieces move as follows, with the number per player: 1 x Shah (king): one space orthogonally or diagonally; 1 x Fers (counselor): one square diagonally; 2 x Rukh (rook): any number of spaces orthogonally; 2x Dabbaba: one space orthogonally or diagonally; 2 x Pil (elephant): two squares diagonally, jumping over the first. Cannot capture another Pil; 2 x Asb (horse): one square orthogonally, and then one square diagonally, jumping over any intervening pieces; 10 x Sarbaz (soldier): one space forward orthogonally or one space forward diagonally to capture. No en passant. Sarbaz begins in the third rank, and is promoted to Fers when reaching the tenth rank, only if the Fers has been captured. Otherwise, the other player captures it. No castling. Stalemate results in win for player causing it. The player who checkmates the king wins.")
        (source "Murray 1913: 341-342.")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/chaturanga")
        (credit "Eric Piette")
        }
    )

    (graphics {
        (piece Scale "Pawn" 0.825)
        (show Check "King")
        (board Style Chess)
    })

    (ai
        "Shatranj at-Tamma_ai"
    )
)
