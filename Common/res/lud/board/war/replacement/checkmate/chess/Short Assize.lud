(define "CaptureToPiece"
    (apply 
        (if ("IsEnemyAt" (to))
            (forEach Level (to)
                (remove 
                    (to) level:(level)
                )
            )
        )
    ) 
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (from (from) level:(level))
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to))
            (apply 
                (forEach Level (to)
                    (remove 
                        (to) level:(level)
                    )
                )
            )
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King" Next)))))
)

//------------------------------------------------------------------------------

(game "Short Assize"
    (players {(player N) (player S)})  
    (equipment { 
        (board (square 8)) 
        (piece "King" Each
            (move
                Step 
                (from (from) level:(level))
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece" 
                )
            )
        )
        (piece "Rook" Each
            (move
                Slide 
                (from (from) level:(level))
                Orthogonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPiece"
                ) 
            )		
        )
        (piece "Bishop" Each
            (move
                Hop 
                (from (from) level:(level))
                Diagonal 
                (between if:True) 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece" 
                )
            )		
        )
        (piece "Knight" Each
            (move
                Leap 
                (from (from) level:(level))
                "KnightWalk" 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece"
                ) 
            )			
        )
        (piece "Queen" Each
            (move
                Step 
                (from (from) level:(level))
                Diagonal
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPiece" 
                )
            )		
        )
        (piece "Pawn" Each
            (or 
                (move Step 
                    (from (from) level:(level)) 
                    Forward 
                    (to if:(is Empty (to)))
                )
                "CaptureForwardDiagonal"
                (then
                    (if (is In (last To) (sites Mover "Promotion")) 
                        (promote (last To) (piece (id "Queen" Mover)))
                    )
                )
            )		
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    })
    (rules 
        (start {
            (place "Bishop1" (sites {"C1" "F1"})) (place "Rook1" (sites {"C2" "F2"}))
            (place "Knight1" (sites {"D2" "E2"}))
            (place "King1" coord:"E1") (place Stack "Pawn1" (sites Row 2)) 
            (place Stack "Queen1" (sites {"D3"}))
            
            (place "Bishop2" (sites {"C8" "F8"})) (place "Rook2" (sites {"C7" "F7"}))
            (place "Knight2" (sites {"D7" "E7"}))
            (place "King2" coord:"E8") (place Stack "Pawn2" (sites Row 5)) 
            (place Stack "Queen2" (sites {"D6"}))
        })
        (play (do (forEach Piece) ifAfterwards:(not ("IsInCheck" "King" Mover))))
        (end {
            (if (and 
                    ("IsInCheck" "King" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
            ("BlockWin")
            (if (= (count Pieces Next) 1) (result Mover Win)) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The Short Assize was a form of Chess played in medieval France and England. It is thought to have been a way of speeding up the game by starting the pieces in an advanced position.")
        (rules "8x8 board. The pieces move as follows, with the number per player: 1 x King (king): moves one space orthogonally or diagonally. 1 x Queen: One square diagonally. 2 x Rook: Any number of spaces orthogonally. 2 x Fil (elephant): Two squares diagonally, jumping over the first. Cannot capture another Fil. 2 x Knight: Moves as a chess knight. 8 x Pawn: Moves one space forward orthogonally; one space forward diagonally to capture. No en passant. Promoted to Queen when reaching the eighth rank. The pieces begin in the following position: Fils on the third and sixth spaces of the first row, King on the fifth space of the first row, Rooks on the third and sixth spaces of the second row, Knights on the fourth and fifth spaces of the second row, Pawns on the third row, the Queen sharing a space with the Pawn in the fifth space. Kings are on the same column. The only time two pieces can be on the same space is in this initial arrangement. No castling. An opponent's piece is captured by moving a player's own piece onto a space occupied by the opponent's piece. When a King can be captured on the next turn by an opponent's piece, it is in check. The King must not be in check at the end of the player's turn. If this is not possible, it is checkmate and the opponent wins. Stalemate results in a win for that player causing it. Capturing all of an opponent's pieces except the King also results in a win.
        ")
        (source "Murray 1913: 455.")
        (id "1333")
        (version "1.3.4")
        (classification "board/war/replacement/checkmate/chess")
        (credit "Eric Piette")
        (origin  "This game was played in England, from around 1496 to 1498.")
        }
    )
    
    (graphics {
        (board Style Chess)
        (stackType GroundDynamic)
    })
    
)
