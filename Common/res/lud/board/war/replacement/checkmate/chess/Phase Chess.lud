(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King" Next (forEach Piece))))) )
)

(define "KingInTheInitialPlace"
    (= (what at:(mapEntry "King" (mover))) (id "King" Mover))
)

(define "HasNeverMoved"
    (= (state at:(mapEntry #1 (mover))) 1)
)

(define "PieceHasMoved"
    (set State at:(last To) 0)
)

(define "RememberPieceHasMoved"
    (then
        (if
            (= (state at:(last To)) 1)
            "PieceHasMoved"
        )
    )
)

(define "CaptureToPieceAndResetCounter"
    (apply
        (if (is Enemy (who at:(to)))
            (remove
                (to)
                (then (set Counter))
            )
        )
    )
)

(define "KingNotCheckedAndToEmpty"
    (and
        (is Empty (to))
        (not ("IsInCheck" "King" Mover at:(to)))
    )
)

(define "DoCastle"
    (move
        Slide
        (from (mapEntry #1 (mover)))
        #2
        (between
            (exact #3)
            if:#4
        )
        #5
    )
)

(define "SmallCastling"
    ("DoCastle" "King" E 2 "KingNotCheckedAndToEmpty" (then (and ("PieceHasMoved") ("DoCastle" "RookRight" W 2 True))))
)

(define "BigCastling"
    ("DoCastle" "King" W 2 "KingNotCheckedAndToEmpty" (then (and ("PieceHasMoved") ("DoCastle" "RookLeft" E 3 True))))
)

(define "SetEnPassantLocation"
    (then
        (set Pending (ahead (last To) Backward))
    )
)

(define "DoubleStep"
    (move
        Slide
        Forward
        (between (exact 2) if:(is Empty (between)))
        (to if:(is Empty (to)))
        "SetEnPassantLocation"
    )
)

(define "InLocationEnPassant"
    (and
        (is Pending)
        (= (to) (value Pending))
    )
)

(define "EnPassant"
    (move
        Step
        (directions {FR FL})
        (to if:"InLocationEnPassant")
        (then
            (remove
                (ahead (last To) Backward)
            )
        )
    )
)

(define "CaptureForwardDiagonal"
    (move
        Step
        (directions {FR FL})
        (to
            if:(is Enemy (who at:(to)))
            (apply (remove (to)))
        )
    )
)

(define "PawnMove"
    (or {
        (if (is In (from) (sites Start (piece (what at:(from)))))
            "DoubleStep"
        )
        "StepForwardToEmpty"
        "CaptureForwardDiagonal"
        "EnPassant"
        }
        (then
            (and
                (if (is In (last To) (sites Mover "Promotion"))
                    (moveAgain)
                )
                (set Counter)
            )
        )
    )
)

(define "RookMove"
    (move
        Slide
        Orthogonal
        (to
            if:(is Enemy (who at:(to)))
            "CaptureToPieceAndResetCounter"
        )
        "RememberPieceHasMoved"
    )
)

(define "KingMove"
    (move
        Step
        (to
            if:(not (is Friend (who at:(to))))
            "CaptureToPieceAndResetCounter"
        )
        "RememberPieceHasMoved"
    )
)

(define "BishopMove"
    (move
        Slide
        Diagonal
        (to
            if:(is Enemy (who at:(to)))
            "CaptureToPieceAndResetCounter"
        )
    )
)

(define "KnightMove"
    (move
        Leap
        "KnightWalk"
        (to
            if:(not (is Friend (who at:(to))))
            "CaptureToPieceAndResetCounter"
        )
    )
)

(define "QueenMove"
    (move
        Slide
        (to
            if:(is Enemy (who at:(to)))
            "CaptureToPieceAndResetCounter"
        )
    )
)

//------------------------------------------------------------------------------

(game "Phase Chess"
    (players {(player N) (player S)})
    (equipment {
        (board (square 8))
        (piece "Pawn" Each ("PawnMove"))
        (piece "Rook" Each ("RookMove"))
        (piece "King" Each ("KingMove"))
        (piece "Bishop" Each ("BishopMove"))
        (piece "Knight" Each ("KnightMove"))
        (piece "Queen" Each ("QueenMove"))

        (map "King" {(pair 1 "E1") (pair 2 "E8")})
        (map "RookLeft" {(pair 1 "A1") (pair 2 "A8")})
        (map "RookRight" {(pair 1 "H1") (pair 2 "H8")})
        (regions "Promotion" P1 (sites Top) )
        (regions "Promotion" P2 (sites Bottom) )
        }
    )
    (rules
        (start
            {
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"} state:1) (place "Knight1" {"B1" "G1"}) (place "Bishop1" {"C1" "F1"}) (place "Queen1" coord:"D1") (place "King1" coord:"E1" state:1)
            (place "Rook2" {"A8" "H8"} state:1) (place "Knight2" {"B8" "G8"}) (place "Bishop2" {"C8" "F8"}) (place "Queen2" coord:"D8") (place "King2" coord:"E8" state:1)
            }
        )

        (play
            (if "SameTurn"
                (move Promote (last To) (piece {"Queen" "Knight" "Bishop" "Rook"}) Mover)
                (priority {
                    (if (!= 0 (count Moves))
                        (do
                            (or {
                                (forEach Piece "Queen"
                                    (if (= (phase of:(from)) (phase of:(last To)))
                                        ("QueenMove")
                                    )
                                )
                                (forEach Piece "Knight"
                                    (if (= (phase of:(from)) (phase of:(last To)))
                                        ("KnightMove")
                                    )
                                )
                                (forEach Piece "Bishop"
                                    (if (= (phase of:(from)) (phase of:(last To)))
                                        ("BishopMove")
                                    )
                                )
                                (forEach Piece "King"
                                    (if (= (phase of:(from)) (phase of:(last To)))
                                        ("KingMove")
                                    )
                                )
                                (forEach Piece "Rook"
                                    (if (= (phase of:(from)) (phase of:(last To)))
                                        ("RookMove")
                                    )
                                )
                                (forEach Piece "Pawn"
                                    (if (= (phase of:(from)) (phase of:(last To)))
                                        ("PawnMove")
                                    )
                                )
                                (if (= 0 (phase of:(last To)))
                                    (if (and {
                                            "KingInTheInitialPlace"
                                            ("HasNeverMoved" "King")
                                            (not ("IsInCheck" "King" Mover))
                                        })
                                        (or
                                            (if (and
                                                    ("HasNeverMoved" "RookLeft")
                                                    (can Move ("DoCastle" "RookLeft" E 3 (is Empty (to))))
                                                )
                                                "BigCastling"
                                            )
                                            (if (and
                                                    ("HasNeverMoved" "RookRight")
                                                    (can Move ("DoCastle" "RookRight" W 2 (is Empty (to))))
                                                )
                                                "SmallCastling"
                                            )
                                        )
                                    )
                                )
                            })
                            ifAfterwards:(not ("IsInCheck" "King" Mover (forEach Piece)))
                        )
                    )
                    (do
                        (or
                            (forEach Piece)
                            (if (and {
                                    "KingInTheInitialPlace"
                                    ("HasNeverMoved" "King")
                                    (not ("IsInCheck" "King" Mover))
                                })
                                (or
                                    (if (and
                                            ("HasNeverMoved" "RookLeft")
                                            (can Move ("DoCastle" "RookLeft" E 3 (is Empty (to))))
                                        )
                                        "BigCastling"
                                    )
                                    (if (and
                                            ("HasNeverMoved" "RookRight")
                                            (can Move ("DoCastle" "RookRight" W 2 (is Empty (to))))
                                        )
                                        "SmallCastling"
                                    )
                                )
                            )
                        )
                        ifAfterwards:(not ("IsInCheck" "King" Mover))
                    )
                })
            )
        )

        (end
            {
            (if (and
                    ("IsInCheck" "King" Next)
                    ("NextCanNotMove")
                )
                (result Mover Win)
            )
            (if (or (no Moves Mover) (= (counter) 100)) (result Mover Draw))
            }
        )
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Phase Chess is a variant of Chess.")
        (rules "Phase Chess is a variant of Chess played on standard Chess board and Chess set, with same rules as Chess except the next player must move from a cell of the same colour just moved to if they can. It they can not, they can move any piece.")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/chess")
        (author "Cameron Browne")
        (credit "Eric Piette")
        (date "13-10-2020")
        }
    )

    (graphics {
        (show Check "King")
        (piece Scale "Pawn" 0.825)
        (piece Families {"Defined" "Microsoft" "Pragmata" "Symbola"})
        (board Style Chess)
    })

    (ai
        "Phase Chess_ai"
    )
)
