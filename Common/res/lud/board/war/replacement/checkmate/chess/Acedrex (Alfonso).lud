(define "CaptureToPiece"
    (apply 
        (if (is Enemy (who at:(to)))
            (remove 
                (to) 
                (then (set Var 1))
            )
        )
    ) 
)

(define "HadACapture" (= (var) 1))
(define "MovedBefore" (= 1 (state at:#1)))
(define "RememberMoved" (set State at:#1 1))

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:(is Enemy (who at:(to))) 
            "CaptureToPiece"
        )
    )
)

(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King" Next)))) )
)

(define "DoubleStep"
    (move
        Slide 
        Forward 
        (between (exact 2) if:(is Empty (between))) 
        (to if:(is Empty (to))) 
    )
)

//------------------------------------------------------------------------------

(game "Acedrex (Alfonso)"  
    (players {(player N) (player S)})  
    (equipment 
        { 
        (board (square 8)) 
        (piece "King" Each
            (move
                Step 
                (to 
                    if:(not (is Friend (who at:(to)))) 
                    "CaptureToPiece"
                ) 
            )		
        )
        (piece "Queen" Each
            (or
                (move
                    Step 
                    Diagonal
                    (to 
                        if:(not (is Friend (who at:(to)))) 
                        "CaptureToPiece"
                    ) 
                )
                (if (not ("MovedBefore" (from)))
                    (move Hop Diagonal 
                        (between if:True) 
                        (to if:(is Empty (to)))
                    ) 
                )
                (then 
                    (if (not ("MovedBefore" (last To)))
                        ("RememberMoved" (last To))
                    )
                )
            )
        )
        (piece "Bishop" Each
            (move Hop Diagonal 
                (between if:True) 
                (to 
                    if:(not (is Friend (who at:(to)))) 
                    "CaptureToPiece"
                )
            )		
        )
        (piece "Knight" Each
            (move
                Leap 
                "KnightWalk" 
                (to 
                    if:(not (is Friend (who at:(to)))) 
                    "CaptureToPiece"
                ) 
            )		
        )
        (piece "Rook" Each
            (move
                Slide 
                Orthogonal 
                (to 
                    if:(is Enemy (who at:(to))) 
                    "CaptureToPiece"
                ) 
            )		
        )
        (piece "Pawn" Each
            (or {
                (if (and (not ("HadACapture")) (is In (from) (sites Start (piece (what at:(from))))))
                    "DoubleStep"
                )
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                }
                (then
                    (if (and (= Off (where "Queen" Mover)) (is In (last To) (sites Mover "Promotion")))
                        (promote (last To) (piece "Queen") Mover)
                    )
                )
            )		
        )
        
        (regions "Promotion" P1 (sites Top) )
        (regions "Promotion" P2 (sites Bottom) )
        }
    )
    
    (rules 
        (start
            { 
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 6))
            (place "Rook1" {"A1" "H1"} state:1) (place "Knight1" {"B1" "G1"}) (place "Bishop1" {"C1" "F1"}) (place "Queen1" coord:"D1") (place "King1" coord:"E1" state:1) 
            (place "Rook2" {"A8" "H8"} state:1) (place "Knight2" {"B8" "G8"}) (place "Bishop2" {"C8" "F8"}) (place "Queen2" coord:"D8") (place "King2" coord:"E8" state:1) 
            }
        )       
        (play 
            (do
                (forEach Piece)
                ifAfterwards:(not ("IsInCheck" "King" Mover))
            )
        )
        
        (end 
            (if (and 
                    ("IsInCheck" "King" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Acedrex is the word for Chess in Spain. This is the version given in the Libro de los Juegos of Alfonso X, from the thirteenth century.")
        (rules "8x8 board. The pieces move as follows: King (x1): one space in any direction' Queen (x1): moves one square diagonally. On its first move, the Queen may jump diagonally two squares. The Queen cannot capture when making this move. Bishop (x2): jumps exactly two squares diagonally; Knight (x2): moves one space orthogonally and then one space diagonally, jumping over the intervening spaces; Rook (x2): moves any distance orthogonally; Pawns (x8): move one square forward orthogonally or one square forward diagonally to capture. Pawns may move two squares on their first move, but only if there have been no captures yet in the game. When reaching the opposite edge of the board, pawns may be promoted to Queen if the Queen has already been taken. If the King can be captured on the opponent's next move, it is in check. The King must not be in check at the end of the player's turn. If this is impossible, it is checkmate, and the opponent wins. ")
        (source "Libro de los Juegos 1-5.")
        (id "1439")
        (version "1.3.4")
        (classification "board/war/replacement/checkmate/chess")
        (credit "Eric Piette")
        (origin  "This game was played in Spain, around 1283.")
        }
    )
    
    (graphics {
        (board Style Chess)
    })
    
)
