(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King" Next (forEach Piece))))) )
)

(define "KingInTheInitialPlace"
    (= (what at:(mapEntry "King" (mover))) (id "King" Mover))
)

(define "HasNeverMoved"
    (= (state at:(mapEntry #1 (mover))) 1)
)

(define "PieceHasMoved"
    (set State at:(last To) 0)
)

(define "RememberPieceHasMoved"
    (then
        (if
            (= (state at:(last To)) 1)
            "PieceHasMoved"
        )
    )
)

(define "CaptureToPieceAndResetCounter"
    (apply
        (if (is Enemy (who at:(to)))
            (remove
                (to)
                (then (set Counter))
            )
        )
    )
)

(define "KingNotCheckedAndToEmpty"
    (and
        (is Empty (to))
        (not ("IsInCheck" "King" Mover at:(to)))
    )
)

(define "DoCastle"
    (move
        Slide
        (from (mapEntry #1 (mover)))
        #2
        (between
            (exact #3)
            if:#4
        )
        #5
    )
)

(define "RightCastling"
    ("DoCastle" "King" E 2 "KingNotCheckedAndToEmpty" (then (and ("PieceHasMoved") ("DoCastle" "RookRight" W 3 True))))
)

(define "LeftCastling"
    ("DoCastle" "King" W 2 "KingNotCheckedAndToEmpty" (then (and ("PieceHasMoved") ("DoCastle" "RookLeft" E 3 True))))
)

(define "SetEnPassantLocation"
    (then (set Var (last To)))
)

(define "InitialPawnMove"
    (forEach Value min:2 max:3
        (move
            Slide
            Forward
            (between (exact (value)) if:(is Empty (between))
                (apply (set Pending (between)))
            )
            (to if:(is Empty (to)))
            "SetEnPassantLocation"
        )
    )
)

(define "InLocationEnPassant"
    (is In (to) (sites Pending))
)

(define "LastPawnMoved"
    (var)
)

(define "EnPassant"
    (move
        Step
        (directions {FR FL})
        (to if:"InLocationEnPassant")
        (then (remove ("LastPawnMoved")))
    )
)

(define "CaptureForwardDiagonal"
    (move
        Step
        (directions {FR FL})
        (to
            if:(is Enemy (who at:(to)))
            (apply (remove (to)))
        )
    )
)

(define "PawnMove"
    (or {
        (if (is In (from) (sites Start (piece (what at:(from)))))
            "InitialPawnMove"
        )
        "StepForwardToEmpty"
        "CaptureForwardDiagonal"
        "EnPassant"
        }
        (then
            (and
                (if (is In (last To) (sites Mover "Promotion"))
                    (moveAgain)
                )
                (set Counter)
            )
        )
    )
)

(define "RookMove"
    (move
        Slide
        Orthogonal
        (to
            if:(is Enemy (who at:(to)))
            "CaptureToPieceAndResetCounter"
        )
        "RememberPieceHasMoved"
    )
)

(define "KingMove"
    (move
        Step
        (to
            if:(not (is Friend (who at:(to))))
            "CaptureToPieceAndResetCounter"
        )
        "RememberPieceHasMoved"
    )
)

(define "BishopMove"
    (move
        Slide
        Diagonal
        (to
            if:(is Enemy (who at:(to)))
            "CaptureToPieceAndResetCounter"
        )
    )
)

(define "KnightMove"
    (move
        Leap
        "KnightWalk"
        (to
            if:(not (is Friend (who at:(to))))
            "CaptureToPieceAndResetCounter"
        )
    )
)

(define "QueenMove"
    (move
        Slide
        (to
            if:(is Enemy (who at:(to)))
            "CaptureToPieceAndResetCounter"
        )
    )
)

(define "RowSquareSection" (% (row of:(last To)) 3))
(define "MinRow" (* "RowSquareSection" 3))
(define "MaxRow" (+ ("MinRow") 2))

(define "ColumnSquareSection" (% (column of:(last To)) 3))
(define "MinColumn" (* "ColumnSquareSection" 3))
(define "MaxColumn" (+ ("MinColumn") 2))

(define "RightSection"
    (and {
        (<= (column of:(from)) ("MaxColumn"))
        (>= (column of:(from)) ("MinColumn"))
        (<= (row of:(from)) ("MaxRow"))
        (>= (row of:(from)) ("MinRow"))
    })
)
//------------------------------------------------------------------------------

(game "Ultimate Chess"
    (players {(player N) (player S)})
    (equipment {
        (board (square 9))
        (piece "Pawn" Each ("PawnMove"))
        (piece "Rook" Each ("RookMove"))
        (piece "King" Each ("KingMove"))
        (piece "Bishop" Each ("BishopMove"))
        (piece "Knight" Each ("KnightMove"))
        (piece "Queen" Each ("QueenMove"))

        (map "King" {(pair 1 "E1") (pair 2 "E9")})
        (map "RookLeft" {(pair 1 "A1") (pair 2 "A9")})
        (map "RookRight" {(pair 1 "I1") (pair 2 "I9")})
        (regions "Promotion" P1 (sites Top) )
        (regions "Promotion" P2 (sites Bottom) )
        }
    )
    (rules
        (start
            {
            (place "Pawn1" (sites Row 1))
            (place "Pawn2" (sites Row 7))
            (place "Rook1" {"A1" "I1"} state:1) (place "Knight1" {"B1" "H1"}) (place "Bishop1" {"C1" "G1"}) (place "Queen1" {"D1" "F1"}) (place "King1" coord:"E1" state:1)
            (place "Rook2" {"A9" "I9"} state:1) (place "Knight2" {"B9" "H9"}) (place "Bishop2" {"C9" "G9"}) (place "Queen2" {"D9" "F9"}) (place "King2" coord:"E9" state:1)
            }
        )

        (play
            (if "SameTurn"
                (move Promote (last To) (piece {"Queen" "Knight" "Bishop" "Rook"}) Mover)
                (priority {
                    (if (!= 0 (count Moves))
                        (do
                            (or {
                                (forEach Piece "Queen"
                                    (if ("RightSection")
                                        ("QueenMove")
                                    )
                                )
                                (forEach Piece "Knight"
                                    (if ("RightSection")
                                        ("KnightMove")
                                    )
                                )
                                (forEach Piece "Bishop"
                                    (if ("RightSection")
                                        ("BishopMove")
                                    )
                                )
                                (forEach Piece "King"
                                    (if ("RightSection")
                                        ("KingMove")
                                    )
                                )
                                (forEach Piece "Rook"
                                    (if ("RightSection")
                                        ("RookMove")
                                    )
                                )
                                (forEach Piece "Pawn"
                                    (if ("RightSection")
                                        ("PawnMove")
                                    )
                                )
                                (if (and (= "RowSquareSection" 0) (= "ColumnSquareSection" 1))
                                    (if (and {
                                            "KingInTheInitialPlace"
                                            ("HasNeverMoved" "King")
                                            (not ("IsInCheck" "King" Mover))
                                        })
                                        (or
                                            (if (and
                                                    ("HasNeverMoved" "RookLeft")
                                                    (can Move ("DoCastle" "RookLeft" E 3 (is Empty (to))))
                                                )
                                                "LeftCastling"
                                            )
                                            (if (and
                                                    ("HasNeverMoved" "RookRight")
                                                    (can Move ("DoCastle" "RookRight" W 2 (is Empty (to))))
                                                )
                                                "RightCastling"
                                            )
                                        )
                                    )
                                )
                            })
                            ifAfterwards:(not ("IsInCheck" "King" Mover (forEach Piece)))
                        )
                    )
                    (do
                        (or
                            (forEach Piece)
                            (if (and {
                                    "KingInTheInitialPlace"
                                    ("HasNeverMoved" "King")
                                    (not ("IsInCheck" "King" Mover))
                                })
                                (or
                                    (if (and
                                            ("HasNeverMoved" "RookLeft")
                                            (can Move ("DoCastle" "RookLeft" E 3 (is Empty (to))))
                                        )
                                        "LeftCastling"
                                    )
                                    (if (and
                                            ("HasNeverMoved" "RookRight")
                                            (can Move ("DoCastle" "RookRight" W 3 (is Empty (to))))
                                        )
                                        "RightCastling"
                                    )
                                )
                            )
                        )
                        ifAfterwards:(not ("IsInCheck" "King" Mover))
                    )
                })
            )
        )

        (end
            {
            (if (and
                    ("IsInCheck" "King" Next)
                    ("NextCanNotMove")
                )
                (result Mover Win)
            )
            (if (or (no Moves Mover) (= (counter) 100)) (result Mover Draw))
            }
        )
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Ultimate Chess is a variant of Chess.")
        (rules "Ultimate Chess is a variant of Chess played on a 9x9 Sudoku checkerboard as shown, with same rules as Chess except:
            - Pawns can move 2 or 3 steps forward in their first move. The pawn can be captured by En Passant if they do a such move.
            - Kings can make a large casting in either side.
        - The next player must move from the sub-grid corresponding to the cell just moved to (if they have a legal move from there).")
        (version "1.3.0")
        (classification "board/war/replacement/checkmate/chess")
        (author "Cameron Browne")
        (credit "Eric Piette")
        (date "13-10-2020")
        }
    )

    (graphics {
        (show Check "King")
        (piece Scale "Pawn" 0.825)
        (piece Families {"Defined" "Microsoft" "Pragmata" "Symbola"})
        (board Style Chess)
        (show line {{30 39} {60 69} {3 93} {6 96}})
    })

    (ai
        "Ultimate Chess_ai"
    )
)
