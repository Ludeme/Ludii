(define "NextCanNotMove"
    (not (can Move (do (forEach Piece Next) ifAfterwards:(not ("IsInCheck" "King" Next)))))
)

(define "HasNeverMoved"
    (= (state at:#1) 1) 
)

(define "PieceHasMoved"
    (set State at:#1 0) 
)

(define "RememberPieceHasMoved"
    (then 
        (if 
            (= (state at:(last To)) 1) 
            ("PieceHasMoved" (last To))
        ) 
    ) 
)

(define "CaptureToPieceAndResetCounter"
    (apply 
        (if ("IsEnemyAt" (to))
            (remove 
                (to) 
                (then (set Counter))
            )
        )
    ) 
)

(define "SetEnPassantLocation"
    (then 
        (set Pending (ahead (last To) Backward))
    )
)

(define "DoubleStep"
    (move
        Slide 
        Forward 
        (between (exact 2) if:(is Empty (between))) 
        (to if:(is Empty (to))) 
        "SetEnPassantLocation"
    )
)

(define "InLocationEnPassant"
    (and 
        (is Pending) 
        (= (to) (value Pending)) 
    )
)

(define "EnPassant"
    (move
        Step 
        (directions {FR FL}) 
        (to if:"InLocationEnPassant") 
        (then 
            (remove 
                (ahead (last To) Backward)
            )
        )
    )
)

(define "CaptureForwardDiagonal"
    (move
        Step 
        (directions {FR FL}) 
        (to 
            if:("IsEnemyAt" (to)) 
            (apply (remove (to)))
        )
    )
)

//------------------------------------------------------------------------------

(game "Fischer Random Chess" 
    ("TwoPlayersNorthSouth") 
    (equipment { 
        (board (square 8)) 
        
        (piece "Pawn" Each 
            (or {
                (if (is In (from) (sites Start (piece (what at:(from)))))
                    "DoubleStep"
                )
                "StepForwardToEmpty" 
                "CaptureForwardDiagonal"
                "EnPassant"
                }
                (then
                    (and
                        (if (is In (last To) (sites Mover "Promotion")) 
                            (moveAgain)
                        )
                        (set Counter)
                    )
                )
            )
        )
        
        (piece "Rook" Each 
            (move
                Slide 
                Orthogonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPieceAndResetCounter"
                ) 
                "RememberPieceHasMoved"
            )
        )
        (piece "King" Each 
            (move
                Step 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPieceAndResetCounter"
                ) 
                "RememberPieceHasMoved"
            )
        )
        (piece "Bishop" Each 
            (move
                Slide 
                Diagonal 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPieceAndResetCounter"
                ) 
            )
        )
        (piece "Knight" Each 
            (move
                Leap 
                "KnightWalk" 
                (to 
                    if:(not ("IsFriendAt" (to))) 
                    "CaptureToPieceAndResetCounter"
                ) 
            )
        )
        (piece "Queen" Each 
            (move
                Slide 
                (to 
                    if:("IsEnemyAt" (to)) 
                    "CaptureToPieceAndResetCounter"
                ) 
            ) 
        )
        (regions "Promotion" P1 (sites Top))
        (regions "Promotion" P2 (sites Bottom))
    }) 
    (rules 
        (start { 
            (place "Rook1" (sites Random (difference (sites Bottom) (expand (sites Right)))) state:1)
            (place "King1" (sites Random (difference (intersection {(sites Bottom) (expand (sites Occupied by:Mover) steps:8 E) (sites Empty)}) (sites Right))) state:1)
            (place "Rook1" (sites Random (intersection {(sites Bottom) (expand (sites Occupied by:Mover component:"King") steps:8 E) (sites Empty)})) state:1)
            (place "Bishop1" (sites Random (intersection {(sites Bottom) (sites Empty) (sites Phase 0)})))
            (place "Bishop1" (sites Random (intersection {(sites Bottom) (sites Empty) (sites Phase 1)})))
            (place "Knight1" (sites Random (intersection (sites Bottom) (sites Empty)) num:2))
            (place "Queen1" (sites Random (intersection (sites Bottom) (sites Empty))))
            (place "Pawn1" (sites Row 1))
            
            (place "Rook2"
                (forEach (sites Top)
                    if:(= (id "Rook1") (what at:(ahead (site) steps:7 S)))
                )
                state:1
            )
            (place "Bishop2"
                (forEach (sites Top)
                    if:(= (id "Bishop1") (what at:(ahead (site) steps:7 S)))
                )
            )
            (place "Knight2"
                (forEach (sites Top)
                    if:(= (id "Knight1") (what at:(ahead (site) steps:7 S)))
                )
            )
            (place "Queen2"
                (forEach (sites Top)
                    if:(= (id "Queen1") (what at:(ahead (site) steps:7 S)))
                )
            )
            (place "King2"
                (forEach (sites Top)
                    if:(= (id "King1") (what at:(ahead (site) steps:7 S)))
                )
                state:1
            )
            (place "Pawn2" (sites Row 6))
        })
        
        (play 
            (if "SameTurn"
                (move Promote (last To) (piece {"Queen" "Knight" "Bishop" "Rook"}) Mover)
                (do 
                    (or 
                        (forEach Piece)
                        (if (and 
                                ("HasNeverMoved" (where "King" Mover))
                                (not ("IsInCheck" "King" Mover)) 
                            )
                            (if (is Mover P1) // Left Castling.
                                (forEach Site (sites Occupied by:Mover component:"Rook")
                                    (if ("HasNeverMoved" (site)) // Never Moved.
                                        (if (< (column of:(site)) (column of:(where "King" Mover))) // That's the left rook.
                                            (if (or
                                                    (can Move 
                                                        (slide 
                                                            (from (site)) 
                                                            (if (< (column of:(site)) (column of:(coord "D1")))
                                                                E
                                                                W
                                                            ) 
                                                            (between 
                                                                (exact (count Steps (coord "D1") (site))) 
                                                                if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                            )
                                                            (to 
                                                                if:True
                                                                (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                            )
                                                        )
                                                    )
                                                    (= (site) (coord "D1"))
                                                )
                                                (if (= (where "King" Mover) (coord "C1"))
                                                    (do (set Var "RookToMove" (site)) next:(move Select (from (where "King" Mover))))
                                                    (move
                                                        Slide 
                                                        (from (where "King" Mover)) 
                                                        (if (< (column of:(where "King" Mover)) (column of:(coord "C1")))
                                                            E
                                                            W
                                                        ) 
                                                        (between 
                                                            (exact (count Steps (coord "C1") (where "King" Mover))) 
                                                            if:(and 
                                                                (or (is Empty (to)) (= (to) (site)))
                                                                (not ("IsInCheck" "King" Mover at:(to)))
                                                            ) 
                                                        ) 
                                                        (to 
                                                            if:True
                                                            (apply 
                                                                if:(or (= (to) (site)) (is Empty (to)))
                                                                (and
                                                                    ("PieceHasMoved" (from))
                                                                    (set Var "RookToMove" (site))
                                                                )
                                                            )
                                                        )
                                                    )
                                                    (then
                                                        (if (= (what at:(var "RookToMove")) (id "Rook" Mover))
                                                            (slide 
                                                                (from (var "RookToMove")) 
                                                                (if (< (column of:(var "RookToMove")) (column of:(coord "D1")))
                                                                    E
                                                                    W
                                                                ) 
                                                                (between 
                                                                    (exact (count Steps (coord "D1") (var "RookToMove"))) 
                                                                    if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                                )
                                                                (to 
                                                                    if:True
                                                                    (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                                )
                                                            )
                                                            (add (piece (id "Rook" Mover)) (to (coord "D1")))
                                                        )
                                                    )
                                                )
                                            )
                                            (if (or
                                                    (can Move // Rook Right
                                                        (slide 
                                                            (from (site)) 
                                                            (if (< (column of:(site)) (column of:(coord "F1")))
                                                                E
                                                                W
                                                            ) 
                                                            (between 
                                                                (exact (count Steps (coord "F1") (site))) 
                                                                if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                            )
                                                            (to 
                                                                if:True
                                                                (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                            )
                                                        )
                                                    )
                                                    (= (site) (coord "F1"))
                                                )
                                                (if (= (where "King" Mover) (coord "G1"))
                                                    (do (set Var "RookToMove" (site)) next:(move Select (from (where "King" Mover))))
                                                    (move
                                                        Slide 
                                                        (from (where "King" Mover)) 
                                                        (if (< (column of:(where "King" Mover)) (column of:(coord "G1")))
                                                            E
                                                            W
                                                        ) 
                                                        (between 
                                                            (exact (count Steps (coord "G1") (where "King" Mover))) 
                                                            if:(and 
                                                                (or (is Empty (to)) (= (to) (site)))
                                                                (not ("IsInCheck" "King" Mover at:(to)))
                                                            ) 
                                                        ) 
                                                        (to 
                                                            if:True
                                                            (apply 
                                                                if:(or (= (to) (site)) (is Empty (to)))
                                                                (and
                                                                    ("PieceHasMoved" (from))
                                                                    (set Var "RookToMove" (site))
                                                                )
                                                            )
                                                        )
                                                    )
                                                    (then
                                                        (if (= (what at:(var "RookToMove")) (id "Rook" Mover))
                                                            (slide 
                                                                (from (var "RookToMove")) 
                                                                (if (< (column of:(var "RookToMove")) (column of:(coord "F1")))
                                                                    E
                                                                    W
                                                                ) 
                                                                (between 
                                                                    (exact (count Steps (coord "F1") (var "RookToMove"))) 
                                                                    if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                                )
                                                                (to 
                                                                    if:True
                                                                    (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                                )
                                                            )	
                                                            
                                                            (add (piece (id "Rook" Mover)) (to (coord "F1")))
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                                (forEach Site (sites Occupied by:Mover component:"Rook")
                                    (if ("HasNeverMoved" (site)) // Never Moved.
                                        (if (> (column of:(site)) (column of:(where "King" Mover))) // That's the left rook.
                                            (if (or
                                                    (can Move 
                                                        (slide 
                                                            (from (site)) 
                                                            (if (< (column of:(site)) (column of:(coord "F8")))
                                                                E
                                                                W
                                                            ) 
                                                            (between 
                                                                (exact (count Steps (coord "F8") (site))) 
                                                                if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                            )
                                                            (to 
                                                                if:True
                                                                (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                            )
                                                        )
                                                    )
                                                    (= (site) (coord "F8"))
                                                )
                                                (if (= (where "King" Mover) (coord "G8"))
                                                    (do (set Var "RookToMove" (site)) next:(move Select (from (where "King" Mover))))
                                                    (move
                                                        Slide 
                                                        (from (where "King" Mover)) 
                                                        (if (< (column of:(where "King" Mover)) (column of:(coord "G8")))
                                                            E
                                                            W
                                                        ) 
                                                        (between 
                                                            (exact (count Steps (coord "G8") (where "King" Mover))) 
                                                            if:(and 
                                                                (or (is Empty (to)) (= (to) (site)))
                                                                (not ("IsInCheck" "King" Mover at:(to)))
                                                            ) 
                                                        ) 
                                                        (to 
                                                            if:True
                                                            (apply
                                                                if:(or (= (to) (site)) (is Empty (to)))
                                                                (and
                                                                    ("PieceHasMoved" (from))
                                                                    (set Var "RookToMove" (site))
                                                                )
                                                            )
                                                        )
                                                    )
                                                    (then
                                                        (if (= (what at:(var "RookToMove")) (id "Rook" Mover))
                                                            (slide 
                                                                (from (var "RookToMove")) 
                                                                (if (< (column of:(var "RookToMove")) (column of:(coord "F8")))
                                                                    E
                                                                    W
                                                                ) 
                                                                (between 
                                                                    (exact (count Steps (coord "F8") (var "RookToMove"))) 
                                                                    if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                                )
                                                                (to 
                                                                    if:True
                                                                    (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                                )
                                                            )
                                                            (add (piece (id "Rook" Mover)) (to (coord "F8")))
                                                        )
                                                    )
                                                )
                                            )
                                            (if (or
                                                    (can Move // Rook Right
                                                        (slide 
                                                            (from (site)) 
                                                            (if (< (column of:(site)) (column of:(coord "D8")))
                                                                E
                                                                W
                                                            ) 
                                                            (between 
                                                                (exact (count Steps (coord "D8") (site))) 
                                                                if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                            )
                                                            (to 
                                                                if:True
                                                                (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                            )
                                                        )
                                                    )
                                                    (= (site) (coord "D8"))
                                                )
                                                (if (= (where "King" Mover) (coord "C8"))
                                                    (do (set Var "RookToMove" (site)) next:(move Select (from (where "King" Mover))))
                                                    (move
                                                        Slide 
                                                        (from (where "King" Mover)) 
                                                        (if (< (column of:(where "King" Mover)) (column of:(coord "C8")))
                                                            E
                                                            W
                                                        ) 
                                                        (between 
                                                            (exact (count Steps (coord "C8") (where "King" Mover))) 
                                                            if:(and 
                                                                (or (is Empty (to)) (= (to) (site)))
                                                                (not ("IsInCheck" "King" Mover at:(to)))
                                                            ) 
                                                        ) 
                                                        (to 
                                                            if:True
                                                            (apply
                                                                if:(or (= (to) (site)) (is Empty (to)))
                                                                (and
                                                                    ("PieceHasMoved" (from))
                                                                    (set Var "RookToMove" (site))
                                                                )
                                                            )
                                                        )
                                                    )
                                                    (then
                                                        (if (= (what at:(var "RookToMove")) (id "Rook" Mover))
                                                            (slide 
                                                                (from (var "RookToMove")) 
                                                                (if (< (column of:(var "RookToMove")) (column of:(coord "D8")))
                                                                    E
                                                                    W
                                                                ) 
                                                                (between 
                                                                    (exact (count Steps (coord "D8") (var "RookToMove"))) 
                                                                    if:(or (is Empty (to)) (= (what at:(to)) (id "King" Mover)))
                                                                )
                                                                (to 
                                                                    if:True
                                                                    (apply if:(or (= (what at:(to)) (id "King" Mover)) (is Empty (to))) ("PieceHasMoved" (from)))
                                                                )
                                                            )	
                                                            
                                                            (add (piece (id "Rook" Mover)) (to (coord "D8")))
                                                        )
                                                    )
                                                )
                                            )
                                        )
                                    )
                                )
                            )
                        )
                    )
                    ifAfterwards:(not ("IsInCheck" "King" Mover))
                )
            )
        )
        
        (end {
            (if (and 
                    ("IsInCheck" "King" Next)
                    ("NextCanNotMove")
                ) 
                (result Mover Win)
            ) 
            (if (or (no Moves Mover) (= (counter) 99)) (result Mover Draw)) 
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "A variation of the game of chess invented by the former World Chess Champion Bobby Fischer.")
        (aliases {"Chess960; Chess9LX"})
        (rules "Before the game, a starting position is randomly determined and set up, subject to certain requirements. White's pieces (not pawns) are placed randomly on the first rank, following two rules: The bishops must be placed on opposite-color squares. The king must be placed on a square between the rooks. Black's pieces are placed equal-and-opposite to White's pieces. (For example, if the white king is randomly determined to start on f1, then the black king is placed on f8.) Pawns are placed on the players' second ranks as in classical chess. After setup, the game is played the same as classical chess in all respects.")
        (id "1670")
        (source "<a href=\"https://en.wikipedia.org/wiki/Fischer_random_chess\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.5")
        (classification "board/war/replacement/checkmate/chess")
        (author "Bobby Fischer")
        (credit "Matthew Stephenson and Eric Piette (for Castling)")
        (date "19-06-1996")
        }
    )
    
    (graphics {
        (show Check "King")
        (piece Scale "Pawn" 0.825)
        (piece Families {"Defined" "Microsoft" "Pragmata" "Symbola"})
        (board Style Chess)
    })
    
)
