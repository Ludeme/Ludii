(define "ThrowValue" (mapEntry "ThrowDiceValue" (count Pips)))
(define "DieNotUsed" (!= (pips) 0))

//------------------------------------------------------------------------------

(game "Udat Pagada" 
    (players 2) 
    (equipment { 
        (board 
            (add
                (add
                    (hole
                        (merge 
                            (shift 0 8 (rectangle 4 20)) 
                            (shift 8 0 (rectangle 20 4))
                        )
                        (poly {{8 8} {8 11} {11 11} {11 8}})
                    )
                    cells:{ { 8 28 48 68 69 70 71 51 31 11 10 9 } }
                )
                vertices:{{9.5 4} {15 4} {15 9.5} {15 15} {9.5 15} {4 15} {4 9.5} {4 4}}
            )
            {
            (track "Track" {144..151} loop:True)
            }
            use:Vertex
        )
        ("StickDice" 7)
        (piece "Pawn" Each
            (move 
                (from (from) level:(level))
                (to ("NextSiteOnTrack" ("ThrowValue") from:(from) "Track")
                    (apply 
                        (if ("IsEnemyAt" (to))
                            (forEach Level (to) FromTop
                                (remove (to) level:(level))
                            )
                        )
                    )
                )
            )		
        )
        (hand Each)
        (map "Throw" {(pair 0 7) (pair 1 10) (pair 2 2) (pair 3 3) (pair 4 4) (pair 5 25) (pair 6 <ThrowSix:value>) (pair 7 14)})
        (map "Entry" {(pair P1 144) (pair P2 148)})
    }) 
    (rules 
        (start {
            (place Stack "Pawn1" (handSite P1) count:4)
            (place Stack "Pawn2" (handSite P2) count:4)
        })
        
        (play 
            ("RollMove"
                (or
                    (if (is Occupied (handSite Mover))
                        (move 
                            (from (handSite Mover))
                            (to ("NextSiteOnTrack" (- ("ThrowValue") 1) from:(mapEntry "Entry" Mover) "Track")
                                (apply 
                                    (if ("IsEnemyAt" (to))
                                        (forEach Level (to) FromTop
                                            (remove (to) level:(level))
                                        )
                                    )
                                )
                            )
                        )
                    )
                    (forEach Piece)
                )
            )
        )
        
        (end ("CaptureAll" Next))
    )
)

//------------------------------------------------------------------------------

(option "ThrowSixUpValue" <ThrowSix> args:{ <value> }
    {
    (item "36" <36> "6 faces up = 36.")* 
    (item "30" <30> "6 faces up = 30.") 
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Udat Pagada (Described)" {
        "ThrowSixUpValue/36"
    })
    
    (ruleset "Ruleset/Throw of Thirty (Described)" {
        "ThrowSixUpValue/30"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Udat Pagada is a game played on a  Chaupar board, but ignoring the spaces on the board. It was played up until the early twentieth century by girls. Graffiti suggests that the game could be much older.")
        (useFor "Ruleset/Udat Pagada (Described)" (rules "Played on a Chaupar board. Two players. Four pieces per player. Seven cowries, used as dice. The throws are as follows: one face up = 10; two faces up = 2; three faces up = 3; four faces up = 4; five faces up = 25; six faces up = 36; seven faces up = 14; all faces down = 7. Players start from opposite ends of the board. In this game, the divisions of the arms of the board are ignored; each arm is considered one space, and the spaces between the arms are also considered a space, making eight total spaces. Players sit opposite each other, and the point in front of them is the starting point for that player. Play proceeds in an anti-clockwise direction. When one player's piece lands on a space occupied by an opponent's piece, the opponent's piece is captured. A player wins by capturing all of the opponent's pieces."))
        (useFor "Ruleset/Udat Pagada (Described)" (source "Deodhar 1905 (translated by Dnyaneshwari Kamath)."))
        (useFor "Ruleset/Udat Pagada (Described)" (id "1177"))
        (useFor "Ruleset/Throw of Thirty (Described)" (rules "The throw of six cowries face up = 30."))
        (useFor "Ruleset/Throw of Thirty (Described)" (source "Deodhar 1905 (translated by Dnyaneshwari Kamath."))
            (useFor "Ruleset/Throw of Thirty (Described)" (id "1564"))
            (source "Kamath 2020: 27.")
            (version "1.3.9")
            (classification "board/war/replacement/eliminate/all")
            (credit "Eric Piette")
            (origin "This game was played in India, around 1905.")
            }
        )
        
        (graphics {
            (board Style Board)
            (stackType 0 Ground)
            (stackType 1 Count)
            (stackType 2 Count)
            (board Colour Phase0 (colour 223 178 110))
            (board Colour InnerEdges (colour Black))
            (board Colour OuterEdges (colour Black))
        })
        
    )
