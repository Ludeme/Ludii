(define "ThrowValue" (mapEntry "ThrowDiceValue" (count Pips)))

(define "SiteToMoveOnTrack" (trackSite Move steps:("ThrowValue")))

(define "ActivatePiece" (if (= (state at:(last To)) 1) (set State at:(last To) 0)))

(define "ActivatedPiece" (= (state at:(from)) 0))

(define "InactivePiece" (= (state at:(from)) 1))

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (remove (to))
   ) 
)

(define "PieceMove" 
    (if (or 
            ("ActivatedPiece") 
            (and (= (count Pips) (mapEntry "Sir" (mover))) ("InactivePiece"))
       )
        (if (!= "SiteToMoveOnTrack" Off)
            (if (or
                    (is In "SiteToMoveOnTrack" (sites Empty))
                    (is Enemy (who at:"SiteToMoveOnTrack"))
               )
                (move 
                    (from)
                    (to 
                        "SiteToMoveOnTrack" 
                        "CaptureEnemyPiece"
                   )
               )
           )
            (then ("ActivatePiece"))
       )
   )
)

//------------------------------------------------------------------------------

(game "Siryu (War)"
    (players 2)
    (equipment {
        (board 
            (rectangle 4 12) 
            {
            (track "Track1" "0,E,N1,W,N1,E,23,E,W,N1,E,23,W,N1,E,23,W,N1,E,23,W,N1,E,23,W,N1,E,23,W,N1,E,23"  loop:True P1)
            (track "Track2" "47,W,S1,E,S1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24,E,N1,W,24"  loop:True P2)
            }
            use:Vertex
       )
        (piece "Stick" P1 ("PieceMove"))
        (piece "Marker" P2 ("PieceMove"))
        (regions "AllSites" (sites Board))
        (dice d:2 from:0 num:6)
        (map "ThrowDiceValue" {(pair 0 6) (pair 1 5) (pair 2 0) (pair 3 3) (pair 4 0) (pair 5 5) (pair 6 6)})
        (map "Sir" {(pair 1 1) (pair 2 5)})
    })
    (rules 
        (start {  
            (place "Stick1" (sites Bottom) state:1)
            (place "Marker2" (sites Top) state:1)
        })
        (play 
            (do 
                (roll) 
                next:(if (can Move (forEach Piece))
                    (forEach Piece (then (if (= ("ThrowValue") 5) (moveAgain))))
                    (move Pass (then (if (= ("ThrowValue") 5) (moveAgain))))
               )
           )
       )
        (end 
            (if (no Pieces Next) (result Mover Win))
       )
   )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The word Siryu is used for several board games in the Tabelbala Oasis of Algeria. This one is a capturing game that is similar to the game of Tab that is played throughout North Africa and Southwest Asia.")
        (rules "4x12 board. Twelve pieces per player, one playing as sticks and the other as stones. Pieces begin on the board, one each in every space of the outer rows. Six sticks, with one black side and one white side, used as dice. Players choose who will play as black and who will play as white, and the throws are as follows: six black or white up = 6, five black or white up (called sir)= 5 plus another throw; four black or white up = 0, three black and three white up = 3. A player must throw sir in their colour to unlock a piece before moving it. Pieces move from left to right in their home row, right to left in the next row, left to right in the row following that, and then return to the second row. When a player's piece lands on a spot occupied by an opponent's piece, the opponent's piece is captured. The player who captures all of the opponent's pieces wins.")
        (source "Champault 1969: 353-354.")
        (id "811")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin  "This game was played in Algeria, around 1969.")
        }
   )
    
    (graphics {
        (show Edges Hidden)    
        (show Symbol "disc" "AllSites" Vertex fillColour:(colour White) edgeColour:(colour Black) scale:1)
        (piece Colour "Die" state:1 fillColour:(colour Black))
        (piece Scale "Stick" 0.5)
        (piece Scale "Marker" 0.5)
        
    })
    
    (ai
        "Siryu (War)_ai"
   )
)

