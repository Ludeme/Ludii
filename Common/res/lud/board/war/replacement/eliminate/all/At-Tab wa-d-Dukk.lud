(define "ThrowValue" (mapEntry (count Pips)))

(define "SiteToMoveOnTrack" (trackSite Move steps:("ThrowValue")))

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (remove (to) (then (addScore Mover 1)))
    ) 
)

(define "ActivatePiece" (set State at:(last To) 0))

(define "ActivatedPiece" (= (state at:(from)) 0))

(define "InactivePiece" (= (state at:(from)) 1))

(define "InOpponentHome" (is In (from) (sites Next)))

(define "OpponentPieceAtHome" (= (count Sites in:(intersection (sites Occupied by:Next) (sites Next))) 0))

(define "ThrowEqualSizeKingMinusOne" (= (+ ("ThrowValue") 1) (count at:(from))))

(define "Move" 
    (move 
        (from)
        (to 
            "SiteToMoveOnTrack" 
            "CaptureEnemyPiece"
        )
        count:#1
    )
)

(define "KingMove" ("Move" (count at:(from))))
(define "UncoupledKing" ("Move" ("ThrowValue")))
(define "CreateAKing" 
    (move 
        (from)
        (to  "SiteToMoveOnTrack")
    )		
)

(define "AllPieceInOpponentHome" 
    (and
        (= (count Sites in:(difference (sites Occupied by:P1) (sites P2))) 0)
        (= (count Sites in:(difference (sites Occupied by:P2) (sites P1))) 0)
    )		
)

//------------------------------------------------------------------------------

(game "At-Tab wa-d-Dukk"
    (players 2)
    (equipment {
        (board
            (rectangle 4 <Board:size>)
            {
            (track "Track1" "0,E,N1,W,N1,E,N1,W" P1 directed:True)
            (track "Track2" "51,W,S1,E,S1,W,S1,E" P2 directed:True)
            }
        )
        (dice d:2 from:0 num:4)
        (map {(pair 0 6) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4)})
        (piece "Marker" Each
            (if (or ("ActivatedPiece") (and (= 1 ("ThrowValue")) ("InactivePiece")))
                (if (!= "SiteToMoveOnTrack" Off)
                    (if (or 
                            (not ("InOpponentHome")) 
                            (and
                                ("InOpponentHome")
                                ("OpponentPieceAtHome")
                            )
                        )
                        (if (or
                                (is In "SiteToMoveOnTrack" (sites Empty))
                                (is Enemy (who at:"SiteToMoveOnTrack"))
                            )
                            (or 
                                (if ("ThrowEqualSizeKingMinusOne")
                                    ("UncoupledKing")
                                )
                                ("KingMove")
                            )
                            (if (and 
                                    (is In "SiteToMoveOnTrack" (sites "CentralRows"))
                                    (is Friend (who at:"SiteToMoveOnTrack"))
                                )
                                ("CreateAKing")
                            )
                        )
                    )
                    (then ("ActivatePiece"))
                )
            )
        )
        (regions "AnotherThrow" (sites {1 4 6}))
        (regions "CentralRows" (union (sites Row 1) (sites Row 2)))
        (regions P1 (sites Bottom))
        (regions P2 (sites Top))
    })
    (rules 
        (start { 
            (place "Marker1" (sites Bottom) state:1)
            (place "Marker2" (sites Top) state:1)
        })
        (play 
            (do 
                (roll) 
                next:(if (can Move (forEach Piece))
                    (forEach Piece (then (if (is In ("ThrowValue") (sites "AnotherThrow")) (moveAgain))))
                    (move Pass (then (if (is In ("ThrowValue") (sites "AnotherThrow")) (moveAgain))))
                )
            )
        )
        (end 
            (if (or ("AllPieceInOpponentHome") (no Pieces Mover))
                (byScore)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "13" <13>  "The board has 13 columns.") 
    (item "19" <19>  "The board has 19 columns.")  
    (item "21" <21>  "The board has 21 columns.")  
    (item "29" <29>  "The board has 29 columns.") 
    }
)

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/at-Tab wa-d-Dukk (Observed)" {
        "Board Size/13"
        }
        variations:{"Board Size/19" "Board Size/21" "Board Size/29"}
    )*
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "At-Tab wa-d-Dukk is a capturing game from Early Modern Southwest Asia. It is similar to the game of Tab still played today in the region, and may be the ancestor of several similar games.")
        (aliases {"Tab" "Tab wa Dukk" "Bazi Qamish" "Kami≈ü Oyunu" "Sitta" "Tab al-Qasab"})
        (rules "4x13, 19, 21, or 29 board. The number of pieces per player equals the number of spaces in a row, which begin the game arrayed in the outer rows of the board. Four sticks, each with a white side and a yellow side. Throws equal the number of white sides which fall up; when only yellow sides are up the throw equals 6. A throw of 1, 4, or 6 grants another throw to the player. Players  perform all of their throws first, and then move pieces according to the values of the throws without subdividing the value of a single throw. Players cannot move their pieces until the throw a 1. Pieces cannot move past one another in the home row. Each piece in the home row must individually be unlocked with a throw of 1 before it can move. In the central rows, when two of a player's pieces land on the same spot, they become a king and can be moved as one piece. There is no limit to the number of pieces in the king. They may be uncoupled with a throw of 1, or another throw which removes that number of pieces from the king. When a player's piece lands in a space occupied by an opponent's piece, the opponent's piece is removed from the board. Play progresses from left to right in the player's home row, and then from right to left in the second row, left to right in the third row, and then right to left in the opponent's row. When a piece enters the opponent's row, it cannot move if there are other pieces which can be moved. The player to capture the most of the opponent's pieces wins.")
        (source "Hyde 1694: 217-223.")
        (id "760")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin  "This game was played in Southwest Asia, from around 1248 to 1762.")
        }
    )
    
    (graphics {
        (piece Colour "Die" state:0 fillColour:(colour Yellow))
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "At-Tab wa-d-Dukk_ai"
    )
)
