(game "Quad Wrangle"
    (players 2)
    (equipment {
        (board (square 8))
        (piece "Ball" Each
            (move Slide
                (then
                    (forEach Site
                        (sites Around (last To))
                        (if ("IsEnemyAt" (site))
                            (and
                                (remove (site))
                                (add (piece (id "Ball" Mover)) (to (site)))
                            )
                        )
                    )
                )
            )
        )
    })
    (rules
        (start {
            (place "Ball1" (difference (union (sites Top) (sites Right)) (union {(sites Left) (sites Bottom) (intersection (sites Top) (sites Right))})))
            (place "Ball2" (difference (union (sites Bottom) (sites Left)) (union {(sites Right) (sites Top) (intersection (sites Bottom) (sites Left))})))
        })
        
        (play
            (or {
                (move Add
                    (to (sites Around (sites Occupied by:Mover) Empty))
                    (then
                        (forEach Site
                            (sites Around (last To))
                            (if ("IsEnemyAt" (site))
                                (and
                                    (remove (site))
                                    (add (piece (id "Ball" Mover)) (to (site)))
                                )
                            )
                        )
                    )
                )
                (forEach Piece)
                (move Add
                    (to (difference (sites Empty) (sites Around (sites Occupied by:Mover) Empty)))
                )
            })
        )
        
        (end (if (is Full)
                (byScore {
                    (score P1 (count Pieces P1))
                    (score P2 (count Pieces P2))
                })
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The game is played on a 7x7 board.")
        (rules "MOVES - A move consists of either:
            - A growth, i.e., a drop of a stone on an empty adjacent (orthogonal or diagonal) to friendly cell;
            - An orthogonal or diagonal slide of a stone already on board (like a chess Queen) to an empty cell;
            - A drop of a stone on any empty cell.
            - Any opponent adjacent stones to the new piece (either through growth or slide) are flipped to the moving player's colour. Notice that the third option (simple drop) do not flip the colour of adjacent stones.
            
        GOAL - Wins the player with most stones when the board is filled.")
        (id "1818")
        (source "<a href=\"http://www.di.fc.ul.pt/~jpn/gv/quadwrangle.htm\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.di.fc.ul.pt</a>")
        (version "1.3.7")
        (classification "board/war/replacement/eliminate/all")
        (author "Keith Carter")
        (credit "Eric Piette")
        (date "2001")
        }
    )
    
    (graphics {
        (board Style Chess)
    })
    
    (ai
        "Quad Wrangle_ai"
    )
)
