(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "EntryPoint" (mapEntry "Entry" (mover)))

(define "SetCapturePiece"
    (set State #1 #2 2)
)

(define "SetCapturingPiece"
    (set State #1 #2 1)
)

(define "UnsetCapturingPiece" (set State #1 #2 0))

(define "UnsetCapturingPieces" 
    (forEach Level (last From) FromTop
        (if (is Friend (who at:(last From) level:(level)))
            ("UnsetCapturingPiece" at:(last From) level:(level))
        )
    ) 
)

(define "AtLeastAnEnemyPieceThere"
    (is In #1 (sites Occupied by:Enemy top:False))
)

(define "CaptureMove" 
    (forEach Level (last To) FromTop
        (and
            (if (is Enemy (who at:(last To) level:(level)))
                ("SetCapturePiece" at:(last To) level:(level))
            )
            (if (is Friend (who at:(last To) level:(level)))
                ("SetCapturingPiece" at:(last To) level:(level))
            )
        )
    ) 
)

(define "CapturingPiece" (= 1 #1))

(define "CapturedPiece" (= 2 #1))

(define "FreePiece" (= 0 #1))

(define "EnterAPiece" 
    (move
        (from (handSite Mover))
        (to ("NextSiteOnTrack" (- "ThrowValue" 1) from:("EntryPoint" (mover)) "Track"))
    )
)

(define "RemoveCapturedPieces" 
    (forEach Level (last From) FromTop
        (if ("CapturedPiece" (state at:(last From) level:(level)))
            (remove (last From) level:(level))
        )
    )
)
(define "RebirthCapturingPiece"
    (add
        (piece (id "Marker" Mover))
        (to (handSite Mover))
    )
)

(define "CapturedPiecesFollowCapturingPiece"
    (forEach Level (last From) FromTop
        (if ("CapturedPiece" (state at:(last From) level:(level)))
            (fromTo 
                (from (last From) level:(level))
                (to (last To))
            )
        )
    )	
)

(define "CapturedPiecesFollowCapturingPiece"
    (forEach Level (last From) FromTop
        (if ("CapturedPiece" (state at:(last From) level:(level)))
            (fromTo 
                (from (last From) level:(level))
                (to (last To))
            )
        )
    )	
)

//------------------------------------------------------------------------------

(game "Puluc"
    (players 2)
    (equipment {
        (board 
            (rectangle 1 10) 
            {
            (track "Track1" {0..8} loop:True P1)
            (track "Track2" {8..0} loop:True P2)
            (track "CaptureTrack1" {8..0} P1 directed:True)
            (track "CaptureTrack2" {0..8} P2 directed:True)
            } 
            use:Edge
        )
        (piece "Marker" Each
            (or
                (if ("FreePiece" (state at:(from) level:(level)))
                    (move 
                        (from (from) level:(level))
                        (to 
                            ("NextSiteOnTrack" ("ThrowValue") from:(from) "Track")
                        )
                        (then 
                            (if ("AtLeastAnEnemyPieceThere" (last To))
                                ("CaptureMove")
                            )
                        )
                    )
                )
                (if ("CapturingPiece" (state at:(from) level:(level)))
                    (if ("IsNotOffBoard" ("NextSiteOnTrack" ("ThrowValue") from:(from) "CaptureTrack"))
                        (move 
                            (from (from) level:(level))
                            (to 
                                ("NextSiteOnTrack" ("ThrowValue") from:(from) "CaptureTrack")
                            )
                            (then 
                                (and
                                    ("CapturedPiecesFollowCapturingPiece")
                                    ("UnsetCapturingPieces")
                                )
                            )
                        )
                        (move Remove (from) level:(level)
                            (then
                                (and {
                                    ("UnsetCapturingPieces")
                                    ("RemoveCapturedPieces")	
                                    ("RebirthCapturingPiece")
                                })
                            )
                        )
                    )
                )
            )		
        )
        (regions "AllSites" (sites Board Vertex))
        (map "Throw" {(pair 0 5) (pair 1 3) (pair 2 2) (pair 3 3) (pair 4 4)})
        (map "Entry" { (pair 1 0) (pair 2 8)})
        ("StickDice" 4)
        (hand Each)
    })
    (rules
        (start {
            (place Stack "Marker1" (handSite P1) count:5)
            (place Stack "Marker2" (handSite P2) count:5)
        })
        (play 
            ("RollMove"
                (or {
                    (if (is Occupied (handSite Mover))
                        ("EnterAPiece")
                    )
                    (forEach Piece)
                })
                (then
                    (if ("NewTurn")
                        (moveAgain)
                    )
                )
            )
        )
        (end {
            ("CaptureAll" P2)
            ("CaptureAll" P1)
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Puluc is a capturing game played by the Qeqchi people of Guatemala. Opponent's pieces arre captured and dragged back to the point where a player starts.")
        (rules "Ten corn kernels are placed in a line; the spaces between the kernels are the playing spaces. Four kernels of corn are used as dice, blackened on one side. The throws are as follows: Two of the same side up = 2, three of the same side up = 3; four black sides up = 4; four unblackened sides up = 5. Five pieces per player. Players take turns moving pieces according to the throws of the corn, with two throws per turn. When a player reaches the opposite end of the board, they move to the start and continue moving in the same direction. If a piece lands on a space occupied by an opponent's piece, the player then moves in the reverse direction, carrying the opponent's piece with it in an attempt to move past the starting points and off the board. Upon moving off the board, the opponent's piece is captured. The player then enters their piece again on their next turn. However, if the opponent lands on a piece carrying one of their pieces away, they then start carrying both of those pieces back to their starting point, freeing the captured piece and capturing the other player's piece. The player who captures all of the opponent's pieces wins.")
        (source "Sapper 1906: 284.")
        (id "1001")
        (version "1.3.13")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin "This game was played in Guatemala, around 1906.")
        }
    )
    
    (graphics {
        (piece Scale "Stick" 0.5)
        (show Edges (colour Hidden))
        (show Symbol "corn" "AllSites" Vertex fillColour:(colour Yellow) edgeColour:(colour Black) scale:0.5)
        (piece Colour "Die" state:1 fillColour:(colour Black))
        (piece Scale "Marker" 0.5)
        (stackType 0 Ground)
        (stackType 1 Count)
        (stackType 2 Count)
    })
    
)
