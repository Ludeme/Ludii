(game "Toono"
    (players 2)
    (equipment {
        (board 
            (add
                (concentric {1 0 8 8})
                edges:{{0 2} {0 4} {0 6} {0 8}}
            )
            use:Vertex
        )
        (piece "Marker" Each
            (move 
                (from) 
                (to 
                    (sites Distance from:(from) (exact 3))
                    if:(not ("IsFriendAt" (to)))
                    (apply (if ("IsEnemyAt" (to)) (remove (to))))
                )
            )
        )
    })
    (rules
        (start {
            (place "Marker1" (difference (expand (sites Bottom) steps:2) (expand (sites Top) steps:2)))
            (place "Marker2" (difference (expand (sites Top) steps:2) (expand (sites Bottom) steps:2)))
        })
        (play (forEach Piece))
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Toono (Yurt's Roof) is a capturing game played in Mongolia. The shape of the board is said to resemble the roof of a yurt. The pieces jump three places on the board and capture a piece they land on.")
        (rules "Two concentric circles, with two perpendicular diameters intersecting both circles, and four other lines, positioned diagonally, connecting the circumferences of the two circles. Six pieces per player. which begin on opposite sides of the circle from the other player, three on each circle. Players alternate turns moving their pieces. Pieces move three spaces along the lines on the board, regardless of whether they are occupied, capturing any piece on the third. Pieces may change direction in a turn, as long as the lines are followed and there is no backtracking. The player who captures all of the opponent's pieces wins.")
        (source "Popova 1974: 34, 36-37.")
        (id "1039")
        (version "1.3.5")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )
    
    (graphics {
        
    })
    
    (ai
        "Toono_ai"
    )
)
