(game "Fission" 
    (players 2) 
    (equipment { 
        (board (square 8))
        (piece "Ball" Each
            (move 
                (from (from))
                (to (sites LineOfSight Farthest at:(from))
                    if:(not (is In (to) (sites Around (from))))		
                )
                (then
                    (if (!= (ahead (last To) ("LastDirection" Cell)) (last To))
                        (remove
                            (sites Around (last To) includeSelf:True)
                        )
                    )
                )
            )
        ) 
    }) 
    (rules 
        (start {
            (place "Ball1" (intersection (sites Phase 1) (expand (sites Centre) steps:2 Orthogonal)))
            (place "Ball2" (intersection (sites Phase 0) (expand (sites Centre) steps:2 Orthogonal)))
        })
        
        (play (forEach Piece)) 
        
        (end {
            (if (and (not (no Pieces Mover)) (no Pieces Next))
                (result Mover Win)
            )
            (if 
                (or 
                    (no Moves Next)
                    (and
                        (= 1 (count Pieces P1))
                        (= 1 (count Pieces P2))
                    )
                )
                (result Mover Draw)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Fission is a game where players attempt to eliminate all of their opponents stones but must have at least one stone left on the board in order to win.")
        (rules "TURN - On each turn, each player slides (orthogonally or diagonally) a stone until it hits another stone or a wall.
            
            If it stops because of a stone, the moving stone and all adjacent stones (of either color) are removed.
            
            GOAL - The player with no stones onboard loses.
            
        The game is a draw if (i) the board becomes empty, or (ii) each player has just one stone, or (iii) the moving player has no valid moves.")
        (id "1805")
        (source "<a href=\"https://boardgamegeek.com/boardgame/30213/fission\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />BGG</a>")
        (version "1.3.6")
        (classification "board/war/replacement/eliminate/all")
        (author "Robert A. Kraus")
        (credit "Eric Piette")
        (date "2003")
        }
    )
    
    (graphics {
        (board Style Chess)
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
    })
    
)
