(define "MoveAPiece"
    (if (or
            (= (state at:(from)) 1)
            (and (= (state at:(from)) 0) (= 1 (pips)))
        )
        (move
            Slide
            #1
            (between
                (exact (pips))
                if:(is Empty (between))
            )
            (to
                if:(is Enemy (who at:(to)))
                (apply if:(not (is Friend (who at:(to)))) (remove (to)))
            )
            (then
                (if (= (state at:(last To)) 0)
                    (set State at:(last To) 1)
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Daldosa"
    (players 2)
    (equipment {
        (board
            (add
                (merge {
                    (rectangle 1 8)
                    (shift -4 1.5 (rectangle 1 12) )
                    (shift 0 3 (rectangle 1 8) )
                })
                vertices:{{-1 0.1} {-2 0.3} {-2.9 0.5} {-3.6 0.8} {-1 2.9} {-2 2.7} {-2.9 2.5} {-3.6 2.2}}
            )
            {
            (track "Track1" {31..28 0..7 19..8 35..32 20..27 19..8} loop:True P1)
            (track "Track2" {35..32 20..27 19..8 31..28 0..7 19..8} loop:True P2)
            }
            use:Vertex
        )
        (regions "AllSites" (sites Board))
        (dice d:4 num:2)
        (piece "Minus" P1 ("MoveAPiece" "Track1"))
        (piece "Minus" P2 ("MoveAPiece" "Track2"))
    })
    (rules
        (start {
            (place "Minus1" (sites {0..7 28..31}))
            (place "Minus2" (sites {20..27 32..35}))
        })
        (play
            (do
                (if (not "SameTurn")
                    (roll)
                )
                next:(forEach Die
                    replayDouble:(and (= (face 36) 1) (= (face 37) 1))
                    (forEach Piece
                        (then
                            (if (not (all DiceUsed))
                                (if (can Move (forEach Die (forEach Piece)))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                )
            )
        )
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Daldøsa is a capturing game that is played in the Jæren region of Norway. It is very similar to other games, like Daldøs and Sáhkku, played in the region, which are played in a similar way to games like Tab. The history of the game is unknown, but it appears to have once been popular.")
        (aliases {"Daldøsa"})
        (rules "3x12 board, with the outer rows of holes slightly curved to form the appearance of an arc. Twelve pieces per player, which start on the board in each of the holes in the outer row belonging to a player. Two four-sided dice, marked 2-4 and \"X\" for 1. The throw of double 1s gives the player an extra throw. Players move their pieces according to the throws of the dice, moving one piece the value of one of the dice and another the value of the other die, or one piece the value of both dice. A piece cannot be moved until it is \"activated\" with a throw of 1. When a piece is activated, it is rotated 90 degrees and moved one space. One player moves from left to right in their row into the central row, moving right to left down it and when reaching the end of the central row, proceed into the opponent's row, and move from left to right in it, returning to the central row upon reaching the end. The opponent follows the same track, except moving from right to left in their row. Opponent's pieces are taken when a player's piece lands in the same spot as the opponent's piece. When in the opponent's row, the player may capture as many pieces of the opponent as hypothetically allowed by the dice. Players may not move one of their pieces past one of their other pieces. The player who captures all of the opponent's pieces wins.

        ")
        (source "Næsheim 2001.")
        (version "1.3.0")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin  "This game was played in Norway, from around 1900 to 2001.")
        }
    )

    (graphics {
        (show Edges Hidden)
        (show Symbol "disc" "AllSites" Vertex fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
        (piece Foreground "Die" state:0 image:"star" fillColour:(colour White) scale:0.2)

    })

    (ai
        "Daldosa_ai"
    )
)

