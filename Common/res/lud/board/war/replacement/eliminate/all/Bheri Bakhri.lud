(define "ThrowValue" (count Pips))

(define "SiteToMoveOnTrack" (trackSite Move steps:#1))

(define "CaptureEnemyPiece" 
    (apply 
        if:(is Enemy (who at:(to))) 
        (remove (to))
    ) 
)

(define "ActivatePiece" (set State at:#1 1))

(define "PieceActivated" (!= 0 (state at:#1)))

(define "SiteInHome" (is In #1 (sites Mover "Home")))

(define "CountOwnPiecesOutHome"
    (count Pieces Mover in:(difference (sites Board) (sites Mover "Home")))
)

(define "OnlyOnePieceMovingOutOfHome" 
    (if (not ("SiteInHome" (from)))
        True
        (if ("SiteInHome" ("SiteToMoveOnTrack" ("ThrowValue")))
            True
            (= ("CountOwnPiecesOutHome") 0)
        )
    )	
)

(define "Move"
    (if 
        (and {
            (if ("PieceActivated" (from))
                True
                (= 1 ("ThrowValue")) 
            )
            #1
        })
        (if (and 
                (not (is Friend (who at:("SiteToMoveOnTrack" ("ThrowValue")))))
                ("OnlyOnePieceMovingOutOfHome")
            )
            (move 
                (from)
                (to 
                    ("SiteToMoveOnTrack" ("ThrowValue")) 
                    "CaptureEnemyPiece"
                )
                (then
                    (if (and (not ("PieceActivated" (last To)))  (= 1 ("ThrowValue")))
                        ("ActivatePiece" (last To))
                    )
                )
            )
        )	
    )
)

//------------------------------------------------------------------------------

(game "Bheri Bakhri"
    (players 2)
    (equipment {
        (board
            (rectangle 3 8)
            {
            (track "Track1" "0,E,N1,W,N1,E,S1,W" loop:True P1)
            (track "Track2" "23,W,S1,E,S1,W,N1,E" loop:True P2)
            }
        )
        (dice d:2 from:0 num:4)
        (piece "Marker" Each ("Move"))
        (regions "Home" P1 (sites Bottom))
        (regions "Home" P2 (sites Top))
    })
    (rules 
        (start { 
            (place "Marker1" (sites Bottom))
            (place "Marker2" (sites Top))
        })
        (play 
            (do 
                (roll) 
                next:(if (= 1 ("ThrowValue"))
                    (priority {
                        (forEach Piece
                            ("Move" (is In (from) (sites Mover "Home")))
                        )
                        (forEach Piece)
                    })
                    (forEach Piece)
                )
                (then 
                    (if (= 1 ("ThrowValue"))
                        (moveAgain)
                    )
                )
            )
        )
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Bheri Bakhri (\"Sheep and Goat\") is a capturing game played in the Garhwal region of India. It is plated similarly to Tablan and other games throughout Southwest Asia and North Africa. ")
        (rules "3x8 board. Eight pieces per player, which start in the spaces of the outer rows of the board. Four cowrie shells used as dice, the number of mouths face up being the value of the throw. A throw of 1 grants the player another throw. A player must throw 1 for the first move of each of their pieces. Players may only play with one piece out of the home row at a time and cannot move the next of their pieces until the piece being played has been captured. Throws of 1 must be used to move a piece in the home row, if possible. Pieces move from left to right in the player's home row, then from right to left in the central row, left to right in the opponent's home row, and right to left in the central row. When a piece lands on a space occupied by an opponent's piece, the opponent's piece is captured. The player who captures all of the opponent's pieces wins.")
        (source "Das Gupta 1927: 298-299.")
        (id "765")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1926.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
)

