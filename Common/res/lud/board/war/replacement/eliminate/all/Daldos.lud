(define "MoveAPiece" 
    (if (or
            (= (state at:(from)) 1) 
            (and (= (state at:(from)) 0) (= 1 (pips)))
        )
        (move
            Slide
            #1 
            (between 
                (exact (pips))
                if:(is Empty (between))
            ) 
            (to 
                if:(is Enemy (who at:(to))) 
                (apply if:(not (is Friend (who at:(to)))) (remove (to)))
            )
            (then
                (if (= (state at:(last To)) 0)
                    (set State at:(last To) 1)
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Daldos"
    (players 2)
    (equipment {
        (board 
            (merge {
                (rectangle 1 16)
                (shift -1 1 (rectangle 1 17) )
                (shift 0 2 (rectangle 1 16) )
            })
            {
            (track "Track1" {0..15 32..16 33..48 32..16} loop:True P1)
            (track "Track2" {33..48 32..16 0..15 32..16} loop:True P2)
            }
            use:Vertex
        )
        (regions "AllSites" (sites Board))
        (dice d:4 num:2)
        (piece "Minus" P1 ("MoveAPiece" "Track1"))
        (piece "Minus" P2 ("MoveAPiece" "Track2"))
    })
    (rules 
        (start {
            (place "Minus1" (sites Bottom))
            (place "Minus2" (sites Top))
        })
        (play 
            (do 
                (if (not "SameTurn") 
                    (roll) 
                )
                next:(forEach Die 
                    replayDouble:(and (= (face 36) 1) (= (face 37) 1))
                    (forEach Piece
                        (then 
                            (if (not (all DiceUsed))
                                (if (can Move (forEach Die (forEach Piece)))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                )
            )
        )
        (end (if (no Pieces Next) (result Mover Win)))
    )
)
//------------------------------------------------------------------------------

(option "Variant" <Variant> args:{ <name> } {
    (item "Ostergaard and Gaston"    <>   "")   
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Ostergaard and Gaston (Scholarly)" {
        "Variant/Ostergaard and Gaston"
    })*
    
    (ruleset "Ruleset/Historical Information (Incomplete)" {
        
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Daldøs is a capturing game played in Scandinavia. Though it was on the wane in the nineteenth and twentieth centuries, it had somewhat of a revival when it was sold commercially. It was likely to have been much more widespread, as evidenced by its persistence in regional Danish dialects and its similarity to games games in Norway and Finland. It is remarkably similar to the game Tab, played in North Africa and Southwest Asia.")
        (aliases {"Daldøs"})
        (useFor "Ruleset/Historical Information (Incomplete)" (rules "Three-row board, outer rows with sixteen holes and the central with seventeen. Sixteen pieces per player, which start the board in each of the holes in the outer row belonging to a player. Two four-sided dice. Players move their pieces according to the throws of the dice, moving one piece the value of one of the dice and another the value of the other die, or one piece the value of both dice. Players move from their row into the central row, and when reaching the end of the central row, proceed into the opponent's row. Opponent's pieces are taken when a player's piece lands in the same spot as the opponent's piece. Players may not move one of their pieces past one of their other pieces."))
        (useFor "Ruleset/Historical Information (Incomplete)" (source "DLP evidence."))
        (useFor "Ruleset/Historical Information (Incomplete)" (id "754"))
        (useFor "Ruleset/Ostergaard and Gaston (Scholarly)" (rules "Three-row board, outer rows with sixteen holes and the central with seventeen. Sixteen pieces per player, which start the board in each of the holes in the outer row belonging to a player. Two four-sided dice, marked 2-4 and \"A\" for 1. The throw of double 1s gives the player an extra throw. Players move their pieces according to the throws of the dice, moving one piece the value of one of the dice and another the value of the other die, or one piece the value of both dice. A piece cannot be moved until it is \"activated\" with a throw of 1. When a piece is activated, it is rotated 90 degrees and moved one space. One player moves from left to right in their row into the central row, moving right to left down it and when reaching the end of the central row, proceed into the opponent's row, and move from left to right in it, returning to the central row upon reaching the end. The opponent follows the same track, except moving from right to left in their row. Opponent's pieces are taken when a player's piece lands in the same spot as the opponent's piece. When in the opponent's row, the player my captured as many pieces of the opponent as hypothetically allowed by the dice. Players may not move one of their pieces past one of their other pieces. The player who captures all of the opponent's pieces wins."))
        (useFor "Ruleset/Ostergaard and Gaston (Scholarly)" (source "Østergaard and Gaston 2001."))
        (useFor "Ruleset/Ostergaard and Gaston (Scholarly)" (id "756"))
        (id "756")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin  "This game was played in Scandinavia, from around 1831 to 1927.")
        }
    )
    
    (graphics {
        (show Edges Hidden)  
        (show Symbol "disc" "AllSites" Vertex fillColour:(colour Grey) edgeColour:(colour Black) scale:0.5)
        (piece Foreground "Die" state:0 image:"A" fillColour:(colour White) scale:0.2)
        
    })
    
    (ai
        "Daldos_ai"
    )
)

