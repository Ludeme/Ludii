(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "SiteToMoveOnTrack" 
    (if (= (state at:(from)) 0)
        ("NextSiteOnTrack" ("ThrowValue") "Track")
        (if (= (state at:(from)) 1)
            ("NextSiteOnTrack" ("ThrowValue") "TrackLeft")
            ("NextSiteOnTrack" ("ThrowValue") "TrackRight")
        )
    )
)

(define "UpdateState" 
    (if (and (= (state at:(last To)) 0) (is In (last To) (sites "CentralTrack")))
        (if (or (is Mover P1) (is Mover P2))
            (set State at:(last To) 2)
            (set State at:(last To) 1)
        )
    )
)

//------------------------------------------------------------------------------

(game "Los Palos" 
    (players 4)
    (equipment {
        (board 
            (merge {
                (shift 11.3 -3.7
                    (graph 
                        vertices:{ {0 0} {1.3 -1} {2 0} {1.1 0.6} }
                        edges:{{0 1} {1 2} {2 3} {3 0}}
                    )
                )
                (shift 10.4 -2.4
                    (graph 
                        vertices:{ {0 0} {0.9 -1.3} {2 -0.7} {1 0.4} }
                        edges:{{0 1} {1 2} {2 3} {3 0}}
                    )
                )
                (shift 10.1 -1.1
                    (graph 
                        vertices:{ {0 0} {0.3 -1.3} {1.3 -0.9} {1 0.2} }
                        edges:{{0 1} {1 2} {2 3} {3 0}}
                    )
                )
                (shift 10 0 
                    (graph 
                        vertices:{ {0 0} {0.1 -1.1} {1.1 -0.9} {1 0} }
                        edges:{{0 1} {1 2} {2 3} {3 0}}
                    )
                )
                (shift 11 1.87 
                    (graph 
                        vertices:{ {0 0} {0.5 0.5} {0 1} }
                        edges:{{0 1} {1 2} {2 0}}
                    )
                )
                (shift 10 1.87 
                    (graph 
                        vertices:{ {0 0} {-0.5 0.5} {0 1} }
                        edges:{{0 1} {1 2} {2 0}}
                    )
                )
                (shift 10 1.87 (square 1))
                (shift 10 1 (tri 1))
                (shift 1 2 
                    (graph 
                        vertices:{ {1 0} {-0.5 0.87} {0 0} }
                        edges:{{0 1} {1 2} {2 0}}
                    )
                )
                (shift 0 2 
                    (graph 
                        vertices:{ {-1 0} {0.5 0.87} {0 0} }
                        edges:{{0 1} {1 2} {2 0}}
                    )
                )
                (shift 0 2 (tri 1))
                (shift 0 1 (square 1))
                (rectangle 1 11)
                (shift 0 -1 (rectangle 1 4))
            })
            {
            (track "Track1"  "20,W,N1,E" P1 directed:True)
            (track "Track2"  "11,12,S2,E" P2 directed:True)
            (track "Track3"  "6,E1,9,S1,W" P3 directed:True)
            (track "Track4"  "0,1,2,3,4,W" P4 directed:True)
            (track "TrackRight" "14,E,W" directed:True) // Local state = 2
            (track "TrackLeft" "4,W,E" directed:True) // Local state = 1
        })
        ("StickDice" 5)
        (piece "Marker" Each
            (move
                (from (from))
                (to 
                    ("SiteToMoveOnTrack")
                    if:(or 
                        (and (not (is In (to) (sites "SafeSites"))) ("IsEnemyAt" (to)))
                        (is Empty (to))
                    )
                    (apply (if ("IsEnemyAt" (to))
                            (remove (to))
                        )
                    )
                )
                (then ("UpdateState"))
            )
        )
        (map "Throw" {(pair 0 5) (pair 1 1) (pair 2 2) (pair 3 3) (pair 4 4) (pair 5 5)})
        (map "EntryPoint" {(pair 1 20) (pair 2 11) (pair 3 6) (pair 4 0)})
        (regions "SafeSites" (sites {4 14 23}))
        (regions "CentralTrack" (sites {4 5 14 15 19 21..26}))
    })
    (rules 
        (start {
            (place "Marker1" (mapEntry "EntryPoint" P1) count:3)
            (place "Marker2" (mapEntry "EntryPoint" P2) count:3)
            (place "Marker3" (mapEntry "EntryPoint" P3) count:3)
            (place "Marker4" (mapEntry "EntryPoint" P4) count:3)
            (set Team 1 {P1 P3})
            (set Team 2 {P2 P4})
        })
        (play ("RollMove" (forEach Piece)))
        (end ("CaptureAllTeam"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Los Palos is a capturing game played by the Chinantec people of Oaxaca, Mexico. Played by two teams, two teams move their pieces along a track in an attempt to capture all of the opponent's pieces. ")
        (aliases {"El Juego con Palos"})
        (rules "The main track of the board is a row of eleven squares, with both end squares and the central square marked with an X. On the left side, a row of four squares runs under and adjacent to the first four squares of the central row. Perpendicular to the left end square of the central row, there is a square and then three triangles. On the opposite end, below and perpendicular to the right end of the central track is a row of four squares, curving slightly to the right. Above the end square of the central row, a triangular space, adjacent to an oval divided into three. Two teams with two players on each team. Players each start from a different point: one from the right square in the bottom left row, one from one of the triangle spaces on the left, one from the end of the curving track on the right, and the other from one of the spaces in the oval. Three pieces per player. Five sticks, with a flat side and a curved sides, used as dice. The value of a throw is equal to the number of flat sides that land face up; five curved sides up = 5. Players move their pieces from their entry points onto the central track of the board and progressing to the opposite end of the board from where they started. Pieces must move backward when they reach the end of the central track. When a player's piece lands on a space occupied by an opponent's piece, the opponent's piece is taken. A piece resting on a space marked with an X is safe from capture. The team to successfully capture all of their opponent's pieces wins.")
        (source "Weitlaner and Castro 1973: 189; 191.")
        (id "328")
        (version "1.3.7")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin "This game was played in Mexico, from around 1943 to 1953.")
        }
    )
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (show Line {{26 25} {24 27} {31 40} {39 32} {8 17} {16 9}} scale:2)
    })
    
)
