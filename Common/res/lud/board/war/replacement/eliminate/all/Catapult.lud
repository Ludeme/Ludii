(define "NoncapturingMove"
    (move 
        (from)
        (to 
            (sites Direction
                from:(from)
                Forwards
                stop:(not (is Empty (to)))
                distance:(^ 2 
                    (- 3
                        (count Sites
                            in:(sites Direction
                                from:(from)
                                Forwards
                                stop:(is Mover (who at:(to)))
                                distance:1
                            )
                        )
                    )
                )
            )
        )
    )
)

(define "CapturingMove"
    (move 
        (from)
        (to 
            (sites Direction
                from:(from)
                stop:(is Mover (who at:(to)))
                distance:(^ 2 
                    (- 3
                        (count Sites
                            in:(sites Direction
                                from:(from)
                                Forwards
                                stop:(is Mover (who at:(to)))
                                distance:1
                            )
                        )
                    )
                )
            )
            if:(< 0
                (count Sites
                    in:(intersection
                        (sites Occupied by:Next)
                        (sites Between
                            (directions Cell from:(from) to:(to))
                            from:(from)
                            to:(to)
                            toIncluded:True
                        )
                    )
                )
            ) 
            (apply 
                (remove
                    (sites Between
                        (directions Cell from:(from) to:(to))
                        from:(from)
                        to:(to)
                        toIncluded:True
                    )
                )
            )
        )
    )
)

//-----------------------------------------------------------------------------

(game "Catapult"
    (players {(player N) (player S)})
    (equipment
        {
        (board (rotate 45 (square <Size>)))
        (piece "Disc" Each
            (or 
                ("NoncapturingMove")
                ("CapturingMove")
            )
        )
        }
    )
    (rules
        (start {
            (place "Disc1" 
                (difference
                    (expand (sites Bottom) steps:(- <Size> 3) Orthogonal)
                    (sites Phase (% <Size> 2))
                )
            )
            (place "Disc2"
                (difference 
                    (expand (sites Top) steps:(- <Size> 3) Orthogonal)
                    (sites Phase (% <Size> 2))
                )
            )
        })
        (play
            (forEach Piece)
        )
        (end 
            (if 
                (no Pieces Next) 
                (result Next Loss)
            )
        )
    )
)

(option "Board Size" <Size> args:{ <size> }
    {
    (item "8x8"    <8>  "A size 8 board is currently selected")*
    (item "9x9"    <9>  "A size 9 board is currently selected")
    }
)

//-----------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Catapult is a decisive two-player army game of total annihilation. It was invented by Alek Erickson and Michael Amundsen in January 2024.")
        (rules "The goal of Catapult is to capture all your opponent's pieces.
            Starting with light, players move one of their pieces each turn. 
            If you cannot make a legal move, you must pass. 
            Pieces move in a straight line, up to its range, passing only enemy or empty spaces, and capturing all passed enemies, including one landed on. 
            Each piece has default range of 1. Range is doubled for each friendly piece or wall adjacent to it along the three forward directions. 
            Non-capturing movements (a move that only passes and lands on empty spaces) must be in one of the three forward directions, towards the opposite corner. 
        Capturing movements (a move that lands on or passes through 1 or more opponent pieces) may be in any of the eight orthogonal or diagonal directions.")
        (id "4210")
        (version "1.3.14")
        (classification "board/war/replacement/eliminate/all")
        (author "Alek Erickson and Michael Amundsen")
        (credit "Michael Amundsen")
        (date "01-2024")
        }
    )
    (graphics {
        (player Colour P1 (colour White))
        (player Colour P2 (colour Black))
        (board Style Chess)
    })
)
