(define "ThrowValue" (mapEntry "Throw" (count Pips)))

(define "SpecialThrows" 
    (is In ("ThrowValue") (sites {1 4 6}))
)

(define "CaptureEnemyPiece" 
    (apply 
        if:("IsEnemyAt" (to)) 
        (remove (to))
    ) 
)

(define "ActivatePiece" (set State at:#1 1))

(define "PieceActivated" (!= 0 (state at:#1)))

(define "Tab" (= 1 ("ThrowValue")))

(define "CountOwnPiecesInHome"
    (count Pieces Mover in:(sites Mover "Home"))
)

(define "Move"
    (if 
        (and {
            (if ("PieceActivated" (from))
                True
                ("Tab") 
            )
            #1
        })
        (if (is In (from) (sites Mover "Home"))
            (if (not ("IsFriendAt" ("NextSiteOnTrack" ("ThrowValue") "Track")))
                (move 
                    (from)
                    (to 
                        ("NextSiteOnTrack" ("ThrowValue") "Track")
                        "CaptureEnemyPiece"
                    )
                    (then
                        (if (and (not ("PieceActivated" (last To))) (= 1 ("ThrowValue")))
                            ("ActivatePiece" (last To))
                        )
                    )
                )
            )
            (if (not ("IsFriendAt" ("NextSiteOnTrack" ("ThrowValue") "MiddleTrack")))
                (move 
                    (from)
                    (to 
                        ("NextSiteOnTrack" ("ThrowValue") "MiddleTrack")
                        "CaptureEnemyPiece"
                    )
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Kioz"
    (players 2)
    (equipment {
        (board 
            (rectangle 4 22)
            {
            (track "Track1" "0,E,N1,W," P1 directed:True)
            (track "Track2" "87,W,S1,E" P2 directed:True)
            (track "MiddleTrack" "44,E,S1,W" loop:True)
            }
        )
        ("StickDice" 4)
        (piece "Marker" Each)
        (piece "King" Each)
        (map "Throw" {(pair 0 4) (pair 1 1) (pair 2 2) (pair 3 2) (pair 4 6)})
        (regions "Home" P1 (sites Bottom))
        (regions "Home" P2 (sites Top))
    })
    (rules 
        (start { 
            (place "Marker1" (intersection (sites Bottom) (expand (sites Left) steps:17)))
            (place "King1" (intersection (sites Bottom) (expand (sites Right) steps:3)))
            (place "Marker2" (intersection (sites Top) (expand (sites Right) steps:17)))
            (place "King2" (intersection (sites Top) (expand (sites Left) steps:3)))
        })
        (play 
            ("RollMove"
                (if ("Tab")
                    (priority {
                        (forEach Piece ("Move" (not ("PieceActivated" (from)))))
                        (forEach Piece ("Move"))
                    })
                    (forEach Piece ("Move"))
                )
                (then 
                    (if ("SpecialThrows")
                        (moveAgain)
                    )
                )
            )
        )
        (end ("CaptureAll" Next))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Kiôz is a capturing game played in the Levant in the seventeenth century. It is closely related to the game of Tab, which is still played in the region.")
        (aliases {"Kiôz" "Djôz"})
        (rules "4x22 board. 22 pieces per player, four kings and eighteen regular pieces. Kings have no difference in movement or power than regular pieces. Moves are determined by four two-sided sticks, black on one side and white on the other. The moves are determined by the number of white sides that land face up: 1= a move of 1. A throw of 1 is known as \"tab.\" 2= a move of 2, 3=2, 4=6, 0=4. Throws of 1, 4, and 6 grant the player an additional throw. To begin, a player must roll tab to move their first piece, and every tab after that must be used to move a piece which has not moved yet. Pieces move from left to right in the home row, then circulate in a clockwise direction in the central two rows. Multiple pieces cannot occupy the same space. When a player's piece lands on a space occupied by an opponent's piece, that piece is captured. The player to capture all of the opponent's pieces wins.")
        (source "Murray 1951: 98; Depaulis 2001: 59.")
        (id "167")
        (version "1.3.9")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin "This game was played in Levant, around 1694.")
        }
    )
    
    (graphics {
        (piece Colour "Die" state:0 fillColour:(colour Black))
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
    })
    
    (ai
        "Kioz_ai"
    )
)

