(define "NoFriendlyAdjacent"
    (= 
        0
        (count Sites in:(sites Around (to) Own))
    )
)

(define "FriendlyAdjacent"
    (< 
        0
        (count Sites in:(sites Around (to) Own))
    )
)

(define "Captures"
    (and
        (all Sites 
            (sites Around (sites Group at:(last To)) Enemy)
            if:(>
                (size Group at:(last To))
                (size Group at:(site))
            )	
        )
        (< 
            0
            (count Sites 
                in:(sites Around (sites Group at:(last To)) Enemy)
            )
        )
    )
)

(define "RemoveGroups"
    (forEach Site
        (sites Around
            (sites Group at:(last To))
            Enemy
        )
        (remove
            (sites Group
                at:(site)
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Oust"
    (players 2)
    (equipment
        {
        (board <Board>)
        (piece "Disc" Each)
        }
    )
    (rules
        (play
            (or
                (move Add
                    (to (sites Empty)
                        if:"NoFriendlyAdjacent"
                    )
                )
                (do
                    (move Add
                        (to (sites Empty)
                            if:"FriendlyAdjacent"
                        )
                    )
                    ifAfterwards:"Captures"
                    (then
                        (and
                            ("RemoveGroups")
                            (moveAgain)
                        )
                    )
                )
            )
        )
        (end
            (if
                (and 
                    (< 2 (count Moves))
                    (=
                        0
                        (count Sites
                            in:(sites Occupied by:Enemy)
                        )
                    )
                )
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <size> }
    {
    (item "5x5"    <(hex 5)>    "The game is played on a size 5 board")   
    (item "7x7"    <(hex 7)>    "The game is played on a size 7 board")*   
    (item "9x9"    <(hex 9)>    "The game is played on a size 9 board")   
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "A simple, drawless, and finite annihilation game where groups only grow by capturing.")
        (rules "Oust is a game for two players, played on a hexagonally patterned board which is initially empty. The two players, Black and White, take turns placing stones of their color onto unoccupied cells on the board. A group is a set of interconnected, like-colored stones. A group can be a singleton, a single stone which is not connected to any other stones of its color. A group includes all the stones of its own color connected to it. There are two types of moves - non-capturing placements and capturing placements. Non-capturing placements either forms no connections (adjacencies) with any stones, or forms one or more connections with only enemy stones. A non-capturing placement does not form any connection with stones of its own color. Making a non-capturing placement concludes your turn. When you place a stone which forms one or more connections with your own groups, you will create a new, larger group of your own stones. You can only make such a placement if said new group will have one or more connections with enemy groups upon its creation and if all said enemy groups are smaller than said new group. Upon making such a placement, all said enemy groups are removed from the board. After capturing one or more enemy groups and while it is still your turn, you must continue to add stones until you make a non-capturing placement, at which time your turn is concluded. If you have a placement available on your turn you must make one. If you don't have any placements available, you must pass your turn. There will always be a placement available for at least one player. You win by making a placement which captures all of the enemy stones on the board.")
        (id "1107")
        (source "<a href=\"http://www.marksteeregames.com/Oust_rules.pdf\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Mark Steere Games</a>")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (author "Mark Steere")
        (credit "Michael Amundsen")
        (date "2007")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour DarkBlue))
        (piece Colour P1 
            fillColour:(colour Red) 
            strokeColour:(colour 0 0 0 0)
        )
        (piece Colour P2 
            fillColour:(colour DarkBlue) 
            strokeColour:(colour 0 0 0 0)
        )
        (show Symbol "disc" Cell (sites Board)
            fillColour:(colour VeryLightGrey) 
            edgeColour:(colour Black) 
        )
        (region Colour (sites Board) (colour 0 0 0 0))
        (board Colour InnerEdges (colour 0 0 0 0))
        (board Colour OuterEdges (colour 0 0 0 0))
    })
    
    (ai
        "Oust_ai"
    )
)

