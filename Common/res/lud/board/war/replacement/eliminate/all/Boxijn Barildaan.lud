
(define "StepMove" 
    (move
        Step 
        #1
        (to 
            if:(and 
                (= 0 (state at:(to)))
                (if (< (count MovesThisTurn) 2)
                    (is Empty (to))
                    (is In (to) (union (sites Occupied by:Next) (sites Empty)))
                )
            )
            (apply (if ("IsEnemyAt" (to)) (remove (to))))
        )
        (then 
            (if (= 2 (count Sites in:(forEach  (sites Board) if:(= 1 (state at:(site))))))
                (forEach Site (sites Board) (if (= 1 (state at:(site))) (set State at:(site) 0)))
                (and
                    (set State at:(last From) 1)
                    (moveAgain)
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Boxijn Barildaan"
    (players 2)
    (equipment {
        (board 
            (merge {
                (shift 1 0 (rectangle 4 2))
                (shift 0 1 (rectangle 2 4))
            })
            use:Vertex
        )
        (piece "Marker" Each ("StepMove"))
    })
    (rules
        (start {
            (place "Marker1" (expand (sites Bottom)))
            (place "Marker2" (expand (sites Top)))
        })
        (play 
            (if ("SameTurn")
                ("StepMove" (from (last To)))
                (forEach Piece)
            )
        )
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Böxijn Barildaan (the struggle) is a capturing game played in Mongolia.  ")
        (aliases {"Böxijn Barildaan"})
        (rules "Four squares, arranged in a cross shape. The game is played along the lines. Four pieces per player, which begin on the corners of a square, opposite the square where the opponent's pieces are arranged. Players alternate turns moving their pieces. Pieces move three spaces along the lines on the board, capturing any piece on the third. The first two spaces in the move must be empty. Pieces may change direction in a turn, as long as the lines are followed and there is no backtracking. The player who captures all of the opponent's pieces wins.")
        (source "Popova 1974: 34, 36-37.")
        (id "1040")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1966.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        
    })
    (ai
        "Boxijn Barildaan_ai"
    )
)
