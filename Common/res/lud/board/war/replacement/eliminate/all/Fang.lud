(define "SurroundedDirections" 
    (and {
        (is In (last To) (sites {(ahead (site) #1) (ahead (site) #2) (ahead (site) #3) (ahead (site) #4) }))
        (is Mover (who at:(ahead (site) #1)))
        (is Mover (who at:(ahead (site) #2)))
        (is Mover (who at:(ahead (site) #3)))
        (is Mover (who at:(ahead (site) #4)))
    })		
)

(define "HowManySquareSurroundedEmptyPoint"
    (count Sites
        in:(forEach (sites Around (last To))
            if:(if (is Empty (site))
                (or
                    ("SurroundedDirections" N S W E)
                    ("SurroundedDirections" NE SE SW NW)
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Fang"
    (players 2)
    (equipment {
        (board 
            (add
                (square 7)
                edges:{{16 24} {24 32} {30 24} {24 18}}
            )
            use:Vertex
        )
        (piece "Marker" Each
            (move Step 
                (to if:(is Empty (to)))
                (then 
                    (if (> ("HowManySquareSurroundedEmptyPoint") 0)
                        (and
                            (set Value Mover "HowManySquareSurroundedEmptyPoint")
                            (moveAgain)
                        )
                    )
                )
            )
        )
        (hand Each)
    })
    
    (rules
        (start (place "Marker" "Hand" count:24))
        
        phases:{
        (phase "FirstPlacement" P1
            (play (move (from (handSite Mover)) (to (sites Centre))))
            (nextPhase "Placement")	
        )
        (phase "Placement" 
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase Mover ("HandEmpty" Mover) "Playing")	
        )
        (phase "Playing" 
            (play
                (if ("SameTurn")
                    (move
                        Remove 
                        (sites Occupied by:Next) 
                        (then 
                            (if (> (value Player Mover) 1)
                                (and 
                                    (moveAgain)
                                    (set Value Mover (- (value Player Mover) 1))
                                )
                                (set Value Mover 0)
                            )
                        )
                    )
                    (forEach Piece)
                )
            )
        )
        }
        
        (end (if (no Pieces Next) (result Mover Win)))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Fang is a game played by the Uighur people of the Xinjiang Uighur Autonomous Region of China. It incorporates principles of alignment and territory games. ")
        (rules "7x7 board, with diagonals in the square formed by the central 3x3 lines. 24 pieces per player. Black plays first. Players alternate turns placing a piece on one of the empty spots on the board. The first stone must be placed in the central spot. They attempt to orthogonally surround an empty point with four of their pieces, making a square. When all of the pieces are placed, players alternate turns removing one of their opponent's pieces from the board. They then move one of their pieces along the lines of the board to the place left vacant by the piece they removed. When a player makes a new square, they may remove another of the opponent's pieces from the board. Pieces that are in a square around an empty point may not be captured, and captures are not made in the placement phase. The player who captures all of the opponent's pieces wins.")
        (source "Masukawa 2002: 33-36.")
        (id "1134")
        (version "1.3.4")
        (classification "board/war/replacement/eliminate/all")
        (credit "Eric Piette")
        (origin "This game was played in Xinjiang, from around 1992 to 1997.")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Fang_ai"
    )
)
