(define "ThrowValue" (mapEntry (count Pips)))
(define "ActivatePiece" (set State at:(last To) 0))
(define "ActivatedPiece" (= (state at:(from)) 0))
(define "InactivePiece" (= (state at:(from)) 1))
(define "AllowToActivate" (= ("ThrowValue") 1))
(define "AllowToReplay" 
    (and
        (is In ("ThrowValue") (sites {1 4 6 5}))
        (!= 5 (count Pips))
    )
)

//------------------------------------------------------------------------------

(game "Issiren"
    (players 2) 
    (equipment { 
        (board 
            (rectangle 4 12)
            {
            (track "Track1" "11,W,N1,E,N1,W,N1,E,S1,W,S1,E" loop:True P1)
            (track "Track2" "36,E,S1,W,S1,E,S1,W,N1,E,N1,W" loop:True P2)
            }
        )
        ("StickDice" 6)
        (piece "Marker" Each
            (move 
                (from
                    (from)
                    if:(if ("ActivatedPiece") 
                        True
                        ("AllowToActivate")
                    )
                )
                (to
                    ("NextSiteOnTrack" ("ThrowValue"))
                    if:(not ("IsFriendAt" (to)))
                    (apply
                        (if ("IsEnemyAt" (to))
                            (remove (to))
                        )
                    )
                )
                (then 
                    (if (= 1 (state at:(last To)))
                        ("ActivatePiece")
                    )
                )
            )
        )
        (map {(pair 0 4) (pair 1 5) (pair 2 2) (pair 3 3) (pair 4 1) (pair 5 5) (pair 6 6)})
    }) 
    
    (rules 
        (start { 
            (place "Marker1" (sites Bottom) state:1)
            (place "Marker2" (sites Top) state:1)
        })
        
        (play 
            ("RollMove" 
                (forEach Piece)
                (then
                    (if ("AllowToReplay") 
                        (moveAgain)
                    )
                )
            )
        )
        
        (end ("CaptureAll" Next))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Issiren is a capturing game with dice played in the Illizi region of Algeria by the Kel Ajjer Tuareg people. ")
(aliases {"Ishigh√§n"})
(rules "4x12 board. Twelve pieces per player, which begin one in each space in the row closest to the player. Six stick dice, green on one side and white on the other. The value of the throws equals the number of green faces, with the following exceptions: four green faces up allows a piece to be moved for the first time and a move of 1, plus another throw is granted; six white faces = 4 plus another throw; six green = 6 plus another throw; five white faces = move 5 plus another throw. Pieces move from right to left in the player's home row, then into the second row, moving left to right, then into the third row moving right to left, and then into the opponent's home row moving left to right, then back into the third row moving right to left, to the second row moving left to right, and then back into the player's home row moving right to left. If a player's piece lands on a space occupied by one of the opponent's pieces, the opponent's piece is captured. The player who captures all of the opponent's pieces wins.")
(source "Bellin 1963: 73-74.")
(id "1168")
(version "1.3.9")
(classification "board/war/replacement/eliminate/all")
(credit "Eric Piette")
(origin "This game was played in Algeria, around 1960.")
}
)
    
    (graphics {
        (board Colour Phase0 (colour 223 178 110))
        (board Colour InnerEdges (colour Black))
        (board Colour OuterEdges (colour Black))
        (piece Colour "Die" state:1 fillColour:(colour Green))
    })
    
    (ai
        "Issiren_ai"
    )
)
