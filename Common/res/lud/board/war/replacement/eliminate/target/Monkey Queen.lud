(game "Monkey Queen"
    (players 2)
    (equipment {
        (board (square 12))
        (piece "Marker"
            (or {
                (move Slide
                    (to 
                        if:("IsEnemyAt" (to)) // Capturing Queen Move and Capturing Baby Monkey Move
                        (apply 
                            if:("IsEnemyAt" (to))
                            (remove (to) count:(size Stack at:(to)))
                        )
                    )
                    stack:True
                    (then
                        (if (< 1 (size Stack at:(last To)))
                            (set Value Mover (last To))
                        )
                    )
                )
                (if (< 2 (size Stack at:(from))) // Non capturing Queen Move
                    (move Slide
                        stack:True
                        (then
                            (and
                                (fromTo
                                    (from (last To) level:0)
                                    (to (last From))
                                )
                                (set Value Mover (last To))
                            )
                        )
                    )
                )
                (if (= 1 (size Stack at:(from))) // Non capturing Baby Monkey Move
                    (move Slide
                        (between 
                            if:(and
                                (< (count Steps (to) (value Player Next)) (count Steps (from) (value Player Next)))
                                (is Empty (between))
                            )
                        )
                    )
                )
            })
        )
    })
    (rules
        (meta (swap))
        (start {
            (place Stack "Marker1" 6 count:20)
            (place Stack "Marker2" 137 count:20)
        })
        (play (forEach Piece top:True))
        (end 
            (if 
                (or
                    (all Sites (sites Occupied by:Next) if:(= 1 (size Stack at:(site))))
                    (no Moves Next) 
                )
                (result Mover Win)
            )
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Monkey Queen is a two player game, played on a 12x12 checkboard.")
        (rules "The two players take turns moving stacks of their own color (including singletons - stacks of height one), one stack per turn.  Players will always have a move available and must make one.
            
            OBJECT OF THE GAME: Kill the enemy queen, or deprive your opponent of moves. 
            
            The pie rule is used in Monkey Queen. 
            
            Each player will always have exactly one queen monkey on the board which is a stack of two or more checkers all of the player's own color.  Additionally, each player may have one or more baby monkeys on the board which are singletons of his own color.
            
            CAPTURING QUEEN MOVES - A monkey queen captures exactly like a Chess queen.  Slide the entire stack in any direction (horizontally, vertically or diagonally) along a straight sequence of unoccupied squares terminated by an enemy occupied square, and capture the enemy queen or baby by replacement.
            
            NOTE: A queen may not give birth to its own baby and kill an enemy baby in the same move.
            
            NOTE: A queen of height two may not make a non-capturing move.
            
            NON-CAPTURING QUEEN MOVES - When not capturing, a queen moves any distance in any one direction, exactly like a Chess queen, except it leaves its bottom checker behind on the originating square, reducing the stack height by one.  The queen monkey has thus given birth to a baby monkey.
            
            CAPTURING BABY MOVES - A monkey baby, like a monkey queen, captures exactly like a Chess queen.  Slide the baby (singleton) in any direction along a straight sequence of unoccupied squares terminated by an enemy occupied square, and capture the enemy queen or baby by replacement.
            
            NON-CAPTURING BABY MOVES - When not capturing, a baby must move toward the enemy queen in the following sense:  The straight line distance between your baby and the enemy queen must be shortened by your move.
            
        NOTE: There's no requirement to make a kill in Monkey Queen. ")
        (id "1765")
        (source "<a href=\"http://www.marksteeregames.com/Monkey_Queen_rules.html\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.marksteeregames.com</a>")
        (version "1.3.9")
        (classification "board/war/replacement/eliminate/target")
        (author "Mark Steere")
        (publisher "<a href=\"http://www.marksteeregames.com/Monkey_Queen_rules.html\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />www.marksteeregames.com</a>")
        (credit "Eric Piette")
        (date "2011")
        }
    )
    (graphics {
        (player Colour P2 (colour Brown))
        (board Style Chess)
        (board Colour Phase0 (colour 210 181 141))
        (board Colour Phase1 (colour 250 238 216))
        (board Background image:"square.svg" fillColour:(colour 250 238 216) edgeColour:(colour 250 238 216) scale:1.2)
        (stackType Count)
    })
)
