(game "Three Musketeers"
    (players 2)
    (equipment {
        (board (square 5))
        (piece "Musketeer" P1 
            (move Step Orthogonal (to if:("IsEnemyAt" (to)) (apply (remove (to)))))
        )
        (piece "Enemy" P2 ("StepOrthogonalToEmpty"))
    })
    (rules
        (start {
            (place "Musketeer1" {"A1" "C3" "E5"})
            (place "Enemy2" (difference (sites Board) (sites {"A1" "C3" "E5"})))
        })
        (play (forEach Piece))
        (end {
            (if (no Moves P1) (result P1 Win))
            (if 
                (and 
                    (is Mover P1)
                    (is Line 3 Orthogonal contiguous:False)
                )
                (result P2 Win)
            )
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Invented in 1969 by Haar Hoolim and published in Sid Sackson's A Gamut of Games.")
(rules "Played on a 5x5 board. There are 3 'Musketeer' pieces and 22 'Enemy' pieces. Musketeers can only move by capturing enemies on any adjacent space. Enemies can only move to empty adjacent spaces. Enemies win if all three musketeers are ever on the same line or column. Musketeers win as soon as they run out of legal moves.")
(id "1242")
(source "<a href=\"https://en.wikipedia.org/wiki/Three_Musketeers_(game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(version "1.3.7")
(classification "board/hunt")
(author "Haar Hoolim")
(credit "Fabio Barbero")
(date "1969")
}
)
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
    })
    (ai
        "Three Musketeers_ai"
    )
)
