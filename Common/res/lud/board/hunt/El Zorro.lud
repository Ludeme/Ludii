(game "El Zorro"  
    (players {(player N) (player S)})  
    (equipment { 
        ("CrossBoard" 3 7 use:Vertex diagonals:Alternating)
        (piece "Fox" P1 (or ("HopCapture") "StepToEmpty"))
        (piece "Hen" P2 ("StepToEmpty" (directions {Rightward Leftward Forwards}))) 
    })  
    (rules 
        (start { 
            (place "Hen2" (union (sites Top) (expand  (sites Row 4))))
            (place "Fox1" (intersection (union (sites Column 4) (sites Column 2)) (sites Bottom)))
        })
        
        (play (forEach Piece))
        
        (end {
            (if
                (all Sites (expand (sites Bottom) steps:2) if:(= (who at:(site)) P2))
                (result P2 Win)
           )
            (if (no Pieces P2) (result P1 Win))
        }) 
   )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "El Zorro is a hunt game played on the island of La Palma in the Canary Islands since at least the early twentieth century. It was typically played on lines etched in stone or on the ground, and the pieces were pebbles or corn kernels.")
        (rules "Five squares which are each divided with lines connecting the midpoints of their sides and diagonals. The five squares are arranged in a cross-shaped board. One player plays as two foxes the other as twenty hens. The hens begin on the twenty spaces on one half of the board, the foxes on the bottom two corners of the square on the opposite arm of the cross. The foxes may move in any direction, and hop over a hen to an empty space immediately adjacent on the opposite side of the hen along the lines of the board to capture. The hens may not move backward. The foxes win by capturing all the hens, the hens win by occupying all of the spaces in the square of the arm of the cross opposite from where they began.")
        (source "Espinel Cejas and González García 1994: 194-196.")
        (id "1943")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Canary Islands, from around 1923 to 1994.")
        }
   )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
    })
    
)
