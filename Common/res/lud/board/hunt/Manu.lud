(game "Manu"  
    (players 2)  
    (equipment { 
        (board 
            (merge 
                (shift 0 2 (rectangle 3 7 diagonals:Alternating) )
                (shift 2 0 (rectangle 7 3 diagonals:Alternating) ) 
            )
            use:Vertex
        )
        (hand P1)
        (piece "Stick" P1 (or ("HopCapture") "StepToEmpty" ) )
        (piece "Marker" P2 "StepToEmpty") 
    })  
    (rules 
        (start { 
            (place "Marker2" (union (expand (sites Bottom)) (sites Row 2) ) )
            (place "Stick1" (handSite P1))
        })
        
        phases:{ 
        (phase "Placement" P1 
            (play 
                (move 
                    (from (handSite P1)) 
                    (to (sites Empty))
                )
            ) 
            (nextPhase ("HandEmpty" P1) "Movement")
        )
        (phase "Movement" (play (forEach Piece)))
        }
        
        (end {
            ("NoMoves" Loss)
            (if (no Pieces P2) (result P1 Win))
        }) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Manu is a hunt game played in Hawaii in the nineteenth century. ")
        (rules "Played on cross-shaped board, made of five squares, each divided into four squares and with the diagonals of the larger squares drawn. One player plays with thirteen pieces situated on every point of one arm of the cross and along the line immediately perpendicular to that arm. The other plays as the Punipeki, which is placed on any empty spot on the first turn. Pieces move along the lines to an adjacent empty spot. The Punipeki may jump over an adjacent piece to capture it. The Punipeki wins if it can capture all the opponent's pieces, the other player wins by blocking the Punipeki from moving.")
        (source "Culin 1899: 244-245.")
        (id "1109")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Hawaii, around 1899.")
        }
    )
    
    (graphics {
        (board Style Graph)
        (player Colour P1 (colour VeryDarkGrey))
        (player Colour P2 (colour White))
        
        }
    )
    
    (ai
        "Manu_ai"
    )
)
