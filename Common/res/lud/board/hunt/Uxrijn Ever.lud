(game "Uxrijn Ever"
    (players 2)
    (equipment {
        (board 
            (graph 
                vertices:{
                {0 0} {5 0}
                {0.17 1} {4.65 1}
                {0.3 2} {4.3 2}
                {0.38 3} {3.9 3}
                {0.25 4} {3.5 4}
                {-0.13 5} {2.97 5}
                {-0.75 6} {2.2 6}
                {-1.47 7} {0.95 7}
                {-2.22 8} 
                {-3 9} 
                }
                edges:{
                {0 1} {0 2} {2 4}
                {4 6} {6 8} {8 10}
                {10 12} {12 14} {14 16}
                {16 17} {1 3} {3 5}
                {5 7} {7 9} {9 11}
                {11 13} {13 15} {1 2}
                {2 3} {3 4} {4 5}
                {5 6} {6 7} {7 8}
                {8 9} {9 10} {10 11}
                {11 12} {12 13} {13 14} 
                {14 15} {15 16} {15 17} 
                }
            )
            use:Vertex
        )
        (piece "Cow" P1)
        (piece "Calf" P1    
            (move Step 
                (to 
                    if:(or (= (what at:(to)) (id "Cow" P1)) (is Empty (to)))
                    (apply (remove (to)))
                )
            )
        )
        (piece "Human" P2   
            "StepToEmpty"
        )
    })
    (rules
        (start {
            (place "Cow1" 0)
            (place "Calf1" 3)
            (place "Human2" (sites {1 2}))
        })
        (play (forEach Piece))
        (end {
            (if (is In (where "Calf" P1) (sites Top)) (result P2 Win))
            (if (= 1 (count Sites in:(sites Occupied by:P1))) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Üxrijn Ever is a game played in Mongolia. The game is thought to be a representation of cattle herding practices. One player plays as a cow and a calf, the other as two children. The calf attempts to reach its mother, which remains at the start, while the children try to force the calf into the pasture, at the opposite end of the board.")
        (aliases {"Üxrijn Ever" "Ever Bukh"})
        (rules "Two arcs which meet, in the shape of a horn, curving to the left. Nine points along the left side, and eight along the right, with one at the apex. Zig-zag lines connect points  on either side of the board, connecting the first on the left to the first on the right, the first on the right to the second on the left, the second on the left to the second on the right, and so on. One player plays as the cow and the calf (black). The cow begins on the first point on the left, the calf on the second point on the right. The other player plays as two children (white), which begin on the second point on the left and the first point on the right. Players alternate turns moving a piece to an empty adjacent spot along the lines on the board. The cow never moves from its position. The calf plays first. The calf wins when it reaches the cow; the children win when they force the calf onto the apex of the board (the pasture).")
        (source "Popova 1974: 20-21.")
        (id "1015")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Mongolia, from around 1963 to 1991.")
        }
    )
    
    (graphics {
        (player Colour P1 (colour VeryDarkGrey))
        (player Colour P2 (colour White))
        (piece Scale "Calf" 0.5)
        (piece Rename piece:"Calf" "Cow")
        (piece Families {"Abstract" "Themed"})
        (show Line {{1 17}} curve:{0.7 0.7 0.7 0.7} Bezier)
        (show Line {{0 17}} curve:{0.47 0.47 0.47 0.47} Bezier)
        (show Line {{1 3} {3 5} {5 7} {7 9} {9 11} {11 13} {13 15} {15 17}} (colour Hidden))
        (show Line {{0 2} {2 4} {4 6} {6 8} {8 10} {10 12} {12 14} {14 16} {16 17}} (colour Hidden))
        (no sunken)
    })
    
    (ai
        "Uxrijn Ever_ai"
    )
)
