(game "Sher Bakar"
    (players 2)
    (equipment {
        ("AlquerqueBoard" 5 5)
        (piece "Goat" P1 
            (move Step 
                (to if:(or (and (is In (to) (sites "StartingSitesGoats")) (is Friend (who at:(to)))) (is Empty (to))))
            )
        )
        (piece "Tiger" P2
            (or
                ("StepToEmpty")
                ("HopSequenceCapture")
            )
        )
        (regions "StartingSitesGoats" (sites {"B2" "D2" "D4" "B4"}))
    })
    (rules 
        (start {
            (place "Tiger2" (sites {"C1" "C5"}))
            (place Stack "Goat1" (sites {"B2" "D2" "D4"}) count:5)
            (place Stack "Goat1" (sites {"B4"}) count:4)
        })
        
        (play 
            (if	
                ("SameTurn")
                (or
                    (move Pass)
                    ("HopSequenceCaptureAgain")
                )
                (forEach Piece top:True)
            )
            
        )
        
        (end { 
            (if (no Moves P2) (result P1 Win))
            (if (no Pieces P1) (result P2 Win))
        })  
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Sher Bakar is a hunt game played in Mianwali, Pakistan. It begins with four stacks of goats and two tigers, and is similar to Bagh Guti. It is played by all ages and genders.")
        (rules "5x5 board, played on intersections, with diagonals for each quadrant of the board. One player plays with two tiger pieces, placed on the midpoints of two opposite sides. The other player plays with nineteen goats, divided into three stacks of five and one stack of four, placed in the center of each quadrant. The goats move first. Goats may move one at a time to any adjacent vacant spot. More than one goat can be placed on the goats' starting spots, but not elsewhere. The tiger moves in the same manner, but also may capture a piece by hopping over it. Multiple captures can be made on the same turn with subsequent hops, but only the top goat in a stack is captured when a tiger leaps over it. The goal of the goats is to surround the tigers so they cannot move. The goal of the tigers is to capture all the goats.
        ")
        (source "Das-Gupta 1926a: 145-146.")
        (id "712")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in South Asia, around 1922.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        
    })
    
)

