(game "Gioco dell'Orso"
    (players 2)  
    (equipment { 
        (board
            (add
                (concentric {1 4 12})
                edges:{{6 8} {9 11} {12 14} {5 15}}
            )
            use:Vertex
        )
        (piece "Human" Each ("StepToEmpty"))
        (piece "Bear" Each ("StepToEmpty"))
        (hand Each)
    })  
    
    (rules 
        (start {
            (place "Human1" (handSite P1) count:3)
            (place "Bear2" (sites Centre))
        })	
        
        phases:{
        (phase "PlacementP1" 
            (play
                (move 
                    (from (handSite P1))
                    (to (intersection (sites Empty) (sites {1..4})))
                    (then (if (not ("HandEmpty" P1)) (moveAgain)))
                )
            )
            (nextPhase ("HandEmpty" P1) "HuntingP2")
        )
        (phase "HuntingP2" 
            (play 
                (forEach Piece
                    (then 
                        (if (not 
                                (can Move 
                                    (step 
                                        (from (where "Bear" P2))
                                        (to if:(is Empty (to)))
                                    )
                                )
                            )
                            (and {
                                (addScore P2 (- (count Moves) 3))
                                (remove (sites Occupied by:All container:"Board"))
                                (add (piece "Bear1") (to (sites Centre)))
                                (add (piece "Human2") (to (handSite P2)) count:3)
                            })
                        )
                    )
                )
            )
            (nextPhase (= 1 (count Sites in:(sites Occupied by:All container:"Board"))) "PlacementP2")
        )
        (phase "PlacementP2" 
            (play
                (move 
                    (from (handSite P2))
                    (to (intersection (sites Empty) (sites {1..4})))
                    (then (if (not ("HandEmpty" P2)) (moveAgain)))
                )
            )
            (nextPhase ("HandEmpty" P2) "HuntingP1")
        )
        (phase "HuntingP1" 
            (play 
                (forEach Piece
                    (then 
                        (if (not 
                                (can Move 
                                    (step 
                                        (from (where "Bear" P1))
                                        (to if:(is Empty (to)))
                                    )
                                )
                            )
                            (addScore P1 (- (count Moves) (+ (score P2) 6)))
                        )
                    )
                )
            )
            (end (if (!= 0 (score P1)) (byScore)))
        )
        }
        
    )		
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Gioco dell'Orso is a game that was played in Piedmont, Northern Italy. It was documented in the twentieth century, but graffiti in the region suggests that the game could be much older.")
        (rules "Two concentric circles, with four radii from the outer circle to in the center, dividing the circles into four equal parts. There are four arcs, each of which bisects a radius between where each radius intersects the circumference of each circle, the arc also intersecting with the outer circle's circumference. One player plays as a bear, which begins on the central point, the other as three hunters, which begin on any three points on the inner circle. The bear plays first. Players alternate turns moving a piece to an empty adjacent spot along the lines. When the bear is unable to move, the game ends and the players play again, switching sides. The player who lasts longest while playing as the bear wins.")
        (source "Depaulis and Gavazzi 1999: 47.")
        (version "1.2.8")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Italy, from around 1910 to 1994.")
        }
    )
    (graphics {
        (show Line {{6 8}} curve:{0.2 0.6 0.2 0.4} Bezier)
        (show Line {{9 11}} curve:{0.4 0.2 0.6 0.2} Bezier)
        (show Line {{12 14}} curve:{0.8 0.4 0.8 0.6} Bezier)
        (show Line {{5 15}} curve:{0.4 0.8 0.6 0.8} Bezier)
        
    })
    
    (ai
        "Gioco dell'Orso_ai"
    )
)
