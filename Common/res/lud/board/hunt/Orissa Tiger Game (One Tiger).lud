(game "Orissa Tiger Game (One Tiger)" 
    (players 2)
    (equipment { 
        (board 
            (rotate 180 ("AlquerqueGraphWithBottomTriangle"))
            use:Vertex
        ) 
        (piece "Tiger" P1 (or ("StepToEmpty") ("HopCapture")))
        (piece "Goat" P2 ("StepToEmpty"))
    }) 
    
    (rules 
        (start { 
            (place "Tiger1" (sites {"C6"}))
            (place "Goat2" (union (expand (sites Bottom)) (sites {"A3" "E3"})))
        })
        
        (play (forEach Piece)) 
        
        (end ("NoMovesP1NoPiecesP2"))
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "This hunt game played in the Orissa region of India is played with one tiger trying to eat twelve goats, while the goats try to block the tiger from moving.")
        (rules "5x5 lines, intersecting to form a square. Diagonals are drawn in the four quadrants of the board. One triangle, its apex intersecting the main board at the midpoint of one side. The base of the triangle is bisected by a line drawn from the apex, and this line is bisected and intersects with the other two sides of the triangle. One player plays as the tiger, placed on the central point of the triangle, and the other plays as twelve goats, which begin on the first two rows of points opposite the triangle and on the outer two points in the third row. The tiger moves first. Pieces move to an empty adjacent spot along the lines on the board. The tiger alone may capture a goat by hopping over it to an empty spot immediately opposite a goat. The tiger wins by capturing all the goats; the goats win by blocking the tiger from being able to move.")
        (source "Das Gupta 1926: 212.")
        (id "914")
        (version "1.3.10")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1926.")
        }
    )
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Orissa Tiger Game (One Tiger)_ai"
    )
)
