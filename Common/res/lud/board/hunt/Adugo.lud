(game "Adugo" 
    (players 2) 
    (equipment { 
        ("AlquerqueBoardWithBottomTriangle") 
        (piece "Jaguar" P1 (or "HopCapture" "StepToEmpty"))
        (piece "Dog" P2 "StepToEmpty") 
    }) 
    (rules 
        (start { 
            (place "Dog2" (difference (expand (sites Top) steps:2) (sites {"C5"})))
            (place "Jaguar1" coord:"C5")
        })
        
        (play (forEach Piece)) 
        
        (end ("NoMovesLossAndLessNumPiecesPlayerLoss" P2 <Num:dogs>))
    )
)

//------------------------------------------------------------------------------

(option "End Rules" <Num> args:{ <dogs> }
    {
    (item "Capture all the dogs"  <0> "The objective of the player 1 is to capture all the dogs.")* 
    (item "Capture at least five dogs" <9> "The objective of the player 1 is to capture at least five dogs.") 
    }
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Adugo has been reported among several indigenous groups in Brazil, including the Bororo, Pareci, Guaraní, and the Manchakeri. The game is played on a board which is usually scratched into the earth, with stones or other small objects used as pieces.")
        (aliases {"Jogo da Onça" "Jogo da Onça e do Cachorro"})
        (rules "5x5 intersecting lines, forming a grid Diagonals in each quadrant. A triangular appendage, the apex being the midpoint of one of the sides of the grid. A line from the apex to the midpoint of the base, and a line bisecting this line and intersecting with the midpoints of the two sides of the triangle. There are fourteen \"dog\" pieces and one \"jaguar\" piece. The jaguar attempts to capture all of the dogs by hopping over them. The dogs attempt to surround the jaguar and block it from moving. Pieces move from the points where lines intersect to adjacent points along the lines on the board.")
        (source "Lima 2004.")
        (id "37")
        (version "1.3.10")
        (classification "board/hunt")
        (credit "Eric Piette")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    
    (ai 
        "Adugo_ai"
    )
    
)
