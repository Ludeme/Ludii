(define "WolfBeyondSheepRow"
    (= 0 
        (count Sites 
            in:(forEach 
                (sites Occupied by:P2) 
                if:(<= 
                    (row of:(site)) 
                    (row of:(where "Wolf" P1))
                )
            )
        )
    )
)

//------------------------------------------------------------------------------

(game "Wolf und Schaaf" 
    (players 2) 
    (equipment { 
        (board (square 8)) 
        (piece "Sheep" P2 N ("StepToEmpty" (directions {FR FL}))) 
        (piece "Wolf" P1 "StepDiagonalToEmpty")
        (hand P1)
    }) 
    (rules 
        (start {
            (place "Wolf1" (handSite P1))
            (place "Sheep2" {"A1" "C1" "E1" "G1"})
        })
        
        phases:{
        (phase "Placement" P1 
            (play 
                (move 
                    (from (handSite P1)) 
                    (to (intersection (sites Phase 0) (sites Empty)))
                )
            )
            (nextPhase "Playing")
        )
        (phase "Playing" (play (forEach Piece)))	
        }
        
        (end {
            (if (and (no Moves P1) (is Mover P1)) (result P2 Win))
            (if ("WolfBeyondSheepRow") (result P1 Win))
        }) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Wold und Schaaf (wolf and sheep) is a game played in Germany. One player plays as four sheep, which attempt to block one wolf, who attempts to move behind the line of sheep.")
        (aliases {"Wolfsspiel"})
        (rules "8x8 checkered board. One player plays as four sheep (white pieces), which begin on the four dark squares on one side of the board. The other player plays as one wolf (black piece), which can be placed on any dark space on the board. Players alternate turns moving a piece to an empty adjacent dark square on the board. The sheep move forward only but the wolf may also move backward. The goal of the sheep is to block the wolf from being able to move, the goal of the wolf is to move beyond the line of sheep.")
        (source "Alvensleben 1853: 645.")
        (id "1075")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in Germany, around 1853.")
        }
    )
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Style Chess)
        (piece Families {"Abstract" "Themed"})
    })
    
    (ai
        "Wolf und Schaaf_ai"
    )
)
