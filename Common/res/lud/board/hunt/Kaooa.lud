(game "Kaooa" 
    (players 2) 
    (equipment { 
        ("StarBoard" 5)
        (hand Each) 
        (piece "Marker" P1 (or ("HopSequenceCapture") "StepToEmpty")) 
        (piece "Marker" P2 "StepToEmpty") 
    }) 
    (rules 
        (start {
            (place "Marker1" (handSite P1) count:1)
            (place "Marker2" (handSite P2) count:<Num>)
        })
        
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                ) 
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        ("PhaseMovePiece" "Movement"
            (end ("NoMoves" Loss))
        )
        }
    )
)

//------------------------------------------------------------------------------

(option "Start rules" <Num> args:{ <pieces> }
    {
    (item "3 Pieces" <3> "The game starts with 3 discs for the second player.") 
    (item "4 Pieces" <4> "The game starts with 4 discs for the second player.") 
    (item "5 Pieces" <5> "The game starts with 5 discs for the second player.") 
    (item "6 Pieces" <6> "The game starts with 6 discs for the second player.") 
    (item "7 Pieces" <7> "The game starts with 7 discs for the second player.")* 
    (item "8 Pieces" <8> "The game starts with 8 discs for the second player.") 
    (item "9 Pieces" <9> "The game starts with 9 discs for the second player.") 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Kaooa is a hunt game documented in the early twentieth century in India, but it is probably much older, as graffiti with the pattern of the board have been found at sites such as medieval Vijayanagara.")
        (rules "Played on a five-pointed star shaped board. One player plays with one piece, the \"tiger,\" and the other plays with seven pieces, the \"kaooas.\" The player with the kaooas attempt to checkmate the tiger by moving to one of the points where the lines of the board intersect. The tiger captures kaooas by hopping over them. The tiger wins by capturing all the kaooas.")
        (source "Gupta 1924: 167.")
        (id "468")
        (version "1.3.7")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1924.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
    })
    
    (ai 
        "Kaooa_ai"
    )
    
)
