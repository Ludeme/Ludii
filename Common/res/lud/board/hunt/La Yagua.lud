(game "La Yagua" 
    (players 2) 
    (equipment { 
        (board 
            (add
                (rotate 180 ("AlquerqueGraphWithBottomTriangle"))
                edges:{{27 29} {29 25}}
            )
            use:Vertex
        ) 
        (piece "Jaguar" P1 (or "HopCapture" "StepToEmpty"))
        (piece "Dog" P2 "StepToEmpty") 
    }) 
    (rules 
        (start { 
            (place "Dog2" (union (expand (sites Bottom) steps:1) (sites {"A3" "E3"})))
            (place "Jaguar1" coord:"C7")
        })
        
        (play (forEach Piece)) 
        
        (end ("NoMovesLossAndLessNumPiecesPlayerLoss" P2 6))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "La Yagua is a hunt game played in South America. This version was played by the Chiriguano-Chane people of Argentina.It was usually drawn on the ground and played with seeds or stones.")
(rules "5x5 grid, with diagonals in each quadrant. A triangle, with the apex connecting to the midpoint of one side of the grid. A line is drawn connecting the apex of the triangle to the midpoint of its base, and another triangle is drawn within the triangle connecting the midpoints of the larger triangle. One player plays as twelve dogs, arranged on the lower two rows of points and the two outer points on the central line; the other plays as one jaguar, placed on the apex of the smaller triangle. The jaguar moves first. Pieces move to an empty adjacent space along the lines of the board. The jaguar may hop over an adjacent dog to an empty space immediately on the opposite side of it, capturing the dog. Dogs cannot capture. The dogs win by blocking the jaguar so it cannot move; the jaguar wins when only six dogs remain.
")
(source "Ferrarese 2006: 22, 27-28.")
(id "1175")
(version "1.3.7")
(classification "board/hunt")
(credit "Eric Piette")
(origin "This game was played in Argentina, from around 1982 to 2006.")
}
)
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    
)
