(game "Orissa Tiger Game (Four Tigers)" 
    (players 2)
    (equipment { 
        ("AlquerqueBoard" 5 5)
        (piece "Goat" P1 ("StepToEmpty"))
        (piece "Tiger" P2 (or ("StepToEmpty") ("HopCapture")))
        (hand P1)
    }) 
    
    (rules 
        (start { 
            (place "Goat1" (handSite P1) count:20)
            (place "Tiger2" (sites {"A5" "E5" "B2" "D2"}))
        })
        
        phases:{
        (phase "Placement" P1 
            (play (move (from (handSite P1)) (to (sites Empty)) (then (if (< 16 (count Cell at:(handSite P1))) (moveAgain))))) 
            (nextPhase Mover ("HandEmpty" Mover) "Movement") 
        )
        ("PhaseMovePiece" "Movement")
        }
        
        (end ("NoMovesP2NoPiecesP1"))
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "This game, the name for which was not recorded, was played in the Orissa region of India. Four tigers play against twenty goats.")
        (rules "5x5 board, played on intersections of the lines, with diagonals for each quadrant of the board. One player plays as four tigers, the other as twenty goats. The tigers begin, two on the top two corners of the board, and two in the centers of the two bottom quadrants. Four of the goats are placed on any available points. The tigers move first, to an empty adjacent spot along the lines on the board. The player playing as the goats places one of the remaining goats on any available point. The goats cannot move until all of the goats are placed. Once they are all placed, the goats move as the tigers do. The tigers alone may capture a goat by hopping over it to an empty spot immediately opposite a goat. The tigers win by capturing all the goats; the goats win by blocking the tigers from being able to move.")
        (source "Das Gupta 1926: 212-213.")
        (id "913")
        (version "1.3.6")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1926.")
        }
    )
    (graphics {
        (piece Families {"Abstract" "Themed"})
    })
    
    (ai
        "Orissa Tiger Game (Four Tigers)_ai"
    )
)
