(game "Haretavl" 
    (players 2) 
    (equipment { 
        ("ThreeMensMorrisBoardWithLeftAndRightTriangles")
        (hand Each)
        (piece "Dog" P1 "StepToEmpty")
        (piece "Hare" P2 "StepToEmpty")
    }) 
    
    (rules 
        (start {
            (place "Dog1" (handSite P1) count:3)
            (place "Hare2" (handSite P2))
        })
        
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                ) 
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        ("PhaseMovePiece" "Movement")
        }
        
        <End:rule>
    )
)

//------------------------------------------------------------------------------

(option "End" <End> args:{ <rule> } {
    
    (item "Cond for P1" 
        <
        (end {
            (if (no Moves P2) (result P1 Win))
        }) 
        > 
        "The game has an ending condition only for P1."
    )*
    
    (item "Cond for P1 and P2" 
        <
        (end {
            (if (no Moves P2) (result P1 Win))
            (if 
                (or
                    (>= (count Moves) (- (value MoveLimit) 10)) 
                    (>= (count Turns) (- (value TurnLimit) 5)) 
                )
                (result P2 Win)
            )
        }) 
        > 
        "The game has an ending condition for each player."
    )
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Haretavl Eleven Points (Described)" {
        "End/Cond for P1"
    })*
    
    (ruleset "Ruleset/Hare Wins (Suggested)" {
        "End/Cond for P1 and P2"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Haretavl is a blockade game from Scandinavia. It is documented from the nineteenth century, but older boards which have the same design are known from the Middle Ages, and thus the game may be much older.")
        (aliases {"Harespil"})
        (useFor "Ruleset/Haretavl Eleven Points (Described)" (rules "Square board, divided into eight equal triangles, with the central horizonal line extended past the square on either side and lines connecting the end point to the two nearby cornes of the square One player plays as the hare, the other player as three hounds. Players take turns placing one of their pieces on the board. When a player has placed all their pieces, they move to an adjacent empty spot. When the hounds block the hare from being able to move, the hounds win.
        "))
        (useFor "Ruleset/Haretavl Eleven Points (Described)" (source "Michaelsen 2014."))
        (useFor "Ruleset/Haretavl Eleven Points (Described)" (id "770"))
        (useFor "Ruleset/Hare Wins (Suggested)" (rules "Hare wins after 250 turns."))
        (useFor "Ruleset/Hare Wins (Suggested)" (id "1898"))
        (source "Michaelsen 2014.")
        (id "770")
        (version "1.3.5")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in Scandinavia, from around 1833 to 1886.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        
    })
    
    (ai
        "Haretavl_ai"
    )
)
