(game "Mao Naga Tiger Game"
    (players 2)
    (equipment {
        (board
            (merge {
                (shift 0 2 (square 5 diagonals:Alternating) )
                (wedge 3)
                (shift 0 6 (rotate 180 (wedge 3)))
            })
            use:Vertex
        )
        (piece "Human" P1 "StepToEmpty")
        (piece "Tiger" P2 (or "StepToEmpty" "HopCapture"))
    })
    (rules
        (start {
            (place Stack "Human1" (sites {"B4" "B6" "D4" "D6"}) count:5)
            (place "Tiger2" (sites {"A5" "E5"}))
        })

        (play
            (forEach Piece top:True)
        )

        (end {
            (if (no Pieces P1) (result P2 Win))
            (if (no Moves P2) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "This game is a hunt game played by the Mao Naga people of Manipur, India in the early twentieth century.")
        (rules "5x5 intersecting lines, with diagonals in each quadrant. Two triangles, the apexes of which intersect with the square at the midpoint of opposite sides. One line bisecting two sides of the triangle. One player plays as twenty people, stacked five each in the four points where the diagonals cross in each quadrant. The other player plays as two tigers, which are placed on the midpoints of the sides without triangles. Players alternate turns moving one piece to an adjacent spot along the lines of the board. The tiger may hop over one of the people to an empty point on the opposite side immediately adjacent to it along the lines of the board. When the tiger hops over one of the stacks, it captures only one of the people. The tigers win by capturing all the people, the people win by blocking the tigers from being able to move.")
        (source "Hodson 1911: 62-63.")
        (version "1.3.0")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1911.")
        }
    )

    (graphics {
        (show Edges Diagonal Hidden)
        (stackType Count)

    })

)
