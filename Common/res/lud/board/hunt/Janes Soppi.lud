(game "Janes Soppi"
    (players 2)
    (equipment {
        (board
            (merge
                {
                (rectangle 3 3)
                (shift 1.5 0.5 (rotate -90 (wedge 2)))
                (shift -1.5 0.5 (rotate 90 (wedge 2)))
                }
            )
            use:Vertex
        )
        (hand Each)
        (piece "Hound" P1 "StepToEmpty")
        (piece "Hare" P2 "StepToEmpty")
        }
    )

    (rules
        (start {
            (place "Hound1" (handSite P1) count:3)
            (place "Hare2" (handSite P2))
        })

        phases:{
        (phase "Placement"
            (play
                (move
                    (from (handSite Mover))
                    (to (sites Empty))
                )
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )

        (phase "Movement"
            (play (forEach Piece))
        )
        }

        (end {
            (if (no Moves P2) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Jänes Soppi (\"the hare to the corner\") is a blockade game from Estonia. ")
        (aliases {"Jänes Soppi"})
        (rules "3x3 intersecting lines, with triangles extending on opposite ends of the large square and the central line extending to the apices. One player plays as the notched stick, called the hare, and the other as three sharp sticks, known as the hounds. Players alternate turns placing a piece on the board. When a player has placed all of their pieces on the board, they move one of their pieces to an empty adjacent spot along the lines. When the hounds block the hare from being able to move, the hounds win.")
        (source "Michaelsen 2014: 207.")
        (version "1.3.0")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Estonia, around 1973.")
        }
    )

    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)

    })

    (ai
        "Janes Soppi_ai"
    )
)
