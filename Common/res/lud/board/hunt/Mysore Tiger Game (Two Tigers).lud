(game "Mysore Tiger Game (Two Tigers)" 
    (players 2) 
    (equipment {
        ("AlquerqueBoardWithFourTriangles")
        (hand Each) 
        (piece "Human" P2 ("StepToEmpty"))
        (piece "Tiger" P1
            (or 
                ("StepToEmpty")
                ("HopCapture")
            )
        )
    }) 
    (rules 
        (start {
            (place "Human2" (handSite P2) count:25)
            (place "Tiger1" (sites Centre))
            (place "Tiger1" (handSite P1))
        })
        
        phases:{
        (phase "Opening" 
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase Mover ("HandEmpty" Mover) "Playing")
        )
        (phase "Playing" (play (forEach Piece)))
        }
        
        (end ("NoMovesP1NoPiecesP2"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This game is presented in a group of manuscripts from the Maharaja Krishnaraja Wadiyar III of Mysore. 25 people attempt to trap two tigers, and the tigers attempt to eat the people.
            
        ")
        (rules "5x5 intersecting lines, with diagonals in each quadrant. At the midpoint of each side, the apex of a triangle. A line is drawn from the apex to the base of each triangle, and another line bisecting this one and the two opposite sides of the triangle. One player plays as two tigers, the other as 25 people. One tiger begins on the central point, and the other may be placed anywhere on the board. Players alternate turns, with the person playing as the people first placing a person on the board, and then one of the tigers moving to an empty adjacent spot along the lines of the board. When all of the people have been placed, the people move on the board in the same fashion. The tigers may capture one of the people by jumping over it to an empty adjacent spot immediately on the opposite side of one of the people along the lines of the board. The tigers win when they capture enough people so that the people cannot block the tiger, the people win when they can block the tiger from being able to move.
        ")
        (source "Vasantha 2006: 30-31.")
        (id "1129")
        (version "1.3.5")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in India, from around 1794 to 1868.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Mysore Tiger Game (Two Tigers)_ai"
    )
)
