(game "Mysore Tiger Game (Three Tigers)" 
    (players 2) 
    (equipment { 
        (board 
            (add
                (remove
                    (merge
                        (shift 0 2 (scale 8 2 (rectangle 3 2)))
                        (scale 1 2 (wedge 5 4)) 
                    )
                    edges:{ {0 1} {2 3} {4 5}}
                )
                edges:{ {0 15} {18 1} {2 11} {14 3} {4 7} {10 5} }
            ) 
            use:Vertex
        ) 
        (hand Each) 
        (piece "Human" P2 ("StepToEmpty"))
        (piece "Tiger" P1
            (or 
                ("StepToEmpty")
                ("HopCapture")
            )
        )
    }) 
    (rules 
        (start {
            (place "Human2" (handSite P2) count:17)
            (place "Tiger1" (sites Top))
            (place "Tiger1" (handSite P1) count:2)
        })
        
        phases:{
        (phase "Opening" P1
            (play (move (from (handSite Mover)) (to (sites Empty)) (then (if (not ("HandEmpty" Mover)) (moveAgain)))))
            (nextPhase Mover ("HandEmpty" Mover) "Playing")
        )
        (phase "Opening" P2
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase Mover ("HandEmpty" Mover) "Playing")
        )
        (phase "Playing"
            (play (forEach Piece))
        )
        }
        
        (end ("NoMovesP1NoPiecesP2"))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This game is presented in a group of manuscripts from the Maharaja Krishnaraja Wadiyar III of Mysore. Seventeen people attempt to trap three tigers, and the tigers attempt to eat the people.
            
        ")
        (rules "A triangle, with two lines drawn from the apex to the base. A rectangle is drawn across the triangle, and another line connecting the midpoints of the short sides of the rectangle. One player plays as three tigers, the other as seventeen people. One tiger begins on the apex of the triangle and the other two may be placed anywhere. Players alternate turns, with the person playing as the people first placing a person on the board, and then one of the tigers moving to an empty adjacent spot along the lines of the board. When all of the people have been placed, the people move on the board in the same fashion. The tigers may capture one of the people by jumping over it to an empty adjacent spot immediately on the opposite side of one of the people along the lines of the board. The tigers win when they capture enough people so that the people cannot block the tiger, the people win when they can block the tiger from being able to move.")
        (source "Vasantha 2006: 29-31.")
        (id "1128")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in India, from around 1794 to 1868.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    
    (ai
        "Mysore Tiger Game (Three Tigers)_ai"
    )
)
