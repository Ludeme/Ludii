(game "Bagh Batti" 
    (players 2) 
    (equipment { 
        ("AlquerqueBoard" 5 5)
        (piece "Goat" P1 "StepToEmpty")
        (piece "Tiger" P2
            (or
                ("StepToEmpty")
                ("HopSequenceCapture")
            )
        )
    }) 
    
    (rules 
        (start { 
            (place Stack "Goat1" (sites {"B2" "D2" "B4" "D4"}) counts:{5 5 5 5})
            (place "Tiger2" (sites {"C1" "C5"}))
        })
        
        (play 
            (if ("SameTurn")
                (or
                    (move Pass)
                    ("HopSequenceCaptureAgain")
                )
                (forEach Piece top:True)
            )
            
        )
        
        (end ("NoMovesP2NoPiecesP1")) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Bagh Batti is a hunt game played in the Garhwal region of India. It it closely similar to the games Bagh Guti and Sher Bakar, in which the hunted goats begin in a stack.")
        (rules "5x5 board, played on intersections, with diagonals for each quadrant of the board. One player plays with two tiger pieces, placed on the midpoints of two opposite sides. The other player plays with twenty goats, divided into four stacks of five, placed in the center of each quadrant. The goats move first. Goats may move one at a time to any adjacent vacant spot. More than one goat can be placed on the goats' starting spots, but not elsewhere. The tiger moves in the same manner, but also may capture a piece by hopping over it. Multiple captures can be made on the same turn with subsequent hops, but only the top goat in a stack is captured when a tiger leaps over it. The goal of the goats is to surround the tigers so they cannot move; when one tiger is blocked the other must be blocked on the next turn. The goal of the tigers is to capture all the goats.
        ")
        (source "Das-Gupta 1927: 297-298.")
        (id "766")
        (version "1.3.13")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in India, around 1926.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
    })
    
)
