(define "RememberSiteWithPossibleCapture"  
    (forEach Site
        (sites From
            (forEach Piece "Marker" 
                ("HopCapture")
            )
        )
        (remember Value "From" (site))
    )
)

//------------------------------------------------------------------------------

(game "Asalto"  
    (players 2)  
    (equipment { 
        (board 
            (merge 
                (shift 0 2 (rectangle 3 7 diagonals:Alternating) )
                (shift 2 0 (rectangle 7 3 diagonals:Alternating) ) 
            )
            use:Vertex
        )
        (piece "Marker" P1 (or ("HopCapture") "StepToEmpty" ) )
        (piece "Marker" P2 "StepToEmpty") 
    })  
    (rules 
        (start {
            (place "Marker1" (sites {"C6" "E6"}))
            (place "Marker2" (union (expand (union (sites Right) (sites Left))) (expand (sites Bottom) steps:3)))
        })
        
        (play
            (if (is Mover P1)
                (do
                    ("RememberSiteWithPossibleCapture") 
                    next:(forEach Piece)
                    (then 
                        (and
                            (if (!= 0 (count Sites in:(sites (values Remembered "From")))) 
                                (if (= 1 (count Steps (last From) (last To)))
                                    (and
                                        (forEach Site (sites (values Remembered "From"))
                                            (remove (site))
                                        )
                                        (if (is In (last From) (sites (values Remembered "From")))
                                            (remove (last To))
                                        )
                                    )
                                )
                            )
                            (forget Value All)
                        )
                    )
                )
                (forEach Piece)
            )
        )
        
        (end {
            (if (no Pieces P2) (result P1 Win))
            (if
                (or
                    (or (no Pieces P1) (no Moves P1))
                    (= 0 (count Sites in:(difference (expand (sites Top) steps:2) (sites Occupied by:P2)))) 
                )
                (result P2 Win)
            )
        }) 
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Asalto is a hunt game known from early modern Europe.")
        (aliases {"Assaut"})
        (rules "Played on the Fox and Geese board, but the top arm of the cross is surrounded by a double line, indicating it is a fortress, in which one player puts two officers. The opponent has 24 pieces, which occupy the points outside the fortress. Officers may capture one of the opponent's pieces by hopping over it to an empty space immediately on the opposite side of the opponent's piece. If the officer does not capture when it is possible, it is huffed. The soldiers win by occupying all of the points in the fortress or by blocking the officers from being able to move; the officers win by capturing enough soldiers to prevent this.")
        (source "Murray 1951: 105.")
        (id "1368")
        (version "1.3.3")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Europe, around 1891.")
        }
    )
    
    (graphics {
        
        (region Colour Edge {70 71 65 66 67 68 69 63 64 58 59 60 61 62 40 41} regionSiteType:Edge (colour Red))
    })
    
    (ai
        "Asalto_ai"
    )
)
