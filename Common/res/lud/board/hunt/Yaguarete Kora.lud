(game "Yaguarete Kora"
    (players 2)  
    (equipment { 
        (board 
            (merge 
                (shift 0 -4 (square 5 diagonals:Alternating)) 
                (rotate 180 (wedge 3))
            ) 
            use:Vertex
        ) 
        (piece "Jaguar" P1
            (or 
                ("StepToEmpty")	
                ("HopCapture")
            )
        )
        (piece "Dog" P2 ("StepToEmpty"))
    })  
    
    (rules 
        (start {
            (place "Jaguar1" coord:"C6")
            (place "Dog2" (expand (sites Bottom) steps:2))
        })
        (play (forEach Piece))
        (end {
            (if (no Moves P1) (result P1 Loss))
            (if (<= (count Pieces P2) 6) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Yaguarete Kora is a hunt game played by the Mbya GuaranÃ­ people of South America. One player plays as one jaguar, and the other plays as fifteen dogs.")
        (aliases {"Yaguarete" "Jaguarete Kora" "Chivi Kora"})
        (rules "5x5 intersecting lines with diagonals in each quadrant. A triangle, the apex intersecting with the apex of one of the sides. A line is drawn from the apex to the midpoint of the base of the triangle. A line bisects this line, connecting the midpoints of the sides of the triangle,  One player plays as one jaguar, which begins on the center of the base of the large triangle's base. The other player plays as fifteen dogs, which begin on the points of the three ranks of the bottom half of the square portion of the board. The jaguar moves first. Pieces move to an empty adjacent space along the lines of the board. The jaguar may hop over an adjacent dog to an empty space immediately on the opposite side of it, capturing the dog. Dogs cannot capture. The dogs win by blocking the jaguar so it cannot move; the jaguar wins when only six dogs remain. ")
        (source "Ferrarese 2006: 21-23.")
        (id "1403")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Argentina, from around 1960 to 2020.")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        (piece Families {"Abstract" "Themed"})
    })
    
)
