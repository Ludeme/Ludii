(game "Bagha Guti"
    (players 2)  
    (equipment { 
        ("AlquerqueBoard" 5 5)
        (piece "Goat" P2 "StepToEmpty")
        (piece "Tiger" P1
            (or
                ("StepToEmpty")
                ("HopCapture")
            )
        )
        (hand P1)
    })  
    
    (rules 
        (start { 
            (place Stack "Goat2" 12 count:21)
            (place "Tiger1" (handSite P1))
        })
        
        phases:{
        (phase "Opening" P1 (play (move (from (handSite Mover)) (to (sites Empty)))) (nextPhase "Playing"))
        (phase "Playing"  (play (forEach Piece top:True)))
        }
        
        (end { 
            (if (no Moves P1) (result P2 Win))
            (if (no Pieces P2) (result P1 Win))
        })  
    )
)

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "Bagha Guti is a hunt game played in the Bihar region of India. The tiger attempts to eat all the goats, and the goats try to trap the tiger. All of the goats begin on the same spot in this game.")
        (rules "5x5 board, played on intersections of the lines, with diagonals for each quadrant of the board. One player plays with a tiger piece, placed anywhere on the board. The other player plays with 21 goats, placed on the central point of the board. The goats move first. Goats may move one at a time to any adjacent vacant spot along the lines of the board. The tiger moves in the same manner, but also may capture a piece by hopping over it to an empty space immediate on the opposite side of a goat. The tiger may hop the stack of goats, but may only capture one. The goal of the goats is to surround the tiger so it cannot move. The goal of the tiger is to capture all the goats.
        ")
        (source "Das Gupta 1935: 412-413.")
        (id "910")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in India, around 1935.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (stackType Count)
    })
    
)
