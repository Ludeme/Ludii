(game "Hat Diviyan Keliya" 
    (players 2) 
    (equipment { 
        (board 
            <Board:type>
            use:Vertex
        )
        (hand Each) 
        (piece "Tiger" P1 (or "HopCapture" "StepToEmpty")) 
        (piece "Leopard" P2 "StepToEmpty") 
    }) 
    
    (rules 
        (start {
            (place "Tiger1" (handSite P1) count:<Board:initTiger>)
            (place "Leopard2" (handSite P2) count:<Board:initLeopard>)
        })
        
        phases:{
        (phase "Placement"
            (play 
                (move 
                    (from (handSite Mover)) 
                    (to (sites Empty))
                ) 
            )
            (nextPhase Mover ("HandEmpty" Mover) "Movement")
        )
        
        ("PhaseMovePiece" "Movement"
            (end ("NoMovesLossAndLessNumPiecesPlayerLoss" P2 <Board:leopardGoal>))
        )
        }
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Board> args:{ <type> <initTiger> <initLeopard> <leopardGoal>}
    {
    (item "Ludovici and Parker" 
        <(scale 1 2 (wedge 4))>
        <1>
        <7>
        <3>
        "Played on the board described in Ludovici and Parker."
    )* 
    (item "Large" 
        <(scale 1 2 (wedge 5))>
        <2>
        <8>
        <5>
        "Played on a larger Board."
    ) 
})

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Game played in Sri Lanka during the nineteenth century, but likely to be older. It is a hunt game like others common in South Asia, particularly those known as \"tiger games.\"")
        (aliases {"Seven Leopards"})
        (rules "The game is played on a triangle board divided in half on its height and then in thirds with lines crossing the height perpendicularly. One player plays as the tiger, and the other plays as seven leopards. The tiger plays their piece on a point where lines intersect first, and then on subsequent turns the leopards are placed one-by-one. Moves occur along the lines to an adjacent intersection. The tiger captures may capture a leopard by hopping over it. The tiger's goal is to capture four of the leopards; the leopards' goal is to block the tiger so it cannot move.")
        (source "Ludovisi 1873: 33-34.")
        (id "390")
        (version "1.3.9")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in South Asia, around 1873.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
    })
    
    (ai 
        "Hat Diviyan Keliya_ai"
    )
    
)
