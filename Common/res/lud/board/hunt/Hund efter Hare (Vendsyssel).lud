(game "Hund efter Hare (Vendsyssel)"
    (players 2)
    (equipment {
        (board 
            (add
                (merge {
                    (add (remove (rectangle 1 7) vertices:{3}) edges:{{2 3}})
                    (scale 2 (shift 0.5 -0.5 (rectangle 1 3)))
                    (scale 2 (shift 0.5 0.5 (rectangle 1 3)))
                })
                edges:{
                {0 9} {0 6} {11 5} {5 8} {1 6} 
                {1 9} {4 11} {4 8} {9 2} {2 7}
                {6 2} {2 10} {10 3} {3 8} {7 3}
                {3 11}
                }
            )
            use:Vertex
        )
        (piece "Hare" P1 "StepToEmpty")
        (piece "Dog" P2 "StepToEmpty")
    })
    (rules 
        (start {
            (place "Hare1" coord:"B2")
            (place "Dog2" (sites {"A2" "B1" "B3"}))
        })
        (play (forEach Piece))
        <End:rule>
    )
)

//------------------------------------------------------------------------------

(option "End" <End> args:{ <rule> } {
    
    (item "Cond for P2" 
        <
        (end {
            (if (no Moves P1) (result P2 Win))
        }) 
        > 
        "The game has an ending condition only for P2."
    )*
    
    (item "Cond for P1 and P2" 
        <
        (end {
            (if (no Moves P1) (result P2 Win))
            (if 
                (or
                    (>= (count Moves) (- (value MoveLimit) 10)) 
                    (>= (count Turns) (- (value TurnLimit) 5)) 
                )
                (result P1 Win)
            )
        }) 
        > 
        "The game has an ending condition for each player."
    )
})

//------------------------------------------------------------------------------

(rulesets { 
    
    (ruleset "Ruleset/Hund efter Hare (Vendsyssel) (Observed)" {
        "End/Cond for P2"
    })*
    
    (ruleset "Ruleset/Hare Wins (Suggested)" {
        "End/Cond for P1 and P2"
    })
    
})

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Hund efter Hare is a blockade game played in Denmark. This version is from Vendsyssel, and has been played at least since the nineteenth century.")
(useFor "Ruleset/Hund efter Hare (Vendsyssel) (Observed)" (rules "Three diamonds in a row, the central one connecting to the others one opposite corners. A horizontal line connects the top corners of the diamonds, another horizontal line connects the bottom corners of the diamonds, and a longer horizontal line connects all the middle corners. The outer two diamonds have vertical lines connecting their top and bottom corners. One player plays as three hounds, which begin on the outer corner and top and bottom corner of one of the end diamonds. The other player plays as one hare, which begins on the central point of the same diamond. Players alternate turns moving a piece to an empty adjacent spot along the lines of the board. The Hare moves first. The hounds win by blocking the hare from being able to move."))
(useFor "Ruleset/Hund efter Hare (Vendsyssel) (Observed)" (source "Michaelsen 2009: 151."))
(useFor "Ruleset/Hund efter Hare (Vendsyssel) (Observed)" (id "1083"))
(useFor "Ruleset/Hare Wins (Suggested)" (rules "If the hounds do not block the hare before the game times out, the hare wins."))
(useFor "Ruleset/Hare Wins (Suggested)" (id "1954"))
(source "Michaelsen 2014: 202-204.")
(id "1083")
(version "1.3.9")
(classification "board/hunt")
(credit "Eric Piette")
(origin "This game was played in Denmark, from around 1898 to 1972.")
}
)
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Hund efter Hare (Vendsyssel)_ai"
    )
)
