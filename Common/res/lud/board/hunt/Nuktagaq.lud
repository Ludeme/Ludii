(game "Nuktagaq"
    (players 2)
    (equipment {
        (board
            (remove
                (merge
                    (shift 0 2 (rectangle 3 7 diagonals:Alternating) )
                    (shift 2 0 (rectangle 7 3 diagonals:Alternating) )
                )
                edges:{{17 10} {9 10} {10 11}}
            )
            use:Vertex
        )
        (hand P1)
        (piece "Marker" P1 (or ("HopCapture") "StepToEmpty" ) )
        (piece "Marker" P2 "StepToEmpty")
    })
    (rules
        (start {
            (place "Marker2" (difference (expand (sites Top) steps:4) (union (sites Row 2) (sites Centre))))
            (place "Marker1" (sites Direction from:(sites Centre) S included:True distance:1) )
        })

        (play (forEach Piece))

        (end {
            ("NoMoves" Loss)
            (if (no Pieces P2) (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Nuktagaq is an Inuit hunt game played in the Canadian Arctic. ")
        (rules "Cross-shaped board made of five squares. The four squares of the arms have diagonals and lines connecting the midpoints of opposite sides. The central square has diagonals and a line connecting the central point with the midpoint of the lower side. One player plays with nineteen pieces, which begin on the uppermost nineteen points. The other player plays with two pieces, which begin on the central space and the one immediately below it. The player with two pieces goes first. Pieces move along the lines of the board to an empty adjacent spot. The player with two pieces may capture an adjacent opponent's piece by hopping over it to an empty spot immediately on the opposite side of it. The player with two pieces wins by capturing all of the opponent's pieces; the player with nineteen pieces wins by blocking the opponent from being able to move.")
        (source "Rowley 2007: 99, Appendix B.")
        (version "1.3.0")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Canada, around 1937.")
        }
    )

    (graphics {
        (board Style Graph)

        }
    )

    (ai
        "Nuktagaq_ai"
    )
)
