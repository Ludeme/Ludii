(game "Jeu du Renard et de la Poule" 
    (players 2) 
    (equipment { 
        ("CrossBoard" 3 7 use:Vertex diagonals:Alternating)
        (hand P1)
        (piece "Fox" P1 (or ("HopSequenceCapture") "StepToEmpty"))
        (piece "Hen" P2 N "StepForwardsToEmpty") 
    }) 
    (rules 
        (start { 
            (place "Fox1" (handSite P1))
            (place "Hen2" (union (expand (sites Bottom)) (sites Row 2)))
        })
        
        phases:{ 
        (phase "Placement" P1 
            (play 
                (move 
                    (from (handSite P1)) 
                    (to (sites Empty))
                )
            ) 
            (nextPhase ("HandEmpty" P1) "Movement")
        )
        ("PhaseMovePiece" "Movement")
        }
        
        (end ("NoMovesLossAndNoPiecesPlayerLoss" P2))
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "The Jeu du Renard et de la Poule is a hunt game documented in seventeenth century France. ")
        (rules "Played on cross-shaped board, made of five squares, each divided into four squares and with the diagonals of the larger squares drawn. One player plays as thirteen hens, which begin on the points on one of the arms of the cross and along the line immediately perpendicular to that arm. The other player plays as one fox, which can be placed on any empty point of the board. Players alternate turns moving to an empty adjacent spot  along the lines of the board. The hens, however, can only move in a forward direction. The fox may hop over an adjacent hen to an empty spot immediately on the opposite side of the hen along the lines of the board. The fox wins by capturing all the hens, the hens win by blocking the fox from being able to move.")
        (source "La Maison des Jeux Acad√©miques: 264-266.")
        (id "984")
        (version "1.3.11")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin "This game was played in France, around 1668.")
        }
    )
    
    (graphics {
        (board Style Graph)
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (piece Families {"Abstract" "Themed"})
        }
    )
    
    (ai
        "Jeu du Renard et de la Poule_ai"
    )
)
