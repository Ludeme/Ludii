(define "NumCapturedPiecesIsOdd"
    (is Even (count Steps (from) (to)))
)

(define "BigHop"
    (move
        Hop
        (between
            (range 1 9)
            if:#1
            (apply (remove (between)))
        )
        (to
            if:(and
                (is Empty (to))
                ("NumCapturedPiecesIsOdd")
            )
        )
    )
)

//-------------------------------------------------------------------------------

(game "Rimau-Rimau (One Tiger)"
    (players 2)
    (equipment {
        (board
            (merge
                {
                (shift 0 2 (square 5 diagonals:Alternating) )
                (wedge 3)
                (shift 0 6 (rotate 180 (wedge 3)))
                }
            )
            use:Vertex
        )
        (hand Each)
        (piece "Tiger" P1
            (or {
                ("StepToEmpty")
                ("BigHop" (is Enemy (who at:(between))))
            })
        )
        (piece "human" P2 "StepToEmpty")
    })
    (rules
        (start {
            (place "human2" (expand (sites Centre)))
            (place "human2" (handSite P2) count:15)
            (place "Tiger1" (handSite P1))
        } )

        phases:{

        (phase "PlacementTiger" P1
            (play (move (from (handSite P1)) (to (sites {"C3" "C7"})) (then (moveAgain))))
            (nextPhase Mover "MoveHuman")
        )

        (phase "MoveHuman" P1
            (play
                (move
                    (from (intersection (expand (sites Centre)) (sites Occupied by:Next)))
                    (to (difference (sites Empty) (expand (sites Centre))))
                    (then
                        (if (< 6 (count Sites in:(intersection (expand (sites Centre)) (sites Occupied by:Next))))
                            (moveAgain)
                        )
                    )
                )
            )
            (nextPhase Mover (= 6 (count Sites in:(intersection (expand (sites Centre)) (sites Occupied by:Next)))) "Movement")
        )

        (phase "PlacementHuman" P2
            (play
                (move
                    (from (handSite Mover))
                    (to (sites Empty))
                )
            )
            (nextPhase
                ("HandEmpty" P2)
                "Movement"
            )
        )

        (phase "Movement"
            (play (forEach Piece) )
            (end {
                (if (no Moves P1) (result P2 Win))
                (if (no Pieces P2)  (result P1 Win))
            })
        )
        }
    )
)

//-------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Rimau-Rimau is a hunt game played in the late nineteenth century in Singapore. ")
        (rules "5x5 intersecting lines, with diagonals in each quadrant. Two triangles, the apexes of which intersect with the square at the midpoint of opposite sides. One line bisecting the base of the triangle, and another bisecting this line. One player plays as the tiger, which is placed on the apex of either triangle. The other player plays as 24  people, nine of which begin on the nine central points of the board. To begin, the person playing as the tiger removes three of the people from their starting position and places them on any points on the board. The person playing as the people then places one of the remaining people on an empty spot on the board. The tiger then moves to an empty adjacent spot along the lines of the board. Play continues like this until all of the people are placed, at which point the people move to an adjacent empty spot on the board as well. On its turn, the tiger may hop over a line of people to an empty spot on the other side of the line, following the lines of the board and only if the number of people in the line is odd. The tiger wins if it captures all the people; the people win when they block the tiger from being able to move.")
        (source "Plitschke 1890: 190-191.")
        (version "1.3.0")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Singapore, around 1889.")
        }
    )

    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)

    })

    (ai
        "Rimau-Rimau (One Tiger)_ai"
    )
)
