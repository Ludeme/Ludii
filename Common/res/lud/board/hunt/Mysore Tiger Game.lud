(game "Mysore Tiger Game"  
    (players 2)  
    (equipment { 
        (board
            (add
                (remove 
                    (scale 1 2 (wedge 3))
                    vertices:{5}
                )
                edges:{{4 5}}
            )
            use:Vertex
        )
        (hand P1) 
        (piece "Human" P1
            ("StepToEmpty")
        )
        (piece "Tiger" P2
            (or 
                ("StepToEmpty")
                ("HopCapture")
            )
        )
        }
    )  
    (rules 
        (start {
            (place "Human1" (handSite P1) count:3)
            (place "Tiger2" (sites Top))
        })
        
        phases:{
        (phase "Opening" P1 
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase Mover ("HandEmpty" Mover) "Playing")
        )
        (phase "Playing"
            (play (forEach Piece))
        )
        }
        
        (end {
            (if (no Moves P2) (result P1 Win))
            (if (< (count Pieces P1) 3) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "This game is presented in a group of manuscripts from the Maharaja Krishnaraja Wadiyar III of Mysore. Three people attempt to trap a tiger, and the tiger attempts to eat the people.")
        (rules "A triangle, with a point in the center and lines connecting it to the apex and the midpoints of the two sides which meet at the apex. One person plays as the tiger, which begins on the apex. The other person plays as three people. Players alternate turns, with the person playing as the people first placing a person on the board, and then the tiger moving to an empty adjacent spot along the lines of the board. When all of the people have been placed, the people move on the board in the same fashion. The tiger may capture one of the people by jumping over it to an empty adjacent spot immediately on the opposite side of one of the people along the lines of the board. The tiger wins when it captures one person, the people win when they can block the tiger from being able to move.")
        (source "Vasantha 2006: 29.")
        (id "1127")
        (version "1.3.4")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in India, from around 1794 to 1868.")
        }
    )
    
    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)
        
    })
    
    (ai
        "Mysore Tiger Game_ai"
    )
)
