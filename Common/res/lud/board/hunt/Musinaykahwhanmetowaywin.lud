(game "Musinaykahwhanmetowaywin"
    (players 2)
    (equipment {
        (board
            (merge
                (shift 0 2 (rectangle 3 7 diagonals:Alternating) )
                (shift 2 0 (rectangle 7 3 diagonals:Alternating) )
            )
            use:Vertex
        )
        (piece "Marker" P1 (or ("HopSequenceCapture") ("StepToEmpty") ) )
        (piece "Disc" P2 ("StepToEmpty"))
        }
    )
    (rules
        (start {
            (place "Disc2" (union (expand (sites Bottom)) (sites Row 2) ) )
            (place "Marker1" (centrePoint))
        })

        (play
            (if "SameTurn"
                (or
                    ("HopSequenceCaptureAgain")
                    (move Pass)
                )
                (forEach Piece)
            )
        )

        (end {
            (if (no Moves P1) (result P2 Win))
            (if (no Pieces P2) (result P1 Win))
        })
    )
)

//-------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Musinaykahwhanmetowaywin is a hunt game played by the Chippewa and Cree people. Players play as an Oke-mow (\"king\"), against thirteen pawns.")
        (rules "Played on cross-shaped board, made of five squares, each divided into four squares and with the diagonals of the larger squares drawn. One player plays with one larger piece, the Oke-mow, placed on the central point, and the other player with thirteen pieces situated on every point of one arm of the cross and along the line immediately perpendicular to that arm. Pieces move along the lines to an adjacent empty spot. The Oke-mow may jump over an adjacent piece to capture it. Multiple captures are allowed. The Oke-mow wins if it can capture all the opponent's pieces, the other player wins by blocking the Oke-mow from moving.
        ")
        (source "Culin 1907: 791.")
        (version "1.3.0")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in Canada, around 1907.")
        }
    )

    (graphics {
        (piece Scale "Disc" 0.5)

    })
    (ai
        "Musinaykahwhanmetowaywin_ai"
    )
)
