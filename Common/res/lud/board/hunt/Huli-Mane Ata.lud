(game "Huli-Mane Ata"
    (players 2)
    (equipment {
        (board
            (scale 1 2 (wedge 4 3))
            use:Vertex
        )
        (hand P1)
        (piece "Lamb" P1
            ("StepToEmpty")
        )
        (piece "Tiger" P2
            (or
                ("StepToEmpty")
                ("HopCapture")
            )
        )
        }
    )
    (rules
        (start {
            (place "Lamb1" (handSite P1) count:5)
            (place "Tiger2" (sites Top))
        })

        phases:{
        (phase "Opening" P1
            (play (move (from (handSite Mover)) (to (sites Empty))))
            (nextPhase Mover ("HandEmpty" Mover) "Playing")
        )
        (phase "Playing"
            (play (forEach Piece))
        )
        }

        (end {
            (if (no Moves P2) (result P1 Win))
            (if (< (count Pieces P1) 4) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata

    (info
        {
        (description "Huli-Mane Ata is a simple hunt game played in the area of Vijayanagara, India by children. Boards for this game appear at the medieval ruins of Vijayanagara, but it is unclear if the game dates to that period.")
        (aliases {"Pulijudamu"})
        (rules "A triangle, with a line drawn from the apex to the base, and two lines drawn through the height connecting the opposite two sides. One player plays as one tiger, the other as five lambs. The tiger begins on the apex of the triangle. The tiger moves to an empty adjacent spot along the lines of the board. The player who plays as the lambs plays first, placing a lamb on an empty spot on the board, and then the tiger player moves. When all of the lambs are placed, the lambs move in the same manner as the tiger. The tiger may capture a lamb by hopping over it to an empty space on the opposite adjacent side of the lamb along the lines on the board. The lambs win when they block the tiger from being able to move, the tiger wins by capturing enough lambs so that it cannot be blocked.")
        (source "Vasantha 2003: 27-28.")
        (version "1.3.0")
        (classification "board/hunt")
        (credit "Eric Piette")
        (origin  "This game was played in India, from around 1200 to 2003.")
        }
    )

    (graphics {
        (piece Families {"Abstract" "Themed"})
        (show Edges Diagonal Hidden)

    })

    (ai
        "Huli-Mane Ata_ai"
    )
)
