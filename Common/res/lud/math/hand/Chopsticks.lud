(define "TransferPoints"	           
    (if (>= (state at:(regionSite (sites Occupied by:Mover) index:#1)) #3)
        (move Select
            (from (regionSite (sites Occupied by:Mover) index:#1))
            (to (regionSite (sites Occupied by:Mover) index:#2)
                if:(< (+ #3 (state at:(to))) 5)
            )	
            (then
                (and 
                    (set State 
                        at:(last From) 
                        (- (state at:(last From)) #3)
                    )
                    (set State 
                        at:(last To) 
                        (+ #3 (state at:(last To)))
                    )
                )
            )
        )
    )
)

//-------------------------------------------------------------------------

(game "Chopsticks"
    (players 2) 
    (equipment { 
        (board (square 2))
        (piece "Hand" Each)
        (piece "HandFlip" Each)
    }) 
    (rules 
        (start { 
            (place "HandFlip1" coord:"A1" state:1) 
            (place "Hand1" coord:"B1" state:1) 
            (place "Hand2" coord:"A2" state:1) 
            (place "HandFlip2" coord:"B2" state:1) 
        })
        (play 
            (or {
                (move Select
                    (from 
                        (sites Occupied by:Mover) 
                        if:(!= (state at:(to)) 0)
                    )
                    (to 
                        (sites Occupied by:Next) 
                        if:(!= (state at:(to)) 0)
                    )
                    (then 
                        (set State 
                            at:(last To) 
                            (% (+ (state at:(last From)) (state at:(last To))) 5)
                        )
                    ) 
                )
                ("TransferPoints" 0 1 1)
                ("TransferPoints" 0 1 2)
                ("TransferPoints" 0 1 3)
                ("TransferPoints" 0 1 4)
                ("TransferPoints" 1 0 1)
                ("TransferPoints" 1 0 2)
                ("TransferPoints" 1 0 3)
                ("TransferPoints" 1 0 4)
            })
        )
        
        (end ("BlockWin"))
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Chopsticks is a combinatorial hand game of uncertain origin. No equipment is required; people play by showing a number of fingers on their hands.")
        (rules "This official set of rules is called rollover where five fingers are subtracted should a hand's sum exceeds 5 as described below.
            
        1) A hand is live if it has at least one finger, and this is indicated by raising at least one finger. If a hand has zero fingers, the hand is dead, and this is indicated by raising zero fingers (i.e. a closed fist).
        
    2) If any hand of any player reaches exactly five fingers, then the hand is dead.
    
3) Each player begins with one finger raised on each hand. After the first player turns proceed clockwise.

4) On a player's turn, they must either attack or split. There are two types of splits, transfers and divisions.

5) To attack, a player uses one of their live hands to strike an opponent's live hand. The number of fingers on the opponent's struck hand will increase by the number of fingers on the hand used to strike.

6) To transfer, a player strikes their own two hands together, and transfers raised fingers from one hand to the other as desired. However, a player cannot transfer fingers to make a hand have more than 4 fingers.

7) If a player has a dead hand, the player can divide the fingers between the other hand and the dead hand by transferring fingers from the other hand to the dead hand. 

8) A player with two dead hands is eliminated from the game.

9) A player wins once all opponents are eliminated.

10) If you go over 5 you subtract the sum of all of the numbers by 5 game.")
(source "<a href=\"https://en.wikipedia.org/wiki/Chopsticks_(hand_game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(id "129")
(version "1.3.12")
(classification "math/hand")
(credit "Eric Piette")
}
)

(graphics {
    (player Colour P1 (colour HumanLight))
    (player Colour P2 (colour HumanDark))
    (piece Rotate P2 degrees:180)
    (piece AddStateToName)
    (no Board)
})

(ai 
    "Chopsticks_ai"
)

)
