(define "NumRow" <Num:row>)
(define "NumColumn" 4)
(define "TopRow" (+ 2 (* 3 (- "NumRow" 1))))
(define "NumTry" (var))
(define "RowToPlace" (- "TopRow" (* 3 ("NumTry"))))
(define "RowToCheck" (- "TopRow" (* 3 (- ("NumTry") 1))))

(define "HintPositions" 
    (union
        (sites Row (+ "RowToPlace" 1))
        (sites Row (- "RowToPlace" 1))
    )
)

(define "RightPositionAndColor"
    (= 
        (what at:(regionSite (sites Row ("RowToPlace")) index:(value))) 
        (what at:(coord row:0 column:(value)))
    )
)

(define "ColorInSolution"
    (is In (what at:(regionSite (sites Bottom) index:(value))) 
        (results from:(sites Row ("RowToPlace")) to:0 (what at:(from)))
    )
)

(define "CombinationPlaced"
    (= 0 (count Sites in:(intersection (sites Empty) (sites Row ("RowToPlace")))))
)

(define "CombinationToFindPlaced"
    (= 0 (count Sites in:(intersection (sites Empty) (sites Bottom))))
)

(define "PositionAndColorOK"
    (= (what at:(site)) (what at:(coord row:0 column:(column of:(site)))))
)

(define "CombinationFound"
    (and
        (= 0 (count Sites in:(intersection (sites Empty) (sites Row ("RowToCheck")))))
        (= "NumColumn" 
            (count Sites in:(forEach (sites Row ("RowToCheck")) if:("PositionAndColorOK")))
        )
    )
)

(define "AddHint" 
    (add (piece #1) (to (regionSite ("HintPositions") index:(value))))		
)

(define "AddBlackHint"
    ("AddHint" "Disc2")
)

(define "AddWhiteHint"
    ("AddHint" "Disc1")
)

//-------------------------------------------------------------------------

(game "Mastermind"
    (players 2)
    (equipment {
        (board
            (merge {
                (rectangle (+ 1 "NumRow") "NumColumn")
                (shift "NumColumn" 0.75 
                    (repeat "NumRow" 1 
                        step:{{1 0} {0 1}} 
                        (poly {{0 0} {0 0.5} {0.5 0.5} {0.5 0}})
                    )
                )
            })
            use:Vertex
        )
        (piece "Marker1" Shared)
        (piece "Marker2" Shared)
        (piece "Marker3" Shared)
        (piece "Marker4" Shared)
        (piece "Marker5" Shared)
        (piece "Marker6" Shared)
        (piece "Marker7" Shared)
        (piece "Marker8" Shared)
        
        (piece "Disc1" Shared)
        (piece "Disc2" Shared)
        
        (hand Shared size:8)
    })
    (rules
        (start {
            (place "Marker1" (handSite Shared))
            (place "Marker2" (handSite Shared 1))
            (place "Marker3" (handSite Shared 2))
            (place "Marker4" (handSite Shared 3))
            (place "Marker5" (handSite Shared 4))
            (place "Marker6" (handSite Shared 5))
            (place "Marker7" (handSite Shared 6))
            (place "Marker8" (handSite Shared 7))
        })
        
        phases:{
        (phase "Placement" P1
            (play 
                (move
                    (from (sites Hand Shared))
                    (to (intersection (sites Empty) (sites Bottom)))
                    copy:True
                    (then
                        (and {
                            (set Hidden at:(last To) to:P2)
                            (if ("CombinationToFindPlaced")
                                (set Var 0) // Init number of try.
                                (moveAgain)
                            )
                        })
                    )
                )
            )
        )
        (phase "Finding" P2
            (play
                (move
                    (from (sites Hand Shared))
                    (to (intersection (sites Empty) (sites Row ("RowToPlace"))))
                    copy:True
                    (then
                        (and 
                            (moveAgain)
                            (if ("CombinationPlaced")
                                (and
                                    (forEach Value min:0 max:(- "NumColumn" 1)
                                        (if ("RightPositionAndColor")
                                            ("AddBlackHint")
                                            (if ("ColorInSolution")
                                                ("AddWhiteHint")
                                            )
                                        )
                                    )
                                    (set Var (+ 1 ("NumTry")))
                                    (then
                                        (if (or ("CombinationFound") (>= "NumTry" "NumRow"))
                                            (forEach Site (sites Bottom)
                                                (set Hidden at:(site) False to:P2)	
                                            )
                                        )		
                                    )
                                )
                            )
                        )
                    )
                )
            )
            (end {
                (if ("CombinationFound") (result P2 Win))
                (if (>= "NumTry" "NumRow") (result P1 Win))
            })
        )
        }
    )
)

//------------------------------------------------------------------------------

(option "Board Size" <Num> args:{ <row> } {
    (item "4 Tries" <4> "P2 has 4 tries.") 
    (item "6 Tries" <6> "The game is played on a 6x6 board.") 
    (item "8 Tries" <8> "The game is played on a 8x8 board.") 
    (item "10 Tries" <10> "The game is played on a 10x10 board.") 
    (item "12 Tries" <12> "The game is played on a 12x12 board.")* 
    (item "14 Tries" <14> "The game is played on a 14x14 board.") 
    (item "16 Tries" <16> "The game is played on a 16x16 board.") 
    (item "18 Tries" <18> "The game is played on a 18x18 board.") 
})

//------------------------------------------------------------------------------

(metadata 
    
(info
{
(description "Mastermind is a code-breaking game for two players.")
(aliases {"Master Mind"})
(rules "At the beginning of the game, the first player places 4 pieces of any colour in the bottom row. These pieces will be hidden to the second player during all the game.

The objective of the second player (called codebreaker) is to try to guess the pattern, in both order and color, within a specific number of tries (corresponding to the number of rows). Each guess is made by placing a row of code pegs on the board.
Once placed, a feedback on his last try is provided by placing from zero to four key pegs in the small holes of the row with the guess. A black key peg is placed for each code peg from the guess which is correct in both color and position. A white key peg indicates the existence of a correct color code peg placed in the wrong position.")
(id "1157")
(source "<a href=\"https://en.wikipedia.org/wiki/Mastermind_(board_game)\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
(version "1.3.7")
(classification "math")
(author "Mordecai Meirowitz")
(credit "Eric Piette")
(date "1970")
}
)
    
    (graphics {
        (show Edges Hidden)
        (piece Colour "Marker1" fillColour:(colour White))
        (piece Colour "Marker2" fillColour:(colour Black))
        (piece Colour "Marker3" fillColour:(colour Red))
        (piece Colour "Marker4" fillColour:(colour Blue))
        (piece Colour "Marker5" fillColour:(colour Yellow))
        (piece Colour "Marker6" fillColour:(colour Purple))
        (piece Colour "Marker7" fillColour:(colour Green))
        (piece Colour "Marker8" fillColour:(colour Orange))
        
        (piece Colour "Disc1" fillColour:(colour White))
        (piece Colour "Disc2" fillColour:(colour Black))
        
        (piece Scale "Marker" container:1 1)
        (piece Scale "Marker" 1.5)
        (board Colour InnerVertices (colour DarkGrey))
        (board Colour OuterVertices (colour DarkGrey))
        (board Background fillColour:(colour 255 250 220) edgeColour:(colour 100 95 75) scale:2.0)
        (show Line {{3 6}} Edge (colour Black) scale:2.0)
        (show Symbol "disc" (expand (sites Bottom) steps:"NumRow") fillColour:(colour DarkGrey) edgeColour:(colour DarkGrey) scale:0.666)
        (hand Placement Shared scale:0.4 offsetX:0.75 offsetY:0.25 vertical:True)
    })
    
)
