(define "RemoveCutBranch" 
    (forEach Site
        (sites Group Edge at:(site))
        (remove Edge (site))
    )
)

(define "NotConnectedToTheGround" 
    (= 0 
        (count Sites in:(intersection 
                ("Ground")
                (sites Group Edge at:(site) if:(is Occupied (to)))
            )
        )
    )
)

(define "Ground" (sites Bottom))

//------------------------------------------------------------------------------

(game "Hackenbush" 
    (players 2) 
    (equipment { 
        (board <Board:type> use:Edge)
        (piece "Marker" Each) 
        (piece "Disc" Shared) 
        (piece "Cross" Neutral) 
        (regions All (expand (sites Bottom)))  
    }) 
    
    (rules
        (start {
            (set Neutral Edge ("Ground")) 
            <Version:start>
        })  
        (play
            (move
                Remove <Version:play>     
                (then		   		
                    (forEach Site 
                        (sites Incident Edge of:Edge at:(last To)) 
                        (if
                            ("NotConnectedToTheGround")
                            ("RemoveCutBranch")
                        )
                    )
                )
            )
        )
        (end      
            (if (no Moves Mover) (result Next Win))   
        )
    )
)

//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> }
    {
    (item "Graph1"    
        <
        (graph
            vertices:{ 
            {0 0} {2 0} {4 0} {6 0} {8 0} {10 0} {15 0} {4 2} {4 4} {6 4} {3 5} {4 6} {10 2}
            {10 4} {8 4} {12 5} {10 6}}
            edges:{ 
            {0 1} {1 2} {2 3} {4 3} {4 5} {6 5} {2 7} {7 8} {7 9} {8 10} {8 11}
            {5 12} {12 14} {12 13} {13 15} {13 16}}
        )
        >    
        "The game is played on the Graph1."
    )* 
    
    (item "Graph2"    
        <
        (graph
            vertices:{ 
            {0 0} {2 0} {4 0} {6 0} {8 0} {10 0} {15 0} {4 2} {6 2} {10 2} {6 3} {4 4} {3 4}
            {3 5} {4 5} {6 5} {6 6} {7 6} {8 3} {8 4} {9 4} {9 5} {10 5} {10 4}}
            edges:{ 
            {0 1} {1 2} {2 3} {4 3} {4 5} {6 5} {2 7} {3 8} {5 9} {8 10} {10 11} {11 12} {11 13}
            {11 14} {10 15} {15 16} {17 15} {8 18} {18 19} {18 20} {20 21} {20 22} {20 23}}
        )
        >      
        "The game is played on the Graph2."
    ) 
    
    (item "Graph3"    
        <
        (graph
            vertices:{ 
            {0 0} {2 0} {4 0} {6 0} {8 0} {10 0} {15 0} {5 1} {4 3} {5 5} {9 1} {7 3} {7 5}
            {3 6} {10 4} {7 8} {8 11} {10 10} {10 9} {5 10} {4 11} {3 11} {3 12} {4 12} {7 12} {9 12}
            {8 13} {9 14} {11 14} {11 12} {13 12}}
            edges:{ 
            {0 1} {1 2} {2 3} {4 3} {4 5} {6 5} {2 7} {7 8} {8 9} {4 10} {10 11} {11 12} {9 13}
            {9 12} {12 14} {13 15} {14 15} {15 16} {16 19} {19 20} {20 21} {21 22} {22 23} {20 23}
            {16 17} {17 18} {16 24} {24 26} {16 25} {25 26} {26 27} {27 28} {28 29} {29 30}}
        )  
        
        >      
        "The game is played on the Graph3."
    ) 
})

//------------------------------------------------------------------------------

(option "Version" <Version> args:{ <start> <play> }
    {
    (item "Hackenbush" 
        <
        (set Shared Edge (difference (sites Board Edge) (sites {0..5})))       
        > 
        <(sites Occupied by:Shared)> 
        "The players play on Hackenbush version."
    )* 
    (item "HackenbushR-B"
        <
        (set P1 Edge (sites {6..12}))
        (set P2 Edge (difference (sites Board Edge) (union (sites {6..12}) (sites {0..5}))))  
        > 
        <(sites Occupied by:Mover)>  
        "The players play on HackenbushR-B version."
    ) 
    (item "HackenbushR-G-B"
        <
        (set P1 Edge (sites {6 8 10}))
        (set P2 Edge (sites {9 12 13}))
        (set Shared Edge (difference (sites Board Edge) (union (union (sites {6 8 10}) (sites {0..5})) (sites {9 12 13}))))   
        > 
        <(difference (sites Occupied by:All on:Edge) (union (sites Occupied by:Neutral on:Edge) (sites Occupied by:Next on:Edge)))>  
        "The players play on HackenbushR-G-B version."
    ) 
})

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "Hackenbush is a two-player game, invented by the mathematician John Horton Conway. It may be played on any configuration of coloured line segments connected to one another by their end points and to a ground line. ")
        (rules "The game starts in drawing a ground line and several line segments such that each line segment is connected to the ground. Any number of segments may meet at a point, and thus there may be multiple paths to ground. Every turn, the current player selects any line segment to be removed. Every line segment no longer connected to the ground by any path falls. The first player who is unable to move loses. In addition to the standard Hackenbush, there are two variants; HackenbushR-B and HackenbushR-G-B. For the Hackenbush version, all the lines are in the same colour (green). At each turn, the current player selects a coloured line to be removed, also removing any other line segments that become disconnected from the ground as a result. The game is over when there are no green edges in the graph. The second version is the Red-Blue version; HackenbushR-B. There is one red player, and one blue player. The players can remove only edges of their own colour. The last version is HackenbushR-G-B; there is a graph with red, green, and blue edges. Players may remove edges of their own colour, as well as the shared colour. For all the versions of Hackenbush, if the current player has no move, then the opposing player wins. ")
        (id "697")
        (source "<a href=\"https://en.wikipedia.org/wiki/Hackenbush\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
        (version "1.3.12")
        (classification "math/graph")
        (author "John Horton Conway")
        (credit "Tahmina Begum 11-06-2020")
        (date "1982")
        }
    )
    
    (graphics {
        (player Colour Neutral (colour Black))
        (player Colour Shared (colour Green)) 
        (player Colour P1 (colour Red)) 
        (player Colour P2 (colour Blue))  
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges ThinDotted (colour 127 127 127))
    })
    
    (ai
        "Hackenbush_ai"
    )
)
