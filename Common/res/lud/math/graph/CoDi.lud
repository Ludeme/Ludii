(game "CoDi" 
    (players 2) 
    (equipment { 
        (board 
                (graph
                        vertices:{ 
                        {0 0} {0 4}
                        {1 1} {1.3 2} {1 3}
                        {2 0.5} {2.3 1.15} {2 2} {2.3 2.85} {2 3.55}
                        {4 0.65} {3.85 1.55} {3.85 2.55} {3.85 3.45}
                        {4.5 0} {4.4 1.3} {4.75 2} {4.4 2.8} {4.4 4}
                        {5.75 2}
                        }
                        edges:{ 
                        {0 1} {0 14} {1 18} {18 19} {19 14} {0 2} 
                        {2 3} {3 4} {4 9} {9 13} {13 18} {13 17}
                        {17 16} {16 19} {15 16} {15 10} {10 14} {11 12}
                        {12 17} {8 12} {8 9} {8 7} {7 6} {7 3} {6 5}
                        {5 2} {5 10} {11 6} {11 15} {1 4}
                        }
                 ) 
                use:Vertex
        )
        (piece "Marker" Each) 
    }) 
    
    (rules
        (play
            (move Add (piece (mover)) (to (sites Empty))
            	(then
            		(forEach Site (sites Around (last To))
            			(if (= (next) (who at:(site)))
            				(remove (site))
            			)
            		)
            	)
            )
        )
        (end      
            (if (or (is Full) (= 80 (count Moves)))
            	(byScore
            		{
            		(score P1 (count Pieces P1))
            		(score P2 (count Pieces P2))
            		}
            	)
            )   
        )
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (info
        {
        (description "CoDi is a game about majorities in a dodecahedron. ")
        (rules "The board depicts the 20 corners of the dodecahedron and each corner is connected to exactly 3 other corners. On his turn the player puts a stone of his color on one of the 20 corners. Stones from other colors on neighbouring corners are removed if the played stone if there already was a stone of the other player's color adjacent to them before. The game ends when all 20 corners have stones on them with the player with the most stones on the board being the winner. The game has also a limit of 80 moves.")
        (version "1.3.12")
        (classification "math/graph")
        (credit "Eric Piette")
        (date "2008")
        }
    )
    
    (graphics {
        (show Edges Diagonal (colour Hidden))
        (board Background image:"rectangle" fillColour:(colour 84 158 108) edgeColour:(colour 84 158 108) scaleX:1.1 scaleY:1.3)
    })
)
