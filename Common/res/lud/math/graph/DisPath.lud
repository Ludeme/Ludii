(game "DisPath"
    (players 2)
    (equipment { 
        (board <Board:type> use:Vertex)
    }) 
    (rules
        (play 
            (or 
                <Play:type>
                (move Pass)	   
            )
        )	  
        (end 	 	
            (if (all Passed)	
                (byScore)
            )
        )
    )
)
//------------------------------------------------------------------------------

(option "Board" <Board> args:{ <type> }
    {
    (item "Graph1"    
        <
        (graph
            vertices:{ 
            {0 0} {0 1} {0 2} {0 3} {0 4} {1 0} {1 1} {1 2} {1 3} {1 4} 
            {2 0} {2 1} {2 2} {2 3} {2 4} {3 0} {3 1} {3 2} {3 3} {3 4} 
            {4 0} {4 1} {4 2} {4 3} {4 4}}
            edges:{ 
            {0 1} {0 5} {1 6} {1 2} {2 7} {2 3} {3 8} {3 4} {4 9} {5 6} 
            {5 10} {6 11} {6 7} {7 12} {7 8} {8 13} {8 9} {9 14} {10 11} 
            {11 12} {12 13} {13 14} {10 15} {11 16} {12 17} {13 18} {14 19} 
            {15 16} {15 20} {16 21} {20 21} {16 17} {17 22} {21 22} {17 18} 
            {18 23} {22 23} {18 19} {19 24} {23 24}}
        )
        >  
        ""
    )* 
    (item "Graph2"    
        <
        (graph
            vertices:{
            {0 0} {2 2} {4 1} {2 3} {4 2} {10 2} {2 5} {6 4} {8 3} {11 3} {5 5}
            {9 4} {12 4} {9 5} {2 7} {4 7} {10 6} {13 5} {13 6} {3 8} {5 8} 
            {8 7} {10 7} {6 9} {8 9} {11 8} {13 8} {9 11} {11 10}}     
            edges:{
            {0 1} {0 3} {1 3} {1 2} {3 4} {2 4} {10 2} {4 10} {1 10} {3 10} {6 10} {7 10} 
            {10 14} {10 15} {10 24} {8 10} {15 20} {14 20} {19 15} {20 15} {20 23} {20 21}
            {23 21} {7 22} {7 16} {22 16} {22 24} {16 24} {13 21} {11 21} {13 11} {8 11} 
            {5 8} {5 11} {9 11} {9 13} {11 17} {16 17} {13 18} {12 16} {12 26} {16 26} 
            {16 18} {25 26} {22 25} {25 27} {26 28} {24 28} {24 27} {27 28}}
        )
        >  
        ""
    ) 
    
    (item "Graph3"    
        <
        (graph
            vertices:{ 
            {0 0} {3 0} {1 1} {4 1} {0 3} {3 3} {1 4} {4 4} {6 3} {9 3} {7 4} 
            {10 4} {6 6} {9 6} {7 7} {10 7}}
            edges:{
            {0 1} {0 2} {0 4} {1 3} {1 5} {2 3} {2 6} {4 5} {4 6} {6 7} {5 7} 
            {3 7} {8 9} {8 10} {11 9} {10 11} {9 13} {11 15} {13 15} {14 15} 
            {12 14} {13 12} {10 14} {8 12} {1 9} {3 11} {2 10} {0 8} {7 15} 
            {5 13} {6 14} {4 12}}
        )
        >  
        ""
    )  
})

//-----------------------------------------------------------------------------------

(option "Play Rules" <Play> args:{ <type> }
    {
    (item "Edge" 
        <
        (move Add
            (to
                Edge 
                (sites Empty Edge)
            )
            (then
                (if 
                    (is Path Edge Mover length:(exact 3)) 
                    (addScore Mover 1)
                )    		 
            )	
        )
        >  
        "The players play on edges of the graph."
    )* 
    (item "Vertex" 
        <
        (move Add
            (to
                Vertex 
                (sites Empty Vertex)
            )
            (then
                (if 
                    (is Path Vertex Mover length:(exact 3)) 
                    (addScore Mover 1)
                )    		 
            )	
        )
        >  
        "The players play on vertices of the graph."
    ) 
})

//------------------------------------------------------------------------------

(metadata 
    (info
        {
        (description "DisPath is a 2-player graph theory game, which can be played on vertices or edges of any graph. The aim of the game is to make more disjoint paths. Every disjoint path needs to be fully disconnected from any other friendly path. The length of the path can be any integer (in this game, we have used k = 3). The motivation comes from the Disjoint path problem in graph theory. ")
        (rules "Each turn, a player selects a graph element (vertex or edge) to create a disjoint path with size k. When a player successfully makes a path of a specific size, he gets a point. The player with the highest score wins the game. ")
        (id "620")
        (version "1.3.12")
        (classification "math/graph")
        (author "Tahmina Begum")
        (credit "Tahmina Begum")
        (date "30-03-2020")
        }
    )
    (graphics {
        (player Colour P1 (colour Red))
        (player Colour P2 (colour Blue))
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (show Edges Orthogonal ThinDotted (colour 127 127 127))
    }) 
    (ai
        "DisPath_ai"
    )
)

