(game "Wagner" 
    (players 2) 
    (equipment { 
        (board 
            (graph
                vertices:{ {2 0} {5 0} {0 2} {0 5} {7 2} {7 5} {2 7} {5 7}}
                edges:{ {0 1} {0 2} {0 7} {1 4} {1 6} {2 3} {2 5} {3 4} {3 6} {4 5} {5 7} {6 7}}
            )
            use:Vertex
        ) 
        (piece "Marker" Each ("StepToEmpty"))   
    }) 
    
    (rules
        (start {
            (set P1 Vertex (sites {0 5 6}))
            (set P2 Vertex (sites {1 2 7}))
        })
        
        (play (forEach Piece)) 
        
        (end 
            (if (= 1 (count Groups if:(= (who at:(to)) (mover)))) 
                (result Mover Win) 
            )     
        )
    )
)

//------------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (description "Wagner is a two-player graph theory game based on the Wagner graph, which is a Mobius ladder with 8 vertices. In this graph, the diameter and the radius are both equal to 2.  ")
        (rules "Wagner is a non-capturing game. Initially, each player places a stone at any vertex (not the centre) to maintain the graph colouring rules (i.e., no two friendly pieces are adjacent to each other). Each turn a player moves a friendly stone to an adjacent empty site. The goal is to make all the friendly pieces into a single group. 
        ")
        (id "614")
        (version "1.3.13")
        (classification "math/graph")
        (author "Tahmina Begum")
        (credit "Tahmina Begum")
        (date "25-04-2020")
        }
    )
    
    (graphics {
        (show Edges Diagonal Hidden)
        (board StyleThickness InnerVertices 5.0)
    })
    
    (ai
        "Wagner_ai"
    )
)

