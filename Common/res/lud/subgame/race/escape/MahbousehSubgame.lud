(define "NotEnemyOrOnlyOne"  
    (or 
        (and 
            ("IsEnemyAt" (to)) 
            (= (topLevel at:(to)) 0)
        ) 
        (not ("IsEnemyAt" (to)))
    )
)

(define "NextSiteFrom" 
    (trackSite Move 
        from:#1 
        steps:(pips)
    )
)

(define "DieNotUsed" (!= (pips) 0))

(define "AllPieceInHome" 
    ("NoSites" 
        (difference 
            (sites Occupied by:Mover) 
            (sites Mover "Home")
        )
    )
)

(define "RemoveAPiece" 
    (move
        Remove
        (site) 
    )		
)

(define "DieNotUsedAndNoEscapeAndNotOffTheTrack" 
    (and { 
        ("DieNotUsed") 
        (!= ("NextSiteFrom" (from)) Off) 
        (!= ("NextSiteFrom" (from)) End) 
    })		
)

(define "MoveAPiece" 
    (move 
        (from #1)
        (to 
            ("NextSiteFrom" #1) 
            if:("NotEnemyOrOnlyOne") 
        )
    )
)

(define "MoveAPieceIf" 
    (forEach Die 
        replayDouble:True 
        if:#1
        ("MoveAPiece" #2)
    )		
)

(define "AllPieceEscaped" (no Pieces Mover))

(define "NumPiece" (size Stack in:(sites Occupied by:#1)))

(define "CanEscape" (= ("NextSiteFrom" (site)) End))

(define "NotEmptyAndNotOffTheBoard" 
    (and 
        (!= (what at:(site)) 0) 
        (!= ("NextSiteFrom" (site)) Off)
    )
)

(define "SetScoreOf" 
    (score 
        #1
        (size Stack in:(sites Occupied by:#2))
    )
)

(define "HaveAPieceAndCanEscape" 
    (and 
        ("IsFriendAt" (site)) 
        (< (trackSite Move from:(site) steps:(pips)) 0)
    )
)

(define "PieceInLastQuadrant" 
    (!= 
        (count Sites in:(intersection (sites Occupied by:Mover) (sites Mover "Home"))) 
        0
    ) 
)

(define "InTheStartingPosition" 
    (= (from) (mapEntry (mover))) 
)

(define "Not14PiecesInThePosition"
    (!= (topLevel at:(from)) 13)
)

//-----------------------------------------------------------------------------

(game "MahbousehSubgame" 
    (players 2) 
    (equipment { 
        (board (rectangle 2 13)
            {
            (track "Track1" {12..7 5..0 13..18 20..25 End} P1 directed:True)
            (track "Track2" {25..20 18..13 0..5 7..12 End} P2 directed:True)
            } 
            use:Vertex
        ) 
        (dice num:2)
        (regions "Home" P1 { 20..25 }) 
        (regions "Home" P2 { 7..12 }) 
        (map {(pair 1 12) (pair 2 25)}) // The starting positions of each player.
        (piece "Disc" Each ("MoveAPieceIf" "DieNotUsedAndNoEscapeAndNotOffTheTrack" (from))) 
    })
    
    (rules
        (start { 
            (place Stack "Disc1" 12 count:15)
            (place Stack "Disc2" 25 count:15)
        })
        
        (play 
            (do 
                (if (not "SameTurn") (roll))
                next:(if "AllPieceInHome"
                    (forEach Die 
                        replayDouble:True 
                        if:("DieNotUsed")
                        (forEach Site 
                            (sites Mover "Home")
                            (if ("NotEmptyAndNotOffTheBoard") 
                                (if ("CanEscape")
                                    ("RemoveAPiece")
                                    ("MoveAPiece" (site))
                                )
                            )
                            noMoveYet:(firstMoveOnTrack "Track" Mover
                                (if "HaveAPieceAndCanEscape"
                                    "RemoveAPiece"
                                )
                            )
                            (then 
                                (if (not (all DiceUsed))
                                    (moveAgain)
                                )
                            )
                        )
                    )
                    (forEach Piece top:True
                        (then 
                            (if (not (all DiceUsed))
                                (moveAgain)
                            )
                        )
                    )
                )
            )
        )
        
        (end 
            (if ("AllPieceEscaped") 
                (byScore { 
                    ("SetScoreOf" P1 P2)
                    ("SetScoreOf" P2 P1)
                })
            )
        )
    )
)

//-------------------------------------------------------------------------

(metadata 
    
    (info
        {
        (version "1.1.0")
        (classification "")
        }
    )
    
    (graphics {
        (board Style backgammon)
        (stackType Backgammon)
    })
    
)
